Функция СведенияОВнешнейОбработке() Экспорт
	
	Версия = "1.0.002";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Регл. актуализация даты расчета пени [" + Версия + "]");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Регл. актуализация даты расчета пени [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
	ДобавитьКоманду(ТаблицаКоманд, "Регл. актуализация даты расчета пени [" + Версия + "]", "ВыполнитьАктуализацию();", "ВызовСерверногоМетода");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

Процедура ВыполнитьКоманду(Индентификатор) Экспорт
	Выполнить(Индентификатор);	
КонецПроцедуры



Процедура ВыполнитьАктуализацию() Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	КурсыВалютСрезПоследних.Валюта,
	                      |	КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность КАК Курс
	                      |ПОМЕСТИТЬ КурсыВалют
	                      |ИЗ
	                      |	РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалютСрезПоследних
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Аналитика.Партнер КАК Партнер,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА ВалютаУправленческогоУчета.Значение = Долги.Валюта
	                      |				ТОГДА Долги.СуммаОстаток
	                      |			ИНАЧЕ Долги.СуммаОстаток / КурсыВалютУПР.Курс * КурсыВалют.Курс
	                      |		КОНЕЦ) КАК Долг
	                      |ПОМЕСТИТЬ Долги
	                      |ИЗ
	                      |	РегистрНакопления.РасчетыСКлиентами.Остатки(
	                      |			&ТекущаяДата,
	                      |			АналитикаУчетаПоПартнерам В
	                      |				(ВЫБРАТЬ
	                      |					АналитикаПоПартнерам.КлючАналитики КАК КлючАналитики
	                      |				ИЗ
	                      |					РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	                      |				ГДЕ
	                      |					АналитикаПоПартнерам.Организация = &Организация)) КАК Долги
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	                      |		ПО Долги.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВалютаУправленческогоУчета КАК ВалютаУправленческогоУчета
	                      |		ПО (ИСТИНА)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУПР
	                      |		ПО (КурсыВалютУПР.Валюта = ВалютаУправленческогоУчета.Значение)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	                      |		ПО (КурсыВалют.Валюта = Долги.Валюта)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	Аналитика.Партнер
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЗаказКлиентаЭтапыГрафикаОплаты.Ссылка,
	                      |	МИНИМУМ(ЗаказКлиентаЭтапыГрафикаОплаты.ДатаПлатежа) КАК ДатаПлатежа
	                      |ПОМЕСТИТЬ ГрафикОплат
	                      |ИЗ
	                      |	Документ.ЗаказКлиента.ЭтапыГрафикаОплаты КАК ЗаказКлиентаЭтапыГрафикаОплаты
	                      |ГДЕ
	                      |	ЗаказКлиентаЭтапыГрафикаОплаты.Ссылка.Организация = &Организация
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ЗаказКлиентаЭтапыГрафикаОплаты.Ссылка
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Аналитика.Партнер КАК Партнер,
	                      |	Обороты.Регистратор,
	                      |	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(ВЫБОР
	                      |					КОГДА Обороты.Регистратор.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	                      |							ИЛИ Обороты.Регистратор.ЗаказКлиента = НЕОПРЕДЕЛЕНО
	                      |							ИЛИ Обороты.Регистратор.ЗаказКлиента = NULL
	                      |						ТОГДА Обороты.Регистратор.ДатаПлатежа
	                      |					ИНАЧЕ ГрафикОплат.ДатаПлатежа
	                      |				КОНЕЦ, ЧАС, ЧАС(Обороты.Период)), МИНУТА, МИНУТА(Обороты.Период)), СЕКУНДА, СЕКУНДА(Обороты.Период)) КАК Период,
	                      |	ВЫБОР
	                      |		КОГДА ВалютаУправленческогоУчета.Значение = Обороты.Валюта
	                      |			ТОГДА Обороты.СуммаПриход
	                      |		ИНАЧЕ Обороты.СуммаПриход / КурсыВалютУПР.Курс * КурсыВалют.Курс
	                      |	КОНЕЦ КАК Сумма,
	                      |	ВЫБОР
	                      |		КОГДА ВалютаУправленческогоУчета.Значение = Обороты.Валюта
	                      |			ТОГДА Обороты.СуммаОборот
	                      |		ИНАЧЕ Обороты.СуммаОборот / КурсыВалютУПР.Курс * КурсыВалют.Курс
	                      |	КОНЕЦ КАК СуммаОборот
	                      |ПОМЕСТИТЬ ОборотыПоКлиенту
	                      |ИЗ
	                      |	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(
	                      |			,
	                      |			&ТекущаяДата,
	                      |			Регистратор,
	                      |			,
	                      |			АналитикаУчетаПоПартнерам В
	                      |				(ВЫБРАТЬ
	                      |					АналитикаПоПартнерам.КлючАналитики КАК КлючАналитики
	                      |				ИЗ
	                      |					РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	                      |				ГДЕ
	                      |					АналитикаПоПартнерам.Организация = &Организация)) КАК Обороты
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	                      |		ПО Обороты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ГрафикОплат КАК ГрафикОплат
	                      |		ПО (ГрафикОплат.Ссылка = Обороты.Регистратор.ЗаказКлиента)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВалютаУправленческогоУчета КАК ВалютаУправленческогоУчета
	                      |		ПО (ИСТИНА)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУПР
	                      |		ПО (КурсыВалютУПР.Валюта = ВалютаУправленческогоУчета.Значение)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	                      |		ПО (КурсыВалют.Валюта = Обороты.Валюта)
	                      |ГДЕ
	                      |	ТИПЗНАЧЕНИЯ(Обороты.Регистратор) = ТИП(Документ.РеализацияТоваровУслуг)
	                      |	И Обороты.СуммаОборот <> 0
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ОборотыПоКлиенту.Партнер КАК Партнер,
	                      |	НАЧАЛОПЕРИОДА(ОборотыПоКлиенту.Период, МЕСЯЦ) КАК НачПериода,
	                      |	КОНЕЦПЕРИОДА(ОборотыПоКлиенту.Период, МЕСЯЦ) КАК КонПериода,
	                      |	СУММА(ОборотыПоКлиенту.СуммаОборот) КАК Сумма
	                      |ПОМЕСТИТЬ ОборотыПоМесяцам
	                      |ИЗ
	                      |	ОборотыПоКлиенту КАК ОборотыПоКлиенту
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ОборотыПоКлиенту.Партнер,
	                      |	НАЧАЛОПЕРИОДА(ОборотыПоКлиенту.Период, МЕСЯЦ),
	                      |	КОНЕЦПЕРИОДА(ОборотыПоКлиенту.Период, МЕСЯЦ)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ОборотыПоМесяцам.Партнер,
	                      |	ОборотыПоМесяцам.НачПериода,
	                      |	ОборотыПоМесяцам.КонПериода,
	                      |	ОборотыПоМесяцам.Сумма,
	                      |	СУММА(ОборотыПоМесяцамКопия.Сумма) КАК СуммаПосле,
	                      |	СУММА(ОборотыПоМесяцамКопия.Сумма) - ОборотыПоМесяцам.Сумма КАК СуммаДо
	                      |ПОМЕСТИТЬ ОборотыПоМесяцамНарастающие
	                      |ИЗ
	                      |	ОборотыПоМесяцам КАК ОборотыПоМесяцам
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОборотыПоМесяцам КАК ОборотыПоМесяцамКопия
	                      |		ПО ОборотыПоМесяцам.Партнер = ОборотыПоМесяцамКопия.Партнер
	                      |			И ОборотыПоМесяцам.НачПериода <= ОборотыПоМесяцамКопия.НачПериода
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ОборотыПоМесяцам.НачПериода,
	                      |	ОборотыПоМесяцам.КонПериода,
	                      |	ОборотыПоМесяцам.Партнер,
	                      |	ОборотыПоМесяцам.Сумма
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Долги.Партнер,
	                      |	Долги.Долг,
	                      |	ОборотыПоМесНарастающие.НачПериода,
	                      |	ОборотыПоМесНарастающие.КонПериода,
	                      |	Долги.Долг - ОборотыПоМесНарастающие.СуммаДо КАК ОстатокДолга
	                      |ПОМЕСТИТЬ ДолгиПоВыбраннымМесяцам
	                      |ИЗ
	                      |	Долги КАК Долги
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОборотыПоМесяцамНарастающие КАК ОборотыПоМесНарастающие
	                      |		ПО Долги.Партнер = ОборотыПоМесНарастающие.Партнер
	                      |			И Долги.Долг > ОборотыПоМесНарастающие.СуммаДо
	                      |			И Долги.Долг <= ОборотыПоМесНарастающие.СуммаПосле
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Обороты.Партнер КАК Партнер,
	                      |	Обороты.Регистратор,
	                      |	Обороты.Период,
	                      |	ДолгиПоВыбМесяцам.Долг,
	                      |	ДолгиПоВыбМесяцам.ОстатокДолга,
	                      |	Обороты.Сумма,
	                      |	Обороты.Период КАК ДатаОтсрочки
	                      |ПОМЕСТИТЬ ДвиженияПоВыбраннымМесяцам
	                      |ИЗ
	                      |	ОборотыПоКлиенту КАК Обороты
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолгиПоВыбраннымМесяцам КАК ДолгиПоВыбМесяцам
	                      |		ПО Обороты.Партнер = ДолгиПоВыбМесяцам.Партнер
	                      |			И Обороты.Период >= ДолгиПоВыбМесяцам.НачПериода
	                      |			И Обороты.Период <= ДолгиПоВыбМесяцам.КонПериода
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ДвиженияПоВыбМесяцам.Партнер,
	                      |	ДвиженияПоВыбМесяцам.Регистратор,
	                      |	ДвиженияПоВыбМесяцам.Период,
	                      |	ДвиженияПоВыбМесяцам.Сумма,
	                      |	СУММА(ДвиженияПоВыбМесяцамКопия.Сумма) КАК СуммаПосле,
	                      |	СУММА(ДвиженияПоВыбМесяцамКопия.Сумма) - ДвиженияПоВыбМесяцам.Сумма КАК СуммаДо,
	                      |	ДвиженияПоВыбМесяцам.ОстатокДолга,
	                      |	ДвиженияПоВыбМесяцам.ДатаОтсрочки
	                      |ПОМЕСТИТЬ ДвиженияПредварительные
	                      |ИЗ
	                      |	ДвиженияПоВыбраннымМесяцам КАК ДвиженияПоВыбМесяцам
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияПоВыбраннымМесяцам КАК ДвиженияПоВыбМесяцамКопия
	                      |		ПО ДвиженияПоВыбМесяцам.Партнер = ДвиженияПоВыбМесяцамКопия.Партнер
	                      |			И ДвиженияПоВыбМесяцам.Период <= ДвиженияПоВыбМесяцамКопия.Период
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ДвиженияПоВыбМесяцам.Партнер,
	                      |	ДвиженияПоВыбМесяцам.Регистратор,
	                      |	ДвиженияПоВыбМесяцам.Период,
	                      |	ДвиженияПоВыбМесяцам.Сумма,
	                      |	ДвиженияПоВыбМесяцам.ОстатокДолга,
	                      |	ДвиженияПоВыбМесяцам.ДатаОтсрочки
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ДвиженияПредв.Партнер,
	                      |	ДвиженияПредв.Регистратор,
	                      |	ДвиженияПредв.Период,
	                      |	ДвиженияПредв.ОстатокДолга - ДвиженияПредв.СуммаДо КАК СуммаДолга,
	                      |	ДвиженияПредв.Сумма
	                      |ПОМЕСТИТЬ ДвиженияОкончательные
	                      |ИЗ
	                      |	ДвиженияПредварительные КАК ДвиженияПредв
	                      |ГДЕ
	                      |	ДвиженияПредв.ОстатокДолга > ДвиженияПредв.СуммаДо
	                      |	И ДвиженияПредв.ОстатокДолга <= ДвиженияПредв.СуммаПосле
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВЫБОР
	                      |		КОГДА Обороты.Регистратор.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	                      |			ТОГДА Обороты.Регистратор
	                      |		ИНАЧЕ Обороты.Регистратор.ЗаказКлиента
	                      |	КОНЕЦ КАК Документ
	                      |ПОМЕСТИТЬ КешДокументыИзЗадолженностью
	                      |ИЗ
	                      |	ОборотыПоКлиенту КАК Обороты
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияОкончательные КАК ДвиженияОконч
	                      |		ПО Обороты.Партнер = ДвиженияОконч.Партнер
	                      |			И Обороты.Период >= ДвиженияОконч.Период
	                      |ГДЕ
	                      |	ВЫБОР
	                      |			КОГДА Обороты.Регистратор.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	                      |				ТОГДА Обороты.Регистратор.Дата > &ДатаЗапрета
	                      |			ИНАЧЕ Обороты.Регистратор.ЗаказКлиента.Дата > &ДатаЗапрета
	                      |		КОНЕЦ
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВЫБОР
	                      |		КОГДА Обороты.Регистратор.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	                      |			ТОГДА Обороты.Регистратор
	                      |		ИНАЧЕ Обороты.Регистратор.ЗаказКлиента
	                      |	КОНЕЦ
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Заказы.ЗаказКлиента КАК Документ
	                      |ПОМЕСТИТЬ КешНеОтгруженныеДокументы
	                      |ИЗ
	                      |	РегистрНакопления.ЗаказыКлиентов.Обороты(, , , ЗаказКлиента.Организация = &Организация) КАК Заказы
	                      |ГДЕ
	                      |	Заказы.КОформлениюОборот <> 0
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	ВЫБОР
	                      |		КОГДА ТоварыКОтгрузке.ДокументОтгрузки.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	                      |			ТОГДА ТоварыКОтгрузке.ДокументОтгрузки
	                      |		ИНАЧЕ ТоварыКОтгрузке.ДокументОтгрузки.ЗаказКлиента
	                      |	КОНЕЦ
	                      |ИЗ
	                      |	РегистрНакопления.ТоварыКОтгрузке.Остатки КАК ТоварыКОтгрузке
	                      |ГДЕ
	                      |	ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КешДокументыИзЗадолженностью.Документ
	                      |ПОМЕСТИТЬ КешОтгруженныеДокументыСЗадолженностью
	                      |ИЗ
	                      |	КешДокументыИзЗадолженностью КАК КешДокументыИзЗадолженностью
	                      |ГДЕ
	                      |	НЕ КешДокументыИзЗадолженностью.Документ В
	                      |				(ВЫБРАТЬ
	                      |					КешНеОтгруженныеДокументы.Документ
	                      |				ИЗ
	                      |					КешНеОтгруженныеДокументы)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЗаказыКлиентов.ЗаказКлиента КАК ДокументОснование,
	                      |	ЗаказыКлиентов.Номенклатура КАК Номенклатура,
	                      |	ЗаказыКлиентов.Склад КАК Склад,
	                      |	ЗаказыКлиентов.Регистратор КАК ДокументРеализации,
	                      |	ЗаказыКлиентов.КОформлениюРасход КАК Количество
	                      |ПОМЕСТИТЬ КешДокументы
	                      |ИЗ
	                      |	РегистрНакопления.ЗаказыКлиентов.Обороты(
	                      |			,
	                      |			,
	                      |			Регистратор,
	                      |			ЗаказКлиента В
	                      |				(ВЫБРАТЬ
	                      |					КешОтгруженныеДокументыСЗадолженностью.Документ
	                      |				ИЗ
	                      |					КешОтгруженныеДокументыСЗадолженностью
	                      |				ГДЕ
	                      |					КешОтгруженныеДокументыСЗадолженностью.Документ ССЫЛКА Документ.ЗаказКлиента)) КАК ЗаказыКлиентов
	                      |ГДЕ
	                      |	ЗаказыКлиентов.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	РеализацииТоваров.ЗаказКлиента,
	                      |	АналитикаНоменклатуры.Номенклатура,
	                      |	АналитикаНоменклатуры.Склад,
	                      |	РеализацииТоваров.ЗаказКлиента,
	                      |	РеализацииТоваров.КоличествоОборот
	                      |ИЗ
	                      |	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(
	                      |			,
	                      |			,
	                      |			,
	                      |			ЗаказКлиента В
	                      |				(ВЫБРАТЬ
	                      |					КешОтгруженныеДокументыСЗадолженностью.Документ
	                      |				ИЗ
	                      |					КешОтгруженныеДокументыСЗадолженностью
	                      |				ГДЕ
	                      |					КешОтгруженныеДокументыСЗадолженностью.Документ ССЫЛКА Документ.РеализацияТоваровУслуг)) КАК РеализацииТоваров
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	                      |		ПО РеализацииТоваров.АналитикаУчетаНоменклатуры = АналитикаНоменклатуры.КлючАналитики
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КешДокументы.ДокументОснование,
	                      |	КешДокументы.ДокументОснование.Дата КАК ДатаОснования,
	                      |	ВЫБОР
	                      |		КОГДА КешДокументы.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	                      |			ТОГДА ТоварыКОтгрузкеОбороты.Регистратор
	                      |		ИНАЧЕ КешДокументы.ДокументРеализации
	                      |	КОНЕЦ КАК Документ,
	                      |	КешДокументы.Номенклатура КАК Номенклатура,
	                      |	КешДокументы.Склад КАК Склад,
	                      |	КешДокументы.Количество
	                      |ПОМЕСТИТЬ КешДокументыОтгрузки
	                      |ИЗ
	                      |	КешДокументы КАК КешДокументы
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОтгрузке.Обороты(
	                      |				,
	                      |				,
	                      |				Регистратор,
	                      |				(ДокументОтгрузки, Номенклатура, Склад) В
	                      |					(ВЫБРАТЬ
	                      |						КешДокументы.ДокументРеализации,
	                      |						КешДокументы.Номенклатура,
	                      |						КешДокументы.Склад
	                      |					ИЗ
	                      |						КешДокументы КАК КешДокументы)) КАК ТоварыКОтгрузкеОбороты
	                      |		ПО КешДокументы.ДокументРеализации = ТоварыКОтгрузкеОбороты.ДокументОтгрузки
	                      |			И КешДокументы.Номенклатура = ТоварыКОтгрузкеОбороты.Номенклатура
	                      |			И КешДокументы.Склад = ТоварыКОтгрузкеОбороты.Склад
	                      |			И (ТоварыКОтгрузкеОбороты.Регистратор ССЫЛКА Документ.РасходныйОрдерНаТовары)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЦеныНоменклатурыСрезПоследних.Номенклатура,
	                      |	ЦеныНоменклатурыСрезПоследних.Цена
	                      |ПОМЕСТИТЬ КешЦеныНоменклатуры
	                      |ИЗ
	                      |	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	                      |			,
	                      |			ВидЦены В (&ВидЦеныВходная)
	                      |				И Номенклатура В
	                      |					(ВЫБРАТЬ
	                      |						КешДокументыОтгрузки.Номенклатура
	                      |					ИЗ
	                      |						КешДокументыОтгрузки КАК КешДокументыОтгрузки)) КАК ЦеныНоменклатурыСрезПоследних
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КешДокументыОтгрузки.ДокументОснование,
	                      |	СУММА(КешДокументыОтгрузки.Количество * ЕСТЬNULL(КешЦеныНоменклатуры.Цена, 1)) КАК Сумма,
	                      |	КешДокументыОтгрузки.ДокументОснование.Дата КАК ДатаДокумента
	                      |ПОМЕСТИТЬ КешСуммаОснования
	                      |ИЗ
	                      |	КешДокументыОтгрузки КАК КешДокументыОтгрузки
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КешЦеныНоменклатуры КАК КешЦеныНоменклатуры
	                      |		ПО КешДокументыОтгрузки.Номенклатура = КешЦеныНоменклатуры.Номенклатура
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	КешДокументыОтгрузки.ДокументОснование,
	                      |	КешДокументыОтгрузки.ДокументОснование.Дата
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КешДокументыОтгрузки.Документ КАК ДокументОтгрузки,
	                      |	СУММА(КешДокументыОтгрузки.Количество * ЕСТЬNULL(КешЦеныНоменклатуры.Цена, 1)) КАК Сумма,
	                      |	КешДокументыОтгрузки.Документ.Дата КАК ДатаДокумента
	                      |ПОМЕСТИТЬ КешСуммаОтгрузки
	                      |ИЗ
	                      |	КешДокументыОтгрузки КАК КешДокументыОтгрузки
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КешЦеныНоменклатуры КАК КешЦеныНоменклатуры
	                      |		ПО КешДокументыОтгрузки.Номенклатура = КешЦеныНоменклатуры.Номенклатура
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	КешДокументыОтгрузки.Документ,
	                      |	КешДокументыОтгрузки.Документ.Дата
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КешДокументыОтгрузки.ДокументОснование КАК ДокументОснование,
	                      |	КешДокументыОтгрузки.ДокументОснование.Соглашение.ГрафикОплаты КАК ГрафикОплаты,
	                      |	РАЗНОСТЬДАТ(КешСуммаОснования.ДатаДокумента, КешСуммаОтгрузки.ДатаДокумента, ДЕНЬ) * (КешСуммаОтгрузки.Сумма / КешСуммаОснования.Сумма) КАК ДобавитьДней,
	                      |	КешДокументыОтгрузки.ДатаОснования
	                      |ПОМЕСТИТЬ КешЗГрафикомОплатыСГруппировками
	                      |ИЗ
	                      |	КешДокументыОтгрузки КАК КешДокументыОтгрузки
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешСуммаОснования КАК КешСуммаОснования
	                      |		ПО КешДокументыОтгрузки.ДокументОснование = КешСуммаОснования.ДокументОснование
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешСуммаОтгрузки КАК КешСуммаОтгрузки
	                      |		ПО КешДокументыОтгрузки.Документ = КешСуммаОтгрузки.ДокументОтгрузки
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	КешДокументыОтгрузки.ДокументОснование,
	                      |	КешДокументыОтгрузки.ДатаОснования,
	                      |	КешДокументыОтгрузки.ДокументОснование.Соглашение.ГрафикОплаты,
	                      |	РАЗНОСТЬДАТ(КешСуммаОснования.ДатаДокумента, КешСуммаОтгрузки.ДатаДокумента, ДЕНЬ) * (КешСуммаОтгрузки.Сумма / КешСуммаОснования.Сумма)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КешЗГрафикомОплатыСГруппировками.ДокументОснование КАК ДокументОснование,
	                      |	КешЗГрафикомОплатыСГруппировками.ГрафикОплаты КАК ГрафикОплаты,
	                      |	СУММА(КешЗГрафикомОплатыСГруппировками.ДобавитьДней) КАК ДобавитьДней,
	                      |	КешЗГрафикомОплатыСГруппировками.ДатаОснования КАК ДатаОснования
	                      |ПОМЕСТИТЬ КешЗГрафикомОплаты
	                      |ИЗ
	                      |	КешЗГрафикомОплатыСГруппировками КАК КешЗГрафикомОплатыСГруппировками
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	КешЗГрафикомОплатыСГруппировками.ДокументОснование,
	                      |	КешЗГрафикомОплатыСГруппировками.ДатаОснования,
	                      |	КешЗГрафикомОплатыСГруппировками.ГрафикОплаты
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КешЗГрафикомОплаты.ДокументОснование КАК ДокументОснование,
	                      |	ДОБАВИТЬКДАТЕ(КешЗГрафикомОплаты.ДатаОснования, ДЕНЬ, (ВЫРАЗИТЬ(КешЗГрафикомОплаты.ДобавитьДней КАК ЧИСЛО(10, 0))) + ГрафикиОплатыЭтапы.Сдвиг) КАК ДатаПлатежа,
	                      |	ГрафикиОплатыЭтапы.НомерСтроки КАК НомерСтроки
	                      |ПОМЕСТИТЬ КешДокументыСДатойПлатежа
	                      |ИЗ
	                      |	КешЗГрафикомОплаты КАК КешЗГрафикомОплаты
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГрафикиОплаты.Этапы КАК ГрафикиОплатыЭтапы
	                      |		ПО КешЗГрафикомОплаты.ГрафикОплаты = ГрафикиОплатыЭтапы.Ссылка
	                      |			И (ГрафикиОплатыЭтапы.ВариантОплаты = ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыКлиентом.КредитПослеОтгрузки))
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ДокументыСДатойПлатежа.ДокументОснование КАК ДокументОснование,
	                      |	ДокументыСДатойПлатежа.ДатаПлатежа КАК ДатаПлатежа,
	                      |	ДокументыСДатойПлатежа.НомерСтроки КАК НомерСтроки
	                      |ИЗ
	                      |	КешДокументыСДатойПлатежа КАК ДокументыСДатойПлатежа
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ГрафикОплат КАК ГрафикОплатЗаказов
	                      |		ПО (ГрафикОплатЗаказов.Ссылка = ДокументыСДатойПлатежа.ДокументОснование)
	                      |ГДЕ
	                      |	ВЫБОР
	                      |			КОГДА ДокументыСДатойПлатежа.ДокументОснование ССЫЛКА Документ.ЗаказКлиента
	                      |				ТОГДА НАЧАЛОПЕРИОДА(ДокументыСДатойПлатежа.ДатаПлатежа, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ГрафикОплатЗаказов.ДатаПлатежа, ДЕНЬ)
	                      |			ИНАЧЕ НАЧАЛОПЕРИОДА(ДокументыСДатойПлатежа.ДатаПлатежа, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ДокументыСДатойПлатежа.ДокументОснование.ДатаПлатежа, ДЕНЬ)
	                      |		КОНЕЦ
	                      |ИТОГИ
	                      |	МИНИМУМ(ДатаПлатежа)
	                      |ПО
	                      |	ДокументОснование");
						  
	ДатаЗапрета = ПолучитьДатуЗапрета();						  
						  
	Запрос.УстановитьПараметр("ВидЦеныВходная", Справочники.ВидыЦен.ПолучитьСсылку(Новый УникальныйИдентификатор("8bdf84fb-404c-11e0-9f98-001517115d85")));
	Запрос.УстановитьПараметр("Организация", Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("0fba8a92-4010-11e0-9f98-001517115d85")));
	Запрос.УстановитьПараметр("ТекущаяДата", КонецДня(ТекущаяДата())+1);
	Запрос.УстановитьПараметр("ДатаЗапрета", ДатаЗапрета);

	ВыборкаДокументОснование = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокументОснование.Следующий() Цикл
		
		ДокументОбъект = ВыборкаДокументОснование.ДокументОснование.ПолучитьОбъект();
		Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
			ДокументОбъект.ДатаПлатежа = ВыборкаДокументОснование.ДатаПлатежа;
		КонецЕсли;
		
		Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказКлиента") Тогда
			ВыборкаДетальныеЗаписи  = ВыборкаДокументОснование.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				ДокументОбъект.ЭтапыГрафикаОплаты[ВыборкаДетальныеЗаписи.НомерСтроки-1].ДатаПлатежа = ВыборкаДетальныеЗаписи.ДатаПлатежа;
			КонецЦикла;
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ВызватьИсключение ОписаниеОшибки();
			ОтменитьТранзакцию();
			Продолжить;
		КонецПопытки;
		
		ЗафиксироватьТранзакцию();
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьДатуЗапрета()

	ДатаЗапрета = Дата('20121016000000');
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ДатыЗапрета.ДатаЗапрета КАК ДатаЗапрета
	                      |ИЗ
	                      |	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапрета
	                      |ГДЕ
	                      |	ДатыЗапрета.Пользователь = ЗНАЧЕНИЕ(Перечисление.ВидыНазначенияДатЗапрета.ДляВсехПользователей)");
						  
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.ДатаЗапрета > ДатаЗапрета Тогда
			ДатаЗапрета = Выборка.ДатаЗапрета;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДатаЗапрета;

КонецФункции // ПолучитьДатуЗапрета()
 
 