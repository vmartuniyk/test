
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗагрузитьТаблицуПартнеров();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаОплат

&НаКлиенте
Процедура ТаблицаОплатВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьЗначение(Элемент.ТекущиеДанные.Партнер);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ТекущиеДанные 	= Элементы.ТаблицаОплат.ТекущиеДанные;
	ТекущийПартнер  = ?(ТекущиеДанные=Неопределено, Неопределено, ТекущиеДанные.Партнер);
	ОбновитьСписокНаСервере(ТекущийПартнер);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьТаблицуПартнеров()

	УстановитьПривилегированныйРежим(Истина);
	
	ДатаПросмотра = НачалоДня(ТекущаяДатаСеанса());
	
	Запрос 			= Новый Запрос;
	Запрос.Текст    = ТекстЗапросаЗагрузкиТаблицы();
	
	Запрос.УстановитьПараметр("ДатаПросмотра", ДатаПросмотра);
	Запрос.УстановитьПараметр("НачалоЗавтрашнегоДня", КонецДня(ДатаПросмотра)+1);
	
	ТаблицаОплат.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры // ЗагрузитьТаблицуПартнеров()

&НаСервере
Функция ТекстЗапросаЗагрузкиТаблицы()

	ТекстЗапроса = "ВЫБРАТЬ
	               |	ПоездкаЗаМаршрутомФинансы.Партнер КАК Партнер,
	               |	ПоездкаЗаМаршрутомФинансы.СуммаУчета КАК СуммаВМаршруте
	               |ПОМЕСТИТЬ ТаблицаПартнеров
	               |ИЗ
	               |	РегистрСведений.ПоездкаЗаМаршрутомФинансы КАК ПоездкаЗаМаршрутомФинансы
	               |ГДЕ
	               |	НАЧАЛОПЕРИОДА(ПоездкаЗаМаршрутомФинансы.ДокументПоездки.Дата, ДЕНЬ) = &ДатаПросмотра
	               |	И ПоездкаЗаМаршрутомФинансы.ДокументПоездки.СтатусПоездки = ЗНАЧЕНИЕ(Перечисление.КТС_СтатусыПоездки.ВПроцессе)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.Партнер,
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.УсловиеОтгрузки,
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.КредитныйЛимит,
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.ДополнительныйПроцент
	               |ПОМЕСТИТЬ ТаблицаУсловийОтгрузок
	               |ИЗ
	               |	РегистрСведений.КТС_НастройкиУсловийОтгрузки.СрезПоследних(
	               |			&ДатаПросмотра,
	               |			Партнер В
	               |				(ВЫБРАТЬ
	               |					ТаблицаПартнеров.Партнер
	               |				ИЗ
	               |					ТаблицаПартнеров КАК ТаблицаПартнеров)) КАК КТС_НастройкиУсловийОтгрузкиСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.УсловиеОтгрузки,
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.КредитныйЛимит,
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.ДополнительныйПроцент
	               |ПОМЕСТИТЬ УсловияОтгрузкиПоУмолчанию
	               |ИЗ
	               |	РегистрСведений.КТС_НастройкиУсловийОтгрузки.СрезПоследних(&ДатаПросмотра, Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)) КАК КТС_НастройкиУсловийОтгрузкиСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КТС_ГрафикПлатежейПартнеровСрезПоследних.Партнер,
	               |	КТС_ГрафикПлатежейПартнеровСрезПоследних.СуммаОстаток КАК ОбщийДолг,
	               |	ВЫБОР
	               |		КОГДА КТС_ГрафикПлатежейПартнеровСрезПоследних.ДатаПлатежа < &НачалоЗавтрашнегоДня
	               |			ТОГДА КТС_ГрафикПлатежейПартнеровСрезПоследних.СуммаОстаток
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаПросрочки
	               |ПОМЕСТИТЬ ТаблицаГрафикаПлатежей
	               |ИЗ
	               |	РегистрСведений.КТС_ГрафикПлатежейПартнеров.СрезПервых(
	               |			&НачалоЗавтрашнегоДня,
	               |			Партнер В
	               |				(ВЫБРАТЬ
	               |					ТаблицаПартнеров.Партнер
	               |				ИЗ
	               |					ТаблицаПартнеров КАК ТаблицаПартнеров)) КАК КТС_ГрафикПлатежейПартнеровСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаГрафикаПлатежей.Партнер,
	               |	СУММА(ТаблицаГрафикаПлатежей.ОбщийДолг) КАК ОбщийДолг,
	               |	СУММА(ТаблицаГрафикаПлатежей.СуммаПросрочки) КАК СуммаПросрочки
	               |ПОМЕСТИТЬ СвернутаяТаблицаГрафикаПлатежей
	               |ИЗ
	               |	ТаблицаГрафикаПлатежей КАК ТаблицаГрафикаПлатежей
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаГрафикаПлатежей.Партнер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаПартнеров.Партнер,
	               |	ТаблицаПартнеров.СуммаВМаршруте,
	               |	ЕСТЬNULL(ТаблицаУсловийОтгрузок.УсловиеОтгрузки, УсловияОтгрузкиПоУмолчанию.УсловиеОтгрузки) КАК УсловиеОтгрузки,
	               |	ЕСТЬNULL(ТаблицаУсловийОтгрузок.КредитныйЛимит, УсловияОтгрузкиПоУмолчанию.КредитныйЛимит) КАК КредитныйЛимит,
	               |	ЕСТЬNULL(ТаблицаУсловийОтгрузок.ДополнительныйПроцент, УсловияОтгрузкиПоУмолчанию.ДополнительныйПроцент) КАК ДополнительныйПроцент,
	               |	0 КАК ИндексКартинки,
	               |	ЕСТЬNULL(СвернутаяТаблицаГрафикаПлатежей.ОбщийДолг, 0) КАК ОбщийДолг,
	               |	ЕСТЬNULL(СвернутаяТаблицаГрафикаПлатежей.СуммаПросрочки, 0) КАК СуммаПросрочки
	               |ИЗ
	               |	ТаблицаПартнеров КАК ТаблицаПартнеров
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаУсловийОтгрузок КАК ТаблицаУсловийОтгрузок
	               |		ПО ТаблицаПартнеров.Партнер = ТаблицаУсловийОтгрузок.Партнер
	               |		ЛЕВОЕ СОЕДИНЕНИЕ СвернутаяТаблицаГрафикаПлатежей КАК СвернутаяТаблицаГрафикаПлатежей
	               |		ПО ТаблицаПартнеров.Партнер = СвернутаяТаблицаГрафикаПлатежей.Партнер
	               |		ЛЕВОЕ СОЕДИНЕНИЕ УсловияОтгрузкиПоУмолчанию КАК УсловияОтгрузкиПоУмолчанию
	               |		ПО (ИСТИНА)";
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаЗагрузкиТаблицы()

&НаСервере
Процедура ОбновитьСписокНаСервере(Партнер)
	ЗагрузитьТаблицуПартнеров();
	Если Партнер <> Неопределено Тогда
		СтруктураПоиска = Новый Структура("Партнер", Партнер);
		РезультатПоиска = ТаблицаОплат.НайтиСтроки(СтруктураПоиска);
		Если РезультатПоиска.Количество() > 0 Тогда
			Элементы.ТаблицаОплат.ТекущаяСтрока = РезультатПоиска[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти