
#Область ПрограммныйИнтерфейс

Функция ПолучитьТекстЗапроса() Экспорт

	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////
		// 0.
		|ВЫБРАТЬ
	    |	КурсыВалют.Период,
	    |	КурсыВалют.Валюта,
	    |	КурсыВалют.Курс / КурсыВалют.Кратность КАК Курс
	    |ПОМЕСТИТЬ КешКурс
	    |ИЗ
	    |	РегистрСведений.КурсыВалют КАК КурсыВалют
	    |
	    |ОБЪЕДИНИТЬ
	    |
	    |ВЫБРАТЬ
	    |	КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ),
	    |	КурсыВалют.Валюта,
	    |	КурсыВалют.Курс / КурсыВалют.Кратность
	    |ИЗ
	    |	РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалют
	    |;
	    |
	    ////////////////////////////////////////////////////////////////////////////////
		// 1.
	    |ВЫБРАТЬ
	    |	ОборотыКурса.Валюта,
	    |	ОборотыКурса.Курс,
	    |	ОборотыКурса.Период КАК НачалоПериода,
	    |	МИНИМУМ(ОборотыКурсаКопия.Период) КАК КонецПериода
	    |ПОМЕСТИТЬ КурсыПоДатам
	    |ИЗ
	    |	КешКурс КАК ОборотыКурса
		|
	    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешКурс КАК ОборотыКурсаКопия
	    |ПО ОборотыКурса.Валюта = ОборотыКурсаКопия.Валюта
	    |И 	ОборотыКурса.Период < ОборотыКурсаКопия.Период
	    |
	    |СГРУППИРОВАТЬ ПО
	    |	ОборотыКурса.Период,
	    |	ОборотыКурса.Валюта,
	    |	ОборотыКурса.Курс
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОборотыКурса.Период,
		|	ОборотыКурса.Валюта	
	    |;
	    ////////////////////////////////////////////////////////////////////////////////
	    |УНИЧТОЖИТЬ КешКурс;
	    ////////////////////////////////////////////////////////////////////////////////
		// 3.
		|ВЫБРАТЬ
		|	Партнеры.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ СписокПодчиненныхПартнеров
		|ИЗ
		|	Справочник.Партнеры КАК Партнеры
		|ГДЕ
		|	Партнеры.ПометкаУдаления = ЛОЖЬ
		|	И Партнеры.Ссылка В ИЕРАРХИИ(&Партнер)

		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		////////////////////////////////////////////////////////////////////////////////
		// 4.      
		|ВЫБРАТЬ
		|	КлючиАналитикиУчетаПоПартнерам.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ Аналитика
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
		|ГДЕ
		|	КлючиАналитикиУчетаПоПартнерам.Партнер В
		|									(ВЫБРАТЬ
		|										СписокПодчиненныхПартнеров.Ссылка
		|									ИЗ
		|										СписокПодчиненныхПартнеров КАК СписокПодчиненныхПартнеров)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		////////////////////////////////////////////////////////////////////////////////
		// 5.
		|УНИЧТОЖИТЬ СписокПодчиненныхПартнеров
		|; 	
	    ////////////////////////////////////////////////////////////////////////////////
		// 6. 
	    |ВЫБРАТЬ
	    |	РасчетыСКлиентами.СуммаОстаток 	КАК СуммаКонечныйДолг,
	    |	РасчетыСКлиентами.Валюта 		КАК Валюта
	    |ПОМЕСТИТЬ КешСуммаКонечныйДолг
	    |ИЗ
	    |	РегистрНакопления.РасчетыСКлиентами.Остатки(
	    |			&ГраницаДатаКон,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)
		|			И Валюта <> &ВалютаРеглУчета
		|			) КАК РасчетыСКлиентами
	    |
	    |ОБЪЕДИНИТЬ ВСЕ
	    |
	    |ВЫБРАТЬ
	    |	РасчетыСПоставщиками.СуммаОстаток,
	    |	РасчетыСПоставщиками.Валюта
	    |ИЗ
	    |	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	    |			&ГраницаДатаКон,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)
		|			И Валюта <> &ВалютаРеглУчета) КАК РасчетыСПоставщиками
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
	    |;
		////////////////////////////////////////////////////////////////////////////////
		// 7. 
		|ВЫБРАТЬ
	    |	КешСуммаКонечныйДолг.Валюта,
	    |	СУММА(ЕСТЬNULL(КешСуммаКонечныйДолг.СуммаКонечныйДолг, 0)) КАК СуммаКонечныйДолг
	    |ПОМЕСТИТЬ КешСуммаКонечногоДолга
	    |ИЗ
	    |	КешСуммаКонечныйДолг КАК КешСуммаКонечныйДолг
	    |
	    |СГРУППИРОВАТЬ ПО
	    |	КешСуммаКонечныйДолг.Валюта
	    |;
		////////////////////////////////////////////////////////////////////////////////
		// 8. 
	    |ВЫБРАТЬ
	    |	IsNull(СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(КешСуммаКонечныйДолг.СуммаКонечныйДолг, 0) * ЕСТЬNULL(КурсыПоДатам.Курс, 0) / ЕСТЬNULL(КурсыУпрУЧ.Курс, 1) КАК ЧИСЛО(15, 2))), 0) КАК СуммаКонечныйДолг
	    |ИЗ
	    |	КешСуммаКонечногоДолга КАК КешСуммаКонечныйДолг
		|
	    |ЛЕВОЕ СОЕДИНЕНИЕ КурсыПоДатам КАК КурсыПоДатам
	    |ПО КешСуммаКонечныйДолг.Валюта = КурсыПоДатам.Валюта
	    |И 	(&ТекущаяДата 	>= КурсыПоДатам.НачалоПериода)
	    |И 	(&ТекущаяДата 	< КурсыПоДатам.КонецПериода)
	    |		
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыПоДатам КАК КурсыУпрУЧ
	    |ПО (&ВалютаУпрУчета = КурсыУпрУЧ.Валюта)
	    |И 	(&ТекущаяДата 	>= КурсыУпрУЧ.НачалоПериода)
	    |И 	(&ТекущаяДата 	< КурсыУпрУЧ.КонецПериода)
	    |;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешСуммаКонечныйДолг;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешСуммаКонечногоДолга;
	    ////////////////////////////////////////////////////////////////////////////////
		// 11.
	    |ВЫБРАТЬ
	    |	РасчетыСКлиентами.СуммаОстаток 	КАК СуммаКонечныйДолгГРН,
	    |	РасчетыСКлиентами.Валюта 		КАК Валюта
	    |ПОМЕСТИТЬ КешСуммаКонечныйДолгГРН
	    |ИЗ
	    |	РегистрНакопления.РасчетыСКлиентами.Остатки(
	    |			&ГраницаДатаКон,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)
		|			И Валюта = &ВалютаРеглУчета) КАК РасчетыСКлиентами
		|
		|ОБЪЕДИНИТЬ ВСЕ
	    |
	    |ВЫБРАТЬ
	    |	РасчетыСПоставщиками.СуммаОстаток,
	    |	РасчетыСПоставщиками.Валюта
	    |ИЗ
	    |	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	    |			&ГраницаДатаКон,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)
		|			И Валюта = &ВалютаРеглУчета) КАК РасчетыСПоставщиками
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
	   	////////////////////////////////////////////////////////////////////////////////
		// 12. 
		|ВЫБРАТЬ
	    |	КешСуммаКонечныйДолг.Валюта,
	    |	СУММА(ЕСТЬNULL(КешСуммаКонечныйДолг.СуммаКонечныйДолгГРН, 0)) КАК СуммаКонечныйДолгГРН
	    |ПОМЕСТИТЬ КешСуммаКонечногоДолгаГРН
	    |ИЗ
	    |	КешСуммаКонечныйДолгГРН КАК КешСуммаКонечныйДолг
	    |
	    |СГРУППИРОВАТЬ ПО
	    |	КешСуммаКонечныйДолг.Валюта
	    |;	   	
		////////////////////////////////////////////////////////////////////////////////
		// 13. 
	    |ВЫБРАТЬ
	    |	IsNull(СУММА(ЕСТЬNULL(КешСуммаКонечныйДолг.СуммаКонечныйДолгГРН, 0)), 0) КАК СуммаКонечныйДолгГРН
	    |ИЗ
	    |	КешСуммаКонечногоДолгаГРН КАК КешСуммаКонечныйДолг
	    |;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешСуммаКонечныйДолгГРН;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешСуммаКонечногоДолгаГРН;
		////////////////////////////////////////////////////////////////////////////////
		// 16.  
	    |ВЫБРАТЬ
	    |	ЕСТЬNULL(РасчетыСКлиентами.СуммаОстаток, 0) КАК СуммаНачальныйДолг,
	    |	РасчетыСКлиентами.Валюта КАК Валюта
	    |ПОМЕСТИТЬ КешСуммаНачальныйДолг
	    |ИЗ
	    |	РегистрНакопления.РасчетыСКлиентами.Остатки(
	    |			&ГраницаДатаНачИскл,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)
		|			И Валюта <> &ВалютаРеглУчета) КАК РасчетыСКлиентами
	    |
	    |ОБЪЕДИНИТЬ ВСЕ
	    |
	    |ВЫБРАТЬ
	    |	ЕСТЬNULL(РасчетыСПоставщиками.СуммаОстаток, 0),
	    |	РасчетыСПоставщиками.Валюта
	    |ИЗ
	    |	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	    |			&ГраницаДатаНачИскл,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)
		|			И Валюта <> &ВалютаРеглУчета) КАК РасчетыСПоставщиками
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
	    |;
		////////////////////////////////////////////////////////////////////////////////
		// 17.
	    |ВЫБРАТЬ
	    |	КешСуммаНачальныйДолг.Валюта,
	    |	СУММА(ЕСТЬNULL(КешСуммаНачальныйДолг.СуммаНачальныйДолг, 0)) КАК СуммаНачальныйДолг
	    |ПОМЕСТИТЬ КешТаблицаСуммаНачальныйДолга
	    |ИЗ
	    |	КешСуммаНачальныйДолг КАК КешСуммаНачальныйДолг
	    |
	    |СГРУППИРОВАТЬ ПО
	    |	КешСуммаНачальныйДолг.Валюта
	    |;
		////////////////////////////////////////////////////////////////////////////////
		// 18. 
		|ВЫБРАТЬ
	    |	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(КешСуммаНачальныйДолг.СуммаНачальныйДолг, 0) * ЕСТЬNULL(КурсыПоДатам.Курс, 0) / ЕСТЬNULL(КурсыУпрУЧ.Курс, 1) КАК ЧИСЛО(15, 2))), 0) КАК СуммаНачальныйДолг
	    |ИЗ
	    |	КешТаблицаСуммаНачальныйДолга КАК КешСуммаНачальныйДолг
		|
	    |ЛЕВОЕ СОЕДИНЕНИЕ КурсыПоДатам КАК КурсыПоДатам
	    |ПО КешСуммаНачальныйДолг.Валюта = КурсыПоДатам.Валюта
	    |И (&ТекущаяДата >= КурсыПоДатам.НачалоПериода)
	    |И (&ТекущаяДата < КурсыПоДатам.КонецПериода)
	    |
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыПоДатам КАК КурсыУпрУЧ
	    |ПО (&ВалютаУпрУчета = КурсыУпрУЧ.Валюта)
	    |И (&ТекущаяДата >= КурсыУпрУЧ.НачалоПериода)
	    |И (&ТекущаяДата < КурсыУпрУЧ.КонецПериода)
	    |;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешСуммаНачальныйДолг;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешТаблицаСуммаНачальныйДолга;
		////////////////////////////////////////////////////////////////////////////////
		// 21. 
	    |ВЫБРАТЬ
	    |	ЕСТЬNULL(РасчетыСКлиентами.СуммаОстаток, 0) КАК СуммаНачальныйДолгГРН,
	    |	РасчетыСКлиентами.Валюта КАК Валюта
	    |ПОМЕСТИТЬ КешСуммаНачальныйДолгГРН
	    |ИЗ
	    |	РегистрНакопления.РасчетыСКлиентами.Остатки(
	    |			&ГраницаДатаНачИскл,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)
		|			И Валюта = &ВалютаРеглУчета) КАК РасчетыСКлиентами
		|
		|ОБЪЕДИНИТЬ ВСЕ
	    |
	    |ВЫБРАТЬ
	    |	ЕСТЬNULL(РасчетыСПоставщиками.СуммаОстаток, 0),
	    |	РасчетыСПоставщиками.Валюта
	    |ИЗ
	    |	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	    |			&ГраницаДатаНачИскл,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)
		|			И Валюта = &ВалютаРеглУчета) КАК РасчетыСПоставщиками		
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
	    ////////////////////////////////////////////////////////////////////////////////
		// 22. 
	    |ВЫБРАТЬ
	    |	КешСуммаНачальныйДолг.Валюта,
	    |	СУММА(ЕСТЬNULL(КешСуммаНачальныйДолг.СуммаНачальныйДолгГРН, 0)) КАК СуммаНачальныйДолгГРН
	    |ПОМЕСТИТЬ КешТаблицаСуммаНачальныйДолгаГРН
	    |ИЗ
	    |	КешСуммаНачальныйДолгГРН КАК КешСуммаНачальныйДолг
	    |
	    |СГРУППИРОВАТЬ ПО
	    |	КешСуммаНачальныйДолг.Валюта
	    |;
	    ////////////////////////////////////////////////////////////////////////////////
		// 23. 
	    |ВЫБРАТЬ
	    |	ЕСТЬNULL(СУММА(ЕСТЬNULL(КешСуммаНачальныйДолг.СуммаНачальныйДолгГРН, 0)), 0) КАК СуммаНачальныйДолгГРН
	    |ИЗ
	    |	КешТаблицаСуммаНачальныйДолгаГРН КАК КешСуммаНачальныйДолг
		|
	    |;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешСуммаНачальныйДолгГРН;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешТаблицаСуммаНачальныйДолгаГРН; 
		////////////////////////////////////////////////////////////////////////////////
		// 25. 
		|ВЫБРАТЬ
	    |	СУММА(Выручка.СуммаВыручкиОборот) КАК Пеня
	    |ПОМЕСТИТЬ кеш
	    |ИЗ
	    |	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&ГраницаДатаНач, 
		|														&ГраницаДатаКон, 
		|														, 
		|														АналитикаУчетаНоменклатуры.Номенклатура = &Номенклатура
		|													  И ЗаказКлиента.Договор <> &ДоговорГривна) КАК Выручка
	    |		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитика КАК Аналитика
	    |ПО (Аналитика.Ссылка = Выручка.АналитикаУчетаПоПартнерам)
	    |;
		////////////////////////////////////////////////////////////////////////////////
		// 27.  
		|ВЫБРАТЬ
		|	ВыручкаИСебестоимостьПродажОбороты.СуммаВыручкиОборот - ЕСТЬNULL(ВлПеня.Пеня, 0) КАК ОборотПоКонтрагенту
	    |ИЗ
	    |	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(
	    |			&ГраницаДатаНач,
	    |			&ГраницаДатаКон,
	    |			,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)
		|			И ЗаказКлиента.Договор <> &ДоговорГривна) КАК ВыручкаИСебестоимостьПродажОбороты
	    |		
		|ЛЕВОЕ СОЕДИНЕНИЕ кеш КАК ВлПеня
	    |ПО (ИСТИНА)
	    |;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Кеш;
		////////////////////////////////////////////////////////////////////////////////
		// 29. 
		|ВЫБРАТЬ
	    |	СУММА(Выручка.СуммаВыручкиРеглОборот) КАК ПеняГРН
	    |ПОМЕСТИТЬ кешГРН
	    |ИЗ
	    |	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&ГраницаДатаНач, 
		|														&ГраницаДатаКон, 
		|														, 
		|														АналитикаУчетаНоменклатуры.Номенклатура = &Номенклатура
		|														И ЗаказКлиента.Договор = &ДоговорГривна) КАК Выручка
	    |		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитика КАК Аналитика
	    |ПО (Аналитика.Ссылка = Выручка.АналитикаУчетаПоПартнерам)
	    |;
		////////////////////////////////////////////////////////////////////////////////
		// 30.  
		|ВЫБРАТЬ
		|	ВыручкаИСебестоимостьПродажОбороты.СуммаВыручкиРеглОборот - ЕСТЬNULL(ВлПеня.ПеняГРН, 0) КАК ОборотПоКонтрагентуГРН
	    |ИЗ
	    |	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(
	    |			&ГраницаДатаНач,
	    |			&ГраницаДатаКон,
	    |			,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)
		|			И ЗаказКлиента.Договор = &ДоговорГривна) КАК ВыручкаИСебестоимостьПродажОбороты
	    |		
		|ЛЕВОЕ СОЕДИНЕНИЕ кешГРН КАК ВлПеня
	    |ПО (ИСТИНА)
	    |;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ кешГРН;
		////////////////////////////////////////////////////////////////////////////////
		// 32. 
	    |ВЫБРАТЬ
	    |	0 КАК ВидДвижения,
	    |	РасчетыСКлиентами.Период КАК ДокументДвиженияДата,
	    |	РасчетыСКлиентами.Регистратор КАК ДокументДвижения,
		|	ВЫБОР
		|		КОГДА РасчетыСКлиентами.Валюта = &ВалютаРеглУчета 
		|		ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ГривневаяОперация,
	    |	ТИПЗНАЧЕНИЯ(РасчетыСКлиентами.Регистратор) КАК ДокументДвиженияТип,
	    |	РасчетыСКлиентами.Регистратор.Номер КАК ДокументДвиженияНомер,
	    |	ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор.Комментарий КАК СТРОКА(100)) КАК ДокументДвиженияКомментарий,
	    |	ВЫБОР
	    |		КОГДА ЕСТЬNULL(РасчетыСКлиентами.Регистратор.Менеджер, 0) <> 0
	    |			ТОГДА РасчетыСКлиентами.Регистратор.Менеджер
	    |		КОГДА ЕСТЬNULL(РасчетыСКлиентами.Регистратор.Кассир, 0) <> 0
	    |			ТОГДА РасчетыСКлиентами.Регистратор.Кассир
	    |		КОГДА ЕСТЬNULL(РасчетыСКлиентами.Регистратор.Ответственный, 0) <> 0
	    |			ТОГДА РасчетыСКлиентами.Регистратор.Ответственный
	    |	КОНЕЦ КАК ДокументДвиженияОтветственный,
	    |	РасчетыСКлиентами.Регистратор.Валюта КАК ВалютаДокумента,
	    |	РасчетыСКлиентами.Валюта КАК ВалютаВзаиморасчетов,
	    |	РасчетыСКлиентами.СуммаПриход,
	    |	РасчетыСКлиентами.СуммаРасход,
	    |	РасчетыСКлиентами.СуммаКонечныйОстаток,
	    |	ВЫБОР
	    |		КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.АктВыполненныхРабот
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор КАК Документ.АктВыполненныхРабот).ДатаПлатежа
	    |		КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.ВыкупВозвратнойТарыКлиентом
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор КАК Документ.ВыкупВозвратнойТарыКлиентом).ДатаПлатежа
	    |		КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор КАК Документ.КорректировкаРеализации).ДатаПлатежа
	    |		КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.ОтчетКомиссионера
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор КАК Документ.ОтчетКомиссионера).ДатаПлатежа
	    |		КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.ОтчетКомиссионераОСписании
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор КАК Документ.ОтчетКомиссионераОСписании).ДатаПлатежа
	    |		КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.ОтчетКомитенту
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор КАК Документ.ОтчетКомитенту).ДатаПлатежа
	    |		КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.ОтчетПоКомиссииМеждуОрганизациями
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор КАК Документ.ОтчетПоКомиссииМеждуОрганизациями).ДатаПлатежа
	    |		КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор КАК Документ.ПередачаТоваровМеждуОрганизациями).ДатаПлатежа
	    |		КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор КАК Документ.РеализацияТоваровУслуг).ДатаПлатежа
	    |		КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.РеализацияУслугПрочихАктивов
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор КАК Документ.РеализацияУслугПрочихАктивов).ДатаПлатежа
	    |	КОНЕЦ КАК ДатаПлатежа
	    |ПОМЕСТИТЬ КешДокументыРасчетыСПартнером
	    |ИЗ
	    |	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(
	    |			&ГраницаДатаНач,
	    |			&ГраницаДатаКон,
	    |			Регистратор,
	    |			,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)) КАК РасчетыСКлиентами
	    |ГДЕ
	    |	РасчетыСКлиентами.Регистратор <> НЕОПРЕДЕЛЕНО
	    |
	    |ОБЪЕДИНИТЬ ВСЕ
	    |
	    |ВЫБРАТЬ
	    |	1,
	    |	РасчетыСПоставщиками.Период,
	    |	РасчетыСПоставщиками.Регистратор,
		|	ВЫБОР
		|		КОГДА РасчетыСПоставщиками.Валюта = &ВалютаРеглУчета 
		|		ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ,
	    |	ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.Регистратор),
	    |	РасчетыСПоставщиками.Регистратор.Номер,
	    |	ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор.Комментарий КАК СТРОКА(100)),
	    |	ВЫБОР
	    |		КОГДА ЕСТЬNULL(РасчетыСПоставщиками.Регистратор.Менеджер, 0) <> 0
	    |			ТОГДА РасчетыСПоставщиками.Регистратор.Менеджер
	    |		КОГДА ЕСТЬNULL(РасчетыСПоставщиками.Регистратор.Кассир, 0) <> 0
	    |			ТОГДА РасчетыСПоставщиками.Регистратор.Кассир
	    |		КОГДА ЕСТЬNULL(РасчетыСПоставщиками.Регистратор.Ответственный, 0) <> 0
	    |			ТОГДА РасчетыСПоставщиками.Регистратор.Ответственный
	    |	КОНЕЦ,
	    |	РасчетыСПоставщиками.Регистратор.Валюта,
	    |	РасчетыСПоставщиками.Валюта,
	    |	РасчетыСПоставщиками.СуммаПриход,
	    |	РасчетыСПоставщиками.СуммаРасход,
	    |	РасчетыСПоставщиками.СуммаКонечныйОстаток,
	    |	ВЫБОР
	    |		КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.КорректировкаПоступления
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.КорректировкаПоступления).ДатаПлатежа
	    |		КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ОтчетКомиссионера
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.ОтчетКомиссионера).ДатаПлатежа
	    |		КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ОтчетКомитенту
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.ОтчетКомитенту).ДатаПлатежа
	    |		КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ОтчетКомитентуОСписании
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.ОтчетКомитентуОСписании).ДатаПлатежа
	    |		КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ОтчетПоКомиссииМеждуОрганизациями
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.ОтчетПоКомиссииМеждуОрганизациями).ДатаПлатежа
	    |		КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.ПередачаТоваровМеждуОрганизациями).ДатаПлатежа
	    |		КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.ПоступлениеТоваровУслуг).ДатаПлатежа
	    |		КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ПоступлениеУслугПрочихАктивов
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.ПоступлениеУслугПрочихАктивов).ДатаПлатежа
	    |		КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	    |			ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.ТаможеннаяДекларацияИмпорт).ДатаПлатежа
	    |	КОНЕЦ
	    |ИЗ
	    |	РегистрНакопления.РасчетыСПоставщиками.ОстаткиИОбороты(
	    |			&ГраницаДатаНач,
	    |			&ГраницаДатаКон,
	    |			Регистратор,
	    |			,
	    |			АналитикаУчетаПоПартнерам В
	    |				(ВЫБРАТЬ
	    |					Аналитика.Ссылка
	    |				ИЗ
	    |					Аналитика)) КАК РасчетыСПоставщиками
	    |ГДЕ
	    |	РасчетыСПоставщиками.Регистратор <> НЕОПРЕДЕЛЕНО
	    |;
	    ////////////////////////////////////////////////////////////////////////////////
		// 33. 
	    |ВЫБРАТЬ
	    |	РасчетыСПартнером.ВидДвижения,
	    |	РасчетыСПартнером.ДокументДвижения,
		|	IsNull(РасчетыСПартнером.ГривневаяОперация, 0) КАК ГривневаяОперация, 
	    |	РасчетыСПартнером.ДокументДвиженияТип,
	    |	РасчетыСПартнером.ДокументДвиженияДата,
	    |	РасчетыСПартнером.ДокументДвиженияНомер,
	    |	РасчетыСПартнером.ДокументДвиженияКомментарий,
	    |	РасчетыСПартнером.ДокументДвиженияОтветственный,
	    |	РасчетыСПартнером.ДатаПлатежа,
	    |	РасчетыСПартнером.ВалютаДокумента,
	    |	РасчетыСПартнером.ВалютаВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА IsNull(РасчетыСПартнером.ГривневаяОперация, 0) = 1
		|		ТОГДА 0
	    |		ИНАЧЕ РасчетыСПартнером.СуммаРасход 
		|	КОНЕЦ КАК СуммаРасход,
	    |	ВЫБОР
		|		КОГДА IsNull(РасчетыСПартнером.ГривневаяОперация, 0) = 1
		|		ТОГДА 0
	    |		ИНАЧЕ РасчетыСПартнером.СуммаПриход 
		|	КОНЕЦ КАК СуммаПриход,
	    |	ВЫБОР
		|		КОГДА IsNull(РасчетыСПартнером.ГривневаяОперация, 0) = 1
		|		ТОГДА 0
	    |		ИНАЧЕ РасчетыСПартнером.СуммаКонечныйОстаток
		|	КОНЕЦ КАК СуммаКонечныйОстаток,
		|	ВЫБОР
		|		КОГДА IsNull(РасчетыСПартнером.ГривневаяОперация, 0) = 0
		|		ТОГДА 0
	    |		ИНАЧЕ РасчетыСПартнером.СуммаРасход 
		|	КОНЕЦ КАК СуммаРасходГ,
	    |	ВЫБОР
		|		КОГДА IsNull(РасчетыСПартнером.ГривневаяОперация, 0) = 0
		|		ТОГДА 0
	    |		ИНАЧЕ РасчетыСПартнером.СуммаПриход 
		|	КОНЕЦ КАК СуммаПриходГ,
	    |	ВЫБОР
		|		КОГДА IsNull(РасчетыСПартнером.ГривневаяОперация, 0) = 0
		|		ТОГДА 0
	    |		ИНАЧЕ РасчетыСПартнером.СуммаКонечныйОстаток
		|	КОНЕЦ КАК СуммаКонечныйОстатокГ,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РасчетыСПартнером.ГривневаяОперация, 0) = 1
		|		ТОГДА ВалютаУпрУч_844.Курс
		|		ИНАЧЕ ВалютаУпрУч_841.Курс
		|	КОНЕЦ КАК КурсДокумента
		|ИЗ
	    |	КешДокументыРасчетыСПартнером КАК РасчетыСПартнером
	    |		
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыПоДатам КАК ВалютаУпрУч_841
		|ПО (ВалютаУпрУч_841.Валюта = &Валюта_841)
		|И 	РасчетыСПартнером.ДокументДвиженияДата > ВалютаУпрУч_841.НачалоПериода
		|И 	РасчетыСПартнером.ДокументДвиженияДата <= ВалютаУпрУч_841.КонецПериода
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыПоДатам КАК ВалютаУпрУч_844
		|ПО (ВалютаУпрУч_844.Валюта = &Валюта_844)
		|И 	РасчетыСПартнером.ДокументДвиженияДата > ВалютаУпрУч_844.НачалоПериода
		|И 	РасчетыСПартнером.ДокументДвиженияДата <= ВалютаУпрУч_844.КонецПериода
	    |
	    |УПОРЯДОЧИТЬ ПО
	    |	РасчетыСПартнером.ДокументДвиженияДата";
		
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапроса()

#КонецОбласти

#Область СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительныйОтчет");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Отчет по взаиморасчетам с партнерами (основной)");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", "2.0.18");    
    ПараметрыРегистрации.Вставить("Информация", "Дополнительный отчет 'Отчет по взаиморасчетам с партнерами (основной)'");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "Отчет по взаиморасчетам с партнерами (основной)", "ФормаОтчета", "ОткрытиеФормы", Ложь, "");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

#КонецОбласти