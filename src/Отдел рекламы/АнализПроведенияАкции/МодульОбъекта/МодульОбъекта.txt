////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);	
	
	АнализПроведенияАкции	= ПолучитьНаборДанных();
	ВнешниеНаборыДанных 		= Новый Структура;
    ВнешниеНаборыДанных.Вставить("АнализПроведенияАкции",АнализПроведенияАкции);
	
 	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки(); 

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);

	Если МакетКомпоновки.НаборыДанных.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru= 'Отчет не сформирован. Включите хотя бы одну группировку в ""Элементы оформления и группировки"".'") ;
	КонецЕсли;
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
Функция ПолучитьЗначениеПараметра(ИмяПараметра)

  ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
   Если ПараметрДанных <> Неопределено Тогда
     ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
     Если ПараметрПользовательскойНастройки <> Неопределено Тогда
       Возврат ПараметрПользовательскойНастройки.Значение;
     Иначе
       Возврат ПараметрДанных.Значение;
     КонецЕсли;
   КонецЕсли;

  Возврат Неопределено;

КонецФункции

Функция ПолучитьНаборДанных()
	Запрос 						=	Новый Запрос;
	Запрос.Текст 				=	ПолучитьТекстЗапросаНаборДанных();
	МаркетинговоеМероприятие 	=	ПолучитьЗначениеПараметра("МаркетинговоеМероприятие");
	Склад 						=	ПолучитьЗначениеПараметра("Склад");

		
	Запрос.УстановитьПараметр("Входная", 					Справочники.ВидыЦен.ПолучитьСсылку(Новый УникальныйИдентификатор("8bdf84fb-404c-11e0-9f98-001517115d85")));
	Запрос.УстановитьПараметр("Склад", 						Склад);
	Запрос.УстановитьПараметр("Подразделения", 				Справочники.СтруктураПредприятия.ПолучитьСсылку(Новый УникальныйИдентификатор("7ffeca68-dd7c-11e2-8f22-001e673c80fa")));
	Запрос.УстановитьПараметр("Розничная", 					Справочники.ВидыЦен.ПолучитьСсылку(Новый УникальныйИдентификатор("8bdf84ff-404c-11e0-9f98-001517115d85")));
	Запрос.УстановитьПараметр("ТекущаяДата", 				ТекущаяДата());	
	Запрос.УстановитьПараметр("МаркетинговоеМероприятие", 	МаркетинговоеМероприятие);
	Запрос.УстановитьПараметр("ВалютаУпр", 					Константы.ВалютаУправленческогоУчета.Получить());	
	Запрос.УстановитьПараметр("ВалютаРегл", 				Константы.ВалютаРегламентированногоУчета.Получить());

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ПолучитьНаборДанных()

Функция ПолучитьТекстЗапросаНаборДанных()
ТекстЗапроса = "ВЫБРАТЬ
               |	МаркетинговыеМероприятия.Ссылка КАК МаркетинговыеМероприятия,
               |	МаркетинговыеМероприятия.СегментНоменклатуры КАК НоменклатураАльтернативы,
               |	""Альтернатива"" КАК СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешСегментАльтернатывы
               |ИЗ
               |	Справочник.МаркетинговыеМероприятия КАК МаркетинговыеМероприятия
               |ГДЕ
               |	МаркетинговыеМероприятия.Ссылка = &МаркетинговоеМероприятие
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ РАЗЛИЧНЫЕ
               |	ВЫРАЗИТЬ(ДействиеСкидокНаценок.Регистратор КАК Документ.УстановкаСкидокПоМаркетинговымМероприятиям).МаркетинговоеМероприятие КАК МаркетинговоеМероприятие,
               |	ДокСкидкиНаценки.ДатаНачала,
               |	ДокСкидкиНаценки.ДатаОкончания,
               |	СкидкиНаценкиУсловияПредоставления.УсловиеПредоставления.СегментНоменклатурыОграничения КАК СегментНоменклатуры,
               |	""Акція"" КАК СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешСегментАкции
               |ИЗ
               |	РегистрСведений.ДействиеСкидокНаценок КАК ДействиеСкидокНаценок
               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УстановкаСкидокПоМаркетинговымМероприятиям.СкидкиНаценки КАК ДокСкидкиНаценки
               |		ПО (ДокСкидкиНаценки.Ссылка = (ВЫРАЗИТЬ(ДействиеСкидокНаценок.Регистратор КАК Документ.УстановкаСкидокПоМаркетинговымМероприятиям)))
               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СкидкиНаценки.УсловияПредоставления КАК СкидкиНаценкиУсловияПредоставления
               |		ПО ДействиеСкидокНаценок.СкидкаНаценка = СкидкиНаценкиУсловияПредоставления.Ссылка
               |ГДЕ
               |	ДокСкидкиНаценки.Ссылка.МаркетинговоеМероприятие.Ссылка = &МаркетинговоеМероприятие
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешСегментАкции.МаркетинговоеМероприятие КАК МаркетинговоеМероприятие,
               |	КешСегментАкции.ДатаНачала КАК ДатаНачала,
               |	КешСегментАкции.ДатаОкончания КАК ДатаОкончания,
               |	КешСегментАкции.СегментНоменклатуры КАК СегментНоменклатуры,
               |	КешСегментАкции.СегментНоменклатураВидаАкции КАК СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешМаркетиноговоеМероприятие
               |ИЗ
               |	КешСегментАкции КАК КешСегментАкции
               |
               |ОБЪЕДИНИТЬ ВСЕ
               |
               |ВЫБРАТЬ
               |	КешСегментАльтернатывы.МаркетинговыеМероприятия,
               |	МИНИМУМ(КешСегментАкции.ДатаНачала),
               |	МАКСИМУМ(КешСегментАкции.ДатаОкончания),
               |	КешСегментАльтернатывы.НоменклатураАльтернативы,
               |	КешСегментАльтернатывы.СегментНоменклатураВидаАкции
               |ИЗ
               |	КешСегментАкции КАК КешСегментАкции
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешСегментАльтернатывы КАК КешСегментАльтернатывы
               |		ПО КешСегментАкции.МаркетинговоеМероприятие = КешСегментАльтернатывы.МаркетинговыеМероприятия
               |
               |СГРУППИРОВАТЬ ПО
               |	КешСегментАльтернатывы.МаркетинговыеМероприятия,
               |	КешСегментАльтернатывы.НоменклатураАльтернативы,
               |	КешСегментАльтернатывы.СегментНоменклатураВидаАкции
               |
               |ИНДЕКСИРОВАТЬ ПО
               |	СегментНоменклатуры
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	Мероприятие.МаркетинговоеМероприятие,
               |	МИНИМУМ(Мероприятие.ДатаНачала) КАК ДатаНачала,
               |	КОНЕЦПЕРИОДА(МАКСИМУМ(Мероприятие.ДатаОкончания), ДЕНЬ) КАК ДатаОкончания,
               |	НоменклатураСегмента.Номенклатура,
               |	Мероприятие.СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешНоменклатура
               |ИЗ
               |	КешМаркетиноговоеМероприятие КАК Мероприятие
               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
               |		ПО (НоменклатураСегмента.Сегмент = Мероприятие.СегментНоменклатуры)
               |
               |СГРУППИРОВАТЬ ПО
               |	Мероприятие.МаркетинговоеМероприятие,
               |	НоменклатураСегмента.Номенклатура,
               |	Мероприятие.СегментНоменклатураВидаАкции
               |
               |ИНДЕКСИРОВАТЬ ПО
               |	НоменклатураСегмента.Номенклатура,
               |	ДатаНачала,
               |	ДатаОкончания
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешНоменклатура.Номенклатура,
               |	ЦеныНоменклатуры.Период,
               |	ЦеныНоменклатуры.Валюта,
               |	ЦеныНоменклатуры.ВидЦены,
               |	ЦеныНоменклатуры.Цена
               |ПОМЕСТИТЬ КешЦени
               |ИЗ
               |	КешНоменклатура КАК КешНоменклатура
               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
               |		ПО (ЦеныНоменклатуры.Номенклатура = КешНоменклатура.Номенклатура)
               |			И (ЦеныНоменклатуры.ВидЦены = &Входная
               |				ИЛИ ЦеныНоменклатуры.ВидЦены = &Розничная)
               |
               |ОБЪЕДИНИТЬ
               |
               |ВЫБРАТЬ
               |	ЦеныНоменклатурыСрезПоследних.Номенклатура,
               |	КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ),
               |	ЦеныНоменклатурыСрезПоследних.Валюта,
               |	ЦеныНоменклатурыСрезПоследних.ВидЦены,
               |	ЦеныНоменклатурыСрезПоследних.Цена
               |ИЗ
               |	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
               |			,
               |			Номенклатура В
               |					(ВЫБРАТЬ
               |						КешНоменклатура.Номенклатура
               |					ИЗ
               |						КешНоменклатура)
               |				И ВидЦены В (&Входная, &Розничная)) КАК ЦеныНоменклатурыСрезПоследних
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	ОборотыЦен.Номенклатура,
               |	ОборотыЦен.ВидЦены,
               |	ОборотыЦен.Валюта,
               |	ОборотыЦен.Период КАК НачалоПериода,
               |	МИНИМУМ(ОборотыЦенКопия.Период) КАК КонецПериода,
               |	ОборотыЦен.Цена
               |ПОМЕСТИТЬ ЦеныПоДатам
               |ИЗ
               |	КешЦени КАК ОборотыЦен
               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешЦени КАК ОборотыЦенКопия
               |		ПО ОборотыЦен.Номенклатура = ОборотыЦенКопия.Номенклатура
               |			И ОборотыЦен.ВидЦены = ОборотыЦенКопия.ВидЦены
               |			И ОборотыЦен.Период < ОборотыЦенКопия.Период
               |
               |СГРУППИРОВАТЬ ПО
               |	ОборотыЦен.Номенклатура,
               |	ОборотыЦен.ВидЦены,
               |	ОборотыЦен.Валюта,
               |	ОборотыЦен.Период,
               |	ОборотыЦен.Цена
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |УНИЧТОЖИТЬ КешЦени
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КурсыВалют.Период,
               |	КурсыВалют.Валюта,
               |	КурсыВалют.Курс / КурсыВалют.Кратность КАК Курс
               |ПОМЕСТИТЬ КешКурс
               |ИЗ
               |	РегистрСведений.КурсыВалют КАК КурсыВалют
               |
               |ОБЪЕДИНИТЬ
               |
               |ВЫБРАТЬ
               |	КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ),
               |	КурсыВалют.Валюта,
               |	КурсыВалют.Курс / КурсыВалют.Кратность
               |ИЗ
               |	РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалют
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	ОборотыКурса.Валюта,
               |	ОборотыКурса.Курс,
               |	ОборотыКурса.Период КАК НачалоПериода,
               |	МИНИМУМ(ОборотыКурсаКопия.Период) КАК КонецПериода
               |ПОМЕСТИТЬ КурсыПоДатам
               |ИЗ
               |	КешКурс КАК ОборотыКурса
               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешКурс КАК ОборотыКурсаКопия
               |		ПО ОборотыКурса.Валюта = ОборотыКурсаКопия.Валюта
               |			И ОборотыКурса.Период < ОборотыКурсаКопия.Период
               |
               |СГРУППИРОВАТЬ ПО
               |	ОборотыКурса.Период,
               |	ОборотыКурса.Валюта,
               |	ОборотыКурса.Курс
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |УНИЧТОЖИТЬ КешКурс
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешНоменклатура.Номенклатура КАК Номенклатура,
               |	ЦеныПоДатам.ВидЦены,
               |	ЦеныПоДатам.Валюта КАК Валюта,
               |	МИНИМУМ(ЦеныПоДатам.Цена) КАК Цена,
               |	КешНоменклатура.ДатаНачала,
               |	КешНоменклатура.ДатаОкончания,
               |	КешНоменклатура.СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешНоменклатуриАкции
               |ИЗ
               |	КешНоменклатура КАК КешНоменклатура
               |		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныПоДатам КАК ЦеныПоДатам
               |		ПО КешНоменклатура.Номенклатура = ЦеныПоДатам.Номенклатура
               |			И (ЦеныПоДатам.НачалоПериода <= КешНоменклатура.ДатаНачала
               |					И КешНоменклатура.ДатаОкончания < ЦеныПоДатам.КонецПериода
               |				ИЛИ ЦеныПоДатам.НачалоПериода <= КешНоменклатура.ДатаНачала
               |					И КешНоменклатура.ДатаНачала < ЦеныПоДатам.КонецПериода
               |				ИЛИ ЦеныПоДатам.НачалоПериода >= КешНоменклатура.ДатаНачала
               |					И ЦеныПоДатам.КонецПериода < КешНоменклатура.ДатаОкончания
               |				ИЛИ ЦеныПоДатам.НачалоПериода >= КешНоменклатура.ДатаНачала
               |					И ЦеныПоДатам.КонецПериода > КешНоменклатура.ДатаОкончания
               |					И ЦеныПоДатам.НачалоПериода <= КешНоменклатура.ДатаОкончания)
               |
               |СГРУППИРОВАТЬ ПО
               |	КешНоменклатура.Номенклатура,
               |	ЦеныПоДатам.ВидЦены,
               |	ЦеныПоДатам.Валюта,
               |	КешНоменклатура.ДатаНачала,
               |	КешНоменклатура.ДатаОкончания,
               |	КешНоменклатура.СегментНоменклатураВидаАкции
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешНоменклатуриАкции.Номенклатура,
               |	КешНоменклатуриАкции.Валюта КАК ВалютаНоменлатури,
               |	КурсыПоДатам.Валюта,
               |	КешНоменклатуриАкции.ВидЦены,
               |	КешНоменклатуриАкции.Цена,
               |	СРЕДНЕЕ(КурсыПоДатам.Курс) КАК Курс,
               |	СРЕДНЕЕ(КурсыПоДатамУпр.Курс) КАК КурсУпр,
               |	СРЕДНЕЕ(КурсыПоДатамРегл.Курс) КАК КурсРегл,
               |	КешНоменклатуриАкции.СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешКурсВалютАкции
               |ИЗ
               |	КешНоменклатуриАкции КАК КешНоменклатуриАкции
               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыПоДатам КАК КурсыПоДатам
               |		ПО КешНоменклатуриАкции.Валюта = КурсыПоДатам.Валюта
               |			И (КурсыПоДатам.НачалоПериода <= КешНоменклатуриАкции.ДатаНачала
               |					И КешНоменклатуриАкции.ДатаОкончания < КурсыПоДатам.КонецПериода
               |				ИЛИ КурсыПоДатам.НачалоПериода <= КешНоменклатуриАкции.ДатаНачала
               |					И КешНоменклатуриАкции.ДатаНачала < КурсыПоДатам.КонецПериода
               |				ИЛИ КурсыПоДатам.НачалоПериода >= КешНоменклатуриАкции.ДатаНачала
               |					И КурсыПоДатам.КонецПериода < КешНоменклатуриАкции.ДатаОкончания
               |				ИЛИ КурсыПоДатам.НачалоПериода >= КешНоменклатуриАкции.ДатаНачала
               |					И КурсыПоДатам.КонецПериода > КешНоменклатуриАкции.ДатаОкончания
               |					И КурсыПоДатам.НачалоПериода <= КешНоменклатуриАкции.ДатаОкончания)
               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыПоДатам КАК КурсыПоДатамУпр
               |		ПО (КурсыПоДатамУпр.Валюта = &ВалютаУпр)
               |			И (КурсыПоДатамУпр.НачалоПериода <= КешНоменклатуриАкции.ДатаНачала
               |					И КешНоменклатуриАкции.ДатаОкончания < КурсыПоДатамУпр.КонецПериода
               |				ИЛИ КурсыПоДатамУпр.НачалоПериода <= КешНоменклатуриАкции.ДатаНачала
               |					И КешНоменклатуриАкции.ДатаНачала < КурсыПоДатамУпр.КонецПериода
               |				ИЛИ КурсыПоДатамУпр.НачалоПериода >= КешНоменклатуриАкции.ДатаНачала
               |					И КурсыПоДатамУпр.КонецПериода < КешНоменклатуриАкции.ДатаОкончания
               |				ИЛИ КурсыПоДатамУпр.НачалоПериода >= КешНоменклатуриАкции.ДатаНачала
               |					И КурсыПоДатамУпр.КонецПериода > КешНоменклатуриАкции.ДатаОкончания
               |					И КурсыПоДатамУпр.НачалоПериода <= КешНоменклатуриАкции.ДатаОкончания)
               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыПоДатам КАК КурсыПоДатамРегл
               |		ПО (КурсыПоДатамРегл.Валюта = &ВалютаРегл)
               |			И (КурсыПоДатамРегл.НачалоПериода <= КешНоменклатуриАкции.ДатаНачала
               |					И КешНоменклатуриАкции.ДатаОкончания < КурсыПоДатамРегл.КонецПериода
               |				ИЛИ КурсыПоДатамРегл.НачалоПериода <= КешНоменклатуриАкции.ДатаНачала
               |					И КешНоменклатуриАкции.ДатаНачала < КурсыПоДатамРегл.КонецПериода
               |				ИЛИ КурсыПоДатамРегл.НачалоПериода >= КешНоменклатуриАкции.ДатаНачала
               |					И КурсыПоДатамРегл.КонецПериода < КешНоменклатуриАкции.ДатаОкончания
               |				ИЛИ КурсыПоДатамРегл.НачалоПериода >= КешНоменклатуриАкции.ДатаНачала
               |					И КурсыПоДатамРегл.КонецПериода > КешНоменклатуриАкции.ДатаОкончания
               |					И КурсыПоДатамРегл.НачалоПериода <= КешНоменклатуриАкции.ДатаОкончания)
               |
               |СГРУППИРОВАТЬ ПО
               |	КешНоменклатуриАкции.Номенклатура,
               |	КешНоменклатуриАкции.Валюта,
               |	КурсыПоДатам.Валюта,
               |	КешНоменклатуриАкции.ВидЦены,
               |	КешНоменклатуриАкции.Цена,
               |	КешНоменклатуриАкции.СегментНоменклатураВидаАкции
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	Вложеный.Номенклатура,
               |	МАКСИМУМ(Вложеный.Курс) КАК Курс,
               |	МИНИМУМ(Вложеный.ЦенаВходная) КАК ЦенаВходная,
               |	МИНИМУМ(Вложеный.ЦенаРозничная) КАК ЦенаРозничная,
               |	МИНИМУМ(Вложеный.ЦенаВходнаяДол) КАК ЦенаВходнаяДол,
               |	Вложеный.СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешЦенаВходнаяДол
               |ИЗ
               |	(ВЫБРАТЬ
               |		КешКурсВалютАкции.Номенклатура КАК Номенклатура,
               |		КешКурсВалютАкции.Курс КАК Курс,
               |		ВЫБОР
               |			КОГДА КешКурсВалютАкции.Валюта = &ВалютаРегл
               |				ТОГДА КешКурсВалютАкции.Цена / КешКурсВалютАкции.КурсУпр
               |			КОГДА КешКурсВалютАкции.Валюта = &ВалютаУпр
               |				ТОГДА КешКурсВалютАкции.Цена
               |			ИНАЧЕ КешКурсВалютАкции.Цена * КешКурсВалютАкции.КурсУпр / КешКурсВалютАкции.Курс
               |		КОНЕЦ КАК ЦенаВходнаяДол,
               |		NULL КАК ЦенаРозничная,
               |		КешКурсВалютАкции.СегментНоменклатураВидаАкции КАК СегментНоменклатураВидаАкции,
               |		ВЫБОР
               |			КОГДА КешКурсВалютАкции.Валюта = &ВалютаРегл
               |				ТОГДА КешКурсВалютАкции.Цена
               |			КОГДА КешКурсВалютАкции.Валюта = &ВалютаУпр
               |				ТОГДА КешКурсВалютАкции.Цена * КешКурсВалютАкции.КурсУпр
               |			ИНАЧЕ КешКурсВалютАкции.Цена * КешКурсВалютАкции.Курс
               |		КОНЕЦ КАК ЦенаВходная
               |	ИЗ
               |		КешКурсВалютАкции КАК КешКурсВалютАкции
               |	ГДЕ
               |		КешКурсВалютАкции.ВидЦены = &Входная
               |	
               |	ОБЪЕДИНИТЬ ВСЕ
               |	
               |	ВЫБРАТЬ
               |		КешКурсВалютАкции.Номенклатура,
               |		КешКурсВалютАкции.Курс,
               |		NULL,
               |		ВЫБОР
               |			КОГДА КешКурсВалютАкции.Валюта = &ВалютаРегл
               |				ТОГДА КешКурсВалютАкции.Цена
               |			КОГДА КешКурсВалютАкции.Валюта = &ВалютаУпр
               |				ТОГДА КешКурсВалютАкции.Цена * КешКурсВалютАкции.КурсУпр
               |			ИНАЧЕ КешКурсВалютАкции.Цена * КешКурсВалютАкции.Курс
               |		КОНЕЦ,
               |		КешКурсВалютАкции.СегментНоменклатураВидаАкции,
               |		NULL
               |	ИЗ
               |		КешКурсВалютАкции КАК КешКурсВалютАкции
               |	ГДЕ
               |		КешКурсВалютАкции.ВидЦены = &Розничная) КАК Вложеный
               |
               |СГРУППИРОВАТЬ ПО
               |	Вложеный.Номенклатура,
               |	Вложеный.СегментНоменклатураВидаАкции
               |
               |ИНДЕКСИРОВАТЬ ПО
               |	Вложеный.Номенклатура
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	СвободныеОстаткиОстаткиИОбороты.Номенклатура КАК Номенклатура,
               |	СвободныеОстаткиОстаткиИОбороты.Склад КАК Склад,
               |	МАКСИМУМ(СвободныеОстаткиОстаткиИОбороты.ПериодДень) КАК МаксимумПериодДень
               |ПОМЕСТИТЬ КешЗалишки
               |ИЗ
               |	КешНоменклатура КАК КешНоменклатура
               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.ОстаткиИОбороты(
               |				,
               |				,
               |				Авто,
               |				,
               |				Номенклатура В
               |						(ВЫБРАТЬ
               |							КешНоменклатура.Номенклатура
               |						ИЗ
               |							КешНоменклатура)
               |					И Склад В (&Склад)) КАК СвободныеОстаткиОстаткиИОбороты
               |		ПО КешНоменклатура.Номенклатура = СвободныеОстаткиОстаткиИОбороты.Номенклатура
               |ГДЕ
               |	СвободныеОстаткиОстаткиИОбороты.ПериодДень <= КешНоменклатура.ДатаНачала
               |
               |СГРУППИРОВАТЬ ПО
               |	СвободныеОстаткиОстаткиИОбороты.Номенклатура,
               |	СвободныеОстаткиОстаткиИОбороты.Склад
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	СвободныеОстаткиОстаткиИОбороты.Номенклатура КАК Номенклатура,
               |	СвободныеОстаткиОстаткиИОбороты.Склад КАК Склад,
               |	МАКСИМУМ(ЕСТЬNULL(СвободныеОстаткиОстаткиИОбороты.ВНаличииКонечныйОстаток, 0)) КАК КоличествоОстатки
               |ПОМЕСТИТЬ КешОстатки
               |ИЗ
               |	КешЗалишки КАК КешЗалишки
               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.ОстаткиИОбороты(
               |				,
               |				,
               |				Авто,
               |				,
               |				Номенклатура В
               |						(ВЫБРАТЬ
               |							КешНоменклатура.Номенклатура
               |						ИЗ
               |							КешНоменклатура)
               |					И Склад В (&Склад)) КАК СвободныеОстаткиОстаткиИОбороты
               |		ПО КешЗалишки.Номенклатура = СвободныеОстаткиОстаткиИОбороты.Номенклатура
               |			И КешЗалишки.Склад = СвободныеОстаткиОстаткиИОбороты.Склад
               |			И КешЗалишки.МаксимумПериодДень = СвободныеОстаткиОстаткиИОбороты.ПериодДень
               |
               |СГРУППИРОВАТЬ ПО
               |	СвободныеОстаткиОстаткиИОбороты.Номенклатура,
               |	СвободныеОстаткиОстаткиИОбороты.Склад
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	РгАналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
               |	РгАналитикаУчетаНоменклатуры.Склад КАК Склад,
               |	ВыручкаИСебестоимостьПродажОбороты.КоличествоОборот,
               |	ВыручкаИСебестоимостьПродажОбороты.Период КАК ПериодДень
               |ПОМЕСТИТЬ КешАналитика
               |ИЗ
               |	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(
               |			,
               |			,
               |			Регистратор,
               |			АналитикаУчетаНоменклатуры.Номенклатура В
               |					(ВЫБРАТЬ
               |						КешНоменклатура.Номенклатура
               |					ИЗ
               |						КешНоменклатура)
               |				И НЕ Подразделение = &Подразделения) КАК ВыручкаИСебестоимостьПродажОбороты
               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК РгАналитикаУчетаНоменклатуры
               |		ПО (РгАналитикаУчетаНоменклатуры.КлючАналитики = ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры)
               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешНоменклатура КАК КешНоменклатура
               |		ПО (КешНоменклатура.Номенклатура = ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура)
               |			И (ВыручкаИСебестоимостьПродажОбороты.Период >= ДОБАВИТЬКДАТЕ(КешНоменклатура.ДатаНачала, ДЕНЬ, -РАЗНОСТЬДАТ(КешНоменклатура.ДатаНачала, КешНоменклатура.ДатаОкончания, ДЕНЬ)))
               |			И ВыручкаИСебестоимостьПродажОбороты.Период <= КешНоменклатура.ДатаОкончания
               |ГДЕ
               |	(ВыручкаИСебестоимостьПродажОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
               |			ИЛИ ВыручкаИСебестоимостьПродажОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента)
               |	И РгАналитикаУчетаНоменклатуры.Склад В(&Склад)
               |
               |ИНДЕКСИРОВАТЬ ПО
               |	РгАналитикаУчетаНоменклатуры.Номенклатура,
               |	ВыручкаИСебестоимостьПродажОбороты.Период
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешНоменклатура.Номенклатура КАК Номенклатура,
               |	КешАналитика.Склад КАК Склад,
               |	СУММА(ЕСТЬNULL(КешАналитика.КоличествоОборот, 0)) КАК КоличествоОборотДоАкции
               |ПОМЕСТИТЬ ПродажиДоАкции
               |ИЗ
               |	КешНоменклатура КАК КешНоменклатура
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешАналитика КАК КешАналитика
               |		ПО КешНоменклатура.Номенклатура = КешАналитика.Номенклатура
               |			И (КешАналитика.ПериодДень < КешНоменклатура.ДатаНачала)
               |
               |СГРУППИРОВАТЬ ПО
               |	КешНоменклатура.Номенклатура,
               |	КешАналитика.Склад
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешНоменклатура.Номенклатура КАК Номенклатура,
               |	КешАналитика.Склад КАК Склад,
               |	СУММА(КешАналитика.КоличествоОборот) КАК ОборотиВоВремяАкции
               |ПОМЕСТИТЬ ПродажиВоВремяАкции
               |ИЗ
               |	КешНоменклатура КАК КешНоменклатура
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешАналитика КАК КешАналитика
               |		ПО КешНоменклатура.Номенклатура = КешАналитика.Номенклатура
               |			И КешНоменклатура.ДатаНачала <= КешАналитика.ПериодДень
               |			И КешНоменклатура.ДатаОкончания >= КешАналитика.ПериодДень
               |
               |СГРУППИРОВАТЬ ПО
               |	КешНоменклатура.Номенклатура,
               |	КешАналитика.Склад
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	ПродажиВоВремяАкции.Номенклатура,
               |	СУММА(ЕСТЬNULL(ПродажиВоВремяАкции.ОборотиВоВремяАкции, 0)) КАК ИтогиОборотиВоВремяАкции
               |ПОМЕСТИТЬ КешНоменклатураКоличествоИтоги1
               |ИЗ
               |	ПродажиВоВремяАкции КАК ПродажиВоВремяАкции
               |
               |СГРУППИРОВАТЬ ПО
               |	ПродажиВоВремяАкции.Номенклатура
               |
               |ИНДЕКСИРОВАТЬ ПО
               |	ПродажиВоВремяАкции.Номенклатура
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	СУММА(ЕСТЬNULL(ПродажиДоАкции.КоличествоОборотДоАкции, 0)) КАК ИтогиКоличествоОборотДоАкции,
               |	ПродажиДоАкции.Номенклатура
               |ПОМЕСТИТЬ КешНоменклатураКоличествоИтоги2
               |ИЗ
               |	ПродажиДоАкции КАК ПродажиДоАкции
               |
               |СГРУППИРОВАТЬ ПО
               |	ПродажиДоАкции.Номенклатура
               |
               |ИНДЕКСИРОВАТЬ ПО
               |	ПродажиДоАкции.Номенклатура
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешНоменклатураКоличествоИтоги1.Номенклатура,
               |	КешНоменклатураКоличествоИтоги1.ИтогиОборотиВоВремяАкции,
               |	КешНоменклатураКоличествоИтоги2.ИтогиКоличествоОборотДоАкции
               |ПОМЕСТИТЬ КешНоменклатураКоличествоИтоги
               |ИЗ
               |	КешНоменклатураКоличествоИтоги1 КАК КешНоменклатураКоличествоИтоги1
               |		ПОЛНОЕ СОЕДИНЕНИЕ КешНоменклатураКоличествоИтоги2 КАК КешНоменклатураКоличествоИтоги2
               |		ПО КешНоменклатураКоличествоИтоги1.Номенклатура = КешНоменклатураКоличествоИтоги2.Номенклатура
               |
               |ИНДЕКСИРОВАТЬ ПО
               |	КешНоменклатураКоличествоИтоги1.Номенклатура
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешНоменклатураКоличествоИтоги.Номенклатура,
               |	КешЦенаВходнаяДол.ЦенаРозничная * КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции КАК СумаПродажТоваруЧтоПродвигался,
               |	КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции,
               |	КешНоменклатураКоличествоИтоги.ИтогиКоличествоОборотДоАкции,
               |	(КешЦенаВходнаяДол.ЦенаРозничная - КешЦенаВходнаяДол.ЦенаВходнаяДол) * КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции КАК СуммаВаловийПрибуток
               |ПОМЕСТИТЬ КЕшДополнительниеКолонки
               |ИЗ
               |	КешЦенаВходнаяДол КАК КешЦенаВходнаяДол
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешНоменклатураКоличествоИтоги КАК КешНоменклатураКоличествоИтоги
               |		ПО КешЦенаВходнаяДол.Номенклатура = КешНоменклатураКоличествоИтоги.Номенклатура
               |
               |ИНДЕКСИРОВАТЬ ПО
               |	КешНоменклатураКоличествоИтоги.Номенклатура
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	Вложенный.СумаПродажТоваруЧтоПродвигался,
               |	Вложенный.ИтогиОборотиВоВремяАкции,
               |	Вложенный.ИтогиКоличествоОборотДоАкции,
               |	(Вложенный.ИтогиОборотиВоВремяАкции - Вложенный.ИтогиКоличествоОборотДоАкции) / Вложенный.ИтогиКоличествоОборотДоАкции КАК Прирост
               |ПОМЕСТИТЬ КЕшДопКолИтоги
               |ИЗ
               |	(ВЫБРАТЬ
               |		СУММА(КешЦенаВходнаяДол.ЦенаРозничная * КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции) КАК СумаПродажТоваруЧтоПродвигался,
               |		СУММА(КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции) КАК ИтогиОборотиВоВремяАкции,
               |		СУММА(ВЫБОР
               |				КОГДА КешНоменклатураКоличествоИтоги.ИтогиКоличествоОборотДоАкции = 0
               |					ТОГДА 1
               |				ИНАЧЕ КешНоменклатураКоличествоИтоги.ИтогиКоличествоОборотДоАкции
               |			КОНЕЦ) КАК ИтогиКоличествоОборотДоАкции
               |	ИЗ
               |		КешЦенаВходнаяДол КАК КешЦенаВходнаяДол
               |			ЛЕВОЕ СОЕДИНЕНИЕ КешНоменклатураКоличествоИтоги КАК КешНоменклатураКоличествоИтоги
               |			ПО КешЦенаВходнаяДол.Номенклатура = КешНоменклатураКоличествоИтоги.Номенклатура) КАК Вложенный
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешНоменклатураКоличествоИтоги.Номенклатура,
               |	КешЦенаВходнаяДол.ЦенаРозничная * КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции КАК СумаПродажТоваруЧтоПродвигался,
               |	(КешЦенаВходнаяДол.ЦенаРозничная - КешЦенаВходнаяДол.ЦенаВходнаяДол) * КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции КАК ПрибыльТоваруЧтоПродвигался,
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешИтогиАкционныхТоваров
               |ИЗ
               |	КешЦенаВходнаяДол КАК КешЦенаВходнаяДол
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешНоменклатураКоличествоИтоги КАК КешНоменклатураКоличествоИтоги
               |		ПО КешЦенаВходнаяДол.Номенклатура = КешНоменклатураКоличествоИтоги.Номенклатура
               |ГДЕ
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции = ""Акція""
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	СУММА(КешЦенаВходнаяДол.ЦенаРозничная * КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции) КАК СумаПродажТоваруЧтоПродвигался,
               |	СУММА((КешЦенаВходнаяДол.ЦенаРозничная - КешЦенаВходнаяДол.ЦенаВходнаяДол) * КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции) КАК ПрибыльТоваруЧтоПродвигался,
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешОбщиеИтогиАкционныхТоваров
               |ИЗ
               |	КешЦенаВходнаяДол КАК КешЦенаВходнаяДол
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешНоменклатураКоличествоИтоги КАК КешНоменклатураКоличествоИтоги
               |		ПО КешЦенаВходнаяДол.Номенклатура = КешНоменклатураКоличествоИтоги.Номенклатура
               |ГДЕ
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции = ""Акція""
               |
               |СГРУППИРОВАТЬ ПО
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешНоменклатураКоличествоИтоги.Номенклатура,
               |	КешЦенаВходнаяДол.ЦенаРозничная * КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции КАК СумаПродажТоваруЧтоПродвигался,
               |	(КешЦенаВходнаяДол.ЦенаРозничная - КешЦенаВходнаяДол.ЦенаВходнаяДол) * КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции КАК ПрибыльТоваруЧтоПродвигался,
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешИтогиАльтернативныхТоваров
               |ИЗ
               |	КешЦенаВходнаяДол КАК КешЦенаВходнаяДол
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешНоменклатураКоличествоИтоги КАК КешНоменклатураКоличествоИтоги
               |		ПО КешЦенаВходнаяДол.Номенклатура = КешНоменклатураКоличествоИтоги.Номенклатура
               |ГДЕ
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции = ""Альтернатива""
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	СУММА(КешЦенаВходнаяДол.ЦенаРозничная * КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции) КАК СумаПродажТоваруЧтоПродвигался,
               |	СУММА((КешЦенаВходнаяДол.ЦенаРозничная - КешЦенаВходнаяДол.ЦенаВходнаяДол) * КешНоменклатураКоличествоИтоги.ИтогиОборотиВоВремяАкции) КАК ПрибыльТоваруЧтоПродвигался,
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешОбщиеИтогиАльтернативныхТоваров
               |ИЗ
               |	КешЦенаВходнаяДол КАК КешЦенаВходнаяДол
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешНоменклатураКоличествоИтоги КАК КешНоменклатураКоличествоИтоги
               |		ПО КешЦенаВходнаяДол.Номенклатура = КешНоменклатураКоличествоИтоги.Номенклатура
               |ГДЕ
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции = ""Альтернатива""
               |
               |СГРУППИРОВАТЬ ПО
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешНоменклатура.Номенклатура,
               |	КешНоменклатура.ДатаНачала,
               |	Склады.Склад КАК Склад
               |ПОМЕСТИТЬ Склади
               |ИЗ
               |	КешНоменклатура КАК КешНоменклатура
               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Модуль_ДоступныеСклады КАК Склады
               |		ПО (Склады.Основной = ИСТИНА)
               |			И (Склады.Склад В (&Склад))
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	СУММА(ВЛОЖЕННЫЙ.СуммаВыручкиОборот) КАК СуммаВыручкиОборот
               |ПОМЕСТИТЬ КешДополнительниеКолонкиВыручкаОборотВдоларах
               |ИЗ
               |	(ВЫБРАТЬ РАЗЛИЧНЫЕ
               |		ВыручкаИСебестоимостьПродажОбороты.СуммаВыручкиОборот КАК СуммаВыручкиОборот,
               |		ВыручкаИСебестоимостьПродажОбороты.Регистратор КАК Регистратор
               |	ИЗ
               |		КешНоменклатура КАК КешНоменклатура
               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(
               |					,
               |					,
               |					Авто,
               |					АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
               |						И АналитикаУчетаНоменклатуры.Склад В (&Склад)
               |						И АналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
               |						И НЕ Подразделение = &Подразделения) КАК ВыручкаИСебестоимостьПродажОбороты
               |			ПО КешНоменклатура.ДатаОкончания >= ВыручкаИСебестоимостьПродажОбороты.ПериодДень
               |				И КешНоменклатура.ДатаНачала <= ВыручкаИСебестоимостьПродажОбороты.ПериодДень
               |	ГДЕ
               |		(ВыручкаИСебестоимостьПродажОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
               |				ИЛИ ВыручкаИСебестоимостьПродажОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента)) КАК ВЛОЖЕННЫЙ
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешДополнительниеКолонкиВыручкаОборотВдоларах.СуммаВыручкиОборот,
               |	КешДополнительниеКолонкиВыручкаОборотВдоларах.СуммаВыручкиОборот * КешКурсВалютАкции.Курс КАК ЗагальнаСуммаПродаж
               |ПОМЕСТИТЬ КешЗагальнаСуммаПродаж
               |ИЗ
               |	КешДополнительниеКолонкиВыручкаОборотВдоларах КАК КешДополнительниеКолонкиВыручкаОборотВдоларах
               |		ПОЛНОЕ СОЕДИНЕНИЕ КешКурсВалютАкции КАК КешКурсВалютАкции
               |		ПО (КешКурсВалютАкции.ВидЦены = &Входная)
               |ГДЕ
               |	ЕСТЬNULL(КешДополнительниеКолонкиВыручкаОборотВдоларах.СуммаВыручкиОборот, 0) <> 0
               |
               |СГРУППИРОВАТЬ ПО
               |	КешДополнительниеКолонкиВыручкаОборотВдоларах.СуммаВыручкиОборот,
               |	КешДополнительниеКолонкиВыручкаОборотВдоларах.СуммаВыручкиОборот * КешКурсВалютАкции.Курс
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КЕшДопКолИтоги.СумаПродажТоваруЧтоПродвигался КАК ЗагальнаСумаПродажТоваруЧтоПродвигался,
               |	КешЗагальнаСуммаПродаж.ЗагальнаСуммаПродаж,
               |	КЕшДопКолИтоги.Прирост КАК ПриростПродажТоварахПоАкциях
               |ПОМЕСТИТЬ КешДополнительниеКолонкиВОтчет
               |ИЗ
               |	КЕшДопКолИтоги КАК КЕшДопКолИтоги
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешЗагальнаСуммаПродаж КАК КешЗагальнаСуммаПродаж
               |		ПО (ИСТИНА)
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешЦенаВходнаяДол.Номенклатура КАК Номенклатура,
               |	КешЦенаВходнаяДол.ЦенаВходная,
               |	КешЦенаВходнаяДол.ЦенаРозничная,
               |	КешЦенаВходнаяДол.Курс,
               |	КешЦенаВходнаяДол.ЦенаВходнаяДол,
               |	КешЦенаВходнаяДол.ЦенаРозничная - КешЦенаВходнаяДол.ЦенаВходнаяДол КАК ВаловаяПрибыль,
               |	Склади.Склад,
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции
               |ПОМЕСТИТЬ КешНоменклатураСклады
               |ИЗ
               |	КешЦенаВходнаяДол КАК КешЦенаВходнаяДол
               |		ЛЕВОЕ СОЕДИНЕНИЕ Склади КАК Склади
               |		ПО КешЦенаВходнаяДол.Номенклатура = Склади.Номенклатура
               |
               |СГРУППИРОВАТЬ ПО
               |	КешЦенаВходнаяДол.Номенклатура,
               |	КешЦенаВходнаяДол.ЦенаВходная,
               |	КешЦенаВходнаяДол.ЦенаРозничная,
               |	КешЦенаВходнаяДол.Курс,
               |	КешЦенаВходнаяДол.ЦенаВходнаяДол,
               |	Склади.Склад,
               |	КешЦенаВходнаяДол.СегментНоменклатураВидаАкции
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешОбщиеИтогиАкционныхТоваров.СегментНоменклатураВидаАкции,
               |	КешОбщиеИтогиАкционныхТоваров.ПрибыльТоваруЧтоПродвигался КАК ВаловаяПрибыльПродажАкционныхТоваров,
               |	100 * КешОбщиеИтогиАкционныхТоваров.СумаПродажТоваруЧтоПродвигался / КешДополнительниеКолонкиВОтчет.ЗагальнаСуммаПродаж КАК ПроцентАкционныхТоваров,
               |	КешОбщиеИтогиАкционныхТоваров.СумаПродажТоваруЧтоПродвигался КАК СуммаПродажАкционныхТоваров
               |ПОМЕСТИТЬ КешАкционныеТовары
               |ИЗ
               |	КешОбщиеИтогиАкционныхТоваров КАК КешОбщиеИтогиАкционныхТоваров
               |		ПОЛНОЕ СОЕДИНЕНИЕ КешДополнительниеКолонкиВОтчет КАК КешДополнительниеКолонкиВОтчет
               |		ПО (ИСТИНА)
               |ГДЕ
               |	ЕСТЬNULL(100 * КешОбщиеИтогиАкционныхТоваров.СумаПродажТоваруЧтоПродвигался / КешДополнительниеКолонкиВОтчет.ЗагальнаСуммаПродаж, 0) <> 0
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ
               |	КешДополнительниеКолонкиВОтчет.ЗагальнаСуммаПродаж,
               |	КешОбщиеИтогиАльтернативныхТоваров.СумаПродажТоваруЧтоПродвигался КАК СуммаПродажАльтернативныхТоваров,
               |	КешОбщиеИтогиАльтернативныхТоваров.ПрибыльТоваруЧтоПродвигался КАК ПрибыльАльтернатывногоТовару,
               |	КешОбщиеИтогиАльтернативныхТоваров.СегментНоменклатураВидаАкции,
               |	100 * КешОбщиеИтогиАльтернативныхТоваров.СумаПродажТоваруЧтоПродвигался / КешДополнительниеКолонкиВОтчет.ЗагальнаСуммаПродаж КАК ПроцентПродаАльтернативныхПозиций
               |ПОМЕСТИТЬ КЕшАльтернатывныхТоваров
               |ИЗ
               |	КешДополнительниеКолонкиВОтчет КАК КешДополнительниеКолонкиВОтчет
               |		ПОЛНОЕ СОЕДИНЕНИЕ КешОбщиеИтогиАльтернативныхТоваров КАК КешОбщиеИтогиАльтернативныхТоваров
               |		ПО (ИСТИНА)
               |ГДЕ
               |	ЕСТЬNULL(КешДополнительниеКолонкиВОтчет.ЗагальнаСуммаПродаж, 0) <> 0
               |;
               |
               |////////////////////////////////////////////////////////////////////////////////
               |ВЫБРАТЬ РАЗЛИЧНЫЕ
               |	КешНоменклатураСклады.Номенклатура,
               |	КешНоменклатураСклады.ЦенаВходная,
               |	КешНоменклатураСклады.ЦенаРозничная,
               |	КешНоменклатураСклады.ЦенаВходнаяДол,
               |	КешНоменклатураСклады.ВаловаяПрибыль,
               |	КешНоменклатураСклады.СегментНоменклатураВидаАкции,
               |	КешНоменклатураСклады.Склад,
               |	ЕСТЬNULL(КешОстатки.КоличествоОстатки, 0) КАК КоличествоОстатки,
               |	ЕСТЬNULL(ПродажиДоАкции.КоличествоОборотДоАкции, 0) КАК КоличествоОборотДоАкции,
               |	ЕСТЬNULL(ПродажиВоВремяАкции.ОборотиВоВремяАкции, 0) КАК ОборотиВоВремяАкции,
               |	КЕшДополнительниеКолонки.СумаПродажТоваруЧтоПродвигался КАК СуммаПродаж,
               |	КЕшДополнительниеКолонки.СуммаВаловийПрибуток,
               |	КешДополнительниеКолонкиВОтчет.ЗагальнаСуммаПродаж,
               |	КешДополнительниеКолонкиВОтчет.ПриростПродажТоварахПоАкциях,
               |	КешДополнительниеКолонкиВОтчет.ЗагальнаСумаПродажТоваруЧтоПродвигался,
               |	КешАкционныеТовары.ПроцентАкционныхТоваров,
               |	КешАкционныеТовары.ВаловаяПрибыльПродажАкционныхТоваров,
               |	КешАкционныеТовары.СуммаПродажАкционныхТоваров,
               |	КЕшАльтернатывныхТоваров.СуммаПродажАльтернативныхТоваров,
               |	КЕшАльтернатывныхТоваров.ПрибыльАльтернатывногоТовару,
               |	КЕшАльтернатывныхТоваров.ПроцентПродаАльтернативныхПозиций
               |ИЗ
               |	КешНоменклатураСклады КАК КешНоменклатураСклады
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешОстатки КАК КешОстатки
               |		ПО КешНоменклатураСклады.Номенклатура = КешОстатки.Номенклатура
               |			И КешНоменклатураСклады.Склад = КешОстатки.Склад
               |		ЛЕВОЕ СОЕДИНЕНИЕ ПродажиДоАкции КАК ПродажиДоАкции
               |		ПО КешНоменклатураСклады.Номенклатура = ПродажиДоАкции.Номенклатура
               |			И КешНоменклатураСклады.Склад = ПродажиДоАкции.Склад
               |		ЛЕВОЕ СОЕДИНЕНИЕ ПродажиВоВремяАкции КАК ПродажиВоВремяАкции
               |		ПО КешНоменклатураСклады.Номенклатура = ПродажиВоВремяАкции.Номенклатура
               |			И КешНоменклатураСклады.Склад = ПродажиВоВремяАкции.Склад
               |		ЛЕВОЕ СОЕДИНЕНИЕ КЕшДополнительниеКолонки КАК КЕшДополнительниеКолонки
               |		ПО КешНоменклатураСклады.Номенклатура = КЕшДополнительниеКолонки.Номенклатура
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешДополнительниеКолонкиВОтчет КАК КешДополнительниеКолонкиВОтчет
               |		ПО (ИСТИНА)
               |		ЛЕВОЕ СОЕДИНЕНИЕ КешАкционныеТовары КАК КешАкционныеТовары
               |		ПО (ИСТИНА)
               |		ЛЕВОЕ СОЕДИНЕНИЕ КЕшАльтернатывныхТоваров КАК КЕшАльтернатывныхТоваров
               |		ПО (ИСТИНА)
               |ГДЕ
               |	ЕСТЬNULL(КешДополнительниеКолонкиВОтчет.ЗагальнаСуммаПродаж, 0) <> 0";
Возврат ТекстЗапроса;
КонецФункции // ПолучитьТекстЗапросаНаборДанных()



////////////////////////////////////////////////////////////////////////////////
// Сведения о внешнем отчете

Функция СведенияОВнешнейОбработке() Экспорт
	Версия = "2.0.0";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительныйОтчет");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Анализ проведения акции");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", "2.0.0");    
    ПараметрыРегистрации.Вставить("Информация", "Анализ проведения акции'");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "Анализ проведения акции", "ФормаОтчета", "ОткрытиеФормы", Ложь, "");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры



