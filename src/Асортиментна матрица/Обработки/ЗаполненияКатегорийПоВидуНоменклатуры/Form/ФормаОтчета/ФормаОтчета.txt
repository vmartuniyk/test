/////////////////////////////////////////////////////////////////
//ФУНКЦИИ ДЛЯ ОБРАБОТКИ  ЭЛЕМЕНТОВ ФОРМЫ
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СформироватьОтчетНаСервере();
КонецПроцедуры

/////////////////////////////////////////////////////////////////
//ФУНКЦИИ ДЛЯ ОБРАБОТКИ  КОМАНД ФОРМЫ

&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПеренаименоватьКлонов(Команда)
	ПеренаименоватьКлоновНаСервере();
КонецПроцедуры




/////////////////////////////////////////////////////////////////
//СЛУЖЕБНЫЕ ФУНКЦИИ И ПРОЦЕДУРЫ


&НаСервере
Процедура СформироватьОтчетНаСервере()
	УстановитьПривилегированныйРежим(Истина);
	Результат.Очистить();
	
	Запрос 			= Новый Запрос;
	ТекстЗапроса   	 = СформироватьТексЗапроса();
	ТекстЗапроса   	 = СтрЗаменить(ТекстЗапроса,"%Отбор%"," ");


	Запрос.Текст 	= ТекстЗапроса;
	
	РезультатЗапроса= Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВывестиРезультатыЗапроса(РезультатЗапроса.Выгрузить());
	КонецЕсли;	
КонецПроцедуры // СформироватьОтчетНаСервере()


&НаСервере
Функция СформироватьТексЗапроса()
	ТекстЗапроса ="	ВЫБРАТЬ
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Номенклатура.Ссылка) КАК Количество,
	|		Номенклатура.Наименование
	|	ПОМЕСТИТЬ КешНоменклатура
	|	ИЗ
	|		Справочник.Номенклатура КАК Номенклатура
	|	ГДЕ
	|		НЕ Номенклатура.ЭтоГруппа     
	
	|	СГРУППИРОВАТЬ ПО
	|		Номенклатура.Наименование
	|	;

	|	////////////////////////////////////////////////////////////////////////////////
	|	ВЫБРАТЬ
	|		Ном.Код КАК Код,
	|		Ном.Ссылка КАК Номенклатура,
	|		КешНоменклатура.Наименование,
	|		КешНоменклатура.Количество КАК КоличествоКлонов,
	|		ЕСТЬNULL(СвободныеОстаткиОстатки.ВНаличииОстаток, 0) КАК СвОст
	|	ИЗ
	|		Справочник.Номенклатура КАК Ном
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешНоменклатура КАК КешНоменклатура
	|			ПО Ном.Наименование = КешНоменклатура.Наименование
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.Остатки КАК СвободныеОстаткиОстатки
	|			ПО Ном.Ссылка = СвободныеОстаткиОстатки.Номенклатура
	|	ГДЕ
	|		КешНоменклатура.Количество > 1 
	|		%Отбор%";		
   Возврат ТекстЗапроса;
	
КонецФункции


&НаСервере
Процедура ВывестиРезультатыЗапроса(СписокКлонов)
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("СписокКлонов", СписокКлонов);
	
	ОбъектОбработки 		= РеквизитФормыВЗначение("Объект");
	СхемаКомпоновкиДанных   = ОбъектОбработки.ПолучитьМакет("КлоныНоменклатуры");
	ЗначениеВРеквизитФормы(ОбъектОбработки, "Объект");
	
	Настройки 					= СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	КомпоновщикМакета 			= Новый КомпоновщикМакетаКомпоновкиДанных;
	ДанныеРасшифровки			= Новый ДанныеРасшифровкиКомпоновкиДанных;
	МакетКомпоновки				= КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанны    = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанны.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВывода				= Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанны);
	
КонецПроцедуры



&НаСервере
Процедура СформироватьНаСервере()
	СформироватьОтчетНаСервере();	
КонецПроцедуры


&НаСервере
Процедура ПеренаименоватьКлоновНаСервере()
	Запрос			 = Новый Запрос;
	ТекстЗапроса 	 = СформироватьТексЗапроса();
	ТекстЗапроса   	 = СтрЗаменить(ТекстЗапроса,"%Отбор%","И ЕСТЬNULL(СвободныеОстаткиОстатки.ВНаличииОстаток, 0)=0 ");


	Запрос.Текст 	 = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить(); 
	Если НЕ РезультатЗапроса.Пустой()  Тогда
	    ВыборкаНоменклатура		 = РезультатЗапроса.Выбрать();
		Пока ВыборкаНоменклатура.Следующий() Цикл
			НоменклатураСсылка 					=  	ВыборкаНоменклатура.Номенклатура;
			НоменклатураКод 					=  	ВыборкаНоменклатура.Код;
			НаименованиеНоменклатуры			= 	НоменклатураСсылка.Наименование;
								
			Длина								= 	СтрДлина(НоменклатураКод);
			НаименованиеНоменклатуры            = 	Лев(НаименованиеНоменклатуры,48-Длина);
			
			СтрокаЗамени                        =   НСтр("ru = '%НаименованиеНоменклатуры% (%НоменклатураКод%)'");
			СтрокаЗамени						=	СтрЗаменить(СтрокаЗамени,"%НаименованиеНоменклатуры%",НаименованиеНоменклатуры); 
			СтрокаЗамени						=	СтрЗаменить(СтрокаЗамени,"%НоменклатураКод%",НоменклатураКод);

			СправочникОбъект					=	НоменклатураСсылка.ПолучитьОбъект();
			СправочникОбъект.Наименование		=	СтрокаЗамени;			
			
		Попытка
			СправочникОбъект.Записать();
		Исключение
			Сообщить("Не удалось записать объект " + НоменклатураСсылка + "",СтатусСообщения.Внимание);
		КонецПопытки;
		КонецЦикла;	
	КонецЕсли;
	СформироватьОтчетНаСервере();
КонецПроцедуры
