
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	СтандартнаяОбработка = Ложь;
	
	ТекстЗапроса = ТекстЗапросаОсновногоНабораДанных();
	СформироватьОтчетПрограммно(ТекстЗапроса, ДанныеРасшифровки, ДокументРезультат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьОтчетПрограммно(ТекстЗапроса, ДанныеРасшифровки, ДокументРезультат)
		
	НаборыДанных 	= СхемаКомпоновкиДанных.НаборыДанных;
	ОсновнойНабор   = НаборыДанных.Найти("НаборДанных1");
	
	Если ОсновнойНабор = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Отчет сломался. Сообщите в службу поддержки.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	КонецЕсли;
	
	ОсновнойНабор.Запрос = ТекстЗапроса;
	
	УстановитьПараметрыКомпоновкиДанных(КомпоновщикНастроек);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки, , , Ложь);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);

КонецПроцедуры

Процедура УстановитьПараметрыКомпоновкиДанных(КомпоновщикНастроек)

	ПараметрыДанных 	= КомпоновщикНастроек.Настройки.ПараметрыДанных;
	НастройкиКТСПомощь  = ПолучитьНастройкиКТСПомощь();
	
	ВидНоменклатурыКТСПомощь	= Справочники.ВидыНоменклатуры.ПолучитьСсылку(Новый УникальныйИдентификатор("91f45981-0c03-11e2-9d4d-001e673c80fc"));
	УстановитьЗначениеПараметраКомпоновкиДанных("ВидНоменклатурыКТСПомощь", ктс_ПредопределенныеЗначенияПовтИсп.ВидНоменклатурыКТСПомощь(), ПараметрыДанных);
	
	УстановитьЗначениеПараметраКомпоновкиДанных("ПроцентПродажыУслуги", ктс_ПредопределенныеЗначенияПовтИсп.ПроцентПродажыУслуги(), ПараметрыДанных);
	
	УстановитьЗначениеПараметраКомпоновкиДанных("ПроцентИсполненияУслуги", ктс_ПредопределенныеЗначенияПовтИсп.ПроцентИсполненияУслуги(), ПараметрыДанных);
	
	УстановитьЗначениеПараметраКомпоновкиДанных("ВидНоменклатурыСертификат", ктс_ПредопределенныеЗначенияПовтИсп.ВидНоменклатурыСертификаты(), ПараметрыДанных);
	
	УстановитьЗначениеПараметраКомпоновкиДанных("ПроцентОтПродажыСертификата", ктс_ПредопределенныеЗначенияПовтИсп.ПроцентОтПродажыСертификата(), ПараметрыДанных);
	
	УстановитьЗначениеПараметраКомпоновкиДанных("СвойствоПомощьКТС", ктс_ПредопределенныеЗначенияПовтИсп.СвойствоПомощьКТС(), ПараметрыДанных);

КонецПроцедуры

Процедура УстановитьЗначениеПараметраКомпоновкиДанных(ИмяПараметра, ЗначениеПараметра, ПараметрыДанных)

	Параметр = ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	
	Если Параметр = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Не удалось получить параметр «%1». Обратитесь в службу технической поддержки.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ИмяПараметра);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Параметр.Значение 		= ЗначениеПараметра;
	Параметр.Использование  = Истина;

КонецПроцедуры
 
Функция ПолучитьНастройкиКТСПомощь()

	Настройки = ХранилищеНастроекДанныхФорм.Загрузить("ДополнительныеНастройкиПроцентовКТСПомощь", , , "ДополнительныеНастройкиПроцентовКТСПомощь");
	
	Если ТипЗнч(Настройки) <> Тип("Структура") Тогда
		ВызватьИсключение НСтр("ru='Не удалось прочитать настройки «КТС Помощь». Обратитесь в службу технической поддержки.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	КонецЕсли;
	
	Если Не Настройки.Свойство("СвойствоПомощьКТС") Тогда
		ВызватьИсключение НСтр("ru='Не установлено свойство «СвойствоПомощьКТС». Обратитесь в службу технической поддержки.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	КонецЕсли;
	
	Если Не Настройки.Свойство("ПроцентПродажыУслуги") Тогда
		ВызватьИсключение НСтр("ru='Не установлено свойство «ПроцентПродажыУслуги». Обратитесь в службу технической поддержки.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	КонецЕсли;
	
	Если Не Настройки.Свойство("ПроцентИсполненияУслуги") Тогда
		ВызватьИсключение НСтр("ru='Не установлено свойство «ПроцентИсполненияУслуги». Обратитесь в службу технической поддержки.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	КонецЕсли;
	
	Если Не Настройки.Свойство("ВидНоменклатурыСертификаты") Тогда
		ВызватьИсключение НСтр("ru='Не установлено свойство «ВидНоменклатурыСертификаты». Обратитесь в службу технической поддержки.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	КонецЕсли;
	
	Если Не Настройки.Свойство("ПроцентОтПродажыСертификата") Тогда
		ВызватьИсключение НСтр("ru='Не установлено свойство «ПроцентОтПродажыСертификата». Обратитесь в службу технической поддержки.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	КонецЕсли;
	
	Возврат Настройки;

КонецФункции // ПолучитьНастройкиКТСПомощь()

Функция ТекстЗапросаОсновногоНабораДанных()

	ТекстЗапроса = "ВЫБРАТЬ
	               |	РасчетыСКлиентамиОстатки.ЗаказКлиента КАК ЗаказКлиента,
	               |	РасчетыСКлиентамиОстатки.СуммаОстаток КАК Сумма
	               |ПОМЕСТИТЬ ДолгиПоЗаказам
	               |ИЗ
	               |	РегистрНакопления.РасчетыСКлиентами.Остатки(&ДатаОкончания {(&ДатаОкончания)}, ) КАК РасчетыСКлиентамиОстатки
	               |ГДЕ
	               |	РасчетыСКлиентамиОстатки.СуммаОстаток > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РасчетыСКлиентамиОстатки.ЗаказКлиента КАК ЗаказКлиента,
	               |	РасчетыСКлиентамиОстатки.СуммаОстаток КАК Сумма
	               |ПОМЕСТИТЬ ВТ_ДолгиПоЗаказамНаНачалоПериода
	               |ИЗ
	               |	РегистрНакопления.РасчетыСКлиентами.Остатки(&ДатаНачала {(&ДатаНачала)}, ) КАК РасчетыСКлиентамиОстатки
	               |ГДЕ
	               |	РасчетыСКлиентамиОстатки.СуммаОстаток > 0
	               |	И РасчетыСКлиентамиОстатки.ЗаказКлиента.Дата >= ДАТАВРЕМЯ(2015, 4, 1)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВЫРАЗИТЬ(ВыручкаИСебестоимостьПродажОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг) КАК Документ,
	               |	ИСТИНА КАК ЕстьДолгПоДокументу
	               |ПОМЕСТИТЬ ДолгиПоРеализациям
	               |ИЗ
	               |	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(
	               |			,
	               |			,
	               |			Регистратор,
	               |			ЗаказКлиента В
	               |				(ВЫБРАТЬ
	               |					ДолгиПоЗаказам.ЗаказКлиента
	               |				ИЗ
	               |					ДолгиПоЗаказам)) КАК ВыручкаИСебестоимостьПродажОбороты
	               |ГДЕ
	               |	ВыручкаИСебестоимостьПродажОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Документ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВЫРАЗИТЬ(ВыручкаИСебестоимостьПродажОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг) КАК Документ,
	               |	ИСТИНА КАК ЕстьДолгПоДокументу
	               |ПОМЕСТИТЬ ДолгиПоРеализациямНаНачалоПериода
	               |ИЗ
	               |	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(
	               |			,
	               |			,
	               |			Регистратор,
	               |			ЗаказКлиента В
	               |				(ВЫБРАТЬ
	               |					ВТ_ДолгиПоЗаказамНаНачалоПериода.ЗаказКлиента
	               |				ИЗ
	               |					ВТ_ДолгиПоЗаказамНаНачалоПериода)) КАК ВыручкаИСебестоимостьПродажОбороты
	               |ГДЕ
	               |	ВыручкаИСебестоимостьПродажОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Документ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КТСПомощь.Номенклатура КАК Номенклатура,
	               |	КТСПомощь.ПроцентПродажы КАК ПроцентПродажы,
	               |	КТСПомощь.ПроцентИсполнения КАК ПроцентИсполнения,
	               |	ИСТИНА КАК ЭтоКТСПомощь
	               |ПОМЕСТИТЬ РасширенныеНастройкиКТСПомощь
	               |ИЗ
	               |	РегистрСведений.КТСПомощь КАК КТСПомощь
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Номенклатура.Ссылка КАК Номенклатура,
	               |	ИСТИНА КАК ЭтоКТСПомощь
	               |ПОМЕСТИТЬ ТаблицаНоменклатурыКТСПомощь
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.ВидНоменклатуры = &ВидНоменклатурыКТСПомощь
	               |	И Номенклатура.ПометкаУдаления = ЛОЖЬ
	               |	И Номенклатура.ЭтоГруппа = ЛОЖЬ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КТС_НастройкаПроцентовСертификатов.Сертификат КАК Сертификат,
	               |	КТС_НастройкаПроцентовСертификатов.Процент КАК Процент,
	               |	ИСТИНА КАК ЭтоСертификат
	               |ПОМЕСТИТЬ РасширенныеНастройкиСертификатов
	               |ИЗ
	               |	РегистрСведений.КТС_НастройкаПроцентовСертификатов КАК КТС_НастройкаПроцентовСертификатов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Номенклатура.Ссылка КАК Номенклатура,
	               |	ИСТИНА КАК ЭтоСертификат
	               |ПОМЕСТИТЬ ТаблицаСертификатов
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.ВидНоменклатуры = &ВидНоменклатурыСертификат
	               |	И Номенклатура.ПометкаУдаления = ЛОЖЬ
	               |	И Номенклатура.ЭтоГруппа = ЛОЖЬ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КТС_КоэффициентыСезонностиМагазинов.Период,
	               |	КТС_КоэффициентыСезонностиМагазинов.Коэффициент
	               |ПОМЕСТИТЬ КоэффициентСезонности
	               |ИЗ
	               |	РегистрСведений.КТС_КоэффициентыСезонностиМагазинов КАК КТС_КоэффициентыСезонностиМагазинов
	               |ГДЕ
	               |	КТС_КоэффициентыСезонностиМагазинов.Период <= &ДатаОкончания
				   |
	               |СГРУППИРОВАТЬ ПО
	               |	КТС_КоэффициентыСезонностиМагазинов.Период,
	               |	КТС_КоэффициентыСезонностиМагазинов.Коэффициент
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	БонусыМенеджерам.Регистратор КАК Ссылка,
	               |	БонусыМенеджерам.Подразделение КАК Подразделение,
	               |	БонусыМенеджерам.Менеджер КАК Менеджер,
	               |	БонусыМенеджерам.Номенклатура КАК Номенклатура,
	               |	БонусыМенеджерам.КоличествоОборот КАК Количество,
	               |	БонусыМенеджерам.СуммаРеглОборот КАК Сумма,
	               |	БонусыМенеджерам.ЗаработокМенеджераРеглОборот КАК ЗаработокМенеджера,
	               |	ЕСТЬNULL(ДолгиПоРеализациям.ЕстьДолгПоДокументу, ЛОЖЬ) КАК ЕстьДолгПоДокументу,
	               |	ЕСТЬNULL(КоэффициентСезонности.Коэффициент, 1) КАК КоэффициентСезонности,
	               |	БонусыМенеджерам.Склад,
	               |	НАЧАЛОПЕРИОДА(БонусыМенеджерам.Период, МЕСЯЦ) КАК Месяц
	               |ПОМЕСТИТЬ ТаблицаЗаработков
	               |ИЗ
	               |	РегистрНакопления.КПП_БонусыМенеджерамНачисленые.Обороты(&ДатаНачала {(&ДатаНачала)}, &ДатаОкончания {(&ДатаОкончания)}, Регистратор, ) КАК БонусыМенеджерам
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ДолгиПоРеализациям КАК ДолгиПоРеализациям
	               |		ПО ((ВЫРАЗИТЬ(БонусыМенеджерам.Регистратор КАК Документ.РеализацияТоваровУслуг)) = ДолгиПоРеализациям.Документ)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентСезонности КАК КоэффициентСезонности
	               |		ПО (НАЧАЛОПЕРИОДА(БонусыМенеджерам.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(КоэффициентСезонности.Период, МЕСЯЦ))
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	БонусыМенеджерам.Регистратор,
	               |	БонусыМенеджерам.Подразделение,
	               |	БонусыМенеджерам.Менеджер,
	               |	БонусыМенеджерам.Номенклатура,
	               |	БонусыМенеджерам.КоличествоОборот,
	               |	БонусыМенеджерам.СуммаРеглОборот,
	               |	БонусыМенеджерам.ЗаработокМенеджераРеглОборот,
	               |	ЕСТЬNULL(ДолгиПоРеализациям.ЕстьДолгПоДокументу, ЛОЖЬ),
	               |	ЕСТЬNULL(КоэффициентСезонности.Коэффициент, 1),
	               |	БонусыМенеджерам.Склад,
	               |	НАЧАЛОПЕРИОДА(БонусыМенеджерам.Период, МЕСЯЦ)
	               |ИЗ
	               |	ДолгиПоРеализациямНаНачалоПериода КАК ДолгиПоРеализациямНаНачалоПериода
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.КПП_БонусыМенеджерамНачисленые.Обороты(, , Регистратор, ) КАК БонусыМенеджерам
	               |		ПО (ДолгиПоРеализациямНаНачалоПериода.Документ = (ВЫРАЗИТЬ(БонусыМенеджерам.Регистратор КАК Документ.РеализацияТоваровУслуг)))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ДолгиПоРеализациям КАК ДолгиПоРеализациям
	               |		ПО ((ВЫРАЗИТЬ(БонусыМенеджерам.Регистратор КАК Документ.РеализацияТоваровУслуг)) = ДолгиПоРеализациям.Документ)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентСезонности КАК КоэффициентСезонности
	               |		ПО (НАЧАЛОПЕРИОДА(БонусыМенеджерам.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(КоэффициентСезонности.Период, МЕСЯЦ))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	КТС_КоэффициентыВыполненияПланаМагазинами.Период,
	               |	КТС_КоэффициентыВыполненияПланаМагазинами.Склад,
	               |	КТС_КоэффициентыВыполненияПланаМагазинами.Коэффициент
	               |ПОМЕСТИТЬ КоэффициентыВыполненияПланаМагазином
	               |ИЗ
	               |	РегистрСведений.КТС_КоэффициентыВыполненияПланаМагазинами КАК КТС_КоэффициентыВыполненияПланаМагазинами
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаЗаработков КАК ТаблицаЗаработков
	               |		ПО КТС_КоэффициентыВыполненияПланаМагазинами.Склад = ТаблицаЗаработков.Склад
	               |			И КТС_КоэффициентыВыполненияПланаМагазинами.Период >= ТаблицаЗаработков.Месяц
	               |			И КТС_КоэффициентыВыполненияПланаМагазинами.Период <= ТаблицаЗаработков.Месяц
	               |ГДЕ
				   |		КТС_КоэффициентыВыполненияПланаМагазинами.Период <= &ДатаОкончания
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ТаблицаЗаработков.Ссылка КАК Документ.РеализацияТоваровУслуг) КАК Ссылка,
	               |	ТаблицаЗаработков.Подразделение КАК Подразделение,
	               |	ТаблицаЗаработков.Номенклатура КАК Номенклатура,
	               |	ТаблицаЗаработков.Количество КАК Количество,
	               |	ТаблицаЗаработков.Сумма КАК Сумма,
	               |	ТаблицаЗаработков.ЕстьДолгПоДокументу КАК ЕстьДолгПоДокументу,
	               |	ТаблицаЗаработков.Месяц,
	               |	ТаблицаЗаработков.Склад,
	               |	ТаблицаЗаработков.КоэффициентСезонности
	               |ПОМЕСТИТЬ РеализацииКТСПомощь
	               |ИЗ
	               |	ТаблицаЗаработков КАК ТаблицаЗаработков
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНоменклатурыКТСПомощь КАК ТаблицаНоменклатурыКТСПомощь
	               |		ПО ТаблицаЗаработков.Номенклатура = ТаблицаНоменклатурыКТСПомощь.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РеализацииКТСПомощь.Ссылка КАК Ссылка,
	               |	РеализацииКТСПомощь.Подразделение КАК Подразделение,
	               |	ВЫРАЗИТЬ(РегистрДопСведения.Значение КАК Справочник.Пользователи) КАК Менеджер,
	               |	РеализацииКТСПомощь.Номенклатура КАК Номенклатура,
	               |	РеализацииКТСПомощь.Количество КАК Количество,
	               |	РеализацииКТСПомощь.Сумма КАК Сумма,
	               |	РеализацииКТСПомощь.ЕстьДолгПоДокументу КАК ЕстьДолгПоДокументу,
	               |	РеализацииКТСПомощь.Месяц,
	               |	РеализацииКТСПомощь.Склад,
	               |	РеализацииКТСПомощь.КоэффициентСезонности
	               |ПОМЕСТИТЬ РеализацииКТСПомощьПоМенеджерам
	               |ИЗ
	               |	РеализацииКТСПомощь КАК РеализацииКТСПомощь
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК РегистрДопСведения
	               |		ПО (РеализацииКТСПомощь.Ссылка = (ВЫРАЗИТЬ(РегистрДопСведения.Объект КАК Документ.РеализацияТоваровУслуг)))
	               |			И (РегистрДопСведения.Свойство = &СвойствоПомощьКТС)
	               |{ГДЕ
	               |	РеализацииКТСПомощь.Подразделение.* КАК Подразделение,
	               |	(ВЫРАЗИТЬ(РегистрДопСведения.Значение КАК Справочник.Пользователи)).* КАК МенеджерОтбор,
	               |	РеализацииКТСПомощь.Номенклатура.* КАК Номенклатура}
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаЗаработков.Менеджер,
	               |	ТаблицаЗаработков.Месяц,
	               |	МАКСИМУМ(КТС_КоэффициентыЭффективностиПродавцаСрезПоследних.Период) КАК Период
	               |ПОМЕСТИТЬ ПериодыЭффективностиМенеджеров
	               |ИЗ
	               |	ТаблицаЗаработков КАК ТаблицаЗаработков
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КТС_КоэффициентыЭффективностиПродавца.СрезПоследних КАК КТС_КоэффициентыЭффективностиПродавцаСрезПоследних
	               |		ПО ТаблицаЗаработков.Менеджер = КТС_КоэффициентыЭффективностиПродавцаСрезПоследних.Менеджер
	               |			И ТаблицаЗаработков.Месяц >= КТС_КоэффициентыЭффективностиПродавцаСрезПоследних.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаЗаработков.Месяц,
	               |	ТаблицаЗаработков.Менеджер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПериодыЭффективностиМенеджеров.Менеджер,
	               |	ПериодыЭффективностиМенеджеров.Месяц,
	               |	ПериодыЭффективностиМенеджеров.Период,
	               |	КТС_КоэффициентыЭффективностиПродавцаСрезПоследних.Значения,
	               |	КТС_КоэффициентыЭффективностиПродавцаСрезПоследних.КоэффициентЭффективности
	               |ПОМЕСТИТЬ КоэффициентыЭффективностиМенеджеров
	               |ИЗ
	               |	ПериодыЭффективностиМенеджеров КАК ПериодыЭффективностиМенеджеров
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КТС_КоэффициентыЭффективностиПродавца.СрезПоследних КАК КТС_КоэффициентыЭффективностиПродавцаСрезПоследних
	               |		ПО ПериодыЭффективностиМенеджеров.Менеджер = КТС_КоэффициентыЭффективностиПродавцаСрезПоследних.Менеджер
	               |			И ПериодыЭффективностиМенеджеров.Период = КТС_КоэффициентыЭффективностиПродавцаСрезПоследних.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаЗаработков.Ссылка КАК Ссылка,
	               |	ТаблицаЗаработков.Подразделение КАК Подразделение,
	               |	ТаблицаЗаработков.Менеджер КАК Менеджер,
	               |	ТаблицаЗаработков.Номенклатура КАК Номенклатура,
	               |	ТаблицаЗаработков.Количество КАК Количество,
	               |	ТаблицаЗаработков.Сумма КАК Сумма,
	               |	ВЫБОР
	               |		КОГДА ТаблицаЗаработков.ЕстьДолгПоДокументу = ИСТИНА
	               |			ТОГДА ВЫБОР
	               |					КОГДА ЕСТЬNULL(РасширенныеНастройкиКТСПомощь.ЭтоКТСПомощь, ЛОЖЬ) = ИСТИНА
	               |						ТОГДА ТаблицаЗаработков.Сумма * РасширенныеНастройкиКТСПомощь.ПроцентПродажы * 0.01
	               |					КОГДА ЕСТЬNULL(ТаблицаНоменклатурыКТСПомощь.ЭтоКТСПомощь, ЛОЖЬ) = ИСТИНА
	               |						ТОГДА ТаблицаЗаработков.Сумма * &ПроцентПродажыУслуги * 0.01
	               |					КОГДА ЕСТЬNULL(РасширенныеНастройкиСертификатов.ЭтоСертификат, ЛОЖЬ) = ИСТИНА
	               |						ТОГДА ТаблицаЗаработков.Сумма * РасширенныеНастройкиСертификатов.Процент * 0.01
	               |					КОГДА ЕСТЬNULL(ТаблицаСертификатов.ЭтоСертификат, ЛОЖЬ) = ИСТИНА
	               |						ТОГДА ТаблицаЗаработков.Сумма * &ПроцентОтПродажыСертификата * 0.01
	               |					ИНАЧЕ ТаблицаЗаработков.ЗаработокМенеджера
	               |				КОНЕЦ
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ВозможныйЗаработок,
	               |	ВЫБОР
	               |		КОГДА ТаблицаЗаработков.ЕстьДолгПоДокументу = ЛОЖЬ
	               |			ТОГДА ВЫБОР
	               |					КОГДА ЕСТЬNULL(РасширенныеНастройкиКТСПомощь.ЭтоКТСПомощь, ЛОЖЬ) = ИСТИНА
	               |						ТОГДА ТаблицаЗаработков.Сумма * ЕСТЬNULL(РасширенныеНастройкиКТСПомощь.ПроцентПродажы, 0) * 0.01
	               |					КОГДА ЕСТЬNULL(ТаблицаНоменклатурыКТСПомощь.ЭтоКТСПомощь, ЛОЖЬ) = ИСТИНА
	               |						ТОГДА ТаблицаЗаработков.Сумма * &ПроцентПродажыУслуги * 0.01
	               |					КОГДА ЕСТЬNULL(РасширенныеНастройкиСертификатов.ЭтоСертификат, ЛОЖЬ) = ИСТИНА
	               |						ТОГДА ТаблицаЗаработков.Сумма * ЕСТЬNULL(РасширенныеНастройкиСертификатов.Процент, 0) * 0.01
	               |					КОГДА ЕСТЬNULL(ТаблицаСертификатов.ЭтоСертификат, ЛОЖЬ) = ИСТИНА
	               |						ТОГДА ТаблицаЗаработков.Сумма * &ПроцентОтПродажыСертификата * 0.01
	               |					ИНАЧЕ ТаблицаЗаработков.ЗаработокМенеджера
	               |				КОНЕЦ
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ЗаработокМенеджера,
	               |	ЕСТЬNULL(КоэффициентыВыполненияПланаМагазином.Коэффициент, 1) КАК КоэффициентВыполненияПлана,
	               |	ЕСТЬNULL(КоэффициентыЭффективностиМенеджеров.Значения, 1) КАК КоэффициентЭффективности,
	               |	ТаблицаЗаработков.КоэффициентСезонности
	               |ПОМЕСТИТЬ ТаблицаОкончательныйЗаработок
	               |ИЗ
	               |	ТаблицаЗаработков КАК ТаблицаЗаработков
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РасширенныеНастройкиКТСПомощь КАК РасширенныеНастройкиКТСПомощь
	               |		ПО ТаблицаЗаработков.Номенклатура = РасширенныеНастройкиКТСПомощь.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНоменклатурыКТСПомощь КАК ТаблицаНоменклатурыКТСПомощь
	               |		ПО ТаблицаЗаработков.Номенклатура = ТаблицаНоменклатурыКТСПомощь.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РасширенныеНастройкиСертификатов КАК РасширенныеНастройкиСертификатов
	               |		ПО ТаблицаЗаработков.Номенклатура = РасширенныеНастройкиСертификатов.Сертификат
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСертификатов КАК ТаблицаСертификатов
	               |		ПО ТаблицаЗаработков.Номенклатура = ТаблицаСертификатов.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыЭффективностиМенеджеров КАК КоэффициентыЭффективностиМенеджеров
	               |		ПО ТаблицаЗаработков.Менеджер = КоэффициентыЭффективностиМенеджеров.Менеджер
	               |			И ТаблицаЗаработков.Месяц = КоэффициентыЭффективностиМенеджеров.Месяц
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыВыполненияПланаМагазином КАК КоэффициентыВыполненияПланаМагазином
	               |		ПО ТаблицаЗаработков.Месяц = КоэффициентыВыполненияПланаМагазином.Период
	               |			И ТаблицаЗаработков.Склад = КоэффициентыВыполненияПланаМагазином.Склад
	               |{ГДЕ
	               |	ТаблицаЗаработков.Менеджер.* КАК МенеджерОтбор}
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	РеализацииКТСПомощьПоМенеджерам.Ссылка,
	               |	РеализацииКТСПомощьПоМенеджерам.Подразделение,
	               |	РеализацииКТСПомощьПоМенеджерам.Менеджер,
	               |	РеализацииКТСПомощьПоМенеджерам.Номенклатура,
	               |	0,
	               |	0,
	               |	ВЫБОР
	               |		КОГДА РеализацииКТСПомощьПоМенеджерам.ЕстьДолгПоДокументу = ИСТИНА
	               |			ТОГДА ВЫБОР
	               |					КОГДА ЕСТЬNULL(РасширенныеНастройкиКТСПомощь.ЭтоКТСПомощь, ЛОЖЬ) = ИСТИНА
	               |						ТОГДА РеализацииКТСПомощьПоМенеджерам.Сумма * ЕСТЬNULL(РасширенныеНастройкиКТСПомощь.ПроцентИсполнения, 0) * 0.01
	               |					ИНАЧЕ РеализацииКТСПомощьПоМенеджерам.Сумма * &ПроцентИсполненияУслуги * 0.01
	               |				КОНЕЦ
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ВЫБОР
	               |		КОГДА РеализацииКТСПомощьПоМенеджерам.ЕстьДолгПоДокументу = ЛОЖЬ
	               |			ТОГДА ВЫБОР
	               |					КОГДА ЕСТЬNULL(РасширенныеНастройкиКТСПомощь.ЭтоКТСПомощь, ЛОЖЬ) = ИСТИНА
	               |						ТОГДА РеализацииКТСПомощьПоМенеджерам.Сумма * ЕСТЬNULL(РасширенныеНастройкиКТСПомощь.ПроцентИсполнения, 0) * 0.01
	               |					ИНАЧЕ РеализацииКТСПомощьПоМенеджерам.Сумма * &ПроцентИсполненияУслуги * 0.01
	               |				КОНЕЦ
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ЕСТЬNULL(КоэффициентыВыполненияПланаМагазином.Коэффициент, 1),
	               |	ЕСТЬNULL(КоэффициентыЭффективностиМенеджеров.Значения, 1),
	               |	РеализацииКТСПомощьПоМенеджерам.КоэффициентСезонности
	               |ИЗ
	               |	РеализацииКТСПомощьПоМенеджерам КАК РеализацииКТСПомощьПоМенеджерам
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РасширенныеНастройкиКТСПомощь КАК РасширенныеНастройкиКТСПомощь
	               |		ПО РеализацииКТСПомощьПоМенеджерам.Номенклатура = РасширенныеНастройкиКТСПомощь.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыЭффективностиМенеджеров КАК КоэффициентыЭффективностиМенеджеров
	               |		ПО РеализацииКТСПомощьПоМенеджерам.Месяц = КоэффициентыЭффективностиМенеджеров.Месяц
	               |			И РеализацииКТСПомощьПоМенеджерам.Менеджер = КоэффициентыЭффективностиМенеджеров.Менеджер
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыВыполненияПланаМагазином КАК КоэффициентыВыполненияПланаМагазином
	               |		ПО РеализацииКТСПомощьПоМенеджерам.Месяц = КоэффициентыВыполненияПланаМагазином.Период
	               |			И РеализацииКТСПомощьПоМенеджерам.Склад = КоэффициентыВыполненияПланаМагазином.Склад
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаОкончательныйЗаработок.Ссылка КАК Ссылка,
	               |	ТаблицаОкончательныйЗаработок.Подразделение КАК Подразделение,
	               |	ТаблицаОкончательныйЗаработок.Менеджер КАК Менеджер,
	               |	ТаблицаОкончательныйЗаработок.Номенклатура КАК Номенклатура,
	               |	ТаблицаОкончательныйЗаработок.Количество КАК Количество,
	               |	ТаблицаОкончательныйЗаработок.Сумма КАК Сумма,
	               |	ТаблицаОкончательныйЗаработок.ВозможныйЗаработок * ТаблицаОкончательныйЗаработок.КоэффициентВыполненияПлана * ТаблицаОкончательныйЗаработок.КоэффициентСезонности * (ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаОкончательныйЗаработок.КоэффициентЭффективности, 1) КАК ЧИСЛО(15, 3))) КАК ВозможныйЗаработок,
	               |	ТаблицаОкончательныйЗаработок.ЗаработокМенеджера * ТаблицаОкончательныйЗаработок.КоэффициентВыполненияПлана * ТаблицаОкончательныйЗаработок.КоэффициентСезонности * (ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаОкончательныйЗаработок.КоэффициентЭффективности, 1) КАК ЧИСЛО(15, 3))) КАК ЗаработокМенеджера,
	               |	ТаблицаОкончательныйЗаработок.КоэффициентВыполненияПлана,
	               |	ТаблицаОкончательныйЗаработок.КоэффициентЭффективности,
	               |	ТаблицаОкончательныйЗаработок.КоэффициентСезонности
	               |ИЗ
	               |	ТаблицаОкончательныйЗаработок КАК ТаблицаОкончательныйЗаработок
				   |ГДЕ
				   |	ТаблицаОкончательныйЗаработок.Менеджер <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)";
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаОсновногоНабораДанных()

#КонецОбласти

#Область СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	Версия = "2.2.42";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительныйОтчет");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Отчет по заработной плате (розница) ["+Версия+"]");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Отчет по заработной плате (розница) ["+Версия+"]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "Отчет по заработной плате (розница) ["+Версия+"]", "ОДП", "ОткрытиеФормы", Ложь, "ОПЗПР");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
	
КонецФункции

Процедура СчитатьДокументыНазначений(МассивНазначений)
	
	//Макет = ПолучитьМакет("МакетДокументыНазначений");
	//
	//Для Инд = 1 По Макет.ВысотаТаблицы Цикл
	//	МассивНазначений.Добавить(Макет.Область(Инд,1).Текст);
	//КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры
	
#КонецОбласти 