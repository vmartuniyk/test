#Область ПрограммныйИнтерфейсс

// Подготавливает форму документа к работе с лояльностью к клиентам 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма документа
//  ВнешнийОбъект 	- Неопределено, ОбработкаОбъект - Внешний объект для переопределения событий формы
//  ИмяОбработки 	- Строка           				- Имя внешней обработки лояльности для подключения методов на клиенте
//
Процедура ПодготовитьЛояльностьКРаботе(ЭтаФорма, ВнешнийОбъект, ИмяОбработки = "") Экспорт
	
	ОбъектСсылка = ЭтаФорма.Объект.Ссылка;
	
	Если ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту")
	 ИЛИ ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.ЗаказКлиента")
	 ИЛИ ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, ЭтаФорма, "НачалоВыбора", "Партнер"    , "ЛояльностьНачалоВыбораПартнер");                    
		ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, ЭтаФорма, "ПриИзменении", "Организация", "ЛояльностьПриИзмененииОрганизация");                
		ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, ЭтаФорма, "НачалоВыбора", "Соглашение" , "ЛояльностьНачалоВыбораСоглашение");                 
		ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, ЭтаФорма, "ПриИзменении", "Соглашение" , "ЛояльностьПриИзмененииСоглашение");				   
		ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, ЭтаФорма, "ПередЗаписью", 			 , "ЛояльностьПередЗаписью");
		
		Реквизиты = Новый Массив; 
		Реквизиты.Добавить(Новый РеквизитФормы("LoyaltyLibraryName",  Новый ОписаниеТипов("Строка")));
		Реквизиты.Добавить(Новый РеквизитФормы("ЛояльностьИспользуетсяПодразделением",  Новый ОписаниеТипов("Булево")));
		ЭтаФорма.ИзменитьРеквизиты(Реквизиты);
		ЭтаФорма.LoyaltyLibraryName = ИмяОбработки;
	
		Если ЛояльностьИспользуетсяПодразделением() Тогда
			Элементы = ЭтаФорма.Элементы;
			Элементы.Партнер.РедактированиеТекста = Ложь;
			РезультатПоиска = Элементы.Найти("Подразделение");
			Если РезультатПоиска <> Неопределено Тогда
				Элементы.Подразделение.ТолькоПросмотр = Истина;
			КонецЕсли;
			ЭтаФорма.ЛояльностьИспользуетсяПодразделением = Истина;
		Иначе
			ЭтаФорма.ЛояльностьИспользуетсяПодразделением = Ложь;	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПодготовитьЛояльностьКРаботе()

// Процедура записывает допольнительные сведения при проведении документа "Установка цен номенклатуры" 
// Вызывается из подписки на событие документов при проведении и удаления проведения
// 
// Параметры:
//  Источник - ДокументОбъект.УстановкаЦенНоменклатуры - Документ объект установки цен номенклатуры
//  Отказ	 - Булево                                  - Флаг ошибки при проведении документа
//
Процедура ОбновитьДопольнительниеСведенияИРеквизитыПриПроведенииИзминенииДокумента(Источник, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;

	Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(Новый УникальныйИдентификатор("fd9a1af9-8b4e-11e4-80e6-001e676b0174"));  // Элемент ПВХ "Бонус по дополнительной программе"
        
    Запрос = Новый Запрос;
    Запрос.Текст = ПолучитьТекстЗапросаБонусовПоДополнительнойПрограмме();
    Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
    Запрос.УстановитьПараметр("ВидЦены", Справочники.ВидыЦен.ПолучитьСсылку(Новый УникальныйИдентификатор("8bdf84ff-404c-11e0-9f98-001517115d85"))); //Роздріб
	Запрос.УстановитьПараметр("Свойство", Свойство);
	Запрос.УстановитьПараметр("ТекущаяДатаСеанса", ТекущаяДатаСеанса());
    
    НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Свойство.Установить(Свойство);    

	Выборка = Запрос.Выполнить().Выбрать();
    Пока Выборка.Следующий() Цикл
        НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);
    	ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
        НаборЗаписей.Записать();	
        НаборЗаписей.Очистить();
    КонецЦикла

КонецПроцедуры // ЗаписатьДопольнительниеСведенияИРеквизитыПриПроведенииИзминенииДокумента()

// Процедура записывает допольнительные сведения 
// Вызывается из подписки на событие документов при проведении и удаления проведения
// 
// Параметры:
//  Нет
//
Процедура ОбновитьДопольнительниеСведенияИРеквизиты() Экспорт
	                                               	
	Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(Новый УникальныйИдентификатор("fd9a1af9-8b4e-11e4-80e6-001e676b0174"));  // Элемент ПВХ "Бонус по дополнительной программе"
        
    Запрос = Новый Запрос;
    Запрос.Текст = ПолучитьТекстЗапросаБонусовПоДополнительнойПрограмме(Истина);
    Запрос.УстановитьПараметр("ВидЦены", Справочники.ВидыЦен.ПолучитьСсылку(Новый УникальныйИдентификатор("8bdf84ff-404c-11e0-9f98-001517115d85"))); //Роздріб
	Запрос.УстановитьПараметр("Свойство", Свойство);
	Запрос.УстановитьПараметр("ТекущаяДатаСеанса", ТекущаяДатаСеанса());

    НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Свойство.Установить(Свойство);    
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	НаборЗаписей.Записать();
	
КонецПроцедуры // ОбновитьДопольнительниеСведенияИРеквизиты()

Процедура СписотьПростроченныеБонусы() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	Партнер 		КАК Партнер,
		|	ДействуетС 		КАК ДействуетС,
		|	ДействуетДо 	КАК ДействуетДо,
		|	БонусОстаток 	КАК Бонус,
		|	Истина 			КАК ЭтоСписаниеБонусов,
		|	""Автоматическое списание простроченных бонусов"" КАК Комментарий
		|ИЗ
		|	РегистрНакопления.Лояльность_Бонусы.Остатки(, ДействуетДо <> ДАТАВРЕМЯ(1,1,1) И ДействуетДо < &НачалоДня)
		|ГДЕ
		|	БонусОстаток > 0	
		|";
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ТекущаяДата()));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Документы.НачислениеБонусов.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Выборка);
		ДокументОбъект.Дата = НачалоДня(ТекущаяДата());
		ДокументОбъект.Менеджер = ПараметрыСеанса.ТекущийПользователь;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	КонецЦикла;
	
КонецПроцедуры // СписотьПростроченныеБонусы()

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Проверка подключения организации к системе лояльности
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - Элемент справочника организации 
//
// Возвращаемое значение:
//   Булево - Используется или нет
Функция ЛояльностьИспользуеться(Организация) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ * ИЗ РегистрСведений.Лояльность_Настройки ГДЕ Организация = &Организация");
	Запрос.УстановитьПараметр("Организация", Организация);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции // ЛояльностьИспользуеться()

// Проверка подключения подразделения к системе лояльности
// 
// Параметры:
//  Нет 
//
// Возвращаемое значение:
//   Булево - Используется или нет
Функция ЛояльностьИспользуетсяПодразделением() Экспорт
	
	Подразделение = ПараметрыСеанса.ТекущийПользователь.ТекущееПодразделение;
	Запрос = Новый Запрос("ВЫБРАТЬ * ИЗ РегистрСведений.Лояльность_Настройки ГДЕ Подразделение = &Подразделение");
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции // ЛояльностьИспользуетсяПодразделением()

// Проверка подключения организации к системе лояльности
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - Элемент справочника организации
//  Партнер 	- СправочникСсылка.Партнеры	   - Элемент справочника партнеры
//  Сумма       - Число						   - Сумма документа
//
// Возвращаемое значение:
//   Булево - Используется или нет
Функция ПроверитьПревышениеСуммыБезРегистрации(Организация, Партнер, Сумма) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ * ИЗ РегистрСведений.Лояльность_Настройки ГДЕ Организация = &Организация И РозничныйКлиент = &Партнер И СуммаБезРегистрации < &Сумма");	
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("Партнер", 	Партнер);
	Запрос.УстановитьПараметр("Сумма", 		Сумма);
	Возврат НЕ Запрос.Выполнить().Пустой();	
	
КонецФункции // ПроверитьПревышениеСуммыБезРегистрации()

Функция ПолучитьПартнераПоУмолчанию(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ 
		|	* 
		|ИЗ 
		|	РегистрСведений.Лояльность_Настройки 
		|ГДЕ 
		|	Организация = &Организация";
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка.РозничныйКлиент, Неопределено);
	
КонецФункции

Функция ЭтотПартнерПоУмолчанию(Партнер) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ 
		|	РозничныйКлиент 
		|ИЗ 
		|	РегистрСведений.Лояльность_Настройки 
		|ГДЕ 
		|	РозничныйКлиент = &Партнер";
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Истина, Ложь);
	
КонецФункции

Функция ПолучитьПартнераПоНомеру(Номер) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ 
		|	* 
		|ИЗ 
		|	РегистрСведений.Лояльность_НомераТелефонов
		|ГДЕ
		|	НомерТелефона = &Номер";
	Запрос.УстановитьПараметр("Номер", Номер);
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка.Партнер, Неопределено);
	
КонецФункции

Функция ПолучитьБонусыПоПартнеру(Партнер) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	ЛояльностьБонусы.Партнер,
		|	СУММА(ЛояльностьБонусы.БонусПриход) - СУММА(ЛояльностьБонусы.БонусРасход) КАК Бонусы
		|ИЗ
		|	РегистрНакопления.Лояльность_Бонусы.ОстаткиИОбороты(, , Регистратор, , Партнер = &Партнер 
		|																		 И ДействуетС < &ТекущаяДата 
		|																		 И (ДействуетДо >= &ТекущаяДата ИЛИ ДействуетДо = ДАТАВРЕМЯ(1,1,1))) КАК ЛояльностьБонусы
		|	
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами.Остатки(
		|					,
		|					АналитикаУчетаПоПартнерам В
		|						(ВЫБРАТЬ
		|							КлючАналитики КАК КлючАналитики
		|						ИЗ
		|							РегистрСведений.АналитикаУчетаПоПартнерам
		|						ГДЕ
		|							Партнер = &Партнер)) КАК РасчетыСКлиентами
		|ПО ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ЛояльностьБонусы.Регистратор КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента = НЕОПРЕДЕЛЕНО
		|			ТОГДА ВЫРАЗИТЬ(ЛояльностьБонусы.Регистратор КАК Документ.РеализацияТоваровУслуг)
		|		КОГДА ВЫРАЗИТЬ(ЛояльностьБонусы.Регистратор КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
		|			ТОГДА ВЫРАЗИТЬ(ЛояльностьБонусы.Регистратор КАК Документ.РеализацияТоваровУслуг)
		|		ИНАЧЕ ВЫРАЗИТЬ(ЛояльностьБонусы.Регистратор КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента
		|	КОНЕЦ = РасчетыСКлиентами.ЗаказКлиента
		|
		|ГДЕ
		|	ЕСТЬNULL(РасчетыСКлиентами.СуммаОстаток, 0) <= 0
		|
		|СГРУППИРОВАТЬ ПО 
		|	ЛояльностьБонусы.Партнер
		|";
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка.Бонусы, 0);
		
КонецФункции // ПолучитьБонусыПоПартнеру()

// Возвращает текст запроса бонусов по дополнительной программе
// 
// Параметры:
//  Нет
//
// Возвращаемое значение:
//    Строка - Текст запроса
//
Функция ПолучитьТекстЗапросаБонусовПоДополнительнойПрограмме(ФоновоеОбновление = Ложь)
	
	ТекстЗапроса = "" + ?(ФоновоеОбновление,
		////////////////////////////////////////////////////////////////////////////////
		// 0.
		
		"ВЫБРАТЬ
		|	Ссылка КАК Номенклатура
		|ПОМЕСТИТЬ ТабТовары
		|ИЗ
		|	Справочник.Номенклатура
		|ГДЕ
		|	ЭтоГруппа = Ложь
		|ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
		|;",
		
		"ВЫБРАТЬ
        |    Номенклатура
        |ПОМЕСТИТЬ ТабТовары
        |ИЗ
        |    Документ.УстановкаЦенНоменклатуры.Товары
        |ГДЕ
        |    Ссылка = &Ссылка
        |И 	 ВидЦены = &ВидЦены
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;" ) + "
		
		////////////////////////////////////////////////////////////////////////////////
		// 1.
		|ВЫБРАТЬ
		|	НоменклатураСегмента.Номенклатура КАК Номенклатура,
		|	НоменклатураСегмента.Сегмент КАК Сегмент,
		|	ЛояльностьАкции.РазмерБонуса,
		|	ЛояльностьАкции.РазмерБонусаСумма  
		|ПОМЕСТИТЬ СегментыТоваров
		|ИЗ
		|	РегистрСведений.Лояльность_Акции КАК ЛояльностьАкции
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|ПО	НоменклатураСегмента.Сегмент = ЛояльностьАкции.АкционныйСегмент
		|
		|ГДЕ
		|	ЛояльностьАкции.ПериодОт <= &ТекущаяДатаСеанса
		|И 	ЛояльностьАкции.ПериодДо >= &ТекущаяДатаСеанса
		|;
		////////////////////////////////////////////////////////////////////////////////
		// 2.
		|ВЫБРАТЬ
		|	ТабТовары.Номенклатура							КАК Номенклатура,
		|	IsNull(СегментыТоваров.РазмерБонуса, 0)			КАК РазмерБонуса,
		|	IsNull(СегментыТоваров.РазмерБонусаСумма, 0) 	КАК РазмерБонусаСумма
		|ПОМЕСТИТЬ КешОбновления
		|ИЗ
		|	ТабТовары КАК ТабТовары
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ СегментыТоваров КАК СегментыТоваров
		|ПО	ТабТовары.Номенклатура = СегментыТоваров.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ТабТовары.Номенклатура	
		|;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТабТовары;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СегментыТоваров;
		////////////////////////////////////////////////////////////////////////////////
		// 5.
        |ВЫБРАТЬ
        |	КешОбновления.Номенклатура												КАК Объект,
		|  	&Свойство                                                               КАК Свойство,
		|	ВЫБОР
		|		КОГДА КешОбновления.РазмерБонусаСумма <> 0
		|				И ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) <> 0
		|			ТОГДА КешОбновления.РазмерБонусаСумма
		|		КОГДА КешОбновления.РазмерБонуса <> 0
		|				И ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) <> 0
		|			ТОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) * КешОбновления.РазмерБонуса / 100
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Значение   
        |ИЗ
        |    КешОбновления КАК КешОбновления
        |        
        |ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, 
		|                                                                Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ КешОбновления)
        |                                                                И ВидЦены = &ВидЦены
        |                                                                ) КАК ЦеныНоменклатуры
        |ПО ЦеныНоменклатуры.Номенклатура = КешОбновления.Номенклатура
        |;
        ////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешОбновления;
		////////////////////////////////////////////////////////////////////////////////
		|";
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапросаБонусовПоДополнительнойПрограмме()

Функция ПолучитьДоступныеБонусыПоДокументу(ДокументСсылка, ВалютаКассы)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаРасчетаДоступныхБонусовПоДокументу();
	Запрос.УстановитьПараметр("Документ",		ДокументСсылка);
	Запрос.УстановитьПараметр("ВалютаДок",		ДокументСсылка.Валюта);
	Запрос.УстановитьПараметр("ВалютаКассы",	ВалютаКассы);
	Запрос.УстановитьПараметр("Партнер",		ДокументСсылка.Партнер);
	Запрос.УстановитьПараметр("ТекущаяДата", 	ТекущаяДата());
	Запрос.УстановитьПараметр("ВалютаРеглУчета",Константы.ВалютаРегламентированногоУчета.Получить());
    Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат ?(Выборка.Следующий(), Выборка.Бонус, 0);	
	
КонецФункции

Функция ПолучитьТекстЗапросаРасчетаДоступныхБонусовПоДокументу()      

	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВЫБОР
	               |		КОГДА &ВалютаКассы = &ВалютаРеглУчета
	               |			ТОГДА 1
	               |		ИНАЧЕ ДокКурс.Курс / ДокКурс.Кратность / (КурсКассы.Курс / КурсКассы.Кратность)
	               |	КОНЕЦ КАК Коэффициент
	               |ПОМЕСТИТЬ КоэффициентРеглУчета
	               |ИЗ
	               |	РегистрСведений.КурсыВалют.СрезПоследних(&ТекущаяДата, Валюта = &ВалютаРеглУчета) КАК ДокКурс
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ТекущаяДата, Валюта = &ВалютаКассы) КАК КурсКассы
	               |		ПО (ИСТИНА)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВЫБОР
	               |		КОГДА &ВалютаКассы = &ВалютаДок
	               |			ТОГДА 1
	               |		ИНАЧЕ ДокКурс.Курс / ДокКурс.Кратность / (КурсКассы.Курс / КурсКассы.Кратность)
	               |	КОНЕЦ КАК Коэффициент
	               |ПОМЕСТИТЬ КоефициентБонусов
	               |ИЗ
	               |	РегистрСведений.КурсыВалют.СрезПоследних(&ТекущаяДата, Валюта = &ВалютаДок) КАК ДокКурс
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ТекущаяДата, Валюта = &ВалютаКассы) КАК КурсКассы
	               |		ПО (ИСТИНА)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЛояльностьБонусы.Партнер,
	               |	СУММА(ЛояльностьБонусы.БонусПриход) - СУММА(ЛояльностьБонусы.БонусРасход) КАК Бонусы
	               |ПОМЕСТИТЬ БонусПартнера
	               |ИЗ
	               |	РегистрНакопления.Лояльность_Бонусы.ОстаткиИОбороты(
	               |			,
	               |			,
	               |			Регистратор,
	               |			,
	               |			Партнер = &Партнер
	               |				И ДействуетС < &ТекущаяДата) КАК ЛояльностьБонусы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами.Остатки(
	               |				,
	               |				АналитикаУчетаПоПартнерам В
	               |					(ВЫБРАТЬ
	               |						РегистрСведений.АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	               |					ИЗ
	               |						РегистрСведений.АналитикаУчетаПоПартнерам
	               |					ГДЕ
	               |						РегистрСведений.АналитикаУчетаПоПартнерам.Партнер = &Партнер)) КАК РасчетыСКлиентами
	               |		ПО (ВЫБОР
	               |				КОГДА ВЫРАЗИТЬ(ЛояльностьБонусы.Регистратор КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента = НЕОПРЕДЕЛЕНО
	               |					ТОГДА ВЫРАЗИТЬ(ЛояльностьБонусы.Регистратор КАК Документ.РеализацияТоваровУслуг)
	               |				КОГДА ВЫРАЗИТЬ(ЛояльностьБонусы.Регистратор КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	               |					ТОГДА ВЫРАЗИТЬ(ЛояльностьБонусы.Регистратор КАК Документ.РеализацияТоваровУслуг)
	               |				ИНАЧЕ ВЫРАЗИТЬ(ЛояльностьБонусы.Регистратор КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента
	               |			КОНЕЦ = РасчетыСКлиентами.ЗаказКлиента)
	               |ГДЕ
	               |	ЕСТЬNULL(РасчетыСКлиентами.СуммаОстаток, 0) <= 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЛояльностьБонусы.Партнер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА РасчетыСКлиентамиОстатки.ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	               |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентамиОстатки.ЗаказКлиента КАК Документ.ЗаказКлиента).СуммаДокумента * КоефициентБонусов.Коэффициент
	               |		КОГДА РасчетыСКлиентамиОстатки.ЗаказКлиента ССЫЛКА Документ.РеализацияТоваровУслуг
	               |			ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентамиОстатки.ЗаказКлиента КАК Документ.РеализацияТоваровУслуг).СуммаДокумента * КоефициентБонусов.Коэффициент
	               |	КОНЕЦ КАК Сумма,
	               |	РасчетыСКлиентамиОстатки.СуммаОстаток * КоефициентБонусов.Коэффициент КАК ОстатокКОплате,
	               |	РасчетыСКлиентамиОстатки.Валюта КАК Валюта,
	               |	РасчетыСКлиентамиОстатки.ЗаказКлиента КАК Документ,
	               |	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам.Партнер КАК Партнер
	               |ПОМЕСТИТЬ РасчетиПоДокументам
	               |ИЗ
	               |	РегистрНакопления.РасчетыСКлиентами.Остатки(
	               |			,
	               |			АналитикаУчетаПоПартнерам В
	               |					(ВЫБРАТЬ
	               |						АналитикаПоПартнерам.КлючАналитики
	               |					ИЗ
	               |						РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	               |					ГДЕ
	               |						АналитикаПоПартнерам.Партнер = &Партнер)
	               |				И ВЫБОР
	               |					КОГДА ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	               |						ТОГДА (ВЫРАЗИТЬ(ЗаказКлиента КАК Документ.ЗаказКлиента)) = &Документ
	               |					КОГДА ЗаказКлиента ССЫЛКА Документ.РеализацияТоваровУслуг
	               |						ТОГДА (ВЫРАЗИТЬ(ЗаказКлиента КАК Документ.РеализацияТоваровУслуг)) = &Документ
	               |				КОНЕЦ) КАК РасчетыСКлиентамиОстатки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КоефициентБонусов КАК КоефициентБонусов
	               |		ПО (ИСТИНА)
	               |ГДЕ
	               |	РасчетыСКлиентамиОстатки.СуммаОстаток > 0
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Партнер,
	               |	Документ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(РасчетыСКлиентамиОбороты.СуммаРасход) КАК СуммаРасходБонусов,
	               |	Лояльность_БонусыОстаткиИОбороты.Партнер КАК Партнер
	               |ПОМЕСТИТЬ БонусПартнераРасход
	               |ИЗ
	               |	РегистрНакопления.РасчетыСКлиентами.Обороты(
	               |			,
	               |			,
	               |			Регистратор,
	               |			АналитикаУчетаПоПартнерам В
	               |					(ВЫБРАТЬ
	               |						РегистрСведений.АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	               |					ИЗ
	               |						РегистрСведений.АналитикаУчетаПоПартнерам
	               |					ГДЕ
	               |						РегистрСведений.АналитикаУчетаПоПартнерам.Партнер = &Партнер)
	               |				И ВЫБОР
	               |					КОГДА ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	               |						ТОГДА ВЫРАЗИТЬ(ЗаказКлиента КАК Документ.ЗаказКлиента)
	               |					КОГДА ЗаказКлиента ССЫЛКА Документ.РеализацияТоваровУслуг
	               |						ТОГДА ВЫРАЗИТЬ(ЗаказКлиента КАК Документ.РеализацияТоваровУслуг)
	               |				КОНЕЦ = &Документ) КАК РасчетыСКлиентамиОбороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Лояльность_Бонусы.ОстаткиИОбороты(, , Регистратор, , Партнер = &Партнер) КАК Лояльность_БонусыОстаткиИОбороты
	               |		ПО РасчетыСКлиентамиОбороты.АналитикаУчетаПоПартнерам.Партнер = Лояльность_БонусыОстаткиИОбороты.Партнер
	               |			И (ВЫБОР
	               |				КОГДА РасчетыСКлиентамиОбороты.Регистратор ССЫЛКА Документ.СписаниеЗадолженности
	               |					ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентамиОбороты.Регистратор КАК Документ.СписаниеЗадолженности)
	               |			КОНЕЦ = ВЫБОР
	               |				КОГДА Лояльность_БонусыОстаткиИОбороты.Регистратор ССЫЛКА Документ.СписаниеЗадолженности
	               |					ТОГДА ВЫРАЗИТЬ(Лояльность_БонусыОстаткиИОбороты.Регистратор КАК Документ.СписаниеЗадолженности)
	               |			КОНЕЦ)
	               |ГДЕ
	               |	РасчетыСКлиентамиОбороты.СуммаРасход > 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Лояльность_БонусыОстаткиИОбороты.Партнер
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Партнер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	БонусПартнера.Партнер,
	               |	БонусПартнера.Бонусы * ЕСТЬNULL(КоэффициентРеглУчета.Коэффициент, 1) КАК Бонусы,
	               |	ЕСТЬNULL(БонусПартнераРасход.СуммаРасходБонусов, 0) * ЕСТЬNULL(КоэффициентРеглУчета.Коэффициент, 1) КАК РасходБонусов
	               |ПОМЕСТИТЬ БонуснаСистема
	               |ИЗ
	               |	БонусПартнера КАК БонусПартнера
	               |		ЛЕВОЕ СОЕДИНЕНИЕ БонусПартнераРасход КАК БонусПартнераРасход
	               |		ПО БонусПартнера.Партнер = БонусПартнераРасход.Партнер
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентРеглУчета КАК КоэффициентРеглУчета
	               |		ПО (ИСТИНА)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
				   |	РасчетиПоДокументам.Партнер,
				   |	ВЫБОР
				   |		КОГДА РасчетиПоДокументам.Сумма * 0.5 > РасчетиПоДокументам.ОстатокКОплате + ЕСТЬNULL(БонуснаСистема.РасходБонусов, 0)
				   |			ТОГДА РасчетиПоДокументам.ОстатокКОплате + ЕСТЬNULL(БонуснаСистема.РасходБонусов, 0)
				   |		ИНАЧЕ РасчетиПоДокументам.Сумма * 0.5
				   |	КОНЕЦ КАК ДопустимаяОплатаБонусами
				   |ПОМЕСТИТЬ СуммаКОплате
				   |ИЗ
			       |	РасчетиПоДокументам КАК РасчетиПоДокументам
				   |		ЛЕВОЕ СОЕДИНЕНИЕ БонуснаСистема КАК БонуснаСистема
				   |		ПО РасчетиПоДокументам.Партнер = БонуснаСистема.Партнер  
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СуммаКОплате.Партнер,
	               |	ВЫБОР
	               |		КОГДА СуммаКОплате.ДопустимаяОплатаБонусами - БонуснаСистема.РасходБонусов >= БонуснаСистема.Бонусы
	               |			ТОГДА БонуснаСистема.Бонусы
	               |		ИНАЧЕ СуммаКОплате.ДопустимаяОплатаБонусами - БонуснаСистема.РасходБонусов
	               |	КОНЕЦ КАК Бонус
	               |ИЗ
	               |	СуммаКОплате КАК СуммаКОплате
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БонуснаСистема КАК БонуснаСистема
	               |		ПО СуммаКОплате.Партнер = БонуснаСистема.Партнер";	
	Возврат ТекстЗапроса;	
	
КонецФункции

#КонецОбласти

#Область СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	//Версия обработки
	Версия = "2.0.59";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "LoyaltyLibrary");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "LoyaltyLibrary [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
	ДобавитьКоманду(ТаблицаКоманд, "LoyaltyLibrary: [" + Версия + "]", "Форма", "ОткрытиеФормы", Ложь, "LoyaltyLibrary");
	ДобавитьКоманду(ТаблицаКоманд, "LoyaltyLibrary: ОбновитьДопольнительниеСведенияИРеквизиты [" + Версия + "]", "ОбновитьДопольнительниеСведенияИРеквизиты()", "ВызовСерверногоМетода");
	ДобавитьКоманду(ТаблицаКоманд, "LoyaltyLibrary: Списать простроченные бонусы [" + Версия + "]", "СписотьПростроченныеБонусы()", "ВызовСерверногоМетода");
	
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

Процедура ВыполнитьКоманду(ИдентификаторКоманды) Экспорт
	Выполнить(ИдентификаторКоманды);	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииПереопределенияОбработчиков
	
// Только для внутреннего использования
Процедура ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, Форма, ИмяСобытияФормы = "", ПолноеИмяЭлементаФормы = "", НовоеДействие, ОбработкаИсключений = Ложь)

	Если ВнешнийОбъект = Неопределено Тогда
		git_ПереопределениеОбработчиковСервер.ПодменитьДействиеУправляемоеПриложение(Форма, ИмяСобытияФормы, ПолноеИмяЭлементаФормы, НовоеДействие, ОбработкаИсключений);
	Иначе
		ВнешнийОбъект.ПодменитьДействиеУправляемоеПриложение(Форма, ИмяСобытияФормы, ПолноеИмяЭлементаФормы, НовоеДействие, ОбработкаИсключений);
	КонецЕсли;
		
КонецПроцедуры

// Только для внутреннего использования
Процедура ПереместитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, ИмяЭлемента, ИмяРодителя, МестоРасположения)
	
	Если ВнешнийОбъект = Неопределено Тогда
		git_ПереопределениеОбработчиковСервер.ПереместитьЭлементВКоллекциюЭлементовФормы(Элементы, ИмяЭлемента, ИмяРодителя, МестоРасположения);
	Иначе
		ВнешнийОбъект.ПереместитьЭлементВКоллекциюЭлементовФормы(Элементы, ИмяЭлемента, ИмяРодителя, МестоРасположения);
	КонецЕсли;
	
КонецПроцедуры // ПереместитьЭлементВКоллекциюЭлементовФормы()

// Только для внутреннего использования
Процедура ПереместитьЭлементВКоллекциюЭлементовФормыБезПоиска(ВнешнийОбъект, Элементы, Элемент, Родитель, МестоРасположения = Неопределено)
	
	Если ВнешнийОбъект = Неопределено Тогда
		git_ПереопределениеОбработчиковСервер.ПереместитьЭлементВКоллекциюЭлементовФормыБезПоиска(Элементы, Элемент, Родитель, МестоРасположения);
	Иначе
		ВнешнийОбъект.ПереместитьЭлементВКоллекциюЭлементовФормыБезПоиска(Элементы, Элемент, Родитель, МестоРасположения);
	КонецЕсли;
	
КонецПроцедуры // ПереместитьЭлементВКоллекциюЭлементовФормыБезПоиска()


// Только для внутреннего использования
Функция ДобавитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, Параметры, Родитель = Неопределено) Экспорт
	
	Если ВнешнийОбъект = Неопределено Тогда
		Возврат git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, Параметры, Родитель);
	Иначе
		Возврат ВнешнийОбъект.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, Параметры, Родитель);
	КонецЕсли;
	
КонецФункции // ДобавитьЭлементВКоллекциюЭлементовФормы()

// Только для внутреннего использования
Функция ДобавитьКомандуВКоллекциюКомандФормы(ВнешнийОбъект, Команды, Параметры) 
		
	Если ВнешнийОбъект = Неопределено Тогда
		Возврат git_ПереопределениеОбработчиковСервер.ДобавитьКомандуВКоллекциюКомандФормы(Команды, Параметры);
	Иначе
		Возврат ВнешнийОбъект.ДобавитьКомандуВКоллекциюКомандФормы(Команды, Параметры);
	КонецЕсли;
		
КонецФункции // ДобавитьКомандуВКоллекциюКомандФормы()

#КонецОбласти



Функция ГенерироватьСлучайныеЧисла(Начало, Конец) Экспорт
	
	UID			= Новый УникальныйИдентификатор();
	UID 		= СтрЗаменить(UID,"-","");
	Значение 	= "";
	Для ind = 1 По СтрДлина(UID) Цикл
		Симв 		= Сред(UID, ind, 1);
		Значение 	= Значение + Прав(КодСимвола(Симв), 1);
	КонецЦикла;
	
	ГСЧ = Новый ГенераторСлучайныхЧисел(Число(Значение));
	Возврат ГСЧ.СлучайноеЧисло(Начало, Конец);
		
КонецФункции

Функция ГенерироватьПинДоступа() Экспорт
		
	UID			= Новый УникальныйИдентификатор();
	UID 		= СтрЗаменить(UID,"-","");
	Значение 	= "";
	Для ind = 1 По СтрДлина(UID) Цикл
		Симв 		= Сред(UID, ind, 1);
		Значение 	= Значение + Прав(КодСимвола(Симв), 1);
	КонецЦикла;
	
	ГСЧ = Новый ГенераторСлучайныхЧисел(Число(Значение));
	Пин = Формат(ГСЧ.СлучайноеЧисло(1000, 9999), "ЧГ=0");
	
	Возврат Пин;
	
КонецФункции

Функция ПолучитьМассивЭА(Лояльность_Событие) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ * ИЗ РегистрСведений.Лояльность_Оповещение ГДЕ Лояльность_Событие = &Парам";
	Запрос.УстановитьПараметр("Парам", Лояльность_Событие);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Кому = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Кому.Добавить(Новый Структура("Адрес, Представление", Выборка.Кому, Выборка.Представление));	
	КонецЦикла;
	
	Возврат Кому;
	
КонецФункции



Функция ВыборкаИнфоИзРгЛояльностьНомераТелефонов(Номер = Неопределено, Партнер = Неопределено, Основной = Неопределено) Экспорт
	
	ПараметрыНомер 		= "";
	ПараметрыПартнер 	= "";
	ПараметрыОсновной	= "";	
	
	Если Номер <> Неопределено Тогда
		ПараметрыНомер 		= " НомерТелефона = &Номер ";
	КонецЕсли;
	
	Если Партнер <> Неопределено Тогда
		ПараметрыПартнер 	= " Партнер = &Партнер ";	
	КонецЕсли;
	
	Если Основной <> Неопределено Тогда
		ПараметрыОсновной 	= " Основной = &Основной ";	
	КонецЕсли;

	Если НЕ ПустаяСтрока(ПараметрыНомер) Тогда
		ПараметрыОграничения = "ГДЕ " + ПараметрыНомер 	+ ?(ПустаяСтрока(ПараметрыПартнер), " ", " И " + ПараметрыПартнер) + ?(ПустаяСтрока(ПараметрыОсновной), " ", " И " + ПараметрыОсновной); 	
	ИначеЕсли НЕ ПустаяСтрока(ПараметрыПартнер) Тогда
		ПараметрыОграничения = "ГДЕ " + ПараметрыПартнер+ ?(ПустаяСтрока(ПараметрыОсновной), " ", " И " + ПараметрыОсновной);
	ИначеЕсли НЕ ПустаяСтрока(ПараметрыОсновной) Тогда
		ПараметрыОграничения = "ГДЕ " + ПараметрыОсновной;
	Иначе
		ПараметрыОграничения = "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ * ИЗ РегистрСведений.Лояльность_НомераТелефонов " + ПараметрыОграничения;
	Запрос.УстановитьПараметр("Номер", 		Номер);
	Запрос.УстановитьПараметр("Партнер", 	Партнер);
	Запрос.УстановитьПараметр("Основной", 	Основной);
	
	Возврат Запрос.Выполнить().Выбрать();	
	
КонецФункции



Функция ЛояльностьИспользуетьсяCДаты(Организация, ДатаПодключения) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ * ИЗ РегистрСведений.Лояльность_Настройки ГДЕ Организация = &Организация И ДатаПодключения < &ДатаПодключения");
	Запрос.УстановитьПараметр("Организация", 	 Организация);
	Запрос.УстановитьПараметр("ДатаПодключения", ДатаПодключения);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции



Функция СоглашениеРавноОрганизации(Соглашение, Организация) Экспорт
	
	Возврат Соглашение.Организация = Организация;	
	
КонецФункции




Функция РезультатЗапросаИнфоИзРгЛояльностьНомераТелефонов(Номер = Неопределено, Партнер = Неопределено) Экспорт
	
	Если Номер <> Неопределено И Партнер <> Неопределено Тогда
		ПараметрыОграничения = "ГДЕ НомерТелефона = &Номер И Партнер = &Партнер";	
	ИначеЕсли Номер <> Неопределено Тогда
		ПараметрыОграничения = "ГДЕ НомерТелефона = &Номер";
	ИначеЕсли Партнер <> Неопределено ТОгда
		ПараметрыОграничения = "ГДЕ Партнер = &Партнер";
	Иначе
		ПараметрыОграничения = "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ * ИЗ РегистрСведений.Лояльность_НомераТелефонов " + ПараметрыОграничения;
	Запрос.УстановитьПараметр("Номер", 		Номер);
	Запрос.УстановитьПараметр("Партнер", 	Партнер);	
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ПолучитьСписокНомеровТелефонов(Партнер) Экспорт
	
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.ЗагрузитьЗначения(РезультатЗапросаИнфоИзРгЛояльностьНомераТелефонов(, Партнер).Выгрузить().ВыгрузитьКолонку("НомерТелефона"));
	Возврат СписокЗначений;
	
КонецФункции



///////////////////////////////////////////////////// Проведение по лояльности ////////////////////////////////////////////////////////
//                                                                                                                                   //

Процедура ВыполнитьДвижения(Источник, Отказ, РежимПроведения = Неопределено) Экспорт
	                                               
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеДокумента			= Источник.Метаданные();		
	ДокументСсылка 				= Источник.Ссылка;
	Объект		   				= Источник;
	Движения       				= Источник.Движения;
	ДополнительныеСвойства 		= Источник.ДополнительныеСвойства;
	
	
	Если РежимПроведения = Неопределено Тогда  
		//Контроль не нужен так как механизм схож с механизмом взаиморасчетов	
		ВыполнитьУдалениеПроведенияДокумента(ДокументСсылка, Объект, Движения, ДополнительныеСвойства, Отказ);	
	Иначе		
		ВыполнитьПроведениеДокумента(ДокументСсылка, Объект, Движения, РежимПроведения, ДополнительныеСвойства, Отказ);		
	КонецЕсли;
			   
КонецПроцедуры

Процедура ВыполнитьПроведениеДокумента(ДокументСсылка, Объект, Движения, РежимПроведения, ДополнительныеСвойства, Отказ)
	
	Свойства = ИнициализироватьСвойстваДляПроведения(ДокументСсылка, РежимПроведения, ДополнительныеСвойства);		
	 	
	ТаблицаДвижений = ИнициализироватьДанныеДокумента(ДокументСсылка, Свойства, Отказ);
	Если ТаблицаДвижений.Количество() = 0 Тогда Возврат; КонецЕсли;

	ПодготовитьНаборыЗаписейКРегистрацииДвижений(Объект); 
	
	ОтразитьДвижения(ДополнительныеСвойства, Движения, ТаблицаДвижений, ДокументСсылка, Отказ);

	ЗаписатьНаборыЗаписей(Объект, Движения, Свойства);	
	
КонецПроцедуры

Процедура ВыполнитьУдалениеПроведенияДокумента(ДокументСсылка, Объект, Движения, ДополнительныеСвойства, Отказ)
//	
//	ДополнительныеСвойстваЛояльность = ИницДополнительныеСвойстваДляПроведения(ДокументСсылка, , ДополнительныеСвойства);
//	
//	ПодготовитьНаборыЗаписейКРегистрацииДвижений(Объект);
//	
//	ЗаписатьНаборыЗаписей(Объект, Движения, ДополнительныеСвойстваЛояльность);
//		
//	//ВыполнитьКонтрольРезультатовПроведения(Объект, ДополнительныеСвойстваЛояльность, Отказ);
//	
//	ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойстваЛояльность);
//	
КонецПроцедуры

Процедура ПроверитьВерноеКоличествоБонусов(ДокументСсылка,Отказ)
	    
	Если Отказ = Истина Тогда
	    Возврат;	
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаСписанияБонусов();
	Запрос.УстановитьПараметр("ДокументСсылка",ДокументСсылка);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() = Истина Тогда
		Возврат;	
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Бонус =   ПолучитьДоступныеБонусыПоДокументу(Выборка.Заказ,Выборка.ВалютаВзаиморасчетов);
		Если Бонус < Выборка.Сумма Тогда
			Ошибка = НСтр("ru = 'Для оплаты данного документа клиент может использовать %1 бонусов!'");
			Ошибка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Ошибка, Бонус);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибка,,,,Отказ);
			Возврат;	
		КонецЕсли;  
	КонецЦикла;     	
	
КонецПроцедуры

Функция ПолучитьТекстЗапросаСписанияБонусов()

	ТекстЗапроса = "ВЫБРАТЬ
	               |	СписаниеЗадолженностиЗадолженность.ТипРасчетов,
	               |	СписаниеЗадолженностиЗадолженность.Партнер,
	               |	СписаниеЗадолженностиЗадолженность.Заказ,
	               |	СписаниеЗадолженностиЗадолженность.ВалютаВзаиморасчетов,
	               |	СУММА(СписаниеЗадолженностиЗадолженность.Сумма) КАК Сумма
	               |ИЗ
	               |	Документ.СписаниеЗадолженности.Задолженность КАК СписаниеЗадолженностиЗадолженность
	               |ГДЕ
	               |	СписаниеЗадолженностиЗадолженность.Ссылка = &ДокументСсылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СписаниеЗадолженностиЗадолженность.ТипРасчетов,
	               |	СписаниеЗадолженностиЗадолженность.Партнер,
	               |	СписаниеЗадолженностиЗадолженность.Заказ,
	               |	СписаниеЗадолженностиЗадолженность.ВалютаВзаиморасчетов";
	
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаСписанияБонусов()



Функция ИнициализироватьСвойстваДляПроведения(ДокументСсылка, РежимПроведения = Неопределено, ДополнительныеСвойства)

	МетаданныеДокумента = ДокументСсылка.Метаданные();
	ИмяДокумента  	   	= МетаданныеДокумента.Имя;
	
	ДополнительныеСвойстваЛояльность = Новый Структура;
	ДополнительныеСвойстваЛояльность.Вставить("РежимЗаписи", 	ДополнительныеСвойства.РежимЗаписи);
	ДополнительныеСвойстваЛояльность.Вставить("ЭтоНовый", 		ДополнительныеСвойства.ЭтоНовый);
	ДополнительныеСвойстваЛояльность.Вставить("ИмяДокумента", 	ИмяДокумента);
	ДополнительныеСвойстваЛояльность.Вставить("Реквизиты", 		ПолучитьСтруктуруРеквизитовПоСсылке(ДокументСсылка, МетаданныеДокумента));
	
	// "ТаблицыДляДвижений" - структура, которая будет содержать таблицы значений с данными для выполнения движений.
	ДополнительныеСвойстваЛояльность.Вставить("ТаблицыДляДвижений", Новый Структура);

	// "ДляПроведения" - структура, содержащая свойства и реквизиты документа, необходимые для проведения.
	ДополнительныеСвойстваЛояльность.Вставить("ДляПроведения", Новый Структура);
	
	// Структура, содержащая ключ с именем "МенеджерВременныхТаблиц", в значении которого хранится менеджер временных таблиц.
	// Содержит для каждой временной таблицы ключ (имя временной таблицы) и значение (признак наличия записей во временной таблице).
	//ДополнительныеСвойстваЛояльность.ДляПроведения.Вставить("СтруктураВременныеТаблицы", Новый Структура("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц));
	ДополнительныеСвойстваЛояльность.ДляПроведения.Вставить("РежимПроведения",           РежимПроведения);
	ДополнительныеСвойстваЛояльность.ДляПроведения.Вставить("МетаданныеДокумента",       МетаданныеДокумента);
	ДополнительныеСвойстваЛояльность.ДляПроведения.Вставить("Ссылка",                    ДокументСсылка);
	
	Возврат ДополнительныеСвойстваЛояльность;
	
КонецФункции

Функция ПолучитьСтруктуруРеквизитовПоСсылке(ДокументСсылка, МетаданныеДокумента)
	
	Пользователь 	= ПараметрыСеанса.ТекущийПользователь; 
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Период", 						ДокументСсылка.Дата);
	СтруктураРеквизитов.Вставить("Ссылка", 						ДокументСсылка);
	СтруктураРеквизитов.Вставить("Организация", 				ДокументСсылка.Организация);
	СтруктураРеквизитов.Вставить("Подразделение", 				Пользователь.ТекущееПодразделение);
	СтруктураРеквизитов.Вставить("Партнер", 					ДокументСсылка.Контрагент.Партнер);
	СтруктураРеквизитов.Вставить("ВалютаРегламентногоУчета", 	Константы.ВалютаРегламентированногоУчета.Получить());
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

Функция ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Отказ)
	
	МетаданныеДокумента 		= ДополнительныеСвойства.ДляПроведения.МетаданныеДокумента;
	ИмяДокумента	    		= ДополнительныеСвойства.ИмяДокумента;
	Реквизиты 					= ДополнительныеСвойства.Реквизиты;
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Реквизиты.Организация);
	МассивПараметров.Добавить(Реквизиты.Период);
	ЛояльностьИспользуеться = git_ПереопределениеОбработчиковСервер.ВыполнитьПеренаправлениеВызоваФункции("LoyaltyLibrary", 
																										"git_СистемаЛояльностиСервер", 
																										"ЛояльностьИспользуетьсяCДаты", 
																										МассивПараметров);
	Если ЛояльностьИспользуеться = Ложь Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", 					Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("Период",     				Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",    			Реквизиты.Организация);
	Запрос.УстановитьПараметр("Подразделение",    			Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("Партнер",    				Реквизиты.Партнер);
	Запрос.УстановитьПараметр("ВалютаРегламентногоУчета",   Реквизиты.ВалютаРегламентногоУчета);
	
	Если 		ИмяДокумента = "РеализацияТоваровУслуг" Тогда
		КСписанияЗадолженности = ПолучитьКоэффициентСписания(ДокументСсылка);
		
		Запрос.Текст = ПолучитьТекстЗапросаДляРеализацияТоваровУслуг();
		Запрос.УстановитьПараметр("СписанияЗадолженности", 	КСписанияЗадолженности);
		Запрос.УстановитьПараметр("ЭтоОплатаБонусами", ?(КСписанияЗадолженности = 1, 1, 0));
		Запрос.УстановитьПараметр("ЭтотДокументРанееИзменений22122014", Реквизиты.Период < Дата(2014, 12, 22));
		Возврат Запрос.Выполнить().Выгрузить();	
	ИначеЕсли 	ИмяДокумента = "ВозвратТоваровОтКлиента" Тогда
		Запрос.УстановитьПараметр("ДокументРеализации", ДокументСсылка.ДокументРеализации);
		ТекстЗапроса = ПолучитьТекстЗапросаДляВозвратТоваровОтКлиента();
	ИначеЕсли   ИмяДокумента = "СписаниеЗадолженности" Тогда
		Если ДокументСсылка.ТипЗадолженности = Перечисления.ТипыЗадолженности.Кредиторская Тогда	
			Возврат Новый ТаблицаЗначений;
		КонецЕсли;
		ТекстЗапроса = ПолучитьТекстЗапросаДляСписаниеЗадолженности();	
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции

Функция ПолучитьТекстЗапросаДляРеализацияТоваровУслуг()
	
	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////////////////
		//0. Кеш аналитики учета по партнерам
		|ВЫБРАТЬ
		|	Ссылка КАК КлючАналитики	
		|ПОМЕСТИТЬ АУП
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаПоПартнерам 
		|ГДЕ
		|	Партнер = &Партнер
		|ИНДЕКСИРОВАТЬ ПО
		|	КлючАналитики
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		//1. Временная таблица оборота клиента
		|ВЫБРАТЬ
		|	ОборотыСКлиентами.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
		|   Сумма(
		|		ВЫБОР
		|			КОГДА Курсы.Валюта = &ВалютаРегламентногоУчета
		|			ТОГДА СуммаОборот
		|			ИНАЧЕ ВЫРАЗИТЬ(СуммаОборот 	* ((Курсы.Курс / Курсы.Кратность) / (ЗапросВалютРегламент.Курс / ЗапросВалютРегламент.Кратность)) КАК Число (15,2))
		|		  КОНЕЦ
		|		 ) + ЕСТЬNULL(ДопОборот.Сумма, 0) КАК ОборотПоПартнеру
		|
		|ПОМЕСТИТЬ ВтТаблицаОборот
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами.Обороты(, &Период, Регистратор, АналитикаУчетаПоПартнерам В (ВЫБРАТЬ КлючАналитики ИЗ АУП)) КАК ОборотыСКлиентами 
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ АУП КАК Аналитика
		|ПО Аналитика.КлючАналитики = ОборотыСКлиентами.АналитикаУчетаПоПартнерам
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &ВалютаРегламентногоУчета) КАК ЗапросВалютРегламент
		|ПО ИСТИНА
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период) КАК Курсы
		|ПО ОборотыСКлиентами.Валюта = Курсы.Валюта
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Лояльность_НачальныйОборот КАК ДопОборот
		|ПО ДопОборот.Партнер = &Партнер
		|И  ДопОборот.Дата   <= &Период 
		|
		|ГДЕ
		|		Регистратор Ссылка Документ.РеализацияТоваровУслуг
		|ИЛИ	Регистратор Ссылка Документ.ВозвратТоваровОтКлиента 
		|	
		|СГРУППИРОВАТЬ ПО 
		|	АналитикаУчетаПоПартнерам.Партнер, 
		|	ДопОборот.Сумма
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 2.
		|ВЫБРАТЬ
		//|	КодСтроки,
		|	ВидЦены,
		|	Номенклатура,
		|	СУММА(Количество) 																		КАК Количество,
		|	СРЕДНЕЕ(Цена * (1 - (ПроцентРучнойСкидки * 0.01 + ПроцентАвтоматическойСкидки * 0.01))) КАК Цена,
		|	СУММА(Цена * Количество / 100) 															КАК ОдинПроцент,
		|	СРЕДНЕЕ(ПроцентРучнойСкидки)															КАК ПроцентРучнойСкидки,
		|	СРЕДНЕЕ(ПроцентАвтоматическойСкидки)													КАК ПроцентАвтоматическойСкидки,
		|	Ссылка,
		|	Ссылка.Валюта																			КАК Валюта
		|
		|ПОМЕСТИТЬ ТабТовары
		|ИЗ 
		|	Документ.РеализацияТоваровУслуг.Товары 
		|ГДЕ 
		|	Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО  
		|	Номенклатура, 
		|	ВидЦены, 
		|	Ссылка 
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	ВидЦены
		|;
		////////////////////////////////////////////////////////////////////////////////
		// 3. 
		|ВЫБРАТЬ
		|	НоменклатураСегмента.Сегмент КАК Сегмент
		|ПОМЕСТИТЬ СегментыДокумента	
		|ИЗ
		|	ТабТовары КАК ТабТовары
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|ПО	НоменклатураСегмента.Номенклатура = ТабТовары.Номенклатура
		|
		|ОБЪЕДИНИТЬ 
		|
		|ВЫБРАТЬ
		|	Значение(Справочник.СегментыНоменклатуры.ПустаяСсылка)
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	Сегмент	
		|;	
		////////////////////////////////////////////////////////////////////////////////////////////
		// 4.
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЛояльностьАкции.АкционныйСегмент,
		|	ЛояльностьАкции.Сегмент1,
		|	ЛояльностьАкции.Сегмент2,
		|	ЛояльностьАкции.Сегмент3,
		|	ЛояльностьАкции.Сегмент4,
		|	ЛояльностьАкции.РазмерБонуса,
		|   ЛояльностьАкции.РазмерБонусаСумма,
		|	ЛояльностьАкции.ДействителенДней
		|
		|ПОМЕСТИТЬ КешАкцийДополнительный
		|ИЗ
		|	РегистрСведений.Лояльность_Акции КАК ЛояльностьАкции
		|
		|ГДЕ
		|	ЛояльностьАкции.ПериодОт 					 <= &Период
		|И 	КОНЕЦПЕРИОДА(ЛояльностьАкции.ПериодДо, ДЕНЬ) >= &Период
		|И	ЛояльностьАкции.АкционныйСегмент В (ВЫБРАТЬ Сегмент ИЗ СегментыДокумента)
		|И	ЛояльностьАкции.Сегмент1 		В (ВЫБРАТЬ Сегмент ИЗ СегментыДокумента)
		|И	ЛояльностьАкции.Сегмент2 		В (ВЫБРАТЬ Сегмент ИЗ СегментыДокумента)
		|И	ЛояльностьАкции.Сегмент3 		В (ВЫБРАТЬ Сегмент ИЗ СегментыДокумента)
		|И	ЛояльностьАкции.Сегмент4 		В (ВЫБРАТЬ Сегмент ИЗ СегментыДокумента)
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 5.
		|ВЫБРАТЬ
		|	НоменклатураСегмента.Номенклатура 			КАК Номенклатура,	
		|   КешАкцийДополнительный.РазмерБонуса 		КАК Значение,
		|   КешАкцийДополнительный.РазмерБонусаСумма 	КАК ЗначениеСумма,
		|	КешАкцийДополнительный.ДействителенДней 	КАК ДействителенДней
		|ПОМЕСТИТЬ КешНоменклатурыКНачислению
		|ИЗ
		|	КешАкцийДополнительный КАК КешАкцийДополнительный
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|ПО	НоменклатураСегмента.Сегмент = КешАкцийДополнительный.АкционныйСегмент
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	НоменклатураСегмента.Номенклатура,	
		|   0,
		|   0,
		|	0
		|ИЗ
		|	КешАкцийДополнительный КАК КешАкцийДополнительный
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|ПО	НоменклатураСегмента.Сегмент = КешАкцийДополнительный.Сегмент1
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	НоменклатураСегмента.Номенклатура,	
		|   0,
		|   0,
		|	0
		|ИЗ
		|	КешАкцийДополнительный КАК КешАкцийДополнительный
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|ПО	НоменклатураСегмента.Сегмент = КешАкцийДополнительный.Сегмент2
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	НоменклатураСегмента.Номенклатура,	
		|   0,
		|   0,
		|	0
		|ИЗ
		|	КешАкцийДополнительный КАК КешАкцийДополнительный
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|ПО	НоменклатураСегмента.Сегмент = КешАкцийДополнительный.Сегмент3
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	НоменклатураСегмента.Номенклатура,	
		|   0,
		|   0,
		|	0
		|ИЗ
		|	КешАкцийДополнительный КАК КешАкцийДополнительный
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|ПО	НоменклатураСегмента.Сегмент = КешАкцийДополнительный.Сегмент4
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СегментыДокумента;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешАкцийДополнительный;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 8. 
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 	КАК ВидДвижения,
		|	&Период	 								КАК Период,
		|	&Организация                            КАК Организация,
		|   &Подразделение                          КАК Подразделение,
		|	&Партнер 								КАК Партнер,
		|
		//|	ТабТовары.КодСтроки,
		|	ТабТовары.Номенклатура,
		|
		|	ВЫБОР
		|		КОГДА &ЭтоОплатаБонусами = 0
		|			ТОГДА 0
	  	|
		|      	КОГДА ТабТовары.ВидЦены НЕ В (ВЫБРАТЬ ВидЦеныБонус ИЗ РегистрСведений.УчетнаяПолитикаБонусы.СрезПоследних(&Период))
		|			ТОГДА 0
		|
		
		// Дополнительное уточнение акций
		|		КОГДА IsNull(КешНоменклатурыКНачислению.Номенклатура, Неопределено) <> Неопределено
		|   		ТОГДА ВЫБОР
		|   					КОГДА ТабТовары.Валюта = ЗапросКурсРегламент.Валюта						
		|   						 ТОГДА ВЫБОР
		|   							КОГДА ISNULL(КешНоменклатурыКНачислению.ЗначениеСумма,0)=0     ТОГДА
		|   								ТабТовары.ОдинПроцент * ЕСТЬNULL(КешНоменклатурыКНачислению.Значение, 0) * &ЭтоОплатаБонусами
		|   							ИНАЧЕ							
		|   								ЕСТЬNULL(КешНоменклатурыКНачислению.ЗначениеСумма, 0) * &ЭтоОплатаБонусами
		|     							КОНЕЦ						
		|   					ИНАЧЕ 	ВЫБОР
		|   							КОГДА ISNULL(КешНоменклатурыКНачислению.ЗначениеСумма,0)=0     ТОГДА
		|   								ТабТовары.ОдинПроцент * ЕСТЬNULL(КешНоменклатурыКНачислению.Значение, 0) * &ЭтоОплатаБонусами* (ЗапросКурсДок.Курс / ЗапросКурсДок.Кратность) / (ЗапросКурсРегламент.Курс / ЗапросКурсРегламент.Кратность)
		|   							ИНАЧЕ							
		|   								ЕСТЬNULL(КешНоменклатурыКНачислению.ЗначениеСумма, 0) * &ЭтоОплатаБонусами* (ЗапросКурсДок.Курс / ЗапросКурсДок.Кратность) / (ЗапросКурсРегламент.Курс / ЗапросКурсРегламент.Кратность)
		|   	  						КОНЕЦ	
		|   				КОНЕЦ  	
		|
		|		КОГДА ЕСТЬNULL(ТаблицаОбПроцент.Значение, 0) = 0
		|		ТОГДА 0
		|
		|   	КОГДА ТабТовары.Номенклатура.ЦеноваяГруппа = УчПолБонусы.АкционнаяЦеноваяГруппа
		|		 И НЕ УчПолБонусы.ЕстьСкидкиДляАкций
		|		ТОГДА 0
		|
		|		ИНАЧЕ    	
		|			ВЫБОР 
		//|				КОГДА ТабТовары.ПроцентРучнойСкидки = 0 И ТабТовары.ПроцентАвтоматическойСкидки = 0
		|				КОГДА ТабТовары.ПроцентАвтоматическойСкидки = 0
		|				ТОГДА
		|					ВЫБОР
		|						КОГДА ТабТовары.Валюта = ЗапросКурсРегламент.Валюта
		|       				ТОГДА ТабТовары.ОдинПроцент * ЕСТЬNULL(ТаблицаОбПроцент.Значение, 0) * &ЭтоОплатаБонусами
		|						ИНАЧЕ ТабТовары.ОдинПроцент * ЕСТЬNULL(ТаблицаОбПроцент.Значение, 0) * &ЭтоОплатаБонусами * (ЗапросКурсДок.Курс / ЗапросКурсДок.Кратность) / (ЗапросКурсРегламент.Курс / ЗапросКурсРегламент.Кратность)
		|					КОНЕЦ
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ КАК Бонус,
		|	ВЫБОР
		|		КОГДА ТабТовары.Валюта = ЗапросКурсРегламент.Валюта
		|		ТОГДА ТабТовары.Цена 
		|		ИНАЧЕ ТабТовары.Цена * (ЗапросКурсДок.Курс / ЗапросКурсДок.Кратность) / (ЗапросКурсРегламент.Курс / ЗапросКурсРегламент.Кратность)
		|	КОНЕЦ КАК Цена,
		|	ТабТовары.Количество,
		|   (ВЫБОР
		|		КОГДА ТабТовары.Валюта = ЗапросКурсРегламент.Валюта
		|		ТОГДА ТабТовары.Цена
		|		ИНАЧЕ ТабТовары.Цена * (ЗапросКурсДок.Курс / ЗапросКурсДок.Кратность) / (ЗапросКурсРегламент.Курс / ЗапросКурсРегламент.Кратность)
		|	КОНЕЦ * ТабТовары.Количество) * (1 - &СписанияЗадолженности) 	КАК ОплаченоБонусами,
		|   (ВЫБОР
		|		КОГДА ТабТовары.Валюта = ЗапросКурсРегламент.Валюта
		|		ТОГДА ТабТовары.Цена
		|		ИНАЧЕ ТабТовары.Цена * (ЗапросКурсДок.Курс / ЗапросКурсДок.Кратность) / (ЗапросКурсРегламент.Курс / ЗапросКурсРегламент.Кратность)
		|	КОНЕЦ * ТабТовары.Количество) * &СписанияЗадолженности		 	КАК ОплаченоНаличными,
		|	
		|	ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, 21) КАК ДействуетС,
		|
		|	ВЫБОР
		|		КОГДА &ЭтотДокументРанееИзменений22122014 = Истина
		|		ТОГДА 0
		|		КОГДА IsNull(КешНоменклатурыКНачислению.ДействителенДней, 0) = 0
		|		ТОГДА ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, 90 + 21)
		|		ИНАЧЕ ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, КешНоменклатурыКНачислению.ДействителенДней + 21)
		|	КОНЕЦ КАК ДействуетДо
		|
		|ИЗ
		|	ТабТовары КАК ТабТовары	
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешНоменклатурыКНачислению КАК КешНоменклатурыКНачислению
		|ПО КешНоменклатурыКНачислению.Номенклатура = ТабТовары.Номенклатура
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБонусы.СрезПоследних(&Период) КАК УчПолБонусы
		|ПО Истина
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, Номенклатура  В (ВЫБРАТЬ Номенклатура ИЗ ТабТовары) 
		|																		И ВидЦены В (ВЫБРАТЬ ВидЦены ИЗ РегистрСведений.УчетнаяПолитикаБонусы.СрезПоследних(&Период))) КАК ВходнаяЦена
		|ПО ВходнаяЦена.Номенклатура = ТабТовары.Номенклатура
		|
	    |ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &ВалютаРегламентногоУчета) КАК ЗапросКурсРегламент
		|ПО Истина
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период) КАК ЗапросКурсВходнаяЦена
		|ПО ЗапросКурсВходнаяЦена.Валюта = ВходнаяЦена.Валюта
	    |
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период) КАК ЗапросКурсДок
		|ПО ЗапросКурсДок.Валюта = ТабТовары.Ссылка.Валюта
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаОборот КАК ТаблицаОборот
		|ПО Истина
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СкидкаПоОборотуБонусы КАК ТаблицаОбПроцент
		|ПО ТаблицаОбПроцент.НижняяГраница 	<= 	ТаблицаОборот.ОборотПоПартнеру
		|И	(ТаблицаОбПроцент.ВерхняяГраница > 	ТаблицаОборот.ОборотПоПартнеру 
		|ИЛИ ТаблицаОбПроцент.ВерхняяГраница = 	0)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДинамическаяСкидкаБонусы КАК ТаблицаДинПроцент
		|ПО ТаблицаДинПроцент.НижняяГраница <= ВЫБОР 
		|											КОГДА ЕСТЬNULL(ВходнаяЦена.Цена, 0) 	= 0 
		|											 ИЛИ  ЕСТЬNULL(ТабТовары.Количество, 0) = 0
		|											 ИЛИ  &ЭтоОплатаБонусами				= 0
		|											ТОГДА 0
		|											ИНАЧЕ 
		|		   									  (
		|												ВЫБОР
		|													КОГДА ЗапросКурсДок.Валюта = ЗапросКурсВходнаяЦена.Валюта 
		|													ТОГДА (ТабТовары.Цена / ВходнаяЦена.Цена - 1) * 100
		|													ИНАЧЕ (ТабТовары.Цена * ((ЗапросКурсДок.Курс / ЗапросКурсДок.Кратность) / (ЗапросКурсВходнаяЦена.Курс / ЗапросКурсВходнаяЦена.Кратность)) / ВходнаяЦена.Цена - 1) * 100
		|												КОНЕЦ
		|											   )
		|										КОНЕЦ 
		|И (ТаблицаДинПроцент.ВерхняяГраница >  ВЫБОР 
		|											КОГДА ЕСТЬNULL(ВходнаяЦена.Цена, 0) 	= 0 
		|											 ИЛИ  ЕСТЬNULL(ТабТовары.Количество, 0) = 0
		|											 ИЛИ  &ЭтоОплатаБонусами				= 0
		|											ТОГДА 0
		|											ИНАЧЕ 
		|		   									  (
		|												ВЫБОР
		|													КОГДА ЗапросКурсДок.Валюта = ЗапросКурсВходнаяЦена.Валюта 
		|													ТОГДА (ТабТовары.Цена / ВходнаяЦена.Цена - 1) * 100
		|													ИНАЧЕ (ТабТовары.Цена * ((ЗапросКурсДок.Курс / ЗапросКурсДок.Кратность) / (ЗапросКурсВходнаяЦена.Курс / ЗапросКурсВходнаяЦена.Кратность)) / ВходнаяЦена.Цена - 1) * 100
		|												КОНЕЦ
		|											   )
		|										КОНЕЦ
		|ИЛИ ТаблицаДинПроцент.ВерхняяГраница = 0)
	    |
		|ГДЕ
		|	&Партнер НЕ В (ВЫБРАТЬ РозничныйКлиент ИЗ РегистрСведений.Лояльность_Настройки)
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешНоменклатурыКНачислению;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТабТовары;
		|";
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьТекстЗапросаДляВозвратТоваровОтКлиента()
	
	ТекстЗапроса = "
	//0. Временная таблица оборота клиента
	|ВЫБРАТЬ
	|	&ДокументРеализации КАК Документ,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 	КАК ВидДвижения,		
	|	&Период	 								КАК Период,
	|	&Организация                            КАК Организация,
	|   &Подразделение                          КАК Подразделение,
	|	&Партнер 								КАК Партнер,
	|	Продажи.Номенклатура,
	|	Продажи.Характеристика,
	|	Продажи.Бонус * ВозвратТовары.Количество / Продажи.Количество КАК Бонус,
	|	Продажи.Цена,
	|	ВозвратТовары.Количество,
	|   Продажи.ОплаченоБонусами * ВозвратТовары.Количество / Продажи.Количество КАК ОплаченоБонусами,
	|   Продажи.ОплаченоНаличными* ВозвратТовары.Количество / Продажи.Количество КАК ОплаченоНаличными,
	|   Продажи.ДействуетС						КАК ДействуетС,
	|	Продажи.ДействуетДо					 	КАК ДействуетДо
	|
	|ИЗ
	|	РегистрСведений.Лояльность_Продажи КАК Продажи
	|
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ВозвратТовары 
	|ПО ВозвратТовары.Ссылка		 = &Ссылка
	|И  ВозвратТовары.Номенклатура 	 = Продажи.Номенклатура
	|И  ВозвратТовары.Характеристика = Продажи.Характеристика
	|
	|ГДЕ
	|	ВЫРАЗИТЬ(Продажи.Регистратор КАК Документ.РеализацияТоваровУслуг) = &ДокументРеализации	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДокументРеализации КАК Документ,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),		
	|	&Период,
	|	NULL,
	|   NULL,
	|	&Партнер,
	|	Продажи.Номенклатура,
	|	Продажи.Характеристика,
	|	Продажи.ОплаченоБонусами * ВозвратТовары.Количество / Продажи.Количество,
	|	Продажи.Цена,
	|	ВозвратТовары.Количество,
	|   0,
	|   0,
	|	NULL,
	|   NULL
	|ИЗ
	|	РегистрСведений.Лояльность_Продажи КАК Продажи
	|
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ВозвратТовары 
	|ПО ВозвратТовары.Ссылка		 = &Ссылка
	|И  ВозвратТовары.Номенклатура 	 = Продажи.Номенклатура
	|И  ВозвратТовары.Характеристика = Продажи.Характеристика
	|
	|ГДЕ
	|	ВЫРАЗИТЬ(Продажи.Регистратор КАК Документ.РеализацияТоваровУслуг) = &ДокументРеализации 
	|И	Продажи.ОплаченоБонусами > 0
	|";
	Возврат ТекстЗапроса;

КонецФункции

Функция ПолучитьТекстЗапросаДляСписаниеЗадолженности()
	
	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	Партнер,
		|	ДействуетС,
		|	ДействуетДо,
		|	БонусОстаток
		|ПОМЕСТИТЬ КешБонусов
		|ИЗ
		|	РегистрНакопления.Лояльность_Бонусы.Остатки(, Партнер = &Партнер)
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	КешБонусов.Партнер,
		|	КешБонусов.ДействуетС,
		|	КешБонусов.ДействуетДо,
		|	КешБонусов.БонусОстаток,
		|	Сумма(КешБонусовКопия.БонусОстаток) 							КАК СуммаПосле,
		|	Сумма(КешБонусовКопия.БонусОстаток) - КешБонусов.БонусОстаток 	КАК СуммаДо
		|ПОМЕСТИТЬ КешБонусовНарастающие	
		|ИЗ
		|	КешБонусов КАК КешБонусов
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешБонусов КАК КешБонусовКопия
		|ПО	КешБонусов.ДействуетДо >= КешБонусовКопия.ДействуетДо
		|
		|СГРУППИРОВАТЬ ПО
		|	КешБонусов.Партнер,
		|	КешБонусов.ДействуетС,
		|	КешБонусов.ДействуетДо,
		|	КешБонусов.БонусОстаток	
	    |;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешБонусов;
		////////////////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 	КАК ВидДвижения,
		|	&Период								 	КАК Период,
		|	Задолженность.Партнер 					КАК Партнер,		
		|   ВЫБОР
		|   	КОГДА Задолженность.ВалютаВзаиморасчетов = &ВалютаРегламентногоУчета
		|       ТОГДА Задолженность.Сумма
		|       ИНАЧЕ ВЫРАЗИТЬ(Задолженность.Сумма * ((Курсы.Курс / Курсы.Кратность) / (ЗапросВалютРегламент.Курс / ЗапросВалютРегламент.Кратность)) КАК Число (15,2)) 
		|	КОНЕЦ 									КАК Бонус,
		|	Задолженность.Заказ
		|ПОМЕСТИТЬ ПредДвижения
		|ИЗ
		|	Документ.СписаниеЗадолженности.Задолженность КАК Задолженность
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &ВалютаРегламентногоУчета) КАК ЗапросВалютРегламент
		|ПО Истина
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ  РегистрСведений.КурсыВалют.СрезПоследних(&Период) КАК Курсы
		|ПО Задолженность.ВалютаВзаиморасчетов = Курсы.Валюта
        |
		|ГДЕ
		|	Задолженность.Ссылка					= &Ссылка
		|И	Задолженность.Ссылка.ЭтоОплатаБонусами 	= Истина
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ПредДвижения.ВидДвижения,
		|	ПредДвижения.Период,
		|	ПредДвижения.Партнер,
		|	КешБонусовНарастающие.ДействуетС,
		|	КешБонусовНарастающие.ДействуетДо,
		|	ВЫБОР
		|		КОГДА ПредДвижения.Бонус > КешБонусовНарастающие.СуммаПосле
		|		ТОГДА КешБонусовНарастающие.БонусОстаток
		|		ИНАЧЕ ПредДвижения.Бонус - КешБонусовНарастающие.СуммаДо
		|	КОНЕЦ КАК Бонус,
		|	Заказ
		|ИЗ
		|	ПредДвижения КАК ПредДвижения
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешБонусовНарастающие КАК КешБонусовНарастающие
		|По КешБонусовНарастающие.СуммаДо <= ПредДвижения.Бонус
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешБонусовНарастающие;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПредДвижения;
		|";
	Возврат ТекстЗапроса;

КонецФункции

Функция ПолучитьКоэффициентСписания(ДокументСсылка)
	
	Если 		ДокументСсылка.ЗаказКлиента = Неопределено Тогда
		ЗаказКлиента = ДокументСсылка;
	ИначеЕсли 	ДокументСсылка.ЗаказКлиента.Пустая() Тогда
		ЗаказКлиента = ДокументСсылка;
	Иначе
		ЗаказКлиента = ДокументСсылка.ЗаказКлиента;
	КонецЕсли;
	                        
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|		КОГДА &ВалютаРегламентногоУчета = РасчетыСКлиентами.Валюта
		|		ТОГДА РасчетыСКлиентами.Сумма
		|		ИНАЧЕ ВЫРАЗИТЬ(РасчетыСКлиентами.Сумма * ((Курсы.Курс / Курсы.Кратность) / (ЗапросВалютРегламент.Курс / ЗапросВалютРегламент.Кратность)) КАК Число (15,2))
		|	КОНЕЦ), 0) КАК Сумма
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &ВалютаРегламентногоУчета) КАК ЗапросВалютРегламент
		|ПО Истина
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ  РегистрСведений.КурсыВалют.СрезПоследних(&Период) КАК Курсы
		|ПО РасчетыСКлиентами.Валюта = Курсы.Валюта
		|
		|ГДЕ 
		|	Регистратор Ссылка Документ.СписаниеЗадолженности
		|И	ЗаказКлиента = &ЗаказКлиента
		|И	ВЫРАЗИТЬ(Регистратор КАК Документ.СписаниеЗадолженности).ЭтоОплатаБонусами");
	Запрос.УстановитьПараметр("ЗаказКлиента", 				ЗаказКлиента);
	Запрос.УстановитьПараметр("ВалютаРегламентногоУчета", 	Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("Период", 					ЗаказКлиента.Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат 1 - ?(Выборка.Сумма > 0, Выборка.Сумма, -Выборка.Сумма) / ДокументСсылка.СуммаДокумента;
	Иначе
		Возврат 1;
	КонецЕсли;
	
КонецФункции



Процедура ПодготовитьНаборыЗаписейКРегистрацииДвижений(Объект)
	
	ЕстьДвиженияПродажи = НЕ Объект.Движения.Найти("Лояльность_Продажи") = Неопределено;
	
	Если ЕстьДвиженияПродажи Тогда
		Если Объект.Движения.Лояльность_Продажи.Количество() > 0 Тогда
			Объект.Движения.Лояльность_Продажи.Очистить();
		КонецЕсли;
	КонецЕсли;

	Если Объект.Движения.Лояльность_Бонусы.Количество() > 0 Тогда
		Объект.Движения.Лояльность_Бонусы.Очистить();
	КонецЕсли;
	
	Если Не Объект.ДополнительныеСвойства.ЭтоНовый Тогда
		// Регистры, движения по которым формируются не из модуля менеджера документа.
		Если ЕстьДвиженияПродажи Тогда
       		Объект.Движения.Лояльность_Продажи.Записывать = Истина;
		КонецЕсли;
		Объект.Движения.Лояльность_Бонусы.Записывать 	  = Истина;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОтразитьДвижения(ДополнительныеСвойства, Движения, Таблица, ДокументСсылка, Отказ) 
	
	ЕстьДвиженияПродажи = НЕ Движения.Найти("Лояльность_Продажи") = Неопределено;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Если ЕстьДвиженияПродажи Тогда
		Движения.Лояльность_Продажи.Записывать = Истина;
		Движения.Лояльность_Продажи.Загрузить(Таблица);
	КонецЕсли;
	
	Движения.Лояльность_Бонусы.Записывать = Истина;	
	Если ЕстьДвиженияПродажи Тогда
		Таблица.Свернуть("ВидДвижения, Период, Партнер, ДействуетС, ДействуетДо", "Бонус");
		Движения.Лояльность_Бонусы.Загрузить(Таблица);
		Если Таблица.Количество() = 2 Тогда 
			ОтразитьВозвратБонусныхСредств(ДокументСсылка, Движения, Таблица[1]);//ДополнительныеСвойства, Движения, Таблица[1], ДокументСсылка.ДокументРеализации) 
		КонецЕсли;
	Иначе
		Движения.Лояльность_Бонусы.Загрузить(Таблица);
		Для Каждого Строка ИЗ Таблица Цикл
			Если НЕ Строка.Заказ.Пустая() Тогда 
				Если ТипЗнч(Строка.Заказ) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
				 	Запрос = Новый Запрос;
					Запрос.Текст = "
						|ВЫБРАТЬ
						|	Ссылка
						|ИЗ
						|	КритерийОтбора.СвязанныеДокументы(&ЗначениеКритерияОтбора)
						|ГДЕ
						|	Ссылка Ссылка Документ.РеализацияТоваровУслуг
						|И	Ссылка.Проведен = Истина";
					Запрос.УстановитьПараметр("ЗначениеКритерияОтбора", Строка.Заказ);
					Выборка = Запрос.Выполнить().Выбрать();
					Пока Выборка.Следующий() Цикл
					 	ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
						ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
					КонецЦикла; 
				Иначе
					ДокументОбъект = Строка.Заказ.ПолучитьОбъект();
					ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				КонецЕсли; 
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтразитьВозвратБонусныхСредств(Ссылка, Движения, Запись)	
	МетаданныеДокумента = Ссылка.Метаданные(); 	
	Если МетаданныеДокумента.Имя = "ВозвратТоваровОтКлиента" Тогда
		
		ЭлементДвижения = Движения.РасчетыСКлиентами[0];
		КурсВалюты = ПолучитьКурсВалюты(ЭлементДвижения.Валюта, ЭлементДвижения.ДатаРегистратора);
		
		СуммаВозвратаБонусныхСредств = Запись.Бонус/(КурсВалюты.Курс/КурсВалюты.Кратность);
		СформироватьСписаниеЗадолженности(Ссылка, ЭлементДвижения.Валюта, СуммаВозвратаБонусныхСредств);
		
		Сообщить("Внимание!" + Символы.ПС + "Сумма выплат по документу уменьшена!" + Символы.ПС + "Товар оплачивался бонусами, итого сумма выплаты: " + (ЭлементДвижения.Сумма - СуммаВозвратаБонусныхСредств));
	КонецЕсли;	
КонецПроцедуры

Процедура СформироватьСписаниеЗадолженности(СсылкаНаВозврат, Валюта, СуммаВозвратаБонусныхСредств)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДокументРеализации = СсылкаНаВозврат.ДокументРеализации;
	Настройки = ХранилищеНастроекДанныхФорм.Загрузить("ДополнительныеНастройкиОбработокМодификацииКонфигурации", , , "ДополнительныеНастройкиОбработокМодификацииКонфигурации");
	
	CommonLibrary = git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектПоИмени("Библиотека внешних обработок");
	RefDebtRelief = Настройки.СвойствоЛояльностьСписаниеЗадолжености;
	RefGoodReturn = Настройки.СвойствоЛояльностьВозвратТоваровОтКлиента; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ 
		|	Значение
		|Из 
		|	РегистрСведений.ДополнительныеСведения 
		|ГДЕ 
		|	ВЫРАЗИТЬ(Объект КАК Документ.ВозвратТоваровОтКлиента) = &Ссылка 
		|И  Свойство = &Свойство";
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаВозврат);
	Запрос.УстановитьПараметр("Свойство", RefDebtRelief);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СписаниеЗадолженности = CommonLibrary.ПолучитьСсылкуПоНавигационнойСсылке(Документы.СписаниеЗадолженности, Выборка.Значение).ПолучитьОбъект();
		СписаниеЗадолженности.Задолженность.Очистить();
	Иначе
		СписаниеЗадолженности = Документы.СписаниеЗадолженности.СоздатьДокумент();
		СписаниеЗадолженности.Заполнить(ДокументРеализации);
		СписаниеЗадолженности.Дата = ТекущаяДата();
	КонецЕсли;
	
	СписаниеЗадолженности.ТипЗадолженности	= Перечисления.ТипыЗадолженности.Кредиторская;
	СписаниеЗадолженности.ЭтоОплатаБонусами	= Истина;
	СписаниеЗадолженности.АналитикаДоходов	= ПараметрыСеанса.ТекущийПользователь.ТекущееПодразделение;
	СписаниеЗадолженности.СтатьяДоходов     = ПланыВидовХарактеристик.СтатьиДоходов.НайтиПоКоду("00-000007");
	
	СписаниеЗадолженности.АналитикаРасходов	= ПараметрыСеанса.ТекущийПользователь.ТекущееПодразделение;
	СписаниеЗадолженности.СтатьяРасходов    = ПланыВидовХарактеристик.СтатьиРасходов.ПолучитьСсылку(Новый УникальныйИдентификатор("16ae3f7c-7b54-11e2-8fd5-001e673c80fc"));//ПланыВидовХарактеристик.СтатьиРасходов.НайтиПоКоду("00-000015");
	
	СписаниеЗадолженности.Контрагент	    = ДокументРеализации.Контрагент;
	СписаниеЗадолженности.Организация		= ДокументРеализации.Организация;
	СписаниеЗадолженности.Комментарий		= "Ссылка на реализацию товаров услуг:" + ПолучитьНавигационнуюСсылку(ДокументРеализации);
		
	Запись 						= СписаниеЗадолженности.Задолженность.Добавить();
	Запись.Партнер				= ДокументРеализации.Партнер;
	Запись.Сумма                = СуммаВозвратаБонусныхСредств;
	Запись.ВалютаВзаиморасчетов = Валюта;
	Запись.ТипРасчетов			= Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
	
	СписаниеЗадолженности.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
	
	
	
	НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(СсылкаНаВозврат);
	НаборЗаписей.Отбор.Свойство.Установить(RefDebtRelief);
	Запись = НаборЗаписей.Добавить();
	Запись.Объект 	= СсылкаНаВозврат;
	Запись.Свойство = RefDebtRelief;
	Запись.Значение = ПолучитьНавигационнуюСсылку(СписаниеЗадолженности.Ссылка);
	НаборЗаписей.Записать();
	
	
	
	НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(СписаниеЗадолженности.Ссылка);
	НаборЗаписей.Отбор.Свойство.Установить(RefGoodReturn);
	Запись = НаборЗаписей.Добавить();
	Запись.Объект 	= СписаниеЗадолженности.Ссылка;
	Запись.Свойство = RefGoodReturn;
	Запись.Значение = ПолучитьНавигационнуюСсылку(СсылкаНаВозврат);
	НаборЗаписей.Записать();
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры



Функция ПолучитьКурсВалюты(Валюта, ДатаКурса)

	Возврат РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаКурса, Новый Структура("Валюта", Валюта));
	
КонецФункции

Процедура ЗаписатьНаборыЗаписей(Объект, Движения, ДополнительныеСвойства) 
	
	Перем РегистрыДляКонтроля;

	// Регистры, для которых будут рассчитаны таблицы изменений движений.
	//Если Объект.ДополнительныеСвойства.ДляПроведения.Свойство("РегистрыДляКонтроля", РегистрыДляКонтроля) Тогда
	//	Для Каждого НаборЗаписей Из РегистрыДляКонтроля Цикл
	//		Если НаборЗаписей.Записывать Тогда

	//			// Установка флага регистрации изменений в наборе записей.
	//			НаборЗаписей.ДополнительныеСвойства.Вставить("РассчитыватьИзменения", Истина);
	//			НаборЗаписей.ДополнительныеСвойства.Вставить("ЭтоНовый", Объект.ДополнительныеСвойства.ЭтоНовый);

	//			// Структура для передачи данных в модули наборов записей.
	//			НаборЗаписей.ДополнительныеСвойства.Вставить("ДляПроведения", 
	//					Новый Структура("СтруктураВременныеТаблицы", Объект.ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы));

	//		КонецЕсли;
	//	КонецЦикла;
	//КонецЕсли;
		
	Объект.Движения.Записать();
	
КонецПроцедуры





