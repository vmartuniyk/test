////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Объект.КоличествоЭкземпляров = 1;
	Объект.ШаблонЭтикетки        = Справочники.ШаблоныЭтикетокИЦенников.ШаблонПоУмолчанию(Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаДляСкладскихЯчеек);
	Объект.НазначениеШаблона = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаДляСкладскихЯчеек;
		
	Если ЭтоАдресВременногоХранилища(Параметры.АдресВХранилище) Тогда
		СтуктураПараметров = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
		
		Склад     = СтуктураПараметров.Склад;
		Помещение = СтуктураПараметров.Помещение;
		
		Объект.СкладскиеЯчейки.Загрузить(СтуктураПараметров.Ячейки);
		
		Для Каждого СтрТабл из Объект.СкладскиеЯчейки Цикл
			СтрТабл.Штрихкод = ЧисловойКодПоСсылке(СтрТабл.Ячейка); 
		КонецЦикла;
		
	КонецЕсли;
	
	Склад = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Склад);
	
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Склад",Склад));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Склад",Склад));
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ СКЛАДСКИЕ ЯЧЕЙКИ

&НаКлиенте
Процедура СкладскиеЯчейкиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Ячейка) Тогда
		ТекущиеДанные.Штрихкод = ЧисловойКодПоСсылке(ТекущиеДанные.Ячейка);
	Иначе
		ТекущиеДанные.Штрихкод = "";
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Печать(Команда)
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрКоманды = Новый Массив;   // Добавим объект обработки печати ценников и этикеток в массив параметров печати.
	ПараметрКоманды.Добавить(ПолучитьПараметрыДляСкладскихЯчеек()); // В дальнейшем будем обращаться к объекту через ПараметрКоманды[0]
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ПечатьЭтикетокИЦенников", // Для вызова метода менеджера обработки "Печать".
		"ЭтикеткаСкладскиеЯчейки",
		ПараметрКоманды,
		ЭтаФорма,); // Форма владелец
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Функция ПолучитьПараметрыДляСкладскихЯчеек()
	
	СкладскиеЯчейки = Объект.СкладскиеЯчейки.Выгрузить();
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("СкладскиеЯчейки",        ПоместитьВоВременноеХранилище(СкладскиеЯчейки, УникальныйИдентификатор));
	ПараметрыПечати.Вставить("ШаблонЭтикетки",         Объект.ШаблонЭтикетки);
	ПараметрыПечати.Вставить("КоличествоЭкземпляров",  Объект.КоличествоЭкземпляров);
	ПараметрыПечати.Вставить("СтруктураМакетаШаблона", Неопределено);
	
	Возврат ПараметрыПечати;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЧисловойКодПоСсылке(Ячейка)
	Возврат ШтрихкодированиеПечатныхФорм.ЧисловойКодПоСсылке(Ячейка);
КонецФункции
