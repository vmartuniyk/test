
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СуммаНаличнымиВалюта		= Параметры.Валюта.Наименование;
	СуммаНаличными 				= Параметры.СуммаОплатить;
	
	СуммаЭквайрингомВалюта		= Параметры.Валюта.Наименование;
	СуммаЭквайрингом			= Параметры.СуммаЭйкваринга;
	
	СуммаБонусамиВалюта			= Параметры.Валюта.Наименование;
	СуммаБонусами				= Параметры.СуммаБонусами;
	
	ВнешняяБиблиотекаЛояльности = РеквизитФормыВЗначение("Объект").ПолучитьБиблиотеку("LoyaltyLibrary");
	Пин = ВнешняяБиблиотекаЛояльности.ГенерироватьПинДоступа();
	Партнер = Параметры.Партнер;
	
	ВыборкаЛогин = ВнешняяБиблиотекаЛояльности.ВыборкаИнфоИзРгЛояльностьНомераТелефонов(, Партнер, Истина);
	Если ВыборкаЛогин.Следующий() Тогда
		ОсновнойНомер = ВыборкаЛогин.НомерТелефона;
	Иначе
		ОсновнойНомер = "Не найден!";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОжиданиеРезервногоКанала();	
КонецПроцедуры



&НаКлиенте
Процедура ПодключитьОжиданиеРезервногоКанала()
	ВремяОткрытияРезерв = ТекущаяДата() + 120;
	Элементы.ВыслатьПоРезервномуКаналу.Доступность = Ложь;	
	ПодключитьОбработчикОжидания("СделатьДоступнымРезервныйКанал", 121, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СделатьДоступнымРезервныйКанал()
	Элементы.ВыслатьПоРезервномуКаналу.Доступность = Истина;
	//СостояниеКаналов(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОжиданиеОсновногоКанала()
	ВремяОткрытияОсновной = ТекущаяДата() + 60;
	Элементы.ВыслатьПоОсновномуКаналу.Доступность = Ложь;	
	ПодключитьОбработчикОжидания("СделатьДоступнымОсновнойКанал", 61, 	Истина);	
КонецПроцедуры

&НаКлиенте
Процедура СделатьДоступнымОсновнойКанал()
	Элементы.ВыслатьПоОсновномуКаналу.Доступность = Истина;
	//СостояниеКаналов(Неопределено);
КонецПроцедуры




&НаКлиенте
Процедура ПодключитьОжиданиеВводаПароля()
	Элементы.Пароль.Доступность = Ложь;
	ЗадержкаВводаПароля = ЗадержкаВводаПароля + 2;
	ВремяОткрытияПароля = ТекущаяДата() + ЗадержкаВводаПароля;
	ПодключитьОбработчикОжидания("СчетВремениДляВводаПароля", 1, 	Ложь);
	ПодключитьОбработчикОжидания("СделатьДоступнымВводПароля", ЗадержкаВводаПароля + 1, Истина);	
КонецПроцедуры

&НаКлиенте
Процедура СделатьДоступнымВводПароля()
	Элементы.Пароль.Доступность = Истина;
	ОтключитьОбработчикОжидания("СчетВремениДляВводаПароля");
КонецПроцедуры

&НаКлиенте
Процедура СчетВремениДляВводаПароля()
	Разница = ВремяОткрытияПароля - ТекущаяДата();
	Элементы.Пароль.Заголовок = ?(Разница <= 0, "Пароль", "Пароль" + " (" + Разница + ")");
КонецПроцедуры




&НаСервере
Процедура ВыслатьНовыйПарольНаОсновнойНомер(ОсновнойШлюз = Истина)	
	
	Перем Server, Path, FromWho, Status, ID; 
	
	Если ОсновнойНомер = "Не найден!" Тогда
		ВывестиСообщениеПользователю("Основной номер клиента не найден! Использовать бонусы невозможно.");	
		Возврат;
	КонецЕсли;

	
	Если ОсновнойШлюз Тогда
		ktcОбщийМодульПротоколов.ПолучитьSMS_Server_Path(Server, Path, FromWho, "alphasms.com.ua");
		ktcОбщийМодульПротоколов.ОтправитьСообщениеПоSMS(Server, Path + "send&from=" + FromWho + "&to=" + СокрЛП(ОсновнойНомер) + "&message=Oplata v KTC-bonus: " + СокрЛП(Пин) + " -- Esli vy ne sozdavali dannuyu operaciyu pozvonite na tel. +38(093)177-04-49", Status, ID);
	Иначе
		ktcОбщийМодульПротоколов.ПолучитьSMS_Server_Path(Server, Path, FromWho, "pbx.ktc.local");
		ktcОбщийМодульПротоколов.ОтправитьСообщениеПоSMS(Server, "sms_1.php?phone=" + СокрЛП(ОсновнойНомер) + "&text=""Oplata v KTC-bonus: " + СокрЛП(Пин) + " -- Esli vy ne sozdavali dannuyu operaciyu pozvonite na tel. +38(093)177-04-49""&secret=blabla", Неопределено, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыслатьПоОсновномуКаналу(Команда)
	ВыслатьНовыйПарольНаОсновнойНомер(Истина);
	ПодключитьОжиданиеОсновногоКанала();
КонецПроцедуры

&НаКлиенте
Процедура ВыслатьПоРезервномуКаналу(Команда)
	ВыслатьНовыйПарольНаОсновнойНомер(Ложь);
	ПодключитьОжиданиеРезервногоКанала();
КонецПроцедуры





&НаКлиенте
Процедура Подтвердить(Команда)
	
	Если СтрДлина(СтрЗаменить(Пароль, " ", "")) < 4 Тогда
		ВывестиСообщениеПользователю(НСтр("ru = 'Длина пароля не верна!'"), "Пароль");
		ПодключитьОжиданиеВводаПароля();
		Возврат;
	КонецЕсли;
	
	Если Пин <> Пароль Тогда
		ВывестиСообщениеПользователю(НСтр("ru = 'Пароль не верный, попробуте еще раз!'"), "Пароль");
		ПодключитьОжиданиеВводаПароля();
		Возврат;
	КонецЕсли;
	
	ОплатаПодтверждена = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ОплатаПодтверждена = Ложь;
	Закрыть();
	
КонецПроцедуры



Процедура ВывестиСообщениеПользователю(ТекстСообщения, Поле="", ОчищатьСообщения = Ложь)
	
#Если ТонкийКлиент Тогда	
	Если ОчищатьСообщения Тогда
		ОчиститьСообщения();
	КонецЕсли;
#КонецЕсли	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	Оповестить("ПодтверждениеОплаты", ОплатаПодтверждена);
КонецПроцедуры



