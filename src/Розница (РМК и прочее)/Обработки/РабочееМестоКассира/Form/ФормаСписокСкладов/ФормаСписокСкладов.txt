
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьПривилегированныйРежим(Истина);	
	Если Параметры.Свойство("Организация") = Истина 
		И Параметры.Организация.Пустая() =ЛОЖЬ Тогда
		Организация = Параметры.Организация;  		
		Для Каждого Склад ИЗ Параметры.СписокСкладов Цикл   		
			НоваяСтрока = СписокСкладов.Добавить();
			НоваяСтрока.Склад = Склад.Значение; 		
		КонецЦикла;        	
	КонецЕсли;         		
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура СписокСкладовСкладНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)	
	Элементы.СписокСкладовСклад.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокСкладов());      	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Перенести(Команда)     	
	ИндексСтроки = 0;
	Для Каждого СтрокаТаблицы ИЗ СписокСкладов Цикл
		
		Если ПустаяСтрока(СтрокаТаблицы.Склад) Тогда
			ТекстОшибки = "Поле обязательно к заполнению.";  
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"СписокСкладов[" + Строка(ИндексСтроки) + "].Склад");
			Возврат;    			
		КонецЕсли;
		
		ИндексСтроки = ИндексСтроки + 1;  		
	КонецЦикла;
	
	Закрыть(ВыгрузитьСписокСкладов());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСписокСкладов()
	
	МассивИсключений 	= ВыгрузитьСписокСкладов();
	СписокПодразделений = ПолучитьСписокПодразделений();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	Склады.Ссылка КАК Склад
	                |ИЗ
	                |	Справочник.Склады КАК Склады
					|ГДЕ
					|	Склады.ПометкаУдаления = ЛОЖЬ    
					|	И Склады.ЭтоГруппа = ЛОЖЬ
					|	И Склады.Подразделение В (&СписокПодразделений)";
					
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Если МассивИсключений.Количество() > 0 Тогда
		Запрос.Текст = Запрос.Текст + "				   
			|	И  Склады.Ссылка НЕ В (&МассивИсключений)";  				   
		Запрос.УстановитьПараметр("МассивИсключений", МассивИсключений); 
	КонецЕсли;
				   
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Склад");
	
КонецФункции

&НаСервере
Функция ПолучитьСписокПодразделений()
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СтруктураПредприятия) КАК Подразделения
					|ИЗ
					|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
					|ГДЕ
					|	ДополнительныеСведения.Свойство = &СвойствоОрганизацияПодразделения
					|	И (ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК Справочник.Организации)) = &Организация";
					
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СвойствоОрганизацияПодразделения", ктс_ПредопределенныеЗначенияПовтИсп.СвойствоОрганизацияПодразделения()); 
				   
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Подразделения");
КонецФункции // ПолучитьСписокПодразделений()

&НаСервере
Функция ВыгрузитьСписокСкладов()
		
	Возврат СписокСкладов.Выгрузить(, "Склад").ВыгрузитьКолонку("Склад");
	
КонецФункции

#КонецОбласти



