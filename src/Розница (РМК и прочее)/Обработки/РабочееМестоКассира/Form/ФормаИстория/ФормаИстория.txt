
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивИсторияРаботы = ИсторияРаботыПользователя.Получить();
	Для каждого ЭлементИстории Из МассивИсторияРаботы Цикл
		
		Если Найти(ЭлементИстории.НавигационнаяСсылка, "Документ.ОперацияПоПлатежнойКарте") Тогда
			ДобавитьЭлементИсторииВТаблицу(Документы.ОперацияПоПлатежнойКарте, ЭлементИстории);
		ИначеЕсли Найти(ЭлементИстории.НавигационнаяСсылка, "Документ.ЗаявкаНаРасходованиеДенежныхСредств") Тогда
			ДобавитьЭлементИсторииВТаблицу(Документы.ЗаявкаНаРасходованиеДенежныхСредств, ЭлементИстории);
		ИначеЕсли Найти(ЭлементИстории.НавигационнаяСсылка, "Документ.РасходныйКассовыйОрдер") Тогда
			ДобавитьЭлементИсторииВТаблицу(Документы.РасходныйКассовыйОрдер, ЭлементИстории);
		ИначеЕсли Найти(ЭлементИстории.НавигационнаяСсылка, "Документ.ПриходныйКассовыйОрдер") Тогда
			ДобавитьЭлементИсторииВТаблицу(Документы.ПриходныйКассовыйОрдер, ЭлементИстории);
		КонецЕсли;
				
	КонецЦикла; 	
	
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементИсторииВТаблицу(Менеджер, ЭлементИстории)
	
	НоваяСтрока = История.Добавить();
	НоваяСтрока.Дата = ЭлементИстории.Дата;
	НоваяСтрока.Документ = Менеджер.ПолучитьСсылку(ПолучитьУИДпоНавигационнойСсылке(ЭлементИстории.НавигационнаяСсылка));
	НоваяСтрока.Сумма = НоваяСтрока.Документ.СуммаДокумента;
	НоваяСтрока.Валюта = НоваяСтрока.Документ.Валюта;

КонецПроцедуры

&НаСервере
Функция ПолучитьУИДпоНавигационнойСсылке(стрНавигационнаяССылка) Экспорт
	Результат=Неопределено;
	ПозицияНачалаНавССылки = Найти(стрНавигационнаяССылка,"?ref=");
	Если ПозицияНачалаНавССылки<>0 тогда       
		УидНавССылки=Сред(стрНавигационнаяССылка,ПозицияНачалаНавССылки+5,32);
		УидСтрока=Прав(УидНавССылки,8)+"-"+Сред(УидНавССылки,21,4)+"-"+Сред(УидНавССылки,17,4)+"-"+Лев(УидНавССылки,4)+"-"+Сред(УидНавССылки,5,12);
		УИД=Новый УникальныйИдентификатор(УидСтрока);
		Результат=УИД;
	КонецЕсли;
	Возврат(Результат);
КонецФункции

&НаКлиенте
Процедура ИсторияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьЗначение(Элемент.ТекущиеДанные.Документ);
	
КонецПроцедуры
