
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УстановитьПривилегированныйРежим(Истина);
	
	ДатаСреза = ПолучитьЗначениеПараметра("ДатаСреза");
	ДатаСреза = ?(ТипЗнч(ДатаСреза)=Тип("СтандартнаяДатаНачала"), ДатаСреза.Дата, ДатаСреза);
	Если ДатаСреза = Дата("00010101") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Для корректной работы отчета необходимо указать дату среза, на которую получают остатки.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
		Возврат;
	КонецЕсли;
	
	ВидКПП = ПолучитьЗначениеПараметра("ВидКПП");
	Если ТипЗнч(ВидКПП) <> Тип("Число") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Для корректной работы отчета необходимо задать значения параметра «Вид КПП».'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
		Возврат;
	КонецЕсли;
	
	Если ВидКПП < 1 ИЛИ ВидКПП > 2 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Для корректной работы отчета необходимо задать значения параметра «Вид КПП».'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
		Возврат;
	КонецЕсли;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаНоменклатуры", ПолучитьТаблицуНоменклатуры(ДатаСреза, ВидКПП));

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуНоменклатуры(ДатаСреза, ВидКПП)

	Запрос 			= Новый Запрос;
	Запрос.Текст    = ТекстЗапросаТаблицаНоменклатуры(ВидКПП);
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ПолучитьТаблицуНоменклатуры()

Функция ТекстЗапросаТаблицаНоменклатуры(ВидКПП)

	ТекстЗапроса = "
				|ВЫБРАТЬ
				|	ВложенныйЗапрос.Номенклатура 					КАК Номенклатура,
				|	ВложенныйЗапрос.Характеристика					КАК Характеристика,
				|	ВложенныйЗапрос.Склад							КАК Склад,
				|	СУММА(ВложенныйЗапрос.Количество) 				КАК Количество,
				|	ВложенныйЗапрос.Номенклатура.ВидНоменклатуры 	КАК ВидНоменклатуры
				|ПОМЕСТИТЬ ОстаткиТоваров
				|ИЗ
				|	(ВЫБРАТЬ
				|		Номенклатура 		КАК Номенклатура,
				|		Характеристика 		КАК Характеристика,
				|		Склад 				КАК Склад,
				|		ВНаличииОстаток 	КАК Количество
				|	ИЗ
				|		РегистрНакопления.СвободныеОстатки.Остатки(&ДатаСреза, )
				|	
				|	ОБЪЕДИНИТЬ ВСЕ
				|	
				|	ВЫБРАТЬ
				|		Номенклатура		КАК Номенклатура,
				|		Характеристика		КАК Характеристика,
				|		Склад				КАК Склад,        	
				|		КПоступлениюОстаток КАК Количество
				|	ИЗ
				|		РегистрНакопления.ТоварыКПоступлению.Остатки(&ДатаСреза, )) КАК ВложенныйЗапрос

				|СГРУППИРОВАТЬ ПО
				|	ВложенныйЗапрос.Номенклатура,
				|	ВложенныйЗапрос.Характеристика,
				|	ВложенныйЗапрос.Склад

				|ИНДЕКСИРОВАТЬ ПО
				|	Номенклатура
				|;

				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	Номенклатура 	КАК Номенклатура,
				|	Значение 		КАК КПП
				|ПОМЕСТИТЬ ТаблицаКПП
				|ИЗ
				|	РегистрСведений.%1.СрезПоследних(
				|			&ДатаСреза,
				|			Номенклатура В
				|				(ВЫБРАТЬ
				|					Номенклатура
				|				ИЗ
				|					ОстаткиТоваров))

				|ИНДЕКСИРОВАТЬ ПО
				|	Номенклатура
				|;

				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	Номенклатура 		КАК Номенклатура,
				|	ТоварнаяКатегория   КАК ТоварнаяКатегория
				|ПОМЕСТИТЬ ТаблицаТоварныхКатегорий
				|ИЗ
				|	РегистрСведений.ТОМА_ТоварнаяКатегория.СрезПоследних(
				|			&ДатаСреза,
				|			Номенклатура В
				|				(ВЫБРАТЬ
				|					Номенклатура
				|				ИЗ
				|					ОстаткиТоваров))

				|ИНДЕКСИРОВАТЬ ПО
				|	Номенклатура
				|;

				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ОстаткиТоваров.Номенклатура																					КАК Номенклатура,
				|	ОстаткиТоваров.Характеристика																				КАК Характеристика,
				|	ОстаткиТоваров.Склад																						КАК Склад,
				|	ОстаткиТоваров.Количество																					КАК Количество,
				|	ЕСТЬNULL(ТаблицаКПП.КПП, ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка)) 			КАК КПП,
				|	ЕСТЬNULL(ТаблицаТоварныхКатегорий.ТоварнаяКатегория, ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)) 	КАК ТоварнаяКатегория,
				|	ОстаткиТоваров.ВидНоменклатуры																				КАК ВидНоменклатуры
				|ИЗ
				|	ОстаткиТоваров КАК ОстаткиТоваров
				|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКПП КАК ТаблицаКПП
				|		ПО ОстаткиТоваров.Номенклатура = ТаблицаКПП.Номенклатура
				|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоварныхКатегорий КАК ТаблицаТоварныхКатегорий
				|		ПО ОстаткиТоваров.Номенклатура = ТаблицаТоварныхКатегорий.Номенклатура
				|		
				|ГДЕ
				|	ОстаткиТоваров.Количество > 0";
				
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ?(ВидКПП=1,"КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж", "КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж"));
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаТаблицаНоменклатуры()
 
Функция ПолучитьЗначениеПараметра(ИмяПараметра)
	
	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
   	Если ПараметрДанных <> Неопределено Тогда
    	ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
   		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки.Значение;
     	Иначе
       		Возврат ПараметрДанных.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	Версия = "1.0.1";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительныйОтчет");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Остатки товаров в разрезе КПП");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	ПараметрыРегистрации.Вставить("РассылкаОтчетов", Истина);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Остатки товаров в разрезе КПП");
	
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "Остатки товаров в разрезе КПП", "ОТВРКПП", "ОткрытиеФормы", Ложь, "ОТВРКПП");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
КонецФункции

Процедура СчитатьДокументыНазначений(МассивНазначений)
	
	Макет = ПолучитьМакет("МакетДокументыНазначений");
	
	Для Инд = 1 По Макет.ВысотаТаблицы Цикл
		МассивНазначений.Добавить(Макет.Область(Инд,1).Текст);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры


#КонецОбласти