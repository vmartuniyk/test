////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)	
	
	СтандартнаяОбработка = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);	
	
	ОтчетПоОстаткамПоставщиков	= ПолучитьНаборДанных();
	ВнешниеНаборыДанных 		= Новый Структура;
    ВнешниеНаборыДанных.Вставить("ОтчетПоОстаткамПоставщиков",ОтчетПоОстаткамПоставщиков);
	
 	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки(); 

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);

	Если МакетКомпоновки.НаборыДанных.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru= 'Отчет не сформирован. Включите хотя бы одну группировку в ""Элементы оформления и группировки"".'") ;
	КонецЕсли;
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ


Функция ПолучитьЗначениеПараметра(ИмяПараметра)

  ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
   Если ПараметрДанных <> Неопределено Тогда
     ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
     Если ПараметрПользовательскойНастройки <> Неопределено Тогда
       Возврат ПараметрПользовательскойНастройки.Значение;
     Иначе
       Возврат ПараметрДанных.Значение;
     КонецЕсли;
   КонецЕсли;

  Возврат Неопределено;

КонецФункции

Функция ПолучитьНаборДанных()
	Запрос 		 = Новый Запрос;
	ТекстЗапроса = ПолучитьТекстЗапросаНаборДанных();
	ПериодОтчета = ПолучитьЗначениеПараметра("ПериодОтчета");
	Соглашения 	 = ПолучитьЗначениеПараметра("Соглашения");
	Если Соглашения<>Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%Соглашения%"," И Цены.Соглашение В(&Соглашение)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%Соглашения%"," ");
	КонецЕсли; 
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ВалютаУпрУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ДатаНачало", 	ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	ПериодОтчета.ДатаОкончания);
	Запрос.УстановитьПараметр("Соглашение", 	Соглашения);
	Запрос.УстановитьПараметр("Сегодня", 		НачалоДня(ТекущаяДата()));		
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ПолучитьНаборДанных()


Функция ПолучитьТекстЗапросаНаборДанных()
	ТекстЗапроса = "
		|ВЫБРАТЬ
      	|	Аналитика.Номенклатура КАК Номенклатура,
      	|	Аналитика.Склад КАК Склад
      	|ПОМЕСТИТЬ СпрНоменклатура
      	|ИЗ
      	|	РегистрНакопления.КТС_ДвижениеТоваровПоставщиков.Остатки(&ДатаОкончания, ) КАК КТС_ДвижениеТоваровПоставщиковОстатки
		|
        |ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
        |ПО КТС_ДвижениеТоваровПоставщиковОстатки.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура,
        |	Склад
        |;
   		////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ВложеныйЗапрос.Номенклатура 		КАК Номенклатура,
        |	СУММА(ВложеныйЗапрос.Количество) 	КАК Количество,
        |	ВложеныйЗапрос.Склад 				КАК Склад
        |ПОМЕСТИТЬ ОстатокНаСкладе
        |ИЗ
        |	(ВЫБРАТЬ
        |		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
              |		ТоварыНаСкладахОстатки.ВНаличииОстаток КАК Количество,
              |		ТоварыНаСкладахОстатки.Склад КАК Склад
              |	ИЗ
              |		РегистрНакопления.ТоварыНаСкладах.Остатки(
              |				&ДатаОкончания,
              |				Номенклатура В
              |					(ВЫБРАТЬ
              |						СпрНоменклатура.Номенклатура
              |					ИЗ
              |						СпрНоменклатура)) КАК ТоварыНаСкладахОстатки
              |	
              |	ОБЪЕДИНИТЬ ВСЕ
              |	
              |	ВЫБРАТЬ
              |		ТоварыКПоступлениюОстатки.Номенклатура,
              |		ТоварыКПоступлениюОстатки.КПоступлениюОстаток,
              |		ТоварыКПоступлениюОстатки.Склад
              |	ИЗ
              |		РегистрНакопления.ТоварыКПоступлению.Остатки(
              |				&ДатаОкончания,
              |				ДокументПоступления ССЫЛКА Документ.ПеремещениеТоваров
              |					И Номенклатура В
              |						(ВЫБРАТЬ
              |							СпрНоменклатура.Номенклатура
              |						ИЗ
              |							СпрНоменклатура)) КАК ТоварыКПоступлениюОстатки) КАК ВложеныйЗапрос
              |
              |СГРУППИРОВАТЬ ПО
              |	ВложеныйЗапрос.Номенклатура,
              |	ВложеныйЗапрос.Склад
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	Номенклатура,
              |	Склад
              |;
              |
   		////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	ОстатокНаСкладе.Номенклатура КАК Номенклатура,
              |	ОстатокНаСкладе.Склад КАК Склад
              |ПОМЕСТИТЬ ОстатокНоменклатуры
              |ИЗ
              |	ОстатокНаСкладе КАК ОстатокНаСкладе
              |ГДЕ
              |	ОстатокНаСкладе.Количество > 0
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	Номенклатура,
              |	Склад
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	ОстатокНаСкладе.Номенклатура,
              |	СУММА(ОстатокНаСкладе.Количество) КАК Количество
              |ПОМЕСТИТЬ СуммаОстатокНоменклатуры
              |ИЗ
              |	ОстатокНаСкладе КАК ОстатокНаСкладе
              |
              |СГРУППИРОВАТЬ ПО
              |	ОстатокНаСкладе.Номенклатура
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ РАЗЛИЧНЫЕ
              |	НоменклатураПоставщиков.Код КАК КодПоставщика,
              |	НоменклатураПоставщиков.Номенклатура,
              |	НоменклатураПоставщиков.Артикул КАК АртикулПоставщика,
              |	НоменклатураПоставщиков.Владелец КАК Поставщик
              |ПОМЕСТИТЬ СпрНоменклатураПоставщика
              |ИЗ
              |	Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстатокНоменклатуры КАК ОстатокНоменклатуры
              |		ПО (ОстатокНоменклатуры.Номенклатура = НоменклатураПоставщиков.Номенклатура)
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	Аналитика.Номенклатура КАК Номенклатура,
              |	Аналитика.Склад КАК Склад,
              |	КТС_ДвижениеТоваровПоставщиковОстатки.Поставщик,
              |	КТС_ДвижениеТоваровПоставщиковОстатки.ДокументПоступления,
              |	КТС_ДвижениеТоваровПоставщиковОстатки.КоличествоОстаток
              |ПОМЕСТИТЬ КешОстатокПоПоступлению
              |ИЗ
              |	РегистрНакопления.КТС_ДвижениеТоваровПоставщиков.Остатки(
              |			&ДатаОкончания,
              |			(АналитикаУчетаНоменклатуры.Номенклатура, АналитикаУчетаНоменклатуры.Склад) В
              |				(ВЫБРАТЬ
              |					ОстатокНоменклатуры.Номенклатура,
              |					ОстатокНоменклатуры.Склад
              |				ИЗ
              |					ОстатокНоменклатуры)) КАК КТС_ДвижениеТоваровПоставщиковОстатки
              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
              |		ПО КТС_ДвижениеТоваровПоставщиковОстатки.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
              |ГДЕ
              |	КТС_ДвижениеТоваровПоставщиковОстатки.ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровУслуг
              |
              |СГРУППИРОВАТЬ ПО
              |	Аналитика.Номенклатура,
              |	КТС_ДвижениеТоваровПоставщиковОстатки.Поставщик,
              |	Аналитика.Склад,
              |	КТС_ДвижениеТоваровПоставщиковОстатки.ДокументПоступления,
              |	КТС_ДвижениеТоваровПоставщиковОстатки.КоличествоОстаток
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	Номенклатура,
              |	Склад
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешОстатокПоПоступлению.Номенклатура КАК Номенклатура,
              |	КешОстатокПоПоступлению.Склад КАК Склад,
              |	СУММА(КешОстатокПоПоступлению.КоличествоОстаток) КАК Количество,
              |	КешОстатокПоПоступлению.ДокументПоступления КАК ДокументПоступления,
              |	КешОстатокПоПоступлению.Поставщик,
              |	КТС_ДвижениеТоваровПоставщиков.ДатаПрихода,
              |	КТС_ДвижениеТоваровПоставщиков.ЦенаПрихода,
              |	КТС_ДвижениеТоваровПоставщиков.Количество КАК КоличествоЗакупки
              |ПОМЕСТИТЬ КешПоступления
              |ИЗ
              |	КешОстатокПоПоступлению КАК КешОстатокПоПоступлению
              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.КТС_ДвижениеТоваровПоставщиков КАК КТС_ДвижениеТоваровПоставщиков
              |		ПО КешОстатокПоПоступлению.ДокументПоступления = КТС_ДвижениеТоваровПоставщиков.Регистратор
              |			И КешОстатокПоПоступлению.Номенклатура = КТС_ДвижениеТоваровПоставщиков.АналитикаУчетаНоменклатуры.Номенклатура
              |			И КешОстатокПоПоступлению.Поставщик = КТС_ДвижениеТоваровПоставщиков.Поставщик
              |ГДЕ
              |	КТС_ДвижениеТоваровПоставщиков.Период <= &ДатаОкончания
              |	И КТС_ДвижениеТоваровПоставщиков.Период >= &ДатаНачало
              |	И КТС_ДвижениеТоваровПоставщиков.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
              |	И КТС_ДвижениеТоваровПоставщиков.ДокументПоступления <> НЕОПРЕДЕЛЕНО
              |
              |СГРУППИРОВАТЬ ПО
              |	КТС_ДвижениеТоваровПоставщиков.ДатаПрихода,
              |	КТС_ДвижениеТоваровПоставщиков.ЦенаПрихода,
              |	КешОстатокПоПоступлению.Номенклатура,
              |	КешОстатокПоПоступлению.Склад,
              |	КешОстатокПоПоступлению.Поставщик,
              |	КешОстатокПоПоступлению.ДокументПоступления,
              |	КТС_ДвижениеТоваровПоставщиков.Количество
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	Номенклатура,
              |	Склад
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешПоступления.Номенклатура,
              |	КешПоступления.ДокументПоступления,
              |	СУММА(КешПоступления.Количество) КАК ОстПослеПродаж
              |ПОМЕСТИТЬ КешОстатокПослеПродаж
              |ИЗ
              |	КешПоступления КАК КешПоступления
              |
              |СГРУППИРОВАТЬ ПО
              |	КешПоступления.ДокументПоступления,
              |	КешПоступления.Номенклатура
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ РАЗЛИЧНЫЕ
              |	ВложенныйЗапрос.Период КАК Период,
              |	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
              |	ВложенныйЗапрос.СоглашениеСПоставщиком КАК СоглашениеСПоставщиком,
              |	ВложенныйЗапрос.Актуально КАК Актуально
              |ПОМЕСТИТЬ ТаблицаАктуальность
              |ИЗ
              |	(ВЫБРАТЬ
              |		ALPS_АктуальностьНоменклатуры.Период КАК Период,
              |		ALPS_АктуальностьНоменклатуры.Номенклатура КАК Номенклатура,
              |		ALPS_АктуальностьНоменклатуры.СоглашениеСПоставщиком КАК СоглашениеСПоставщиком,
              |		ALPS_АктуальностьНоменклатуры.Актуально КАК Актуально
              |	ИЗ
              |		РегистрСведений.ALPS_АктуальностьНоменклатуры КАК ALPS_АктуальностьНоменклатуры
              |	ГДЕ
              |		ALPS_АктуальностьНоменклатуры.Период >= &ДатаНачало
              |		И ALPS_АктуальностьНоменклатуры.Период < &ДатаОкончания
              |		И ALPS_АктуальностьНоменклатуры.Актуально
              |		И ALPS_АктуальностьНоменклатуры.Номенклатура В
              |				(ВЫБРАТЬ
              |					ОстатокНоменклатуры.Номенклатура
              |				ИЗ
              |					ОстатокНоменклатуры)
              |	
              |	ОБЪЕДИНИТЬ ВСЕ
              |	
              |	ВЫБРАТЬ
              |		ALPS_АктуальностьНоменклатурыСрезПоследних.Период,
              |		ALPS_АктуальностьНоменклатурыСрезПоследних.Номенклатура,
              |		ALPS_АктуальностьНоменклатурыСрезПоследних.СоглашениеСПоставщиком,
              |		ALPS_АктуальностьНоменклатурыСрезПоследних.Актуально
              |	ИЗ
              |		РегистрСведений.ALPS_АктуальностьНоменклатуры.СрезПоследних(, ) КАК ALPS_АктуальностьНоменклатурыСрезПоследних
              |	ГДЕ
              |		ALPS_АктуальностьНоменклатурыСрезПоследних.Актуально
              |		И ALPS_АктуальностьНоменклатурыСрезПоследних.Номенклатура В
              |				(ВЫБРАТЬ
              |					ОстатокНоменклатуры.Номенклатура
              |				ИЗ
              |					ОстатокНоменклатуры)) КАК ВложенныйЗапрос
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	ТаблицаАктуальность.Актуально КАК Актуально,
              |	ТаблицаАктуальность.Номенклатура КАК Номенклатура,
              |	ТаблицаАктуальность.СоглашениеСПоставщиком КАК СоглашениеСПоставщиком,
              |	ТаблицаАктуальность.Период КАК НачалоПериода,
              |	МИНИМУМ(ЕСТЬNULL(ТаблицаАктуальностьКопия.Период, ДАТАВРЕМЯ(3999, 1, 1))) КАК КонецПериода,
              |	1 КАК Проверка
              |ПОМЕСТИТЬ ТаблицаАктуальностьНаростающие
              |ИЗ
              |	ТаблицаАктуальность КАК ТаблицаАктуальность
              |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаАктуальность КАК ТаблицаАктуальностьКопия
              |		ПО (ТаблицаАктуальностьКопия.Период > ТаблицаАктуальность.Период)
              |			И (ТаблицаАктуальностьКопия.Номенклатура = ТаблицаАктуальность.Номенклатура)
              |
              |СГРУППИРОВАТЬ ПО
              |	ТаблицаАктуальность.Период,
              |	ТаблицаАктуальность.Актуально,
              |	ТаблицаАктуальность.СоглашениеСПоставщиком,
              |	ТаблицаАктуальность.Номенклатура
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешПоступления.Номенклатура,
              |	КешПоступления.Склад,
              |	КешПоступления.ДокументПоступления,
              |	СРЕДНЕЕ(КешПоступления.ЦенаПрихода) КАК СредняяЦенаПрихода
              |ПОМЕСТИТЬ КешСредняяЦенаПрихода
              |ИЗ
              |	КешПоступления КАК КешПоступления
              |
              |СГРУППИРОВАТЬ ПО
              |	КешПоступления.Номенклатура,
              |	КешПоступления.Склад,
              |	КешПоступления.ДокументПоступления
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	Цены.Номенклатура КАК Номенклатура,
              |	МИНИМУМ(ВЫБОР
              |			КОГДА Цены.Валюта = &ВалютаУпрУчета
              |				ТОГДА Цены.Цена
              |			ИНАЧЕ Цены.Цена * (КурсыВалют.Курс / КурсыВалют.Кратность) / (КурсУпрВалюты.Курс / КурсУпрВалюты.Кратность)
              |		КОНЕЦ) КАК Цена,
              |	Цены.Соглашение КАК Партнер,
              |	ЕСТЬNULL(ТаблицаАктуальностьНаростающие.Проверка, 0) КАК Проверка
              |ПОМЕСТИТЬ КешЦенЗаСегодня
              |ИЗ
              |	РегистрСведений.ЦеныНоменклатурыПоставщиков.СрезПоследних(
              |			,
              |			Номенклатура В
              |				(ВЫБРАТЬ
              |					ОстатокНоменклатуры.Номенклатура
              |				ИЗ
              |					ОстатокНоменклатуры)) КАК Цены
              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалют
              |		ПО (КурсыВалют.Валюта = Цены.Валюта)
              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсУпрВалюты
              |		ПО (КурсУпрВалюты.Валюта = &ВалютаУпрУчета)
              |			И (ИСТИНА)
              |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаАктуальностьНаростающие КАК ТаблицаАктуальностьНаростающие
              |		ПО Цены.Номенклатура = ТаблицаАктуальностьНаростающие.Номенклатура
              |			И Цены.Соглашение = ТаблицаАктуальностьНаростающие.СоглашениеСПоставщиком
              |			И Цены.Период >= ТаблицаАктуальностьНаростающие.НачалоПериода
              |			И Цены.Период <= ТаблицаАктуальностьНаростающие.КонецПериода
              |ГДЕ
              |	Цены.Период >= &Сегодня
			  | %Соглашения%
              |
              |СГРУППИРОВАТЬ ПО
              |	Цены.Номенклатура,
              |	Цены.Соглашение,
              |	ЕСТЬNULL(ТаблицаАктуальностьНаростающие.Проверка, 0)
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	Номенклатура,
              |	Цена
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешЦен.Номенклатура,
              |	МИНИМУМ(КешЦен.Цена) КАК Цена
              |ПОМЕСТИТЬ КешМинЦенаЗаСегодня
              |ИЗ
              |	КешЦенЗаСегодня КАК КешЦен
              |
              |СГРУППИРОВАТЬ ПО
              |	КешЦен.Номенклатура
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	КешЦен.Номенклатура
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешМинЦена.Номенклатура,
              |	КешМинЦена.Цена,
              |	КешЦен.Партнер КАК Поставщик,
              |	КешЦен.Проверка
              |ПОМЕСТИТЬ КешПоставщикСМинЦенойЗаСегодня
              |ИЗ
              |	КешЦенЗаСегодня КАК КешЦен
              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешМинЦенаЗаСегодня КАК КешМинЦена
              |		ПО КешЦен.Номенклатура = КешМинЦена.Номенклатура
              |			И КешЦен.Цена = КешМинЦена.Цена
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	КешМинЦена.Номенклатура
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	Цены.Номенклатура КАК Номенклатура,
              |	МИНИМУМ(ВЫБОР
              |			КОГДА Цены.Валюта = &ВалютаУпрУчета
              |				ТОГДА Цены.Цена
              |			ИНАЧЕ Цены.Цена * (КурсыВалют.Курс / КурсыВалют.Кратность) / (КурсУпрВалюты.Курс / КурсУпрВалюты.Кратность)
              |		КОНЕЦ) КАК Цена,
              |	Цены.Соглашение КАК Партнер,
              |	ЕСТЬNULL(ТаблицаАктуальностьНаростающие.Проверка, 0) КАК Проверка
              |ПОМЕСТИТЬ КешЦенЗаПериод
              |ИЗ
              |	РегистрСведений.ЦеныНоменклатурыПоставщиков КАК Цены
              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалют
              |		ПО (КурсыВалют.Валюта = Цены.Валюта)
              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсУпрВалюты
              |		ПО (КурсУпрВалюты.Валюта = &ВалютаУпрУчета)
              |			И (ИСТИНА)
              |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаАктуальностьНаростающие КАК ТаблицаАктуальностьНаростающие
              |		ПО Цены.Номенклатура = ТаблицаАктуальностьНаростающие.Номенклатура
              |			И Цены.Соглашение = ТаблицаАктуальностьНаростающие.СоглашениеСПоставщиком
              |			И Цены.Период >= ТаблицаАктуальностьНаростающие.НачалоПериода
              |			И Цены.Период <= ТаблицаАктуальностьНаростающие.КонецПериода
              |ГДЕ
              |	Цены.Период >= &ДатаНачало
              |	И Цены.Период < &ДатаОкончания
              |	И Цены.Номенклатура В
              |			(ВЫБРАТЬ
              |				ОстатокНоменклатуры.Номенклатура
              |			ИЗ
              |				ОстатокНоменклатуры)
			  | %Соглашения%
              |
              |СГРУППИРОВАТЬ ПО
              |	Цены.Номенклатура,
              |	Цены.Соглашение,
              |	ЕСТЬNULL(ТаблицаАктуальностьНаростающие.Проверка, 0)
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	Номенклатура,
              |	Цена
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешЦен.Номенклатура,
              |	МИНИМУМ(КешЦен.Цена) КАК Цена
              |ПОМЕСТИТЬ КешМинЦенаЗаПериод
              |ИЗ
              |	КешЦенЗаПериод КАК КешЦен
              |
              |СГРУППИРОВАТЬ ПО
              |	КешЦен.Номенклатура
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	КешЦен.Номенклатура
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешМинЦена.Номенклатура,
              |	КешМинЦена.Цена,
              |	КешЦен.Партнер КАК Поставщик,
              |	КешЦен.Проверка
              |ПОМЕСТИТЬ КешПоставщикСМинЦенойЗаПериод
              |ИЗ
              |	КешЦенЗаПериод КАК КешЦен
              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешМинЦенаЗаПериод КАК КешМинЦена
              |		ПО КешЦен.Номенклатура = КешМинЦена.Номенклатура
              |			И КешЦен.Цена = КешМинЦена.Цена
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	КешМинЦена.Номенклатура
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешПоставщикСМинЦенойЗаСегодня.Номенклатура КАК Номенклатура,
              |	КешПоставщикСМинЦенойЗаСегодня.Цена КАК Цена,
              |	МАКСИМУМ(КешПоставщикСМинЦенойЗаСегодня.Проверка) КАК Проверка
              |ПОМЕСТИТЬ КешАктуальныйПоставщикЗаСегодня
              |ИЗ
              |	КешПоставщикСМинЦенойЗаСегодня КАК КешПоставщикСМинЦенойЗаСегодня
              |
              |СГРУППИРОВАТЬ ПО
              |	КешПоставщикСМинЦенойЗаСегодня.Номенклатура,
              |	КешПоставщикСМинЦенойЗаСегодня.Цена
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	Номенклатура,
              |	Цена,
              |	Проверка
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешАктальныйПоставщикЗаСегодня.Номенклатура КАК Номенклатура,
              |	КешАктальныйПоставщикЗаСегодня.Цена,
              |	МАКСИМУМ(КешПоставщикСМинЦенойЗаСегодня.Поставщик.Наименование) КАК Поставщик
              |ПОМЕСТИТЬ КешМинимальныйПоставщикЗаСегодня
              |ИЗ
              |	КешАктуальныйПоставщикЗаСегодня КАК КешАктальныйПоставщикЗаСегодня
              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешПоставщикСМинЦенойЗаСегодня КАК КешПоставщикСМинЦенойЗаСегодня
              |		ПО КешАктальныйПоставщикЗаСегодня.Номенклатура = КешПоставщикСМинЦенойЗаСегодня.Номенклатура
              |			И КешАктальныйПоставщикЗаСегодня.Цена = КешПоставщикСМинЦенойЗаСегодня.Цена
              |			И КешАктальныйПоставщикЗаСегодня.Проверка = КешПоставщикСМинЦенойЗаСегодня.Проверка
              |
              |СГРУППИРОВАТЬ ПО
              |	КешАктальныйПоставщикЗаСегодня.Номенклатура,
              |	КешАктальныйПоставщикЗаСегодня.Цена
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	Номенклатура
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешПоставщикСМинЦенойЗаПериод.Номенклатура КАК Номенклатура,
              |	КешПоставщикСМинЦенойЗаПериод.Цена КАК Цена,
              |	МАКСИМУМ(КешПоставщикСМинЦенойЗаПериод.Проверка) КАК Проверка
              |ПОМЕСТИТЬ КешАктуальныйПоставщикЗаПериод
              |ИЗ
              |	КешПоставщикСМинЦенойЗаПериод КАК КешПоставщикСМинЦенойЗаПериод
              |
              |СГРУППИРОВАТЬ ПО
              |	КешПоставщикСМинЦенойЗаПериод.Номенклатура,
              |	КешПоставщикСМинЦенойЗаПериод.Цена
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	Номенклатура,
              |	Цена,
              |	Проверка
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешАктуальныйПоставщикЗаПериод.Номенклатура КАК Номенклатура,
              |	КешАктуальныйПоставщикЗаПериод.Цена,
              |	МАКСИМУМ(КешПоставщикСМинЦенойЗаПериод.Поставщик.Наименование) КАК Поставщик
              |ПОМЕСТИТЬ КешМинимальныйПоставщикЗаПериод
              |ИЗ
              |	КешАктуальныйПоставщикЗаПериод КАК КешАктуальныйПоставщикЗаПериод
              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешПоставщикСМинЦенойЗаПериод КАК КешПоставщикСМинЦенойЗаПериод
              |		ПО КешАктуальныйПоставщикЗаПериод.Номенклатура = КешПоставщикСМинЦенойЗаПериод.Номенклатура
              |			И КешАктуальныйПоставщикЗаПериод.Цена = КешПоставщикСМинЦенойЗаПериод.Цена
              |			И КешАктуальныйПоставщикЗаПериод.Проверка = КешПоставщикСМинЦенойЗаПериод.Проверка
              |
              |СГРУППИРОВАТЬ ПО
              |	КешАктуальныйПоставщикЗаПериод.Номенклатура,
              |	КешАктуальныйПоставщикЗаПериод.Цена
              |
              |ИНДЕКСИРОВАТЬ ПО
              |	Номенклатура
              |;
              |
              |////////////////////////////////////////////////////////////////////////////////
              |ВЫБРАТЬ
              |	КешПоступления.Поставщик,
              |	КешПоступления.Номенклатура.Код КАК Код,
              |	КешПоступления.Номенклатура.Артикул КАК PartNumber,
              |	ЕСТЬNULL(СпрНоменклатураПоставщика.КодПоставщика, 0) КАК КодПоставщика,
              |	ЕСТЬNULL(СпрНоменклатураПоставщика.АртикулПоставщика, 0) КАК PartNumberПоставщика,
              |	КешПоступления.Номенклатура КАК Номенклатура,
              |	КешПоступления.Склад,
              |	КешПоступления.КоличествоЗакупки - КешОстатокПослеПродаж.ОстПослеПродаж КАК ПродажаДилера,
              |	КешПоступления.ДатаПрихода,
              |	КешПоступления.ЦенаПрихода,
              |	КешПоступления.Количество КАК ОстатокТоваровПоставщика,
              |	КешПоступления.ДокументПоступления,
              |	ЕСТЬNULL(КешМинимальныйПоставщикЗаСегодня.Поставщик, 0) КАК ПоставщикМинЦеныЗаСегодня,
              |	ЕСТЬNULL(КешМинимальныйПоставщикЗаСегодня.Цена, 0) КАК МинЦенаПоставщикаЦенаЗаСегодня,
              |	ЕСТЬNULL(КешМинимальныйПоставщикЗаПериод.Поставщик, 0) КАК ПоставщикМинЦеныЗаПериод,
              |	ЕСТЬNULL(КешМинимальныйПоставщикЗаПериод.Цена, 0) КАК МинЦенаПоставщикаЗаПериод,
              |	ВЫБОР
              |		КОГДА ЕСТЬNULL(КешСредняяЦенаПрихода.СредняяЦенаПрихода, 0) <> 0
              |				И ЕСТЬNULL(КешМинимальныйПоставщикЗаСегодня.Цена, 0) <> 0
              |				И ЕСТЬNULL(КешСредняяЦенаПрихода.СредняяЦенаПрихода, 0) > ЕСТЬNULL(КешМинимальныйПоставщикЗаСегодня.Цена, 0)
              |			ТОГДА ИСТИНА
              |		КОГДА ЕСТЬNULL(КешСредняяЦенаПрихода.СредняяЦенаПрихода, 0) <> 0
              |				И ЕСТЬNULL(КешМинимальныйПоставщикЗаПериод.Цена, 0) <> 0
              |				И ЕСТЬNULL(КешСредняяЦенаПрихода.СредняяЦенаПрихода, 0) > ЕСТЬNULL(КешМинимальныйПоставщикЗаПериод.Цена, 0)
              |			ТОГДА ИСТИНА
              |		ИНАЧЕ ЛОЖЬ
              |	КОНЕЦ КАК ПроверкаМеншуМинЦенуПоставщикаЧемЦенаЗакупки,
              |	ВЫБОР
              |		КОГДА ЕСТЬNULL(КешСредняяЦенаПрихода.СредняяЦенаПрихода, 0) <> КешПоступления.ЦенаПрихода
              |			ТОГДА ИСТИНА
              |		ИНАЧЕ ЛОЖЬ
              |	КОНЕЦ КАК ПроверкаНаОднуЦенуПрихода,
              |	СуммаОстатокНоменклатуры.Количество КАК ОбщийОстатокНаСкладе,
              |	КешПоступления.КоличествоЗакупки
              |ИЗ
              |	КешПоступления КАК КешПоступления
              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешСредняяЦенаПрихода КАК КешСредняяЦенаПрихода
              |		ПО КешПоступления.Номенклатура = КешСредняяЦенаПрихода.Номенклатура
              |			И КешПоступления.Склад = КешСредняяЦенаПрихода.Склад
              |			И КешПоступления.ДокументПоступления = КешСредняяЦенаПрихода.ДокументПоступления
              |		ЛЕВОЕ СОЕДИНЕНИЕ СпрНоменклатураПоставщика КАК СпрНоменклатураПоставщика
              |		ПО КешПоступления.Номенклатура = СпрНоменклатураПоставщика.Номенклатура
              |			И КешПоступления.Поставщик = СпрНоменклатураПоставщика.Поставщик
              |		ЛЕВОЕ СОЕДИНЕНИЕ КешМинимальныйПоставщикЗаПериод КАК КешМинимальныйПоставщикЗаПериод
              |		ПО КешПоступления.Номенклатура = КешМинимальныйПоставщикЗаПериод.Номенклатура
              |		ЛЕВОЕ СОЕДИНЕНИЕ КешМинимальныйПоставщикЗаСегодня КАК КешМинимальныйПоставщикЗаСегодня
              |		ПО КешПоступления.Номенклатура = КешМинимальныйПоставщикЗаСегодня.Номенклатура
              |		ЛЕВОЕ СОЕДИНЕНИЕ ОстатокНаСкладе КАК ОстатокНаСкладе
              |		ПО КешПоступления.Номенклатура = ОстатокНаСкладе.Номенклатура
              |			И КешПоступления.Склад = ОстатокНаСкладе.Склад
              |		ЛЕВОЕ СОЕДИНЕНИЕ СуммаОстатокНоменклатуры КАК СуммаОстатокНоменклатуры
              |		ПО КешПоступления.Номенклатура = СуммаОстатокНоменклатуры.Номенклатура
              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешОстатокПослеПродаж КАК КешОстатокПослеПродаж
              |		ПО КешПоступления.Номенклатура = КешОстатокПослеПродаж.Номенклатура
              |			И КешПоступления.ДокументПоступления = КешОстатокПослеПродаж.ДокументПоступления";
Возврат ТекстЗапроса;
КонецФункции


// Сведения о внешнем отчете
Функция 	СведенияОВнешнейОбработке() Экспорт
	
	Версия = "2.0.3";
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительныйОтчет");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Остатки приходов поставщиков");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Остатки приходов поставщиков'");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "Остатки приходов поставщиков", "ФормаОтчета", "ОткрытиеФормы", Ложь, "");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
КонецФункции

Функция 	ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура 	ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

