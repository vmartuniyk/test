
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПроверитьПараметрыКомпоновщика() Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	Если ПроверкаФормированияНабораДанных(НастройкиОтчета) Тогда
		ВнешниеНаборыДанных = Новый Структура;
		ВнешниеНаборыДанных.Вставить("ТаблицаНоменклатуры", ПолучитьТаблицуНоменклатуры());
	Иначе
		ВнешниеНаборыДанных = Новый Структура;
		ВнешниеНаборыДанных.Вставить("ТаблицаНоменклатуры", ПолучитьТаблицуНоменклатурыРегистратор());
	КонецЕсли;

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуНоменклатуры()

	Запрос 			= Новый Запрос;
	Запрос.Текст    = ТекстЗапросаТаблицаНоменклатуры();
	ПериодОтчета    = ПолучитьЗначениеПараметра("ПериодОтчета");
	
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ПериодОтчета.ДатаОкончания));
	Запрос.УстановитьПараметр("ПодразделениеОптовыйОтдел", Справочники.СтруктураПредприятия.ПолучитьСсылку(Новый УникальныйИдентификатор("ebe6f508-3ffd-11e0-9f98-001517115d85")));
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ПолучитьТаблицуНоменклатуры()

Функция ТекстЗапросаТаблицаНоменклатуры()
	
	ТекстЗапроса = "
			|ВЫБРАТЬ
			|	Продажи.Период										КАК Период,
			|	Продажи.Регистратор									КАК Регистратор,
			|	Продажи.АналитикаУчетаНоменклатуры					КАК АналитикаНоменклатуры,
			|	Продажи.ЗаказКлиента								КАК ЗаказКлиента,
			|	Продажи.АналитикаУчетаПоПартнерам					КАК АналитикаПоПартнерам,
			|	Продажи.Подразделение								КАК Подразделение,
			|	Продажи.ТипЗапасов									КАК ТипЗапасов,
			|	Продажи.ВидЗапасов									КАК ВидЗапасов,
			|	Продажи.Менеджер									КАК Менеджер,
			|	Продажи.КоличествоОборот							КАК Количество,
			|	Продажи.СуммаВыручкиОборот							КАК СуммаВыручки,
			|	Продажи.СебестоимостьОборот							КАК Себестоимость,
			|	Продажи.СуммаДополнительныхРасходовОборот			КАК СуммаДополнительныхРасходов
			|ПОМЕСТИТЬ ТаблицаПродажи
			|ИЗ
			|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, АналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)) КАК Продажи
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	АналитикаНоменклатуры							КАК АналитикаНоменклатуры,
			|	ЗаказКлиента									КАК ЗаказКлиента,
			|	АналитикаПоПартнерам							КАК АналитикаПоПартнерам,
			//|	Подразделение									КАК Подразделение,
			|	ВЫБОР
			|		КОГДА СУММА(Количество) = 0 
			|			ТОГДА 0
			|		ИНАЧЕ СУММА(Себестоимость)/СУММА(Количество)
			|	КОНЕЦ											КАК Себестоимость,
			|	ВЫБОР
			|		КОГДА СУММА(Количество) = 0
			|			ТОГДА 0
			|		ИНАЧЕ СУММА(СуммаДополнительныхРасходов)/СУММА(Количество)
			|	КОНЕЦ											КАК СуммаДополнительныхРасходов
			|ПОМЕСТИТЬ ТаблицаУсредненнойСебестоимости
			|ИЗ
			|	ТаблицаПродажи
			|	
			|СГРУППИРОВАТЬ ПО
			|	АналитикаНоменклатуры,
			|	ЗаказКлиента,
			|	АналитикаПоПартнерам
			//|	Подразделение
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	АналитикаНоменклатуры,
			|	ЗаказКлиента,
			|	АналитикаПоПартнерам
			//|	Подразделение
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Период										КАК Период,
			|	АналитикаНоменклатуры						КАК АналитикаНоменклатуры,
			|	ЗаказКлиента								КАК ЗаказКлиента,
			|	АналитикаПоПартнерам						КАК АналитикаПоПартнерам,
			|	Подразделение								КАК Подразделение,
			|	Менеджер									КАК Менеджер,
			|	СУММА(Количество)							КАК Количество,
			|	СУММА(СуммаВыручки)							КАК СуммаВыручки
			|ПОМЕСТИТЬ ТаблицаВыручки
			|ИЗ
			|	ТаблицаПродажи
			|ГДЕ
			|	ТаблицаПродажи.Количество <> 0
			|	
			|СГРУППИРОВАТЬ ПО
			|	Период,
			|	АналитикаНоменклатуры,
			|	ЗаказКлиента,
			|	АналитикаПоПартнерам,
			|	Подразделение,
			|	Менеджер
			|	
			|ИНДЕКСИРОВАТЬ ПО
			|	АналитикаНоменклатуры,
			|	ЗаказКлиента,
			|	АналитикаПоПартнерам,
			|	Подразделение
			|;
			
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаПродажи;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаВыручки.Период																	КАК Период,
			|	РегистрАналитикаНоменклатуры.Номенклатура												КАК Номенклатура,
			|	РегистрАналитикаНоменклатуры.Характеристика												КАК Характеристика,
			|	РегистрАналитикаНоменклатуры.Склад														КАК Склад,
			|	ТаблицаВыручки.ЗаказКлиента																КАК ЗаказКлиента,
			|	РегистрАналитикаПоПартнерам.Партнер														КАК Партнер,
			|	РегистрАналитикаПоПартнерам.Организация													КАК Организация,
			|	РегистрАналитикаПоПартнерам.Контрагент													КАК Контрагент,
			|	ТаблицаВыручки.Подразделение															КАК Подразделение,
			|	ТаблицаВыручки.Менеджер																	КАК Менеджер,
			|	ТаблицаВыручки.Количество																КАК Количество,
			|	ТаблицаВыручки.СуммаВыручки																КАК СуммаВыручки,
			|	ТаблицаВыручки.Количество * ЕСТЬNULL(ТаблицаУсредненнойСебестоимости.Себестоимость, 0)  КАК Себестоимость,
			|	ТаблицаВыручки.Количество * ЕСТЬNULL(ТаблицаУсредненнойСебестоимости.СуммаДополнительныхРасходов, 0)  КАК СуммаДополнительныхРасходов			
			|ПОМЕСТИТЬ ТаблицаНоменклатуры
			|ИЗ
			|	ТаблицаВыручки КАК ТаблицаВыручки
			|	
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаУсредненнойСебестоимости КАК ТаблицаУсредненнойСебестоимости
			|		ПО ТаблицаВыручки.АналитикаНоменклатуры 	= ТаблицаУсредненнойСебестоимости.АналитикаНоменклатуры
			|			И ТаблицаВыручки.ЗаказКлиента 			= ТаблицаУсредненнойСебестоимости.ЗаказКлиента
			|			И ТаблицаВыручки.АналитикаПоПартнерам 	= ТаблицаУсредненнойСебестоимости.АналитикаПоПартнерам
			//|			И ТаблицаВыручки.Подразделение 			= ТаблицаУсредненнойСебестоимости.Подразделение

			|			
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК РегистрАналитикаНоменклатуры
			|		ПО ТаблицаВыручки.АналитикаНоменклатуры = РегистрАналитикаНоменклатуры.КлючАналитики
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаПоПартнерам
			|		ПО ТаблицаВыручки.АналитикаПоПартнерам = РегистрАналитикаПоПартнерам.КлючАналитики
			|		
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаУсредненнойСебестоимости;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаВыручки;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КПП_Опт.Период			КАК Период,
			|	КПП_Опт.Номенклатура 	КАК Номенклатура,
			|	КПП_Опт.Значение 		КАК Значение
			|ПОМЕСТИТЬ ТаблицаКППОпт
			|ИЗ
			|	РегистрСведений.КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж КАК КПП_Опт
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|		ПО КПП_Опт.Номенклатура = ТаблицаНоменклатуры.Номенклатура
			|ГДЕ
			|	КПП_Опт.Период <= &ДатаОкончания

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КПП_Розница.Период			КАК Период,
			|	КПП_Розница.Номенклатура 	КАК Номенклатура,
			|	КПП_Розница.Значение 		КАК Значение
			|ПОМЕСТИТЬ ТаблицаКППРозница
			|ИЗ
			|	РегистрСведений.КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж КАК КПП_Розница
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|		ПО КПП_Розница.Номенклатура = ТаблицаНоменклатуры.Номенклатура
			|ГДЕ
			|	КПП_Розница.Период <= &ДатаОкончания

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МАКСИМУМ(Период) 	КАК Период,
			|	Номенклатура 		КАК Номенклатура
			|ПОМЕСТИТЬ ТаблицаКППОптНачалоПериода
			|ИЗ
			|	ТаблицаКППОпт
			|ГДЕ
			|	Период <= &ДатаНачала

			|СГРУППИРОВАТЬ ПО
			|	Номенклатура

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаКППОпт.Период			КАК Период,
			|	ТаблицаКППОпт.Номенклатура 		КАК Номенклатура,
			|	ТаблицаКППОпт.Значение 			КАК Значение
			|ПОМЕСТИТЬ ТаблицаКППОптПредварительная
			|ИЗ
			|	ТаблицаКППОпт КАК ТаблицаКППОпт
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаКППОптНачалоПериода КАК ТаблицаКППОптНачалоПериода
			|		ПО ТаблицаКППОпт.Период 			>= ТаблицаКППОптНачалоПериода.Период
			|			И ТаблицаКППОпт.Номенклатура 	= ТаблицаКППОптНачалоПериода.Номенклатура
			
			|ОБЪЕДИНИТЬ
			
			|ВЫБРАТЬ
			|	ТаблицаКППОпт.Период			КАК Период,
			|	ТаблицаКППОпт.Номенклатура 		КАК Номенклатура,
			|	ТаблицаКППОпт.Значение 			КАК Значение
			|ИЗ
			|	ТаблицаКППОпт КАК ТаблицаКППОпт
			|ГДЕ
			|	ТаблицаКППОпт.Период >= &ДатаНачала
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППОпт;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППОптНачалоПериода;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаКППОптПредварительная.Номенклатура 	КАК Номенклатура,
			|	ТаблицаКППОптПредварительная.Значение		КАК Значение,
			|	ТаблицаКППОптПредварительная.Период 		КАК НачалоПериода,
			|	МИНИМУМ(ЕСТЬNULL(ТаблицаКППОптПредварительнаяКопия.Период, ДАТАВРЕМЯ(3999, 1, 1))) КАК КонецПериода
			|ПОМЕСТИТЬ ТаблицаКППОптНаростающая
			|ИЗ
			|	ТаблицаКППОптПредварительная КАК ТаблицаКППОптПредварительная
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКППОптПредварительная КАК ТаблицаКППОптПредварительнаяКопия
			|		ПО ТаблицаКППОптПредварительная.Номенклатура 	= ТаблицаКППОптПредварительнаяКопия.Номенклатура
			|			И ТаблицаКППОптПредварительная.Период 		< ТаблицаКППОптПредварительнаяКопия.Период

			|СГРУППИРОВАТЬ ПО
			|	ТаблицаКППОптПредварительная.Номенклатура,
			|	ТаблицаКППОптПредварительная.Значение,
			|	ТаблицаКППОптПредварительная.Период

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППОптПредварительная;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МАКСИМУМ(Период) 	КАК Период,
			|	Номенклатура 		КАК Номенклатура
			|ПОМЕСТИТЬ ТаблицаКППРозницаНачалоПериода
			|ИЗ
			|	ТаблицаКППРозница
			|ГДЕ
			|	Период <= &ДатаНачала

			|СГРУППИРОВАТЬ ПО
			|	Номенклатура

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаКППРозница.Период		КАК Период,
			|	ТаблицаКППРозница.Номенклатура 	КАК Номенклатура,
			|	ТаблицаКППРозница.Значение 		КАК Значение
			|ПОМЕСТИТЬ ТаблицаКППРозницаПредварительная
			|ИЗ
			|	ТаблицаКППРозница КАК ТаблицаКППРозница
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаКППРозницаНачалоПериода КАК ТаблицаКППРозницаНачалоПериода
			|		ПО ТаблицаКППРозница.Период >= ТаблицаКППРозницаНачалоПериода.Период
			|			И ТаблицаКППРозница.Номенклатура = ТаблицаКППРозницаНачалоПериода.Номенклатура

			|ОБЪЕДИНИТЬ
			
			|ВЫБРАТЬ
			|	ТаблицаКППРозница.Период		КАК Период,
			|	ТаблицаКППРозница.Номенклатура 	КАК Номенклатура,
			|	ТаблицаКППРозница.Значение 		КАК Значение
			|ИЗ
			|	ТаблицаКППРозница КАК ТаблицаКППРозница
			|ГДЕ
			|	ТаблицаКППРозница.Период >= &ДатаНачала
			|	
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППРозница;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППРозницаНачалоПериода;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаКППРозницаПредварительная.Номенклатура КАК Номенклатура,
			|	ТаблицаКППРозницаПредварительная.Значение,
			|	ТаблицаКППРозницаПредварительная.Период КАК НачалоПериода,
			|	МИНИМУМ(ЕСТЬNULL(ТаблицаКППРозницаПредварительнаяКопия.Период, ДАТАВРЕМЯ(3999, 1, 1))) КАК КонецПериода
			|ПОМЕСТИТЬ ТаблицаКППРозницаНаростающая
			|ИЗ
			|	ТаблицаКППРозницаПредварительная КАК ТаблицаКППРозницаПредварительная
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКППРозницаПредварительная КАК ТаблицаКППРозницаПредварительнаяКопия
			|		ПО ТаблицаКППРозницаПредварительная.Номенклатура 	= ТаблицаКППРозницаПредварительнаяКопия.Номенклатура
			|			И ТаблицаКППРозницаПредварительная.Период 		< ТаблицаКППРозницаПредварительнаяКопия.Период
			|			
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаКППРозницаПредварительная.Номенклатура,
			|	ТаблицаКППРозницаПредварительная.Значение,
			|	ТаблицаКППРозницаПредварительная.Период

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППРозницаПредварительная;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварныеКатегории.Период			КАК Период,
			|	ТоварныеКатегории.Номенклатура		КАК Номенклатура,
			|	ТоварныеКатегории.ТоварнаяКатегория	КАК ТоварнаяКатегория
			|ПОМЕСТИТЬ ТаблицаТоварныеКатегории
			|ИЗ
			|	РегистрСведений.ТОМА_ТоварнаяКатегория КАК ТоварныеКатегории
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|		ПО ТоварныеКатегории.Номенклатура = ТаблицаНоменклатуры.Номенклатура
			|ГДЕ
			|	ТоварныеКатегории.Период <= &ДатаОкончания
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ТоварныеКатегории.Номенклатура
			|;
			
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МАКСИМУМ(Период) 	КАК Период,
			|	Номенклатура		КАК Номенклатура
			|ПОМЕСТИТЬ ТоварныеКатегорииНачалоПериода
			|ИЗ
			|	ТаблицаТоварныеКатегории
			|ГДЕ
			|	Период <= &ДатаНачала
			|
			|СГРУППИРОВАТЬ ПО 
			|	Номенклатура
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТоварныеКатегории.Период				КАК Период,
			|	ТаблицаТоварныеКатегории.Номенклатура		КАК Номенклатура,
			|	ТаблицаТоварныеКатегории.ТоварнаяКатегория	КАК ТоварнаяКатегория
			|ПОМЕСТИТЬ ТоварныеКатегорииПредварительные
			|ИЗ
			|	ТаблицаТоварныеКатегории КАК ТаблицаТоварныеКатегории
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварныеКатегорииНачалоПериода КАК ТоварныеКатегорииНачалоПериода
			|		ПО ТаблицаТоварныеКатегории.Период >= ТоварныеКатегорииНачалоПериода.Период
			|			И ТаблицаТоварныеКатегории.Номенклатура = ТоварныеКатегорииНачалоПериода.Номенклатура
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаТоварныеКатегории;
			
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТоварныеКатегорииНачалоПериода;
			
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварныеКатегорииПредварительные.Номенклатура 		КАК Номенклатура,
			|	ТоварныеКатегорииПредварительные.ТоварнаяКатегория	КАК ТоварнаяКатегория,
			|	ТоварныеКатегорииПредварительные.Период 			КАК НачалоПериода,
			|	МИНИМУМ(ЕСТЬNULL(ТоварныеКатегорииПредварительныеКопия.Период, ДАТАВРЕМЯ(3999, 1, 1))) КАК КонецПериода
			|ПОМЕСТИТЬ ТоварныеКатегорииНаростающая
			|ИЗ
			|	ТоварныеКатегорииПредварительные КАК ТоварныеКатегорииПредварительные
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварныеКатегорииПредварительные КАК ТоварныеКатегорииПредварительныеКопия
			|		ПО ТоварныеКатегорииПредварительные.Номенклатура 	= ТоварныеКатегорииПредварительныеКопия.Номенклатура
			|			И ТоварныеКатегорииПредварительные.Период 		< ТоварныеКатегорииПредварительныеКопия.Период
			|			
			|СГРУППИРОВАТЬ ПО
			|	ТоварныеКатегорииПредварительные.Номенклатура,
			|	ТоварныеКатегорииПредварительные.ТоварнаяКатегория,
			|	ТоварныеКатегорииПредварительные.Период

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТоварныеКатегорииПредварительные;
			
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, ГОД)									КАК ПериодГод,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, ДЕКАДА)								КАК ПериодДекада,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, ДЕНЬ)									КАК ПериодДень,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, КВАРТАЛ)								КАК ПериодКвартал,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, МЕСЯЦ)								КАК ПериодМесяц,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, НЕДЕЛЯ)								КАК ПериодНеделя,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, ПОЛУГОДИЕ)							КАК ПериодПолугодие,
			|	ТаблицаНоменклатуры.Номенклатура												КАК Номенклатура,
			|	ТаблицаНоменклатуры.Характеристика												КАК Характеристика,
			|	ТаблицаНоменклатуры.Склад														КАК Склад,
			|	ТаблицаНоменклатуры.ЗаказКлиента												КАК ЗаказКлиента,
			|	ТаблицаНоменклатуры.Партнер														КАК Партнер,
			|	ТаблицаНоменклатуры.Организация													КАК Организация,
			|	ТаблицаНоменклатуры.Контрагент													КАК Контрагент,
			|	ТаблицаНоменклатуры.Подразделение												КАК Подразделение,
			|	ТаблицаНоменклатуры.Менеджер													КАК Менеджер,
			|	ТаблицаНоменклатуры.Количество													КАК Количество,
			|	ТаблицаНоменклатуры.СуммаВыручки												КАК Выручка,
			|	ТаблицаНоменклатуры.Себестоимость  												КАК Себестоимость,
			|	ТаблицаНоменклатуры.СуммаДополнительныхРасходов  								КАК ДополнительныеРасходы,
			|	ТаблицаНоменклатуры.СуммаВыручки
			|		- ТаблицаНоменклатуры.Себестоимость
			|		- ТаблицаНоменклатуры.СуммаДополнительныхРасходов							КАК ВаловаяПрибыль,							
			|	ВЫБОР
			|		КОГДА ТаблицаНоменклатуры.Подразделение = &ПодразделениеОптовыйОтдел
			|			ТОГДА ЕСТЬNULL(ТаблицаКППОптНаростающая.Значение, ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка))
			|		ИНАЧЕ ЕСТЬNULL(ТаблицаКППРозницаНаростающая.Значение, ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка))
			|	КОНЕЦ КАК КПП,
			|	ЕСТЬNULL(ТоварныеКатегорииНаростающая.ТоварнаяКатегория, ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)) КАК ТоварнаяКатегория
			|ИЗ
			|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|	
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКППОптНаростающая КАК ТаблицаКППОптНаростающая
			|		ПО ТаблицаНоменклатуры.Номенклатура = ТаблицаКППОптНаростающая.Номенклатура
			|			И ТаблицаНоменклатуры.Период >= ТаблицаКППОптНаростающая.НачалоПериода
			|			И ТаблицаНоменклатуры.Период < ТаблицаКППОптНаростающая.КонецПериода
			|			
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКППРозницаНаростающая КАК ТаблицаКППРозницаНаростающая
			|		ПО ТаблицаНоменклатуры.Номенклатура = ТаблицаКППРозницаНаростающая.Номенклатура
			|			И ТаблицаНоменклатуры.Период >= ТаблицаКППРозницаНаростающая.НачалоПериода
			|			И ТаблицаНоменклатуры.Период < ТаблицаКППРозницаНаростающая.КонецПериода
			|			
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварныеКатегорииНаростающая КАК ТоварныеКатегорииНаростающая
			|		ПО ТаблицаНоменклатуры.Номенклатура = ТоварныеКатегорииНаростающая.Номенклатура
			|			И ТаблицаНоменклатуры.Период >= ТоварныеКатегорииНаростающая.НачалоПериода
			|			И ТаблицаНоменклатуры.Период < ТоварныеКатегорииНаростающая.КонецПериода";
			
	Возврат ТекстЗапроса;
	
КонецФункции // ТекстЗапросаТаблицаНоменклатуры()

Функция ПолучитьТаблицуНоменклатурыРегистратор()
	
	Запрос 			= Новый Запрос;
	Запрос.Текст    = ТекстЗапросаТаблицаНоменклатурыРегистратор();
	ПериодОтчета    = ПолучитьЗначениеПараметра("ПериодОтчета");
	
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ПериодОтчета.ДатаОкончания));
	Запрос.УстановитьПараметр("ПодразделениеОптовыйОтдел", Справочники.СтруктураПредприятия.ПолучитьСсылку(Новый УникальныйИдентификатор("ebe6f508-3ffd-11e0-9f98-001517115d85")));
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ТекстЗапросаТаблицаНоменклатурыРегистратор()

	ТекстЗапроса = "
			|ВЫБРАТЬ
			|	Продажи.Период										КАК Период,
			|	Продажи.Регистратор									КАК Регистратор,
			|	РегистрАналитикаНоменклатуры.Номенклатура			КАК Номенклатура,
			|	РегистрАналитикаНоменклатуры.Характеристика			КАК Характеристика,
			|	РегистрАналитикаНоменклатуры.Склад					КАК Склад,
			|	Продажи.ЗаказКлиента								КАК ЗаказКлиента,
			|	РегистрАналитикаПоПартнерам.Партнер					КАК Партнер,
			|	РегистрАналитикаПоПартнерам.Организация				КАК Организация,
			|	РегистрАналитикаПоПартнерам.Контрагент				КАК Контрагент,
			|	Продажи.Подразделение								КАК Подразделение,
			|	Продажи.ТипЗапасов									КАК ТипЗапасов,
			|	Продажи.ВидЗапасов									КАК ВидЗапасов,
			|	Продажи.Менеджер									КАК Менеджер,
			|	Продажи.КоличествоОборот							КАК Количество,
			|	Продажи.СуммаВыручкиОборот							КАК СуммаВыручки			
			|ПОМЕСТИТЬ ТаблицаНоменклатуры
			|ИЗ
			|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, АналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)) КАК Продажи
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК РегистрАналитикаНоменклатуры
			|		ПО Продажи.АналитикаУчетаНоменклатуры = РегистрАналитикаНоменклатуры.КлючАналитики
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаПоПартнерам
			|		ПО Продажи.АналитикаУчетаПоПартнерам = РегистрАналитикаПоПартнерам.КлючАналитики
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КПП_Опт.Период			КАК Период,
			|	КПП_Опт.Номенклатура 	КАК Номенклатура,
			|	КПП_Опт.Значение 		КАК Значение
			|ПОМЕСТИТЬ ТаблицаКППОпт
			|ИЗ
			|	РегистрСведений.КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж КАК КПП_Опт
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|		ПО КПП_Опт.Номенклатура = ТаблицаНоменклатуры.Номенклатура
			|ГДЕ
			|	КПП_Опт.Период <= &ДатаОкончания

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КПП_Розница.Период			КАК Период,
			|	КПП_Розница.Номенклатура 	КАК Номенклатура,
			|	КПП_Розница.Значение 		КАК Значение
			|ПОМЕСТИТЬ ТаблицаКППРозница
			|ИЗ
			|	РегистрСведений.КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж КАК КПП_Розница
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|		ПО КПП_Розница.Номенклатура = ТаблицаНоменклатуры.Номенклатура
			|ГДЕ
			|	КПП_Розница.Период <= &ДатаОкончания

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МАКСИМУМ(Период) 	КАК Период,
			|	Номенклатура 		КАК Номенклатура
			|ПОМЕСТИТЬ ТаблицаКППОптНачалоПериода
			|ИЗ
			|	ТаблицаКППОпт
			|ГДЕ
			|	Период <= &ДатаНачала

			|СГРУППИРОВАТЬ ПО
			|	Номенклатура

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаКППОпт.Период			КАК Период,
			|	ТаблицаКППОпт.Номенклатура 		КАК Номенклатура,
			|	ТаблицаКППОпт.Значение 			КАК Значение
			|ПОМЕСТИТЬ ТаблицаКППОптПредварительная
			|ИЗ
			|	ТаблицаКППОпт КАК ТаблицаКППОпт
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаКППОптНачалоПериода КАК ТаблицаКППОптНачалоПериода
			|		ПО ТаблицаКППОпт.Период 			>= ТаблицаКППОптНачалоПериода.Период
			|			И ТаблицаКППОпт.Номенклатура 	= ТаблицаКППОптНачалоПериода.Номенклатура

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППОпт;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППОптНачалоПериода;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаКППОптПредварительная.Номенклатура 	КАК Номенклатура,
			|	ТаблицаКППОптПредварительная.Значение		КАК Значение,
			|	ТаблицаКППОптПредварительная.Период 		КАК НачалоПериода,
			|	МИНИМУМ(ЕСТЬNULL(ТаблицаКППОптПредварительнаяКопия.Период, ДАТАВРЕМЯ(3999, 1, 1))) КАК КонецПериода
			|ПОМЕСТИТЬ ТаблицаКППОптНаростающая
			|ИЗ
			|	ТаблицаКППОптПредварительная КАК ТаблицаКППОптПредварительная
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКППОптПредварительная КАК ТаблицаКППОптПредварительнаяКопия
			|		ПО ТаблицаКППОптПредварительная.Номенклатура 	= ТаблицаКППОптПредварительнаяКопия.Номенклатура
			|			И ТаблицаКППОптПредварительная.Период 		< ТаблицаКППОптПредварительнаяКопия.Период

			|СГРУППИРОВАТЬ ПО
			|	ТаблицаКППОптПредварительная.Номенклатура,
			|	ТаблицаКППОптПредварительная.Значение,
			|	ТаблицаКППОптПредварительная.Период

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППОптПредварительная;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МАКСИМУМ(Период) 	КАК Период,
			|	Номенклатура 		КАК Номенклатура
			|ПОМЕСТИТЬ ТаблицаКППРозницаНачалоПериода
			|ИЗ
			|	ТаблицаКППРозница
			|ГДЕ
			|	Период <= &ДатаНачала

			|СГРУППИРОВАТЬ ПО
			|	Номенклатура

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаКППРозница.Период		КАК Период,
			|	ТаблицаКППРозница.Номенклатура 	КАК Номенклатура,
			|	ТаблицаКППРозница.Значение 		КАК Значение
			|ПОМЕСТИТЬ ТаблицаКППРозницаПредварительная
			|ИЗ
			|	ТаблицаКППРозница КАК ТаблицаКППРозница
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаКППРозницаНачалоПериода КАК ТаблицаКППРозницаНачалоПериода
			|		ПО ТаблицаКППРозница.Период >= ТаблицаКППРозницаНачалоПериода.Период
			|			И ТаблицаКППРозница.Номенклатура = ТаблицаКППРозницаНачалоПериода.Номенклатура

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППРозница;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППРозницаНачалоПериода;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаКППРозницаПредварительная.Номенклатура КАК Номенклатура,
			|	ТаблицаКППРозницаПредварительная.Значение,
			|	ТаблицаКППРозницаПредварительная.Период КАК НачалоПериода,
			|	МИНИМУМ(ЕСТЬNULL(ТаблицаКППРозницаПредварительнаяКопия.Период, ДАТАВРЕМЯ(3999, 1, 1))) КАК КонецПериода
			|ПОМЕСТИТЬ ТаблицаКППРозницаНаростающая
			|ИЗ
			|	ТаблицаКППРозницаПредварительная КАК ТаблицаКППРозницаПредварительная
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКППРозницаПредварительная КАК ТаблицаКППРозницаПредварительнаяКопия
			|		ПО ТаблицаКППРозницаПредварительная.Номенклатура 	= ТаблицаКППРозницаПредварительнаяКопия.Номенклатура
			|			И ТаблицаКППРозницаПредварительная.Период 		< ТаблицаКППРозницаПредварительнаяКопия.Период
			|			
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаКППРозницаПредварительная.Номенклатура,
			|	ТаблицаКППРозницаПредварительная.Значение,
			|	ТаблицаКППРозницаПредварительная.Период

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;

			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаКППРозницаПредварительная;

			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварныеКатегории.Период			КАК Период,
			|	ТоварныеКатегории.Номенклатура		КАК Номенклатура,
			|	ТоварныеКатегории.ТоварнаяКатегория	КАК ТоварнаяКатегория
			|ПОМЕСТИТЬ ТаблицаТоварныеКатегории
			|ИЗ
			|	РегистрСведений.ТОМА_ТоварнаяКатегория КАК ТоварныеКатегории
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|		ПО ТоварныеКатегории.Номенклатура = ТаблицаНоменклатуры.Номенклатура
			|ГДЕ
			|	ТоварныеКатегории.Период <= &ДатаОкончания
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ТоварныеКатегории.Номенклатура
			|;
			
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МАКСИМУМ(Период) 	КАК Период,
			|	Номенклатура		КАК Номенклатура
			|ПОМЕСТИТЬ ТоварныеКатегорииНачалоПериода
			|ИЗ
			|	ТаблицаТоварныеКатегории
			|ГДЕ
			|	Период <= &ДатаНачала
			|
			|СГРУППИРОВАТЬ ПО 
			|	Номенклатура
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТоварныеКатегории.Период				КАК Период,
			|	ТаблицаТоварныеКатегории.Номенклатура		КАК Номенклатура,
			|	ТаблицаТоварныеКатегории.ТоварнаяКатегория	КАК ТоварнаяКатегория
			|ПОМЕСТИТЬ ТоварныеКатегорииПредварительные
			|ИЗ
			|	ТаблицаТоварныеКатегории КАК ТаблицаТоварныеКатегории
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварныеКатегорииНачалоПериода КАК ТоварныеКатегорииНачалоПериода
			|		ПО ТаблицаТоварныеКатегории.Период >= ТоварныеКатегорииНачалоПериода.Период
			|			И ТаблицаТоварныеКатегории.Номенклатура = ТоварныеКатегорииНачалоПериода.Номенклатура
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТаблицаТоварныеКатегории;
			
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТоварныеКатегорииНачалоПериода;
			
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварныеКатегорииПредварительные.Номенклатура 		КАК Номенклатура,
			|	ТоварныеКатегорииПредварительные.ТоварнаяКатегория	КАК ТоварнаяКатегория,
			|	ТоварныеКатегорииПредварительные.Период 			КАК НачалоПериода,
			|	МИНИМУМ(ЕСТЬNULL(ТоварныеКатегорииПредварительныеКопия.Период, ДАТАВРЕМЯ(3999, 1, 1))) КАК КонецПериода
			|ПОМЕСТИТЬ ТоварныеКатегорииНаростающая
			|ИЗ
			|	ТоварныеКатегорииПредварительные КАК ТоварныеКатегорииПредварительные
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварныеКатегорииПредварительные КАК ТоварныеКатегорииПредварительныеКопия
			|		ПО ТоварныеКатегорииПредварительные.Номенклатура 	= ТоварныеКатегорииПредварительныеКопия.Номенклатура
			|			И ТоварныеКатегорииПредварительные.Период 		< ТоварныеКатегорииПредварительныеКопия.Период
			|			
			|СГРУППИРОВАТЬ ПО
			|	ТоварныеКатегорииПредварительные.Номенклатура,
			|	ТоварныеКатегорииПредварительные.ТоварнаяКатегория,
			|	ТоварныеКатегорииПредварительные.Период

			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТоварныеКатегорииПредварительные;
			
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, ГОД)									КАК ПериодГод,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, ДЕКАДА)								КАК ПериодДекада,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, ДЕНЬ)									КАК ПериодДень,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, КВАРТАЛ)								КАК ПериодКвартал,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, МЕСЯЦ)								КАК ПериодМесяц,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, НЕДЕЛЯ)								КАК ПериодНеделя,
			|	НАЧАЛОПЕРИОДА(ТаблицаНоменклатуры.Период, ПОЛУГОДИЕ)							КАК ПериодПолугодие,
			|	ТаблицаНоменклатуры.Регистратор													КАК Регистратор,
			|	ТаблицаНоменклатуры.Номенклатура												КАК Номенклатура,
			|	ТаблицаНоменклатуры.Характеристика												КАК Характеристика,
			|	ТаблицаНоменклатуры.Склад														КАК Склад,
			|	ТаблицаНоменклатуры.ЗаказКлиента												КАК ЗаказКлиента,
			|	ТаблицаНоменклатуры.Партнер														КАК Партнер,
			|	ТаблицаНоменклатуры.Организация													КАК Организация,
			|	ТаблицаНоменклатуры.Контрагент													КАК Контрагент,
			|	ТаблицаНоменклатуры.Подразделение												КАК Подразделение,
			|	ТаблицаНоменклатуры.Менеджер													КАК Менеджер,
			|	ТаблицаНоменклатуры.Количество													КАК Количество,
			|	ТаблицаНоменклатуры.СуммаВыручки												КАК Выручка,
			|	0  																				КАК Себестоимость,
			|	0  																				КАК ДополнительныеРасходы,
			|	0																				КАК ВаловаяПрибыль,							
			|	ВЫБОР
			|		КОГДА ТаблицаНоменклатуры.Подразделение = &ПодразделениеОптовыйОтдел
			|			ТОГДА ЕСТЬNULL(ТаблицаКППОптНаростающая.Значение, ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка))
			|		ИНАЧЕ ЕСТЬNULL(ТаблицаКППРозницаНаростающая.Значение, ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка))
			|	КОНЕЦ КАК КПП,
			|	ЕСТЬNULL(ТоварныеКатегорииНаростающая.ТоварнаяКатегория, ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)) КАК ТоварнаяКатегория
			|ИЗ
			|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|	
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКППОптНаростающая КАК ТаблицаКППОптНаростающая
			|		ПО ТаблицаНоменклатуры.Номенклатура = ТаблицаКППОптНаростающая.Номенклатура
			|			И ТаблицаНоменклатуры.Период >= ТаблицаКППОптНаростающая.НачалоПериода
			|			И ТаблицаНоменклатуры.Период < ТаблицаКППОптНаростающая.КонецПериода
			|			
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКППРозницаНаростающая КАК ТаблицаКППРозницаНаростающая
			|		ПО ТаблицаНоменклатуры.Номенклатура = ТаблицаКППРозницаНаростающая.Номенклатура
			|			И ТаблицаНоменклатуры.Период >= ТаблицаКППРозницаНаростающая.НачалоПериода
			|			И ТаблицаНоменклатуры.Период < ТаблицаКППРозницаНаростающая.КонецПериода
			|			
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварныеКатегорииНаростающая КАК ТоварныеКатегорииНаростающая
			|		ПО ТаблицаНоменклатуры.Номенклатура = ТоварныеКатегорииНаростающая.Номенклатура
			|			И ТаблицаНоменклатуры.Период >= ТоварныеКатегорииНаростающая.НачалоПериода
			|			И ТаблицаНоменклатуры.Период < ТоварныеКатегорииНаростающая.КонецПериода";
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаТаблицаНоменклатурыРегистратор()
 

Функция ПолучитьЗначениеПараметра(ИмяПараметра)
	
	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
   	Если ПараметрДанных <> Неопределено Тогда
    	ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
   		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки.Значение;
     	Иначе
       		Возврат ПараметрДанных.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

Функция ПроверитьПараметрыКомпоновщика()

	ПериодОтчета = ПолучитьЗначениеПараметра("ПериодОтчета");
	
	Если ТипЗнч(ПериодОтчета) <> Тип("СтандартныйПериод") Тогда
		ВызватьИсключение НСтр("ru='Для работы отчета необходимо установить период.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		Возврат Ложь;
	КонецЕсли;
	
	Если ПериодОтчета.ДатаНачала = Дата("00010101") Тогда
		ВызватьИсключение НСтр("ru='Необходимо задать дату начала выполнения отчета.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		Возврат Ложь;
	КонецЕсли;
	
	Если ПериодОтчета.ДатаОкончания = Дата("00010101") Тогда
		ВызватьИсключение НСтр("ru='Необходимо задать дату окончания выполнения отчета.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		Возврат Ложь;
	КонецЕсли;
	
	Если ПериодОтчета.ДатаОкончания < ПериодОтчета.ДатаНачала Тогда
		ВызватьИсключение НСтр("ru='Дата начала выполнения отчета должна быть больше даты окончания выполнения.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		Возврат Ложь;
	КонецЕсли;
	
	Если (ПериодОтчета.ДатаОкончания - ПериодОтчета.ДатаНачала) > 7889229 Тогда // Период не больше 3 месяцев, иначе труба
		ВызватьИсключение НСтр("ru='Пожалуйста, выберите период поменьше, поберегите себя и других пользователей.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции // ПроверитьПараметрыКомпоновщика()
 
Функция ПроверкаФормированияНабораДанных(НастройкиОтчета)

	СтруктураНастроек = НастройкиОтчета.Структура;
	
	Если СтруктураНастроек.Количество() <> 1 Тогда
		Возврат Истина;
	КонецЕсли;
	
	ЭлементСтруктурыНастроек = СтруктураНастроек[0];
	
	Если ТипЗнч(ЭлементСтруктурыНастроек) <> Тип("ГруппировкаКомпоновкиДанных") Тогда
		Возврат Истина;
	КонецЕсли;
	
	ПоляГруппировки = ЭлементСтруктурыНастроек.ПоляГруппировки.Элементы;
	
	Если ПоляГруппировки.Количество() <> 1 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ПоляГруппировки[0].Поле = Новый ПолеКомпоновкиДанных("Регистратор") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции // ПроверкаФормированияНабораДанных()

#КонецОбласти

#Область СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	Версия = "1.0.4";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительныйОтчет");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Валовая прибыль по КПП");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	ПараметрыРегистрации.Вставить("РассылкаОтчетов", Истина);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Валовая прибыль по КПП");
	
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "Валовая прибыль по КПП", "ВПККП", "ОткрытиеФормы", Ложь, "ВПККП");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
КонецФункции

Процедура СчитатьДокументыНазначений(МассивНазначений)
	
	Макет = ПолучитьМакет("МакетДокументыНазначений");
	
	Для Инд = 1 По Макет.ВысотаТаблицы Цикл
		МассивНазначений.Добавить(Макет.Область(Инд,1).Текст);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры


#КонецОбласти 