
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПериодОтчета 	= ПолучитьЗначениеПараметра("ПериодОтчета");
	
	Если Не ЗначениеЗаполнено(ПериодОтчета) Тогда
		СтандартнаяОбработка = Истина;
		Возврат;
	Иначе
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
	ТекстЗапроса = ПолучитьТекстЗапросаКомпоновки();
		
	СформироватьОтчетПрограммно(ТекстЗапроса, ДанныеРасшифровки, ДокументРезультат, ПериодОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗапросаКомпоновки()

	ТекстЗапроса = "
	
		// КУРСЫ ВАЛЮТ
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(КурсыВалют.Курс / КурсыВалют.Кратность КАК ЧИСЛО(15, 4)) КАК Курс,
		|	КурсыВалют.Валюта КАК Валюта,
		|	КурсыВалют.Период КАК Период
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют КАК КурсыВалют
		|;
		|
		|ВЫБРАТЬ
		|  	КурсыВалют.Курс,
		|  	КурсыВалют.Валюта,
		|  	КурсыВалют.Период КАК НачалоПериода,
		|  	МИНИМУМ(ЕСТЬNULL(КурсыВалютКопия.Период, ДАТАВРЕМЯ(3999, 1, 1))) КАК КонецПериода
		|ПОМЕСТИТЬ ТаблицаКурсов
		|ИЗ
		|  	КурсыВалют КАК КурсыВалют
		|		
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютКопия
		|ПО 	КурсыВалютКопия.Период > КурсыВалют.Период
		|И 	КурсыВалютКопия.Валюта = КурсыВалют.Валюта
		|			
		|СГРУППИРОВАТЬ ПО
		|  	КурсыВалют.Период,
		|  	КурсыВалют.Курс,
		|  	КурсыВалют.Валюта
		|
		|ИНДЕКСИРОВАТЬ ПО 
		|	КурсыВалют.Валюта
		|;
		
		// КОНЕЦ КУРСЫ ВАЛЮТ

	
		// АНАЛИЗ ЗАКАЗОВ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбработкаЗаказовКлиентов.Номенклатура 										КАК Номенклатура,
		|	ВЫРАЗИТЬ(ОбработкаЗаказовКлиентов.ЗаказКлиента КАК Документ.ЗаказКлиента) 	КАК ЗаказКлиента,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) 				КАК Характеристика,
		|	ОбработкаЗаказовКлиентов.КодСтроки											КАК КодСтроки,
		|	ОбработкаЗаказовКлиентов.КоличествоВЗаказе									КАК КоличествоВЗаказе,
		|	ОбработкаЗаказовКлиентов.ДатаАктуальности									КАК ДатаАктуальности,
		|	ВЫРАЗИТЬ(&ЛогистическийСклад КАК Справочник.Склады)							КАК ЛогистическийСклад
		|ПОМЕСТИТЬ ОбособленнаяНоменклатура
		|ИЗ
		|	РегистрСведений.ОбработкаЗаказовКлиентов КАК ОбработкаЗаказовКлиентов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|		ПО ОбработкаЗаказовКлиентов.ЗаказКлиента = ЗаказКлиентаТовары.Ссылка
		|			И ОбработкаЗаказовКлиентов.Номенклатура = ЗаказКлиентаТовары.Номенклатура
		|			И ОбработкаЗаказовКлиентов.КодСтроки = ЗаказКлиентаТовары.КодСтроки
		|		И (ЗаказКлиентаТовары.ОтменитьЗаказПодЗаказ = ЛОЖЬ)
		|			И (ЗаказКлиентаТовары.Отменено = ЛОЖЬ)
		|ГДЕ
		|	ОбработкаЗаказовКлиентов.ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
		|	И ВЫРАЗИТЬ(ОбработкаЗаказовКлиентов.ЗаказКлиента КАК Документ.ЗаказКлиента).Дата >= &ДатаНачала
		|	И ВЫРАЗИТЬ(ОбработкаЗаказовКлиентов.ЗаказКлиента КАК Документ.ЗаказКлиента).Дата <= &ДатаОкончания

		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	ЗаказКлиента
		|;

		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ СпрНоменклатура
		|ИЗ
		|	ОбособленнаяНоменклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбособленнаяНоменклатура.Номенклатура КАК Номенклатура,
		|	ОбособленнаяНоменклатура.ЗаказКлиента КАК ЗаказКлиента,
		|	ВЫБОР
		|		КОГДА ОбособленнаяНоменклатура.ЗаказКлиента.Валюта = &ВалютаУпрУчета
		|			ТОГДА СРЕДНЕЕ(ТоварыЗаказа.Цена)
		|		ИНАЧЕ СРЕДНЕЕ(ТоварыЗаказа.Цена) * КурсыВалют.Курс / КурсУпрВалюты.Курс
		|	КОНЕЦ КАК Цена,
		|	ВЫБОР
		|		КОГДА ОбособленнаяНоменклатура.ЗаказКлиента.Валюта = &ВалютаУпрУчета
		|			ТОГДА СУММА(ТоварыЗаказа.СуммаСНДС) / СУММА(ТоварыЗаказа.Количество)
		|		ИНАЧЕ (СУММА(ТоварыЗаказа.СуммаСНДС) / СУММА(ТоварыЗаказа.Количество)) * КурсыВалют.Курс / КурсУпрВалюты.Курс
		|	КОНЕЦ КАК Сумма
		|ПОМЕСТИТЬ СуммыИЦеныЗаказов
		|ИЗ
		|	ОбособленнаяНоменклатура КАК ОбособленнаяНоменклатура
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ТоварыЗаказа
		|		ПО ОбособленнаяНоменклатура.Номенклатура = ТоварыЗаказа.Номенклатура
		|			И ОбособленнаяНоменклатура.ЗаказКлиента = ТоварыЗаказа.Ссылка
		|			И ОбособленнаяНоменклатура.КодСтроки = ТоварыЗаказа.КодСтроки
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсов КАК КурсыВалют
		|		ПО КурсыВалют.Валюта = ОбособленнаяНоменклатура.ЗаказКлиента.Валюта
		|			И ОбособленнаяНоменклатура.ЗаказКлиента.Дата >= КурсыВалют.НачалоПериода
		|			И ОбособленнаяНоменклатура.ЗаказКлиента.Дата < КурсыВалют.КонецПериода
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсов КАК КурсУпрВалюты
		|		ПО КурсУпрВалюты.Валюта = &ВалютаУпрУчета
		|			И ОбособленнаяНоменклатура.ЗаказКлиента.Дата >= КурсУпрВалюты.НачалоПериода
		|			И ОбособленнаяНоменклатура.ЗаказКлиента.Дата < КурсУпрВалюты.КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбособленнаяНоменклатура.Номенклатура,
		|	ОбособленнаяНоменклатура.ЗаказКлиента,
		|	КурсыВалют.Курс,
		|	КурсУпрВалюты.Курс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|   ОбеспечениеЗаказовОбороты.Период																КАК ДатаПоступления,
		|   ОбеспечениеЗаказовОбороты.Номенклатура															КАК Номенклатура,
		|   ВЫРАЗИТЬ(ОбеспечениеЗаказовОбороты.Назначение.Заказ КАК Документ.ЗаказКлиента)					КАК Заказ,
		|   ОбеспечениеЗаказовОбороты.НаличиеПодЗаказПриход													КАК НаличиеПодЗаказПриход,
		|   ВЫРАЗИТЬ(ОбеспечениеЗаказовОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг)			КАК Регистратор,
		|   ВЫРАЗИТЬ(ОбеспечениеЗаказовОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Партнер	КАК ПоставщикТовара,
		|   ОбеспечениеЗаказовОбороты.НаличиеПодЗаказПриход 												КАК КПоступлению,
		|	СРЕДНЕЕ(
		|		ВЫБОР
		|			КОГДА ВЫРАЗИТЬ(ОбеспечениеЗаказовОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Валюта = &ВалютаУпрУчета
		|				ТОГДА Цена
		|			ИНАЧЕ Цена * КурсыВалют.Курс / КурсУпрВалюты.Курс
		|		КОНЕЦ)																						КАК Себестоимость
		|ПОМЕСТИТЬ ОбеспечениеПоДокПоступления
		|ИЗ
		|   РегистрНакопления.ОбеспечениеЗаказов.Обороты(
		|   		&ДатаНачала,
		|   		&КонецСвета,
		|   		Регистратор,
		|   		(Номенклатура, Характеристика, Склад, ВЫРАЗИТЬ(Назначение.Заказ КАК Документ.ЗаказКлиента)) В
		|   			(
		|   				ВЫБРАТЬ
		|   					Номенклатура,
		|   					Характеристика,
		|						ЛогистическийСклад,
		|   					ЗаказКлиента
		|   				ИЗ
		|   					ОбособленнаяНоменклатура)) КАК ОбеспечениеЗаказовОбороты
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ТоварыПоступлений
		|		ПО ТоварыПоступлений.Ссылка = ВЫРАЗИТЬ(ОбеспечениеЗаказовОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг)
		|			И ТоварыПоступлений.Номенклатура = ОбеспечениеЗаказовОбороты.Номенклатура
		|			И ТоварыПоступлений.Назначение = ОбеспечениеЗаказовОбороты.Назначение
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсов КАК КурсыВалют
		|		ПО КурсыВалют.Валюта = ВЫРАЗИТЬ(ОбеспечениеЗаказовОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Валюта
		|			И ОбеспечениеЗаказовОбороты.Период >= КурсыВалют.НачалоПериода
		|			И ОбеспечениеЗаказовОбороты.Период < КурсыВалют.КонецПериода
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсов КАК КурсУпрВалюты
		|		ПО КурсУпрВалюты.Валюта = &ВалютаУпрУчета
		|			И ОбеспечениеЗаказовОбороты.Период >= КурсУпрВалюты.НачалоПериода
		|			И ОбеспечениеЗаказовОбороты.Период < КурсУпрВалюты.КонецПериода
		|ГДЕ
		|   ОбеспечениеЗаказовОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбеспечениеЗаказовОбороты.Период,
		|	ОбеспечениеЗаказовОбороты.Номенклатура,
		|	ОбеспечениеЗаказовОбороты.Назначение,
		|	ОбеспечениеЗаказовОбороты.НаличиеПодЗаказПриход,
		|	ОбеспечениеЗаказовОбороты.Регистратор
		|;
		|               
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|   ТоварыКПоступлениюОбороты.Период КАК ДатаПоступления,
		|   ОбеспечениеЗаказовОбороты.Номенклатура,
		|   ВЫРАЗИТЬ(ОбеспечениеЗаказовОбороты.Назначение.Заказ КАК Документ.ЗаказКлиента) КАК Заказ,
		|   ОбеспечениеЗаказовОбороты.НаличиеПодЗаказПриход,
		|   ВЫРАЗИТЬ(ОбеспечениеЗаказовОбороты.Регистратор КАК Документ.ПеремещениеТоваров) КАК Регистратор,
		|	"""" КАК ПоставщикТовара,
		|   ТоварыКПоступлениюОбороты.КПоступлениюРасход КАК КПоступлению,
		|	0 Себестоимость
		|ПОМЕСТИТЬ ПоступленияПоОбеспечению
		|ИЗ
		|   РегистрНакопления.ОбеспечениеЗаказов.Обороты(
		|   		&ДатаНачала,
		|   		&КонецСвета,
		|   		Регистратор,
		|   		(Номенклатура, Характеристика, Склад, ВЫРАЗИТЬ(Назначение.Заказ КАК Документ.ЗаказКлиента)) В
		|   			(
		|   				ВЫБРАТЬ
		|   					Номенклатура,
		|   					Характеристика,
		|						ЛогистическийСклад,
		|   					ЗаказКлиента
		|   				ИЗ
		|   					ОбособленнаяНоменклатура)) КАК ОбеспечениеЗаказовОбороты
		|   	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКПоступлению.Обороты(
		|   			&ДатаНачала,
		|   			&КонецСвета,
		|   			Регистратор,
		|   			Номенклатура В
		|   					(ВЫБРАТЬ
		|   						Номенклатура
		|   					ИЗ
		|   						ОбособленнаяНоменклатура)
		|   				И Склад = &ЛогистическийСклад) КАК ТоварыКПоступлениюОбороты
		|   	ПО ОбеспечениеЗаказовОбороты.Номенклатура = ТоварыКПоступлениюОбороты.Номенклатура
		|   		И ((ВЫРАЗИТЬ(ОбеспечениеЗаказовОбороты.Регистратор КАК Документ.ПеремещениеТоваров)) = (ВЫРАЗИТЬ(ТоварыКПоступлениюОбороты.ДокументПоступления КАК Документ.ПеремещениеТоваров)))
		|ГДЕ
		|   ОбеспечениеЗаказовОбороты.Регистратор ССЫЛКА Документ.ПеремещениеТоваров

		|ОБЪЕДИНИТЬ

		|ВЫБРАТЬ
		|   ДатаПоступления,
		|   Номенклатура,
		|   Заказ,
		|   НаличиеПодЗаказПриход,
		|   Регистратор,
		|   ПоставщикТовара,
		|   КПоступлению,
		|	Себестоимость
		|ИЗ
		|   ОбеспечениеПоДокПоступления

		|ОБЪЕДИНИТЬ

		|ВЫБРАТЬ
		|   ОбеспечениеЗаказовОбороты.Период,
		|   ОбеспечениеЗаказовОбороты.Номенклатура,
		|   ВЫРАЗИТЬ(ОбеспечениеЗаказовОбороты.Назначение.Заказ КАК Документ.ЗаказКлиента),
		|   ОбеспечениеЗаказовОбороты.НаличиеСоСкладаРасход,
		|   ВЫРАЗИТЬ(ОбеспечениеЗаказовОбороты.Регистратор КАК Документ.ПеремещениеТоваров),
		|	"""",
		|   ОбеспечениеЗаказовОбороты.НаличиеСоСкладаРасход,
		|	0
		|ИЗ
		|   РегистрНакопления.ОбеспечениеЗаказов.Обороты(
		|   		&ДатаНачала,
		|   		&КонецСвета,
		|   		Регистратор,
		|   		(Номенклатура, Характеристика, Склад, ВЫРАЗИТЬ(Назначение.Заказ КАК Документ.ЗаказКлиента)) В
		|   			(
		|   				ВЫБРАТЬ
		|   					Номенклатура,
		|   					Характеристика,
		|						ЛогистическийСклад,
		|   					ЗаказКлиента
		|   				ИЗ
		|   					ОбособленнаяНоменклатура)) КАК ОбеспечениеЗаказовОбороты
		|ГДЕ
		|   ОбеспечениеЗаказовОбороты.Регистратор ССЫЛКА Документ.ПеремещениеТоваров
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|   ОбособленнаяНоменклатура.ЗаказКлиента,
		|	ОбособленнаяНоменклатура.КодСтроки,
		|   ОбособленнаяНоменклатура.Номенклатура,
		|     ОбособленнаяНоменклатура.ДатаАктуальности,
		|   СУММА(ОбособленнаяНоменклатураКопия.КоличествоВЗаказе) - ОбособленнаяНоменклатура.КоличествоВЗаказе КАК КоличествоВЗаказеДо,
		|   СУММА(ОбособленнаяНоменклатураКопия.КоличествоВЗаказе) КАК КоличествоВЗаказеПосле
		|ПОМЕСТИТЬ ОбособленаяНоменклатураПоНаростающим
		|ИЗ
		|   ОбособленнаяНоменклатура КАК ОбособленнаяНоменклатура
		|   	ЛЕВОЕ СОЕДИНЕНИЕ ОбособленнаяНоменклатура КАК ОбособленнаяНоменклатураКопия
		|   	ПО ОбособленнаяНоменклатура.ЗаказКлиента = ОбособленнаяНоменклатураКопия.ЗаказКлиента
		|   		И ОбособленнаяНоменклатура.Номенклатура = ОбособленнаяНоменклатураКопия.Номенклатура
		|   		И ОбособленнаяНоменклатура.ДатаАктуальности >= ОбособленнаяНоменклатураКопия.ДатаАктуальности
		|   		И (ВЫБОР
		|   			КОГДА ОбособленнаяНоменклатура.ДатаАктуальности = ОбособленнаяНоменклатураКопия.ДатаАктуальности
		|   				ТОГДА ОбособленнаяНоменклатура.КодСтроки >= ОбособленнаяНоменклатураКопия.КодСтроки
		|   			ИНАЧЕ ИСТИНА
		|   		КОНЕЦ)

		|СГРУППИРОВАТЬ ПО
		|   ОбособленнаяНоменклатура.ЗаказКлиента,
		|	ОбособленнаяНоменклатура.КодСтроки,
		|   ОбособленнаяНоменклатура.Номенклатура,
		|   ОбособленнаяНоменклатура.ДатаАктуальности,
		|   ОбособленнаяНоменклатура.КоличествоВЗаказе

		|ИНДЕКСИРОВАТЬ ПО
		|   ОбособленнаяНоменклатура.ЗаказКлиента,
		|   ОбособленнаяНоменклатура.Номенклатура
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|   ПоступленияПоОбеспечению.Заказ,
		|   ПоступленияПоОбеспечению.Регистратор,
		|   ПоступленияПоОбеспечению.Номенклатура,
		|   ПоступленияПоОбеспечению.ДатаПоступления КАК ДатаПоступления,
		|   ПоступленияПоОбеспечению.КПоступлению,
		|	ПоступленияПоОбеспечению.Себестоимость,
		|   ПоступленияПоОбеспечению.ПоставщикТовара,
		|   СУММА(ПоступленияПоОбеспечениюКопия.КПоступлению) - ПоступленияПоОбеспечению.КПоступлению КАК КПоступлениюДо,
		|   СУММА(ПоступленияПоОбеспечениюКопия.КПоступлению) КАК КПоступлениюПосле
		|ПОМЕСТИТЬ ПоступленияПоОбеспечениюНаростающие
		|ИЗ
		|   ПоступленияПоОбеспечению КАК ПоступленияПоОбеспечению
		|   	ЛЕВОЕ СОЕДИНЕНИЕ ПоступленияПоОбеспечению КАК ПоступленияПоОбеспечениюКопия
		|   	ПО ПоступленияПоОбеспечению.Заказ = ПоступленияПоОбеспечениюКопия.Заказ
		|   		И ПоступленияПоОбеспечению.Номенклатура = ПоступленияПоОбеспечениюКопия.Номенклатура
		|   		И ПоступленияПоОбеспечению.ДатаПоступления >= ПоступленияПоОбеспечениюКопия.ДатаПоступления
		|   		И (ВЫБОР
		|   			КОГДА ПоступленияПоОбеспечению.ДатаПоступления = ПоступленияПоОбеспечениюКопия.ДатаПоступления
		|   				ТОГДА ПоступленияПоОбеспечению.Регистратор >= ПоступленияПоОбеспечениюКопия.Регистратор
		|   			ИНАЧЕ ИСТИНА
		|   		КОНЕЦ)

		|СГРУППИРОВАТЬ ПО
		|   ПоступленияПоОбеспечению.Заказ,
		|   ПоступленияПоОбеспечению.Регистратор,
		|   ПоступленияПоОбеспечению.Номенклатура,
		|   ПоступленияПоОбеспечению.КПоступлению,
		|   ПоступленияПоОбеспечению.Себестоимость,
		|   ПоступленияПоОбеспечению.ДатаПоступления,
		|   ПоступленияПоОбеспечению.ПоставщикТовара

		|ИНДЕКСИРОВАТЬ ПО
		|   ПоступленияПоОбеспечению.Заказ,
		|   ПоступленияПоОбеспечению.Номенклатура
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|   	ЗаказКлиента 			КАК ЗаказКлиента,
		|   	Номенклатура 			КАК Номенклатура,
		|   	КоличествоВЗаказеПосле 	КАК Точка
		|ПОМЕСТИТЬ КешТочкиПредварительные
		|ИЗ
		|   ОбособленаяНоменклатураПоНаростающим

		|ОБЪЕДИНИТЬ

		|ВЫБРАТЬ
		|   Заказ,
		|   Номенклатура,
		|   КПоступлениюПосле
		|ИЗ
		|   ПоступленияПоОбеспечениюНаростающие

		|ИНДЕКСИРОВАТЬ ПО
		|   ЗаказКлиента,
		|   Номенклатура
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТочкиПредварительные.ЗаказКлиента 				КАК ЗаказКлиента,
		|   	ТочкиПредварительные.Номенклатура 			КАК Номенклатура,
		|   	ТочкиПредварительные.Точка					КАК Точка,
		|   	ТочкиПредварительные.Точка - МАКСИМУМ(ЕСТЬNULL(ТочкиПредварительныеКопия.Точка, 0)) КАК КоличествоВЗаказе
		|ПОМЕСТИТЬ КешТочки
		|ИЗ
		|	КешТочкиПредварительные КАК ТочкиПредварительные
		|		ЛЕВОЕ СОЕДИНЕНИЕ КешТочкиПредварительные КАК ТочкиПредварительныеКопия
		|		ПО ТочкиПредварительные.ЗаказКлиента = ТочкиПредварительныеКопия.ЗаказКлиента
		|			И ТочкиПредварительные.Номенклатура = ТочкиПредварительныеКопия.Номенклатура
		|			И ТочкиПредварительные.Точка > ТочкиПредварительныеКопия.Точка

		|СГРУППИРОВАТЬ ПО
		|	ТочкиПредварительные.ЗаказКлиента,
		|   	ТочкиПредварительные.Номенклатура,
		|   	ТочкиПредварительные.Точка
		|; 	

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|   КешТочки.ЗаказКлиента 																					КАК ЗаказКлиента,
		|   КешТочки.Номенклатура																					КАК Номенклатура,
		|   КешТочки.КоличествоВЗаказе																				КАК КоличествоВЗаказе,
		|   ЕСТЬNULL(ОбособленаяНоменклатураПоНаростающим.КодСтроки, 0) 											КАК КодСтроки,
		|   ЕСТЬNULL(ОбособленаяНоменклатураПоНаростающим.ДатаАктуальности, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) 			КАК ДатаАктуальности,
		|   ЕСТЬNULL(ПоступленияПоОбеспечениюНаростающие.ДатаПоступления, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) 				КАК ДатаПоступления,
		|   ЕСТЬNULL(ПоступленияПоОбеспечениюНаростающие.ПоставщикТовара, """")                                     КАК ПоставщикТовара,
		|	ЕСТЬNULL(ПоступленияПоОбеспечениюНаростающие.КПоступлению, 0)											КАК КПоступлению,
		|	ЕСТЬNULL(ПоступленияПоОбеспечениюНаростающие.Себестоимость, 0)											КАК Себестоимость
		|ПОМЕСТИТЬ ОбъединенияПоНаростающим
		|ИЗ
		|   КешТочки КАК КешТочки
		|   	ЛЕВОЕ СОЕДИНЕНИЕ ОбособленаяНоменклатураПоНаростающим КАК ОбособленаяНоменклатураПоНаростающим
		|   	ПО КешТочки.ЗаказКлиента = ОбособленаяНоменклатураПоНаростающим.ЗаказКлиента
		|   		И КешТочки.Номенклатура = ОбособленаяНоменклатураПоНаростающим.Номенклатура
		|   		И КешТочки.Точка <= ОбособленаяНоменклатураПоНаростающим.КоличествоВЗаказеПосле
		|   		И КешТочки.Точка > ОбособленаяНоменклатураПоНаростающим.КоличествоВЗаказеДо
		|   	ЛЕВОЕ СОЕДИНЕНИЕ ПоступленияПоОбеспечениюНаростающие КАК ПоступленияПоОбеспечениюНаростающие
		|   	ПО КешТочки.ЗаказКлиента = ПоступленияПоОбеспечениюНаростающие.Заказ
		|   		И КешТочки.Номенклатура = ПоступленияПоОбеспечениюНаростающие.Номенклатура
		|   		И КешТочки.Точка <= ПоступленияПоОбеспечениюНаростающие.КПоступлениюПосле
		|   		И КешТочки.Точка > ПоступленияПоОбеспечениюНаростающие.КПоступлениюДо

		|;

		// КОНЕЦ АНАЛИЗА ЗАКАЗОВ
		
		
		
		// ПОЛУЧАЕМ ВЫРУЧКУ
		
		|ВЫБРАТЬ
		|	Продажи.ЗаказКлиента 												КАК ЗаказКлиента,
		|	АналитикаНоменклатуры.Номенклатура									КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА СУММА(Продажи.КоличествоОборот) = 0
		|			ТОГДА 0
		|		ИНАЧЕ СУММА(Продажи.СуммаВыручкиОборот) / СУММА(Продажи.КоличествоОборот) 
		|	КОНЕЦ КАК СредняяВыручка
		|ПОМЕСТИТЬ ТаблицаВыручки
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(,,Регистратор, 
		|					(АналитикаУчетаНоменклатуры.Номенклатура, ЗаказКлиента) В 
		|						(ВЫБРАТЬ
		|							Номенклатура,
		|							ЗаказКлиента
		|						 ИЗ ОбъединенияПоНаростающим)) КАК Продажи
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
		|		ПО АналитикаНоменклатуры.КлючАналитики = Продажи.АналитикаУчетаНоменклатуры
		|СГРУППИРОВАТЬ ПО
		|	Продажи.ЗаказКлиента,
		|	АналитикаНоменклатуры.Номенклатура
		|;
		
		// КОНЕЦ ВЫРУЧКА	
		
		
		
		// ОБЩИЕ ОСТАТКИ ТОВАРОВ НА СКЛАДАХ
			
		|ВЫБРАТЬ
		|	Номенклатура						КАК Номенклатура,
		|	ВНаличииОстаток - ВРезервеОстаток 	КАК ОбщийОстатокНаСкладе
		|ПОМЕСТИТЬ ОстаткиТоваровОбщие	
		|ИЗ
		|	 РегистрНакопления.СвободныеОстатки.Остатки(&ДатаОкончания, Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ СпрНоменклатура))
		|;

		// КОНЕЦ ОБЩИЕ ОСТАТКИ ТОВАРОВ НА СКЛАДАХ
		
		
		
		// ЦЕНЫ ПОСТАВЩИКОВ
		
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Период			КАК Период,
		|	ЦеныНоменклатуры.Соглашение		КАК Соглашение,
		|	ЦеныНоменклатуры.Номенклатура 	КАК Номенклатура,
		|	ВЫБОР &ВалютаУпрУчета 
		|		КОГДА ЦеныНоменклатуры.Валюта
		|		ТОГДА ЦеныНоменклатуры.Цена 
		|		ИНАЧЕ ЦеныНоменклатуры.Цена * КурсыВалют.Курс / КурсУпрВалюты.Курс
		|	КОНЕЦ					  		КАК Цена
		|ПОМЕСТИТЬ ТабЦеныЗаПериод_1
		|Из
		| 	РегистрСведений.ЦеныНоменклатурыПоставщиков КАК ЦеныНоменклатуры
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СпрНоменклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсов КАК КурсыВалют
		|		ПО КурсыВалют.Валюта = ЦеныНоменклатуры.Валюта
		|			И ЦеныНоменклатуры.Период >= КурсыВалют.НачалоПериода
		|			И ЦеныНоменклатуры.Период < КурсыВалют.КонецПериода
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсов КАК КурсУпрВалюты
		|		ПО КурсУпрВалюты.Валюта = &ВалютаУпрУчета
		|			И ЦеныНоменклатуры.Период >= КурсУпрВалюты.НачалоПериода
		|			И ЦеныНоменклатуры.Период < КурсУпрВалюты.КонецПериода
		|;
		|
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура,
		|	Минимум(ЦеныНоменклатуры.Цена) КАК Цена
		|ПОМЕСТИТЬ ТабЦеныЗаПериод_2 
		|ИЗ
		|	ТабЦеныЗаПериод_1 КАК ЦеныНоменклатуры
		|	
		|СГРУППИРОВАТЬ ПО
		|	ЦеныНоменклатуры.Номенклатура
		|;
		|
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура		КАК Номенклатура,
		|	ЦеныНоменклатуры.Цена				КАК Цена,
		|	МАКСИМУМ(ЦеныНоменклатуры.Период)   КАК Период
		|ПОМЕСТИТЬ ТабЦеныЗаПериод_3 
		|ИЗ
		|	ТабЦеныЗаПериод_2 КАК ТабЦеныЗаПериод_2
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ ТабЦеныЗаПериод_1 КАК ЦеныНоменклатуры
		|ПО	ТабЦеныЗаПериод_2.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|И	ТабЦеныЗаПериод_2.Цена		   = ЦеныНоменклатуры.Цена
		|	
		|СГРУППИРОВАТЬ ПО
		|	ЦеныНоменклатуры.Номенклатура,
		|	ЦеныНоменклатуры.Цена
		|;
		|
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура		КАК Номенклатура,
		|	ЦеныНоменклатуры.Соглашение         КАК Соглашение,
		|	ЦеныНоменклатуры.Соглашение.Партнер	КАК Партнер, 
		|	ЦеныНоменклатуры.Цена				КАК Цена
		|ПОМЕСТИТЬ ЦеныНоменклатурыЗаПериод 
		|ИЗ
		|	ТабЦеныЗаПериод_1 КАК ЦеныНоменклатуры
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабЦеныЗаПериод_3 КАК ТабЦеныЗаПериод_3
		|ПО	ТабЦеныЗаПериод_3.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|И	ТабЦеныЗаПериод_3.Цена		   = ЦеныНоменклатуры.Цена
		|И	ТабЦеныЗаПериод_3.Период	   = ЦеныНоменклатуры.Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Период			КАК Период,
		|	ЦеныНоменклатуры.Соглашение		КАК Соглашение,
		|	ЦеныНоменклатуры.Номенклатура 	КАК Номенклатура,
		|	ВЫБОР &ВалютаУпрУчета 
		|		КОГДА ЦеныНоменклатуры.Валюта
		|		ТОГДА ЦеныНоменклатуры.Цена 
		|		ИНАЧЕ ЦеныНоменклатуры.Цена * КурсыВалют.Курс / КурсУпрВалюты.Курс
		|	КОНЕЦ					  		КАК Цена
		|ПОМЕСТИТЬ ТабЦены_1
		|Из
		| 	РегистрСведений.ЦеныНоменклатурыПоставщиков.СрезПервых(&ТекущаяДата, Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ СпрНоменклатура)) КАК ЦеныНоменклатуры
		| 	
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсов КАК КурсыВалют
		|		ПО КурсыВалют.Валюта = ЦеныНоменклатуры.Валюта
		|			И ЦеныНоменклатуры.Период >= КурсыВалют.НачалоПериода
		|			И ЦеныНоменклатуры.Период < КурсыВалют.КонецПериода
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсов КАК КурсУпрВалюты
		|		ПО КурсУпрВалюты.Валюта = &ВалютаУпрУчета
		|			И ЦеныНоменклатуры.Период >= КурсУпрВалюты.НачалоПериода
		|			И ЦеныНоменклатуры.Период < КурсУпрВалюты.КонецПериода
		| 
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//4
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура,
		|	Минимум(ЦеныНоменклатуры.Цена) КАК Цена
		|ПОМЕСТИТЬ ТабЦены_2 
		|ИЗ
		|	ТабЦены_1 КАК ЦеныНоменклатуры
		|	
		|СГРУППИРОВАТЬ ПО
		|	ЦеныНоменклатуры.Номенклатура
		|;				
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//5
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура		КАК Номенклатура,
		|	ЦеныНоменклатуры.Цена				КАК Цена,
		|	МАКСИМУМ(ЦеныНоменклатуры.Период)   КАК Период
		|ПОМЕСТИТЬ ТабЦены_3 
		|ИЗ
		|	ТабЦены_2 КАК ТабЦены_2
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ ТабЦены_1 КАК ЦеныНоменклатуры
		|ПО	ТабЦены_2.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|И	ТабЦены_2.Цена		   = ЦеныНоменклатуры.Цена
		|	
		|СГРУППИРОВАТЬ ПО
		|	ЦеныНоменклатуры.Номенклатура,
		|	ЦеныНоменклатуры.Цена
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//6
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура		КАК Номенклатура,
		|	ЦеныНоменклатуры.Соглашение         КАК Соглашение,
		|	ЦеныНоменклатуры.Соглашение.Партнер	КАК Партнер, 
		|	ЦеныНоменклатуры.Цена				КАК Цена
		|ПОМЕСТИТЬ ЦеныНоменклатурыСегодня 
		|ИЗ
		|	ТабЦены_1 КАК ЦеныНоменклатуры
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабЦены_3 КАК ТабЦены_3
		|ПО	ТабЦены_3.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|И	ТабЦены_3.Цена		   = ЦеныНоменклатуры.Цена
		|И	ТабЦены_3.Период	   = ЦеныНоменклатуры.Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Партнер
		|;

		// КОНЕЦ ЦЕНЫ ПОСТАВЩИКОВ
		
		
		
		// Результирующий запрос
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|   ОбъединенияПоНаростающим.ЗаказКлиента 					КАК ЗаказКлиента,
		|	ОбъединенияПоНаростающим.ЗаказКлиента.Организация 		КАК Организация,
		|	ОбъединенияПоНаростающим.ЗаказКлиента.Подразделение 	КАК Подразделение,
		|	ОбъединенияПоНаростающим.ЗаказКлиента.Склад				КАК Склад,
		|	ОбъединенияПоНаростающим.ЗаказКлиента.Партнер			КАК Клиент,
		|   ОбъединенияПоНаростающим.Номенклатура 					КАК Номенклатура,
		|   ОбъединенияПоНаростающим.ДатаАктуальности 				КАК ДатаАктуальности,
		|   ОбъединенияПоНаростающим.ДатаПоступления 				КАК ДатаПоступления,
		|	ВЫБОР
		|		КОГДА ОбъединенияПоНаростающим.ДатаПоступления = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА 0
		|		ИНАЧЕ РАЗНОСТЬДАТ(ОбъединенияПоНаростающим.ЗаказКлиента.Дата, ОбъединенияПоНаростающим.ДатаПоступления, ДЕНЬ)
		|	КОНЕЦ КАК СрокДоставки,
		|   ВЫБОР
		|		КОГДА ОбъединенияПоНаростающим.ДатаАктуальности = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА 0
		|   	КОГДА НАЧАЛОПЕРИОДА(ОбъединенияПоНаростающим.ДатаПоступления, ДЕНЬ) = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|   			И ОбъединенияПоНаростающим.ДатаАктуальности < &ТекущаяДата
		|   		ТОГДА 1
		|   	КОГДА ОбъединенияПоНаростающим.КПоступлению < ОбъединенияПоНаростающим.КоличествоВЗаказе
		|   			И ОбъединенияПоНаростающим.ДатаАктуальности < &ТекущаяДата
		|   		ТОГДА 1
		|   	КОГДА ОбъединенияПоНаростающим.ДатаАктуальности < НАЧАЛОПЕРИОДА(ОбъединенияПоНаростающим.ДатаПоступления, ДЕНЬ)
		|   			И ОбъединенияПоНаростающим.ДатаАктуальности < &ТекущаяДата
		|   		ТОГДА 1
		|   КОНЕЦ КАК Залет,
		|   ОбъединенияПоНаростающим.Номенклатура.ВидНоменклатуры 								КАК ВидНоменклатуры,
		|   ОбъединенияПоНаростающим.КоличествоВЗаказе 											КАК КоличествоВЗаказе,
		|	ОбъединенияПоНаростающим.ПоставщикТовара			  								КАК ПоставщикТовара,
		|	ВЫБОР
		|		КОГДА ОбъединенияПоНаростающим.ПоставщикТовара ССЫЛКА Справочник.Партнеры
		|			ТОГДА ""Заказ поставщику""
		|		ИНАЧЕ ""Перемещение товаров""
		|	КОНЕЦ 																				КАК ВидОбеспечения,		
		|	ЕСТЬNULL(СуммыИЦеныЗаказов.Цена, 0)													КАК ЦенаВЗаказе,
		|	ОбъединенияПоНаростающим.КоличествоВЗаказе
		|		* ЕСТЬNULL(СуммыИЦеныЗаказов.Сумма, 0)											КАК СуммаВЗаказе,
		|
		|
		|	ОбъединенияПоНаростающим.КПоступлению 
		|			* ОбъединенияПоНаростающим.Себестоимость 									КАК Себестоимость,
		|
		|
		|	ВЫБОР
		|		КОГДА ОбъединенияПоНаростающим.КПоступлению 
		|			* ОбъединенияПоНаростающим.Себестоимость = 0
		|		ТОГДА 0
		|		ИНАЧЕ
		|			ЕСТЬNULL(ТаблицаВыручки.СредняяВыручка, 0) 
		|				* ОбъединенияПоНаростающим.КПоступлению
		|	КОНЕЦ 																				КАК Выручка,
		|
		|
		|	ВЫБОР
		|		КОГДА ОбъединенияПоНаростающим.КПоступлению 
		|			* ОбъединенияПоНаростающим.Себестоимость = 0
		|		ТОГДА 0
		|		ИНАЧЕ
		|			ЕСТЬNULL(ТаблицаВыручки.СредняяВыручка, 0) 
		|				* ОбъединенияПоНаростающим.КПоступлению -
		|			ОбъединенияПоНаростающим.КПоступлению 
		|				* ОбъединенияПоНаростающим.Себестоимость
		|	КОНЕЦ 																				КАК ВаловаяПрибыль,
		|
		|
		|	ВЫБОР
		|		КОГДА
		|			ЕСТЬNULL(ТаблицаВыручки.СредняяВыручка, 0) 
		|				* ОбъединенияПоНаростающим.КПоступлению = 0
		|		ТОГДА 0
		|		КОГДА ОбъединенияПоНаростающим.КПоступлению 
		|			* ОбъединенияПоНаростающим.Себестоимость = 0
		|		ТОГДА 0
		|		ИНАЧЕ
		|			(ЕСТЬNULL(ТаблицаВыручки.СредняяВыручка, 0) 
		|				* ОбъединенияПоНаростающим.КПоступлению -
		|			ОбъединенияПоНаростающим.КПоступлению 
		|				* ОбъединенияПоНаростающим.Себестоимость) / 
		|				ЕСТЬNULL(ТаблицаВыручки.СредняяВыручка, 0) 
		|				* ОбъединенияПоНаростающим.КПоступлению * 100
		|	КОНЕЦ																				КАК Рентабельность,
		|
		|
		|	ЕСТЬNULL(ОстаткиТоваровОбщие.ОбщийОстатокНаСкладе, 0) 								КАК ОбщийОстатокНаСкладе,
		|	ЕСТЬNULL(ЦеныНоменклатурыЗаПериод.Соглашение,"""")      							КАК ПоставщикМинЦеныЗаПериод, 
		|	ЕСТЬNULL(ЦеныНоменклатурыЗаПериод.Цена, 0)			  								КАК МинЦенаПоставщикаЗаПериод,
		|	ЕСТЬNULL(ЦеныНоменклатурыСегодня.Соглашение,"""")      								КАК ПоставщикМинЦеныЗаСегодня, 
		|	ЕСТЬNULL(ЦеныНоменклатурыСегодня.Цена, 0)				  							КАК МинЦенаПоставщикаЦенаЗаСегодня
		|ИЗ
		|   ОбособленнаяНоменклатура КАК ОбособленнаяНоменклатура
		|
		|   	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъединенияПоНаростающим КАК ОбъединенияПоНаростающим
		|   	ПО ОбособленнаяНоменклатура.Номенклатура = ОбъединенияПоНаростающим.Номенклатура
		|   		И ОбособленнаяНоменклатура.ЗаказКлиента = ОбъединенияПоНаростающим.ЗаказКлиента
		|   		И ОбособленнаяНоменклатура.КодСтроки = ОбъединенияПоНаростающим.КодСтроки
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиТоваровОбщие КАК ОстаткиТоваровОбщие
		|		ПО ОбособленнаяНоменклатура.Номенклатура = ОстаткиТоваровОбщие.Номенклатура
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатурыЗаПериод КАК ЦеныНоменклатурыЗаПериод
		|		ПО	ОбособленнаяНоменклатура.Номенклатура = ЦеныНоменклатурыЗаПериод.Номенклатура
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатурыСегодня КАК ЦеныНоменклатурыСегодня
		|		ПО	ОбособленнаяНоменклатура.Номенклатура = ЦеныНоменклатурыСегодня.Номенклатура
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаВыручки КАК ТаблицаВыручки
		|		ПО ОбособленнаяНоменклатура.ЗаказКлиента = ТаблицаВыручки.ЗаказКлиента
		|			И ОбособленнаяНоменклатура.Номенклатура = ТаблицаВыручки.Номенклатура
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыИЦеныЗаказов КАК СуммыИЦеныЗаказов
		|		ПО ОбособленнаяНоменклатура.ЗаказКлиента = СуммыИЦеныЗаказов.ЗаказКлиента
		|			И ОбособленнаяНоменклатура.Номенклатура = СуммыИЦеныЗаказов.Номенклатура
		|
		|{ГДЕ
		|	ОбъединенияПоНаростающим.ПоставщикТовара КАК Поставщик,
		|	ОбъединенияПоНаростающим.ЗаказКлиента.Склад КАК СкладЗаказа}
		|";
	
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаКомпоновки()

Процедура СформироватьОтчетПрограммно(ТекстЗапроса, ДанныеРасшифровки, ДокументРезультат, ПериодОтчета)
		
	НаборыДанных 	= СхемаКомпоновкиДанных.НаборыДанных;
	ОсновнойНабор   = НаборыДанных.Найти("НаборДанных1");
	
	Если ОсновнойНабор = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Отчет сломался. Сообщите в службу поддержки.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	КонецЕсли;
	
	ОсновнойНабор.Запрос = ТекстЗапроса;
	
	УстановитьПараметрыКомпоновкиДанных(КомпоновщикНастроек, ПериодОтчета);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки, , , Ложь);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);

КонецПроцедуры

Процедура УстановитьПараметрыКомпоновкиДанных(КомпоновщикНастроек, ПериодОтчета)

	ПараметрыДанных 	= КомпоновщикНастроек.Настройки.ПараметрыДанных;
	
	ЛогистическийСклад	= Справочники.Склады.ПолучитьСсылку(Новый УникальныйИдентификатор("d10d3409-40c2-11e0-9f98-001517115d85")); // Склад Соборна
	УстановитьЗначениеПараметраКомпоновкиДанных("ЛогистическийСклад", ЛогистическийСклад, ПараметрыДанных);
	
	УстановитьЗначениеПараметраКомпоновкиДанных("ТекущаяДата", ТекущаяДата(), ПараметрыДанных);
	
	ВалютаУпрУчета = Константы.ВалютаУправленческогоУчета.Получить();
	УстановитьЗначениеПараметраКомпоновкиДанных("ВалютаУпрУчета", ВалютаУпрУчета, ПараметрыДанных);
	
	УстановитьЗначениеПараметраКомпоновкиДанных("КонецСвета", Дата("39990101000000"), ПараметрыДанных);
	
	Если ПериодОтчета <> Неопределено Тогда
		УстановитьЗначениеПараметраКомпоновкиДанных("ДатаНачала", ПериодОтчета.ДатаНачала, ПараметрыДанных);
		УстановитьЗначениеПараметраКомпоновкиДанных("ДатаОкончания", ПериодОтчета.ДатаОкончания, ПараметрыДанных);
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьЗначениеПараметраКомпоновкиДанных(ИмяПараметра, ЗначениеПараметра, ПараметрыДанных)

	Параметр = ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	
	Если Параметр = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Не удалось получить параметр «%1». Обратитесь в службу технической поддержки.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ИмяПараметра);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Параметр.Значение 		= ЗначениеПараметра;
	Параметр.Использование  = Истина;

КонецПроцедуры

Функция ПолучитьЗначениеПараметра(ИмяПараметра)
	
	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если ПараметрДанных <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки.Значение;
		Иначе
			Возврат ПараметрДанных.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СведенияОВнешнемОтчете

Функция СведенияОВнешнейОбработке() Экспорт
	
	Версия = "1.0.0";
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительныйОтчет");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Анализ заказов клиентов и их рентабельности");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Анализ заказов клиентов и их рентабельности'");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "Анализ заказов клиентов и их рентабельности", "ФормаОтчета", "ОткрытиеФормы", Ложь, "");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

#КонецОбласти
