
#Область ИнтерфейсАвтоматическихТестов

Перем ЮнитТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮнитТест = ЮнитТестирование;
	
	СписокТестов = Новый Массив;
	
	ПараметрыТеста = Новый Структура("ИмяТеста, Транзакция, Параметр", "Тест_УстановитьКПП", Истина);
	СписокТестов.Добавить(ПараметрыТеста);
	
	Возврат СписокТестов;
	
КонецФункции 



Процедура Тест_УстановитьКПП(Параметр=Неопределено) Экспорт
	
	ПТиУ = СоздатьПоступлениеТоваровИУслуг();
	
	ДопСообщениеОшибки = НСтр("ru='#5249. Не создан документ «Поступление товаров и услуг».'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	ЮнитТест.ПроверитьИстину(ПТиУ.Проведен, ДопСообщениеОшибки);
	
	ДопСообщениеОшибки = НСтр("ru='#5249. В документе «Поступление товаров и услуг» не заполненная таб. часть «Товары».'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	ЮнитТест.ПроверитьИстину(ПТиУ.Товары.Количество(), ДопСообщениеОшибки);
	
	Номенклатура = ПТиУ.Товары[0].Номенклатура; 	
	Остатки = РегистрыНакопления.ТоварыНаСкладах.Остатки(,Новый Структура("Номенклатура", Номенклатура),"Номенклатура","ВНаличии");
	
	ДопСообщениеОшибки = НСтр("ru='#5249. Номенклатура не на остатках.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	ЮнитТест.ПроверитьИстину(Остатки.Количество(), ДопСообщениеОшибки);
	
	УстановитьКПП();
	
	КПП_Опт		= РегистрыСведений.КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж.СрезПоследних(,Новый Структура("Номенклатура",Номенклатура));
	
	ДопСообщениеОшибки = НСтр("ru='#5249. Нет записи про КПП опт.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	ЮнитТест.ПроверитьИстину(КПП_Опт.Количество(), ДопСообщениеОшибки);
	
	ДопСообщениеОшибки = НСтр("ru='#5249. Не установлен КПП опт.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	ЮнитТест.ПроверитьРавенство(Справочники.КоефициентПриоритетностиНоменклатуры.Актуальный, КПП_Опт[0].Значение, ДопСообщениеОшибки);
	
	КПП_Розница	= РегистрыСведений.КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж.СрезПоследних(,Новый Структура("Номенклатура",Номенклатура));
	
	ДопСообщениеОшибки = НСтр("ru='#5249. Нет записи про КПП розница.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	ЮнитТест.ПроверитьИстину(КПП_Розница.Количество(), ДопСообщениеОшибки);
	
	ДопСообщениеОшибки = НСтр("ru='#5249. Не установлен КПП розница.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	ЮнитТест.ПроверитьРавенство(Справочники.КоефициентПриоритетностиНоменклатуры.Актуальный, КПП_Розница[0].Значение, ДопСообщениеОшибки);
	
КонецПроцедуры

Функция СоздатьПоступлениеТоваровИУслуг()
	
	Бибилиотека = ПолучитьБиблиотекуПоступлениеТоваров();
	
	Возврат Бибилиотека.СоздатьИПровестиДокумент();

КонецФункции // СоздатьПоступлениеТоваровИУслуг()

#Область Библиотеки

Функция ПолучитьБиблиотеку(ПутьКБиблиотеке)

	WshShell 	= Новый COMobject("wscript.shell"); 
	WshEnvirUsr = WshShell.Environment("USER");

	ПутьКРепозиторию 		= WshEnvirUsr.Item("TEST_REPOSITORY");
	ПутьКВнешнемуОбъекту 	= ПутьКРепозиторию + ПутьКБиблиотеке;
	
	Возврат git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектИзФайловойСистемы(ПутьКВнешнемуОбъекту);

КонецФункции // ПолучитьБиблиотеку()

Функция ПолучитьБиблиотекуПоступлениеТоваров()
	ПутьКБиблиотеке = "\tests\Документы\ПоступлениеТоваровУслуг\СоздатьИПровестиДокумент.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуПоступлениеТоваров()

#КонецОбласти

#КонецОбласти


#Область ПрограммныйИнтерфейс

Процедура УстановитьКПП() Экспорт
	
	Запрос 					= Новый Запрос;
	Запрос.Текст 			= СтрЗаменить(СформироватьТекстЗапроса(), "%РегистрКПП%", "Розница");
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());	
	
	НаборЗаписей = РегистрыСведений.КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	НаборЗаписей.Записать(Ложь);
	
	Запрос 					= Новый Запрос;
	Запрос.Текст 			= СтрЗаменить(СформироватьТекстЗапроса(), "%РегистрКПП%", "Опт");
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());	
	
	НаборЗаписей = РегистрыСведений.КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьТекстЗапроса()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&ТекущаяДата 															КАК Период,
		|	ТоварыВНаличии.Номенклатура 											КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.Актуальный)	КАК Значение
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыВНаличии
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КПП_%РегистрКПП%_ЗначениеКоефициентаПриоритетностиПродаж.СрезПоследних КАК КПП
		|	ПО 
		|		ТоварыВНаличии.Номенклатура = КПП.Номенклатура
		|ГДЕ
		|	ЕСТЬNULL(КПП.Значение, ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка)
		|	И ЕСТЬNULL(КПП.НеПересчитыватьАвтоматически, ЛОЖЬ) = ЛОЖЬ";

	Возврат ТекстЗапроса;

КонецФункции // СформироватьТекстЗапроса()

#КонецОбласти

#Область СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	Версия = "1.0.0";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Регл. установка КПП остаткам на складах ");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Регл. установка КПП остаткам на складах [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
	ДобавитьКоманду(ТаблицаКоманд, "Регл. установка КПП остаткам на складах [" + Версия + "]", "УстановитьКПП();", "ВызовСерверногоМетода");
	
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

Процедура ВыполнитьКоманду(Индентификатор) Экспорт
	Выполнить(Индентификатор);	
КонецПроцедуры

#КонецОбласти
