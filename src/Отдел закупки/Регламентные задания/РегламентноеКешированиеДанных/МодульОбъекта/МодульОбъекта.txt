
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура РегламентноеКешированиеДанныхГрафикаПлатежей(ПартнерСсылка = Неопределено) Экспорт

	ТекущаяДата = ТекущаяДата();
	НачалоДня = НачалоДня(ТекущаяДата);
	ЗавтраДня = КонецДня(ТекущаяДата) + 1; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаДанныеГрафикаПлатежей();
	Запрос.УстановитьПараметр("Партнер", ПартнерСсылка);
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня);
	Запрос.УстановитьПараметр("ВалютаУпрУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаРеглУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("Сегмент", Справочники.СегментыПартнеров.ПолучитьСсылку(Новый УникальныйИдентификатор("388032f2-44e0-11e0-9f98-001517115d85"))); // Партнери (покупці гуртового відділу)
	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(Новый УникальныйИдентификатор("9405b638-ab95-11e4-80ea-001e676b0175"))); // Договор грн
	//Запрос.УстановитьПараметр("Организация", Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("0fba8a92-4010-11e0-9f98-001517115d85")));
	
	Если ТипЗнч(ПартнерСсылка) = Тип("СправочникСсылка.Партнеры") Тогда
	 	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтсечениеПоПартнеру%", "И ПартнерыСегмента.Партнер = &Партнер");
	Иначе	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтсечениеПоПартнеру%", "");
	КонецЕсли; 
	
	НаборЗаписей = РегистрыСведений.КТС_ГрафикПлатежейПартнеров.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(НачалоДня);
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	НаборЗаписей.Записать();
	
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(НачалоДня - 1));
	НаборЗаписей.Отбор.Период.Установить(НачалоДня(НачалоДня - 1));
	Если ТипЗнч(ПартнерСсылка) = Тип("СправочникСсылка.Партнеры") Тогда
		НаборЗаписей.Отбор.Партнер.Установить(ПартнерСсылка);	
	КонецЕсли;
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	НаборЗаписей.Записать();
	
	Запрос.УстановитьПараметр("ТекущаяДата", ЗавтраДня);
	НаборЗаписей.Отбор.Период.Установить(ЗавтраДня);
	Если ТипЗнч(ПартнерСсылка) = Тип("СправочникСсылка.Партнеры") Тогда
		НаборЗаписей.Отбор.Партнер.Установить(ПартнерСсылка);	
	КонецЕсли;
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	НаборЗаписей.Записать();
	
КонецПроцедуры // РегламентноеКешированиеДанныхГрафикаПлатежей()
  


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьТекстЗапросаДанныеГрафикаПлатежей()

	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////
		// 0.
		|ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Валюта,
		|	КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность КАК Курс
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалютСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КурсыВалютСрезПоследних.Валюта
		|;
		////////////////////////////////////////////////////////////////////////////////
		// 1.
		|ВЫБРАТЬ
		|	Партнер
		|ПОМЕСТИТЬ ПартнерыСегмента
		|ИЗ
		|	РегистрСведений.ПартнерыСегмента
		|ГДЕ
		|	Сегмент = &Сегмент
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Партнер		
		|;
		////////////////////////////////////////////////////////////////////////////////
		// 2.
		|ВЫБРАТЬ
		|	АналитикаУчетаПоПартнерам.Партнер,
		|	АналитикаУчетаПоПартнерам.Организация,
		|	АналитикаУчетаПоПартнерам.КлючАналитики
		|ПОМЕСТИТЬ Аналитика
		|ИЗ
		|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
		|ГДЕ
		|	АналитикаУчетаПоПартнерам.Партнер В (ВЫБРАТЬ Партнер ИЗ ПартнерыСегмента)
		|	%ОтсечениеПоПартнеру%
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПоПартнерам.КлючАналитики
		|;
		////////////////////////////////////////////////////////////////////////////////
		// 3.
		|ВЫБРАТЬ
		|	ДОП.Ссылка КАК Договор_UAH
		|ПОМЕСТИТЬ Договоры_UAH	
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов.ДополнительныеРеквизиты КАК ДОП 
		|ГДЕ
		|	ДОП.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
		|И	Свойство = &Свойство
		|И	Значение = Истина
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Договор_UAH		
		|;
		////////////////////////////////////////////////////////////////////////////////
		// 4.
		|ВЫБРАТЬ
		|	Аналитика.Партнер КАК Партнер,
		|	СУММА(ВЫБОР &ВалютаУпрУчета
		|			КОГДА Долги.Валюта
		|				ТОГДА Долги.СуммаОстаток
		|			ИНАЧЕ Долги.СуммаОстаток / КурсыВалютУПР.Курс * КурсыВалют.Курс
		|		КОНЕЦ) КАК Долг
		|ПОМЕСТИТЬ Долги_USD
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		РасчетыСКлиентами.Валюта КАК Валюта,
		|		РасчетыСКлиентами.СуммаОстаток КАК СуммаОстаток
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентами.Остатки(
		|				&ТекущаяДата,
		|				АналитикаУчетаПоПартнерам В
		|					(ВЫБРАТЬ
		|						Аналитика.КлючАналитики
		|					ИЗ
		|						Аналитика)
		|				И	Валюта <> &ВалютаРеглУчета) КАК РасчетыСКлиентами
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам,
		|		РасчетыСПоставщиками.Валюта,
		|		РасчетыСПоставщиками.СуммаОстаток
		|	ИЗ
		|		РегистрНакопления.РасчетыСПоставщиками.Остатки(
		|				&ТекущаяДата,
		|				АналитикаУчетаПоПартнерам В
		|					(ВЫБРАТЬ
		|						Аналитика.КлючАналитики
		|					ИЗ
		|						Аналитика)
		|				И	Валюта <> &ВалютаРеглУчета) КАК РасчетыСПоставщиками) КАК Долги
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитика КАК Аналитика
		|ПО (Аналитика.КлючАналитики = Долги.АналитикаУчетаПоПартнерам)
		|				
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУПР
		|ПО КурсыВалютУПР.Валюта = &ВалютаУпрУчета
		|		
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|ПО КурсыВалют.Валюта = Долги.Валюта
		|
		|СГРУППИРОВАТЬ ПО
		|	Аналитика.Партнер
		|;
		////////////////////////////////////////////////////////////////////////////////
		// 5.
		|ВЫБРАТЬ
		|	Аналитика.Партнер КАК Партнер,
		|	СУММА(ВЫБОР &ВалютаРеглУчета
		|			КОГДА Долги.Валюта
		|				ТОГДА Долги.СуммаОстаток
		|			ИНАЧЕ Долги.СуммаОстаток / КурсыВалютРегл.Курс * КурсыВалют.Курс
		|		КОНЕЦ) КАК Долг
		|ПОМЕСТИТЬ Долги_UAH
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		РасчетыСКлиентами.Валюта КАК Валюта,
		|		РасчетыСКлиентами.СуммаОстаток КАК СуммаОстаток
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентами.Остатки(
		|				&ТекущаяДата,
		|				АналитикаУчетаПоПартнерам В
		|					(ВЫБРАТЬ
		|						Аналитика.КлючАналитики
		|					ИЗ
		|						Аналитика)
		|				И	Валюта = &ВалютаРеглУчета) КАК РасчетыСКлиентами
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам,
		|		РасчетыСПоставщиками.Валюта,
		|		РасчетыСПоставщиками.СуммаОстаток
		|	ИЗ
		|		РегистрНакопления.РасчетыСПоставщиками.Остатки(
		|				&ТекущаяДата,
		|				АналитикаУчетаПоПартнерам В
		|					(ВЫБРАТЬ
		|						Аналитика.КлючАналитики
		|					ИЗ
		|						Аналитика)
		|				И	Валюта = &ВалютаРеглУчета) КАК РасчетыСПоставщиками) КАК Долги
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитика КАК Аналитика
		|ПО (Аналитика.КлючАналитики = Долги.АналитикаУчетаПоПартнерам)

		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютРегл
		|ПО КурсыВалютРегл.Валюта = &ВалютаРеглУчета
		|		
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|ПО (КурсыВалют.Валюта = Долги.Валюта)
		|
		|СГРУППИРОВАТЬ ПО
		|	Аналитика.Партнер
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ЗаказКлиентаЭтапыГрафикаОплаты.Ссылка,
		|	МИНИМУМ(ЗаказКлиентаЭтапыГрафикаОплаты.ДатаПлатежа) КАК ДатаПлатежа
		|ПОМЕСТИТЬ ГрафикОплат
		|ИЗ
		|	Документ.ЗаказКлиента.ЭтапыГрафикаОплаты КАК ЗаказКлиентаЭтапыГрафикаОплаты
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартнерыСегмента КАК ПартнерыСегмента 
		|ПО	ПартнерыСегмента.Партнер = ЗаказКлиентаЭтапыГрафикаОплаты.Ссылка.Партнер
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиентаЭтапыГрафикаОплаты.Ссылка
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	РасчетыСКлиентамиОстаткиИОбороты.АналитикаУчетаПоПартнерам КАК КлючАналитики,
		|	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.ВозвратТоваровОтКлиента) КАК Регистратор,
		|	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.ВозвратТоваровОтКлиента).Валюта КАК Валюта
		|ПОМЕСТИТЬ ПервыйКеш_Возвраты
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(
		|			,
		|			&ТекущаяДата,
		|			Регистратор,
		|			,
		|			АналитикаУчетаПоПартнерам В
		|				(ВЫБРАТЬ
		|					Аналитика.КлючАналитики
		|				ИЗ
		|					Аналитика)
		|			) КАК РасчетыСКлиентамиОстаткиИОбороты
		|ГДЕ
		|	РасчетыСКлиентамиОстаткиИОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
		|И 	РасчетыСКлиентамиОстаткиИОбороты.СуммаОборот <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	КлючАналитики
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	Возвраты.КлючАналитики,
		|	Возвраты.Валюта,
		|	ВозвратыТовары.ДокументРеализации,
		|	СУММА(ВозвратыТовары.СуммаСНДС) КАК СуммаРасход
		|ПОМЕСТИТЬ ВторойКеш_Возвраты
		|ИЗ
		|	ПервыйКеш_Возвраты КАК Возвраты
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ВозвратыТовары
		|ПО (ВозвратыТовары.Ссылка = Возвраты.Регистратор)
		|
		|СГРУППИРОВАТЬ ПО
		|	Возвраты.КлючАналитики,
		|	Возвраты.Валюта,
		|	ВозвратыТовары.ДокументРеализации
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	РасчетыСКлиентамиОстаткиИОбороты.АналитикаУчетаПоПартнерам КАК КлючАналитики,
		|	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг) КАК Регистратор,
		|	РасчетыСКлиентамиОстаткиИОбороты.Регистратор.МоментВремени КАК МоментВремени,
		|	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента КАК ЗаказКлиента,
		|	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).ДатаПлатежа КАК ДатаПлатежа,
		|	РасчетыСКлиентамиОстаткиИОбороты.Период,
		|	РасчетыСКлиентамиОстаткиИОбороты.Валюта,
		|	РасчетыСКлиентамиОстаткиИОбороты.СуммаПриход,
		|	РасчетыСКлиентамиОстаткиИОбороты.СуммаОборот
		|ПОМЕСТИТЬ Cache_Обороты_USD
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(
		|			,
		|			&ТекущаяДата,
		|			Регистратор,
		|			,
		|			АналитикаУчетаПоПартнерам В
		|				(ВЫБРАТЬ
		|					Аналитика.КлючАналитики
		|				ИЗ
		|					Аналитика)
		|			И	Валюта <> &ВалютаРеглУчета) КАК РасчетыСКлиентамиОстаткиИОбороты
		|ГДЕ
		|	РасчетыСКлиентамиОстаткиИОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
		|И 	РасчетыСКлиентамиОстаткиИОбороты.СуммаОборот <> 0
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	РасчетыСКлиентамиОстаткиИОбороты.АналитикаУчетаПоПартнерам КАК КлючАналитики,
		|	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг) КАК Регистратор,
		|	РасчетыСКлиентамиОстаткиИОбороты.Регистратор.МоментВремени КАК МоментВремени,
		|	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента КАК ЗаказКлиента,
		|	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).ДатаПлатежа КАК ДатаПлатежа,
		|	РасчетыСКлиентамиОстаткиИОбороты.Период,
		|	РасчетыСКлиентамиОстаткиИОбороты.Валюта,
		|	РасчетыСКлиентамиОстаткиИОбороты.СуммаПриход,
		|	РасчетыСКлиентамиОстаткиИОбороты.СуммаОборот
		|ПОМЕСТИТЬ Cache_Обороты_UAH
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(
		|			,
		|			&ТекущаяДата,
		|			Регистратор,
		|			,
		|			АналитикаУчетаПоПартнерам В
		|				(ВЫБРАТЬ
		|					Аналитика.КлючАналитики
		|				ИЗ
		|					Аналитика)
		|			И	Валюта = &ВалютаРеглУчета) КАК РасчетыСКлиентамиОстаткиИОбороты
		|ГДЕ
		|	РасчетыСКлиентамиОстаткиИОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
		|И 	РасчетыСКлиентамиОстаткиИОбороты.СуммаОборот <> 0
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	Аналитика.Партнер КАК Партнер,
		|	Обороты.Регистратор,
		|	Обороты.Период,
		|	ВЫБОР
		|		КОГДА Обороты.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
		|				ИЛИ Обороты.ЗаказКлиента = НЕОПРЕДЕЛЕНО
		|				ИЛИ Обороты.ЗаказКлиента = NULL
		|			ТОГДА Обороты.ДатаПлатежа
		|		ИНАЧЕ ГрафикОплат.ДатаПлатежа
		|	КОНЕЦ КАК ДатаПлатежа,
		|	Обороты.МоментВремени,
		|	ВЫБОР
		|		КОГДА &ВалютаУпрУчета = Обороты.Валюта
		|			ТОГДА Обороты.СуммаПриход
		|		ИНАЧЕ Обороты.СуммаПриход / КурсыВалютУПР.Курс * КурсыВалют.Курс
		|	КОНЕЦ - ВЫБОР
		|		КОГДА ЕСТЬNULL(Возвраты.СуммаРасход, 0) = 0
		|			ТОГДА 0
		|		КОГДА &ВалютаУпрУчета = Возвраты.Валюта
		|			ТОГДА Возвраты.СуммаРасход
		|		ИНАЧЕ Возвраты.СуммаРасход / КурсыВалютУПР.Курс * КурсыВозвраты.Курс
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА &ВалютаУпрУчета = Обороты.Валюта
		|			ТОГДА Обороты.СуммаОборот
		|		ИНАЧЕ Обороты.СуммаОборот / КурсыВалютУПР.Курс * КурсыВалют.Курс
		|	КОНЕЦ - ВЫБОР
		|		КОГДА ЕСТЬNULL(Возвраты.СуммаРасход, 0) = 0
		|			ТОГДА 0
		|		КОГДА &ВалютаУпрУчета = Возвраты.Валюта
		|			ТОГДА Возвраты.СуммаРасход
		|		ИНАЧЕ Возвраты.СуммаРасход / КурсыВалютУПР.Курс * КурсыВозвраты.Курс
		|	КОНЕЦ КАК СуммаОборот
		|ПОМЕСТИТЬ ОборотыПоКлиенту_USD
		|ИЗ
		|	Cache_Обороты_USD КАК Обороты
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитика КАК Аналитика
		|ПО Аналитика.КлючАналитики = Обороты.КлючАналитики
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ВторойКеш_Возвраты КАК Возвраты
		|ПО Возвраты.КлючАналитики = Обороты.КлючАналитики
		|И  Возвраты.ДокументРеализации = Обороты.Регистратор
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВозвраты
		|ПО КурсыВозвраты.Валюта = Возвраты.Валюта
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ГрафикОплат КАК ГрафикОплат
		|ПО ГрафикОплат.Ссылка = Обороты.ЗаказКлиента
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУПР
		|ПО КурсыВалютУПР.Валюта = &ВалютаУпрУчета
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|ПО КурсыВалют.Валюта = Обороты.Валюта
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	Аналитика.Партнер КАК Партнер,
		|	Обороты.Регистратор,
		|	Обороты.Период,
		|	ВЫБОР
		|		КОГДА Обороты.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
		|				ИЛИ Обороты.ЗаказКлиента = НЕОПРЕДЕЛЕНО
		|				ИЛИ Обороты.ЗаказКлиента = NULL
		|			ТОГДА Обороты.ДатаПлатежа
		|		ИНАЧЕ ГрафикОплат.ДатаПлатежа
		|	КОНЕЦ КАК ДатаПлатежа,
		|	Обороты.МоментВремени,
		|	ВЫБОР
		|		КОГДА &ВалютаРеглУчета = Обороты.Валюта
		|			ТОГДА Обороты.СуммаПриход
		|		ИНАЧЕ Обороты.СуммаПриход / КурсыВалютУПР.Курс * КурсыВалют.Курс
		|	КОНЕЦ - ВЫБОР
		|		КОГДА ЕСТЬNULL(Возвраты.СуммаРасход, 0) = 0
		|			ТОГДА 0
		|		КОГДА &ВалютаРеглУчета = Возвраты.Валюта
		|			ТОГДА Возвраты.СуммаРасход
		|		ИНАЧЕ Возвраты.СуммаРасход / КурсыВалютУПР.Курс * КурсыВозвраты.Курс
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА &ВалютаРеглУчета = Обороты.Валюта
		|			ТОГДА Обороты.СуммаОборот
		|		ИНАЧЕ Обороты.СуммаОборот / КурсыВалютУПР.Курс * КурсыВалют.Курс
		|	КОНЕЦ - ВЫБОР
		|		КОГДА ЕСТЬNULL(Возвраты.СуммаРасход, 0) = 0
		|			ТОГДА 0
		|		КОГДА &ВалютаРеглУчета = Возвраты.Валюта
		|			ТОГДА Возвраты.СуммаРасход
		|		ИНАЧЕ Возвраты.СуммаРасход / КурсыВалютУПР.Курс * КурсыВозвраты.Курс
		|	КОНЕЦ КАК СуммаОборот
		|ПОМЕСТИТЬ ОборотыПоКлиенту_UAH
		|ИЗ
		|	Cache_Обороты_UAH КАК Обороты
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитика КАК Аналитика
		|ПО (Аналитика.КлючАналитики = Обороты.КлючАналитики)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ВторойКеш_Возвраты КАК Возвраты
		|ПО (Возвраты.КлючАналитики = Обороты.КлючАналитики)
		|И  (Возвраты.ДокументРеализации = Обороты.Регистратор)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВозвраты
		|ПО (КурсыВозвраты.Валюта = Возвраты.Валюта)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ГрафикОплат КАК ГрафикОплат
		|ПО (ГрафикОплат.Ссылка = Обороты.ЗаказКлиента)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУПР
		|ПО КурсыВалютУПР.Валюта = &ВалютаРеглУчета
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|ПО (КурсыВалют.Валюта = Обороты.Валюта)
		|;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Аналитика;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КурсыВалют;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ГрафикОплат;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Cache_Обороты_USD;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Cache_Обороты_UAH;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ОборотыПоКлиенту.Партнер КАК Партнер,
		|	НАЧАЛОПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ) КАК НачПериода,
		|	КОНЕЦПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ) КАК КонПериода,
		|	СУММА(ОборотыПоКлиенту.СуммаОборот) КАК Сумма
		|ПОМЕСТИТЬ ОборотыПоМесяцам_USD
		|ИЗ
		|	ОборотыПоКлиенту_USD КАК ОборотыПоКлиенту
		|
		|СГРУППИРОВАТЬ ПО
		|	ОборотыПоКлиенту.Партнер,
		|	НАЧАЛОПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ),
		|	КОНЕЦПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ)
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ОборотыПоКлиенту.Партнер КАК Партнер,
		|	НАЧАЛОПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ) КАК НачПериода,
		|	КОНЕЦПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ) КАК КонПериода,
		|	СУММА(ОборотыПоКлиенту.СуммаОборот) КАК Сумма
		|ПОМЕСТИТЬ ОборотыПоМесяцам_UAH
		|ИЗ
		|	ОборотыПоКлиенту_UAH КАК ОборотыПоКлиенту
		|
		|СГРУППИРОВАТЬ ПО
		|	ОборотыПоКлиенту.Партнер,
		|	НАЧАЛОПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ),
		|	КОНЕЦПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ)
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ОборотыПоМесяцам.Партнер,
		|	ОборотыПоМесяцам.НачПериода,
		|	ОборотыПоМесяцам.КонПериода,
		|	ОборотыПоМесяцам.Сумма,
		|	СУММА(ОборотыПоМесяцамКопия.Сумма) КАК СуммаПосле,
		|	СУММА(ОборотыПоМесяцамКопия.Сумма) - ОборотыПоМесяцам.Сумма КАК СуммаДо
		|ПОМЕСТИТЬ ОборотыПоМесяцамНарастающие_USD
		|ИЗ
		|	ОборотыПоМесяцам_USD КАК ОборотыПоМесяцам
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОборотыПоМесяцам_USD КАК ОборотыПоМесяцамКопия
		|ПО 	ОборотыПоМесяцам.Партнер = ОборотыПоМесяцамКопия.Партнер
		|И 	ОборотыПоМесяцам.НачПериода <= ОборотыПоМесяцамКопия.НачПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	ОборотыПоМесяцам.НачПериода,
		|	ОборотыПоМесяцам.КонПериода,
		|	ОборотыПоМесяцам.Партнер,
		|	ОборотыПоМесяцам.Сумма
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ОборотыПоМесяцам.Партнер,
		|	ОборотыПоМесяцам.НачПериода,
		|	ОборотыПоМесяцам.КонПериода,
		|	ОборотыПоМесяцам.Сумма,
		|	СУММА(ОборотыПоМесяцамКопия.Сумма) КАК СуммаПосле,
		|	СУММА(ОборотыПоМесяцамКопия.Сумма) - ОборотыПоМесяцам.Сумма КАК СуммаДо
		|ПОМЕСТИТЬ ОборотыПоМесяцамНарастающие_UAH
		|ИЗ
		|	ОборотыПоМесяцам_UAH КАК ОборотыПоМесяцам
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОборотыПоМесяцам_UAH КАК ОборотыПоМесяцамКопия
		|ПО 	ОборотыПоМесяцам.Партнер = ОборотыПоМесяцамКопия.Партнер
		|И 	ОборотыПоМесяцам.НачПериода <= ОборотыПоМесяцамКопия.НачПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	ОборотыПоМесяцам.НачПериода,
		|	ОборотыПоМесяцам.КонПериода,
		|	ОборотыПоМесяцам.Партнер,
		|	ОборотыПоМесяцам.Сумма
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	Долги.Партнер,
		|	Долги.Долг,
		|	ОборотыПоМесНарастающие.НачПериода,
		|	ОборотыПоМесНарастающие.КонПериода,
		|	Долги.Долг - ОборотыПоМесНарастающие.СуммаДо КАК ОстатокДолга
		|ПОМЕСТИТЬ ДолгиПоВыбраннымМесяцам_USD
		|ИЗ
		|	Долги_USD КАК Долги
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОборотыПоМесяцамНарастающие_USD КАК ОборотыПоМесНарастающие
		|ПО 	Долги.Партнер = ОборотыПоМесНарастающие.Партнер
		|И 	Долги.Долг > ОборотыПоМесНарастающие.СуммаДо
		|И 	Долги.Долг <= ОборотыПоМесНарастающие.СуммаПосле
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	Долги.Партнер,
		|	Долги.Долг,
		|	ОборотыПоМесНарастающие.НачПериода,
		|	ОборотыПоМесНарастающие.КонПериода,
		|	Долги.Долг - ОборотыПоМесНарастающие.СуммаДо КАК ОстатокДолга
		|ПОМЕСТИТЬ ДолгиПоВыбраннымМесяцам_UAH
		|ИЗ
		|	Долги_UAH КАК Долги
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОборотыПоМесяцамНарастающие_UAH КАК ОборотыПоМесНарастающие
		|ПО 	Долги.Партнер = ОборотыПоМесНарастающие.Партнер
		|И 	Долги.Долг > ОборотыПоМесНарастающие.СуммаДо
		|И 	Долги.Долг <= ОборотыПоМесНарастающие.СуммаПосле
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	Обороты.Партнер,
		|	Обороты.МоментВремени,
		|	Обороты.Регистратор,
		|	Обороты.Период,
		|	ДолгиПоВыбМесяцам.Долг,
		|	ДолгиПоВыбМесяцам.ОстатокДолга,
		|	Обороты.Сумма,
		|	Обороты.ДатаПлатежа КАК ДатаОтсрочки
		|ПОМЕСТИТЬ ДвиженияПоВыбраннымМесяцам_USD
		|ИЗ
		|	ОборотыПоКлиенту_USD КАК Обороты
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолгиПоВыбраннымМесяцам_USD КАК ДолгиПоВыбМесяцам
		|ПО 	Обороты.Партнер = ДолгиПоВыбМесяцам.Партнер
		|И 	Обороты.ДатаПлатежа >= ДолгиПоВыбМесяцам.НачПериода
		|И 	Обороты.ДатаПлатежа <= ДолгиПоВыбМесяцам.КонПериода
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	Обороты.Партнер,
		|	Обороты.МоментВремени,
		|	Обороты.Регистратор,
		|	Обороты.Период,
		|	ДолгиПоВыбМесяцам.Долг,
		|	ДолгиПоВыбМесяцам.ОстатокДолга,
		|	Обороты.Сумма,
		|	Обороты.ДатаПлатежа КАК ДатаОтсрочки
		|ПОМЕСТИТЬ ДвиженияПоВыбраннымМесяцам_UAH
		|ИЗ
		|	ОборотыПоКлиенту_UAH КАК Обороты
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолгиПоВыбраннымМесяцам_UAH КАК ДолгиПоВыбМесяцам
		|ПО Обороты.Партнер = ДолгиПоВыбМесяцам.Партнер
		|И 	Обороты.ДатаПлатежа >= ДолгиПоВыбМесяцам.НачПериода
		|И 	Обороты.ДатаПлатежа <= ДолгиПоВыбМесяцам.КонПериода
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ДвиженияПоВыбМесяцам.Партнер,
		|	ДвиженияПоВыбМесяцам.Регистратор,
		|	ДвиженияПоВыбМесяцам.Период,
		|	ДвиженияПоВыбМесяцам.МоментВремени,
		|	ДвиженияПоВыбМесяцам.Сумма,
		|	СУММА(ДвиженияПоВыбМесяцамКопия.Сумма) КАК СуммаПосле,
		|	СУММА(ДвиженияПоВыбМесяцамКопия.Сумма) - ДвиженияПоВыбМесяцам.Сумма КАК СуммаДо,
		|	ДвиженияПоВыбМесяцам.ОстатокДолга,
		|	ДвиженияПоВыбМесяцам.ДатаОтсрочки
		|ПОМЕСТИТЬ ДвиженияПредварительные_USD
		|ИЗ
		|	ДвиженияПоВыбраннымМесяцам_USD КАК ДвиженияПоВыбМесяцам
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияПоВыбраннымМесяцам_USD КАК ДвиженияПоВыбМесяцамКопия
		|ПО 	ДвиженияПоВыбМесяцам.Партнер = ДвиженияПоВыбМесяцамКопия.Партнер
		|И 	ДвиженияПоВыбМесяцам.ДатаОтсрочки <= ДвиженияПоВыбМесяцамКопия.ДатаОтсрочки
		|И 	(ВЫБОР
		|		КОГДА ДвиженияПоВыбМесяцам.ДатаОтсрочки = ДвиженияПоВыбМесяцамКопия.ДатаОтсрочки
		|		ТОГДА ДвиженияПоВыбМесяцам.МоментВремени <= ДвиженияПоВыбМесяцамКопия.МоментВремени
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДвиженияПоВыбМесяцам.Партнер,
		|	ДвиженияПоВыбМесяцам.Регистратор,
		|	ДвиженияПоВыбМесяцам.Период,
		|	ДвиженияПоВыбМесяцам.МоментВремени,
		|	ДвиженияПоВыбМесяцам.Сумма,
		|	ДвиженияПоВыбМесяцам.ОстатокДолга,
		|	ДвиженияПоВыбМесяцам.ДатаОтсрочки
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ДвиженияПоВыбМесяцам.Партнер,
		|	ДвиженияПоВыбМесяцам.Регистратор,
		|	ДвиженияПоВыбМесяцам.Период,
		|	ДвиженияПоВыбМесяцам.МоментВремени,
		|	ДвиженияПоВыбМесяцам.Сумма,
		|	СУММА(ДвиженияПоВыбМесяцамКопия.Сумма) КАК СуммаПосле,
		|	СУММА(ДвиженияПоВыбМесяцамКопия.Сумма) - ДвиженияПоВыбМесяцам.Сумма КАК СуммаДо,
		|	ДвиженияПоВыбМесяцам.ОстатокДолга,
		|	ДвиженияПоВыбМесяцам.ДатаОтсрочки
		|ПОМЕСТИТЬ ДвиженияПредварительные_UAH
		|ИЗ
		|	ДвиженияПоВыбраннымМесяцам_UAH КАК ДвиженияПоВыбМесяцам
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияПоВыбраннымМесяцам_UAH КАК ДвиженияПоВыбМесяцамКопия
		|ПО 	ДвиженияПоВыбМесяцам.Партнер = ДвиженияПоВыбМесяцамКопия.Партнер
		|И 	ДвиженияПоВыбМесяцам.ДатаОтсрочки <= ДвиженияПоВыбМесяцамКопия.ДатаОтсрочки
		|И 	(ВЫБОР
		|		КОГДА ДвиженияПоВыбМесяцам.ДатаОтсрочки = ДвиженияПоВыбМесяцамКопия.ДатаОтсрочки
		|		ТОГДА ДвиженияПоВыбМесяцам.МоментВремени <= ДвиженияПоВыбМесяцамКопия.МоментВремени
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДвиженияПоВыбМесяцам.Партнер,
		|	ДвиженияПоВыбМесяцам.Регистратор,
		|	ДвиженияПоВыбМесяцам.Период,
		|	ДвиженияПоВыбМесяцам.МоментВремени,
		|	ДвиженияПоВыбМесяцам.Сумма,
		|	ДвиженияПоВыбМесяцам.ОстатокДолга,
		|	ДвиженияПоВыбМесяцам.ДатаОтсрочки
		|;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПервыйКеш_Возвраты;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВторойКеш_Возвраты;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОборотыПоМесяцам_USD;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОборотыПоМесяцам_UAH;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОборотыПоМесяцамНарастающие_USD;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОборотыПоМесяцамНарастающие_UAH;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Долги_USD;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Долги_UAH;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДолгиПоВыбраннымМесяцам_USD;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДолгиПоВыбраннымМесяцам_UAH;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДвиженияПоВыбраннымМесяцам_USD;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДвиженияПоВыбраннымМесяцам_UAH;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ДвиженияПредв.Партнер,
		|	ДвиженияПредв.Регистратор,
		|	ДвиженияПредв.Период,
		|	ДвиженияПредв.МоментВремени,
		|	ДвиженияПредв.ОстатокДолга - ДвиженияПредв.СуммаДо КАК СуммаДолга,
		|	ДвиженияПредв.Сумма,
		|	ДвиженияПредв.ДатаОтсрочки
		|ПОМЕСТИТЬ ДвиженияОкончательные_USD
		|ИЗ
		|	ДвиженияПредварительные_USD КАК ДвиженияПредв
		|ГДЕ
		|	ДвиженияПредв.ОстатокДолга > ДвиженияПредв.СуммаДо
		|И 	ДвиженияПредв.ОстатокДолга <= ДвиженияПредв.СуммаПосле
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ДвиженияПредв.Партнер,
		|	ДвиженияПредв.Регистратор,
		|	ДвиженияПредв.Период,
		|	ДвиженияПредв.МоментВремени,
		|	ДвиженияПредв.ОстатокДолга - ДвиженияПредв.СуммаДо КАК СуммаДолга,
		|	ДвиженияПредв.Сумма,
		|	ДвиженияПредв.ДатаОтсрочки
		|ПОМЕСТИТЬ ДвиженияОкончательные_UAH
		|ИЗ
		|	ДвиженияПредварительные_UAH КАК ДвиженияПредв
		|ГДЕ
		|	ДвиженияПредв.ОстатокДолга > ДвиженияПредв.СуммаДо
		|И 	ДвиженияПредв.ОстатокДолга <= ДвиженияПредв.СуммаПосле
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	&ТекущаяДата КАК Период,
		|	Обороты.Партнер КАК Партнер,
		|	Обороты.ДатаПлатежа КАК ДатаПлатежа,
		|	Обороты.Регистратор КАК Документ,
		|	ВЫБОР
		|		КОГДА Обороты.Регистратор = ДвиженияОконч.Регистратор
		|			ТОГДА ДвиженияОконч.СуммаДолга
		|		ИНАЧЕ Обороты.Сумма
		|	КОНЕЦ 				КАК СуммаОстаток,
		|	Обороты.Сумма 		КАК НачальнаяСумма,
		|	&ВалютаУпрУчета 	КАК Валюта,
		|	РАЗНОСТЬДАТ(Обороты.Период, Обороты.ДатаПлатежа, ДЕНЬ) КАК КоличествоДнейОтстрочки,
		|	Обороты.Период КАК ДатаДокумента
		|ИЗ
		|	ОборотыПоКлиенту_USD КАК Обороты
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияОкончательные_USD КАК ДвиженияОконч
		|ПО 	(ДвиженияОконч.Партнер = Обороты.Партнер)
		|И 	(ДвиженияОконч.ДатаОтсрочки <= Обороты.ДатаПлатежа)
		|И 	(ВЫБОР
		|		КОГДА ДвиженияОконч.ДатаОтсрочки = Обороты.ДатаПлатежа
		|		ТОГДА ДвиженияОконч.МоментВремени <= Обороты.МоментВремени
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ)
		|	
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&ТекущаяДата 		КАК Период,
		|	Обороты.Партнер 	КАК Партнер,
		|	Обороты.ДатаПлатежа КАК ДатаПлатежа,
		|	Обороты.Регистратор КАК Документ,
		|	ВЫБОР
		|		КОГДА Обороты.Регистратор = ДвиженияОконч.Регистратор
		|			ТОГДА ДвиженияОконч.СуммаДолга
		|		ИНАЧЕ Обороты.Сумма
		|	КОНЕЦ 				КАК СуммаОстаток,
		|	Обороты.Сумма 		КАК НачальнаяСумма,
		|	&ВалютаРеглУчета	КАК Валюта,
		|	РАЗНОСТЬДАТ(Обороты.Период, Обороты.ДатаПлатежа, ДЕНЬ) КАК КоличествоДнейОтстрочки,
		|	Обороты.Период КАК ДатаДокумента
		|ИЗ
		|	ОборотыПоКлиенту_UAH КАК Обороты
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияОкончательные_UAH КАК ДвиженияОконч
		|ПО 	(ДвиженияОконч.Партнер = Обороты.Партнер)
		|И 	(ДвиженияОконч.ДатаОтсрочки <= Обороты.ДатаПлатежа)
		|И 	(ВЫБОР
		|		КОГДА ДвиженияОконч.ДатаОтсрочки = Обороты.ДатаПлатежа
		|		ТОГДА ДвиженияОконч.МоментВремени <= Обороты.МоментВремени
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаПлатежа
		|;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОборотыПоКлиенту_USD;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОборотыПоКлиенту_UAH;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДвиженияПредварительные_USD;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДвиженияПредварительные_UAH;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДвиженияОкончательные_USD;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДвиженияОкончательные_UAH;
		|";
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаДанныеГрафикаПлатежей()
 



Функция СведенияОВнешнейОбработке() Экспорт
	
	//Версия
	Версия = "2.0.2";
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Регл. кеширование данных графика платежей");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Регл. обновление доп. данных номенклатуры [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
	ДобавитьКоманду(ТаблицаКоманд, "Регл. кеширование данных графика платежей [" + Версия + "]", "РегламентноеКешированиеДанныхГрафикаПлатежей();", "ВызовСерверногоМетода");

    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

Процедура ВыполнитьКоманду(ИдентификаторКоманды) Экспорт
	Выполнить(ИдентификаторКоманды);	
КонецПроцедуры

