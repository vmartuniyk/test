
#Область ПрограммныйИнтерфейс
Функция ПолучитьОписаниеТипаТаблицаРезультата()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Результат.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ТоварнаяКатегория", Новый ОписаниеТипов("СправочникСсылка.ТоварныеКатегории"));
	Результат.Колонки.Добавить("Остаток");
	Результат.Колонки.Добавить("Производитель");
	Результат.Колонки.Добавить("Актуально", Новый ОписаниеТипов("Булево"));
		
	Возврат Результат;
КонецФункции

Функция ПолучитьТаблицуНастроек(Ссылка) 
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Настройки.Ссылка 			КАК Ссылка,
	                      |	Настройки.НомерСтроки 		КАК НомерСтроки,
	                      |	Настройки.ТоварнаяКатегория	КАК ТоварнаяКатегория, 
	                      |	Настройки.ВидЦены			КАК ВидЦены,
	                      |	Настройки.НижняяГраница		КАК НижняяГраница,
	                      |	Настройки.ВерхняяГраница	КАК ВерхняяГраница,
	                      |	Настройки.Производитель		КАК Производитель,
	                      |	Настройки.Содержит			КАК Содержит,
	                      |	Настройки.НеСодержит		КАК НеСодержит
	                      |ИЗ
	                      |	Справочник.ПД_АналогиНоменклатуры.Настройки КАК Настройки
	                      |ГДЕ
	                      |	Настройки.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ПД_Подпихня") Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПД_АналогиНоменклатуры", "ПД_Подпихня");
	КонецЕсли;
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Функция ПолучитьСпискаИспользуемыхЦен(Знач ТаблицаНастроек)
	Результат = ТаблицаНастроек.СкопироватьКолонки();
	Результат.Колонки.Добавить("ИндексКолонки");
	Счетчик = 0;
	Для каждого ЭлементСтроки из ТаблицаНастроек Цикл 
		Если ЗначениеЗаполнено(ЭлементСтроки.ВидЦены) Тогда 
			НоваяСтрока = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементСтроки);
			НоваяСтрока.ИндексКолонки = Счетчик;
			Счетчик = Счетчик + 1;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область ИнтерфейсВнешнегоОбъекта

#Область ПрограммныйИнтерфейс

Функция ПолучитьАналогиНоменклатуры(Ссылка, НоменклатураИсходная = Неопределено, ПеренаправлениеОпределено = Ложь) Экспорт
	Перем Результат;
	
	Если ПеренаправлениеОпределено Тогда
		
		Результат = ПолучитьОписаниеТипаТаблицаРезультата();
		Если Ссылка.Пустая() Тогда 
			Возврат Результат;
		КонецЕсли;
		
		ТаблицаНастроек = ПолучитьТаблицуНастроек(Ссылка);
		
		Результат = РассчитатьАналогиНоменклатуры(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ВидНоменклатуры"), 
													ТаблицаНастроек);
													
													
		Возврат Результат;

	Иначе
		Если git_ПереопределениеОбработчиковСервер.НужноПеренаправлениеВызоваВОбщийМодуль("Настройка аналогов сервис", "Справочники.ПД_АналогиНоменклатуры") Тогда	
			Возврат Справочники.ПД_АналогиНоменклатуры.ПолучитьАналогиНоменклатуры(Ссылка, НоменклатураИсходная, Истина);
		Иначе
			ВнешнийОбъект = git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектПоИмени("Настройка аналогов сервис");
			Возврат ВнешнийОбъект.ПолучитьАналогиНоменклатуры(Ссылка, НоменклатураИсходная, Истина);	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции // ПолучитьТаблицуАналоговНоменклатуры()

Функция ПолучитьАналогиНоменклатурыПоТаблицеНастроек(ВидНоменклатуры, ТаблицаНастроек, ПеренаправлениеОпределено = Ложь) Экспорт 
	
	Если ПеренаправлениеОпределено Тогда
		
		Результат = ПолучитьОписаниеТипаТаблицаРезультата();
		Если ТаблицаНастроек.Количество() = 0 Тогда 
			Возврат Результат;
		КонецЕсли;
		
		Результат = РассчитатьАналогиНоменклатуры(ВидНоменклатуры, ТаблицаНастроек);
		
		Возврат Результат;
		
	Иначе 
		
		Если git_ПереопределениеОбработчиковСервер.НужноПеренаправлениеВызоваВОбщийМодуль("Настройка аналогов сервис", "Справочники.ПД_АналогиНоменклатуры") Тогда	
			Возврат Справочники.ПД_АналогиНоменклатуры.ПолучитьАналогиНоменклатурыПоТаблицеНастроек(ВидНоменклатуры, ТаблицаНастроек, Истина);
		Иначе
			ВнешнийОбъект = git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектПоИмени("Настройка аналогов сервис");
			Возврат ВнешнийОбъект.ПолучитьАналогиНоменклатурыПоТаблицеНастроек(ВидНоменклатуры, ТаблицаНастроек, Истина);	
		КонецЕсли;
	
	КонецЕсли;
	
КонецФункции

Функция РассчитатьАналогиНоменклатуры(ВидНоменклатуры, ТаблицаНастроек) 
	
	ТаблицаСпискаЦен = ПолучитьСпискаИспользуемыхЦен(ТаблицаНастроек);
		
	Запрос = Новый Запрос;
	МВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	Запрос.Текст = ПолучитьТекстРасчитаАналогиНоменклатуры(ТаблицаСпискаЦен, ТаблицаНастроек);
	
	Запрос.УстановитьПараметр("ВидНоменклатуры"		,ВидНоменклатуры);
	Запрос.УстановитьПараметр("ПустаяКатегория"		,Справочники.ТоварныеКатегории.ПустаяСсылка());
	Запрос.УстановитьПараметр("ТаблицаНастройки"	,ТаблицаНастроек);
	Запрос.УстановитьПараметр("ТоварнаяКатегория"	,Справочники.ТоварныеКатегории.ПустаяСсылка());

	Для Каждого ЭлементСписка Из ТаблицаСпискаЦен Цикл 
		Запрос.УстановитьПараметр("ВидЦены"+ЭлементСписка.ИндексКолонки ,ЭлементСписка.ВидЦены);
	КонецЦикла;

	Для каждого Строка Из ТаблицаНастроек Цикл  
		Запрос.УстановитьПараметр("Приоритет"+Строка.НомерСтроки,Строка.НомерСтроки);	
	КонецЦикла;
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	
	Возврат Результат;
	
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаИзЗапросами 

// Возвращает текст запроса рассчета товарных категорий
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Строка   - текст запроса
//
Функция ПолучитьТекстЗапросаРасчитатьТоварнуюКатегориюНоменклатурыВОбщейРезультат(СписокВидовЦен)
	
		
	ШаблонЦены		= 	"ТаблицаНоменклатуры.Цена%Индекс%	КАК Цена%Индекс%, 
						|";
	
	ТекстЗапроса ="
		|;
		|
		|ВЫБРАТЬ
		|	КешРассчитанныйПриоритетПоНаименовании.Ссылка КАК Ссылка,
		|	МИНИМУМ(КешРассчитанныйПриоритетПоНаименовании.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ КешРассчитанныхКатегорий
		|ИЗ
		|	КешРассчитанныйПриоритетПоНаименовании КАК КешРассчитанныйПриоритетПоНаименовании

		|СГРУППИРОВАТЬ ПО
		|	КешРассчитанныйПриоритетПоНаименовании.Ссылка

		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|
		|;
		|
		////////////////////////////////////////////////////////////////////////////////
		// Подставляем рассчитанные товарные категории в таблицу номенклатуры
		|ВЫБРАТЬ
		|	ТаблицаНоменклатуры.Ссылка						КАК Ссылка,
		|	ТаблицаНоменклатуры.Наименование				КАК Наименование,
		|	ТаблицаНоменклатуры.ТоварнаяКатегория			КАК ТоварнаяКатегория,
		|	ТаблицаНоменклатуры.Производитель 				КАК Производитель,
		|	ТаблицаНоменклатуры.Актуально					КАК Актуально,
		|	ТаблицаНоменклатуры.Остаток						КАК Остаток,";
		
		Для Каждого ЭлементСписка Из СписокВидовЦен Цикл 
			ТекстЗапроса =ТекстЗапроса +СтрЗаменить(ШаблонЦены,"%Индекс%",ЭлементСписка.ИндексКолонки);
		КонецЦикла;
		//
		ТекстЗапроса = ТекстЗапроса+
		"	ТаблицаНоменклатуры.ТоварнаяКатегория КАК РассчитаннаяТоварнаяКатегория
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешРассчитанныхКатегорий КАК КешРассчитанныхКатегорий
		|	ПО 
		|		ТаблицаНоменклатуры.Ссылка = КешРассчитанныхКатегорий.Ссылка
		|	
		|";
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаРасчитатьТоварнуюКатегориюНоменклатуры()

// Возвращает текст запроса показывает товарную категорию, что сейчас в номенклатуре
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Строка   - текст запроса
//
Функция ПолучитьТекстЗапросаСтараяТоварнаяКатегорияНоменклатуры(СписокВидовЦен)
	
	ШаблонСоединенияЦены	=	"ЕСТЬNULL(КешЦен%Индекс%.Цена, 0) 			КАК Цена%Индекс%,";
	ШаблонВыбораЦены		= 
								"
								|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, Номенклатура В (ВЫБРАТЬ Ссылка  ИЗ КешНоменклатуры) И ВидЦены = &ВидЦены%Индекс%) КАК КешЦен%Индекс%
								|ПО 
								|КешНоменклатуры.Ссылка = КешЦен%Индекс%.Номенклатура
								|";
		
	ШаблонЦены		= 
						"ТаблицаНоменклатуры.Цена%Индекс%	КАК Цена%Индекс%, 
						|";
	
	ШаблонВидаЦены	=
						"			
						|КОГДА КешНастройки.ВидЦены = &ВидЦены%Индекс%
						|ТОГДА КешНастройки.ВидЦены = &ВидЦены%Индекс% 
						|";
	
	ШаблонЦеныНижняяГраница =
						"КОГДА КешНастройки.ВидЦены = &ВидЦены%Индекс%
						|ТОГДА АктуальнаяНоменклатура.Цена%Индекс% >= КешНастройки.НижняяГраница 
						|";
	
	ШаблонЦеныВерхняяГраница =
						"КОГДА КешНастройки.ВидЦены = &ВидЦены%Индекс%
						|ТОГДА АктуальнаяНоменклатура.Цена%Индекс% < КешНастройки.ВерхняяГраница 
						|";

	ТекстЗапроса =                     
		////////////////////////////////////////////////////////////////////////////////
		// Получаем доступные склады
		"ВЫБРАТЬ
		|	Склад
		|ПОМЕСТИТЬ ДоступныеСклады
		|ИЗ
		|	РегистрСведений.Модуль_ДоступныеСклады 
		|ГДЕ
		|	Основной = ИСТИНА
		|;
		////////////////////////////////////////////////////////////////////////////////
		// Таблица номенклатуры для рассчета товарной категории
		|ВЫБРАТЬ
		|	КешНоменклатуры.Ссылка,
		|	КешНоменклатуры.Наименование,
		|	КешНоменклатуры.ТоварнаяКатегория, 
		|	КешНоменклатуры.Производитель
		|ПОМЕСТИТЬ КешНоменклатуры
		|ИЗ
		|	Справочник.Номенклатура КАК КешНоменклатуры
		|ГДЕ
		|	НЕ КешНоменклатуры.ЭтоГруппа
		|	И НЕ КешНоменклатуры.ПометкаУдаления
		|	И КешНоменклатуры.ВидНоменклатуры = &ВидНоменклатуры 
		|;
		|ВЫБРАТЬ
		|	КешНоменклатуры.ТоварнаяКатегория 					КАК ТоварнаяКатегория,
		|	ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка) КАК РассчитаннаяТоварнаяКатегория,
		|	КешНоменклатуры.Наименование						КАК Наименование,
		|	ЕСТЬNULL(АктуальностьНоменклатуры.Актуально, ЛОЖЬ)	КАК Актуально,
		|	ЕСТЬNULL(АктуальностьНоменклатуры.Актуально, ЛОЖЬ)	КАК ГарантияДоставки,
		|	ЕСТЬNULL(ТоварыНаСкладахОстатки.ВНаличииОстаток, 0) КАК Остаток,
		|	КешНоменклатуры.Производитель						КАК Производитель,";
		Для Каждого ЭлементСписка Из СписокВидовЦен Цикл 
			ТекстЗапроса =ТекстЗапроса +СтрЗаменить(ШаблонСоединенияЦены,"%Индекс%",ЭлементСписка.ИндексКолонки);
		КонецЦикла;
		ТекстЗапроса = ТекстЗапроса +
		"	КешНоменклатуры.Ссылка
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	КешНоменклатуры КАК КешНоменклатуры
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(,
		|				Номенклатура В (ВЫБРАТЬ Ссылка  ИЗ КешНоменклатуры)
		|					И Склад В (ВЫБРАТЬ Склад ИЗ ДоступныеСклады)) КАК ТоварыНаСкладахОстатки
		|		ПО КешНоменклатуры.Ссылка = ТоварыНаСкладахОстатки.Номенклатура
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ALPS_АктуальностьНоменклатуры.СрезПоследних(,
		|				Номенклатура В (ВЫБРАТЬ Ссылка  ИЗ КешНоменклатуры)) КАК АктуальностьНоменклатуры
		|		ПО КешНоменклатуры.Ссылка = АктуальностьНоменклатуры.Номенклатура ";
		Для Каждого ЭлементСписка Из СписокВидовЦен Цикл 
			ТекстЗапроса =ТекстЗапроса +СтрЗаменить(ШаблонВыбораЦены,"%Индекс%",ЭлементСписка.ИндексКолонки);
		КонецЦикла;


		ТекстЗапроса =ТекстЗапроса +"
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		////////////////////////////////////////////////////////////////////////////////
		// Формируем таблицу актуальной номенклатуры  
		
		//- номенклатуры нет в наличиии и актуальна
		|ВЫБРАТЬ
		|	ТаблицаНоменклатуры.Ссылка 						КАК Ссылка,";
		
		Для Каждого ЭлементСписка Из СписокВидовЦен Цикл 
			ТекстЗапроса =ТекстЗапроса +СтрЗаменить(ШаблонЦены,"%Индекс%",ЭлементСписка.ИндексКолонки);
		КонецЦикла;
		//
		ТекстЗапроса = ТекстЗапроса+
		"	ТаблицаНоменклатуры.Ссылка.Производитель 		КАК Производитель,
		|	ТаблицаНоменклатуры.Наименование 				КАК Наименование,
		|	ТаблицаНоменклатуры.ТоварнаяКатегория			КАК ТоварнаяКатегория
		|ПОМЕСТИТЬ АктуальнаяНоменклатура
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		
		|ГДЕ
		|	ТаблицаНоменклатуры.Актуально = ИСТИНА
		|	И ТаблицаНоменклатуры.ГарантияДоставки = ИСТИНА
		|	И ТаблицаНоменклатуры.Остаток <> 0
		|
		| ОБЪЕДИНИТЬ
		|
		//- номенклатура в наличиии 
		|ВЫБРАТЬ
		|	ТаблицаНоменклатуры.Ссылка 						КАК Ссылка,";
		
		Для Каждого ЭлементСписка Из СписокВидовЦен Цикл 
			ТекстЗапроса =ТекстЗапроса +СтрЗаменить(ШаблонЦены,"%Индекс%",ЭлементСписка.ИндексКолонки);
		КонецЦикла;
		//
		ТекстЗапроса = ТекстЗапроса+
		"	ТаблицаНоменклатуры.Ссылка.Производитель 		КАК Производитель,
		|	ТаблицаНоменклатуры.Наименование 				КАК Наименование,
		|	ТаблицаНоменклатуры.ТоварнаяКатегория			КАК ТоварнаяКатегория
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|	
		|ГДЕ 
		|	ТаблицаНоменклатуры.Остаток <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		////////////////////////////////////////////////////////////////////////////////
		//// Формируем таблицу номенклатуры не актуальной и нет в наличии  
		//|ВЫБРАТЬ
		//|	ТаблицаНоменклатуры.Ссылка 						КАК Ссылка,
		//|	ТоварныеКатегории.Ссылка 						КАК РассчитаннаяТоварнаяКатегория
		//|ПОМЕСТИТЬ НеАктуальнаяНоменклатура
		//|ИЗ
		//|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		//|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТоварныеКатегории КАК ТоварныеКатегории
		//|	ПО 
		//|		ТоварныеКатегории.Владелец = &ВидНоменклатуры
		//|	И 	НЕ ТоварныеКатегории.ПометкаУдаления
		//|	И	ТоварныеКатегории.Наименование = ""Прочие""
		//|
		//|ГДЕ 
		//|	ТаблицаНоменклатуры.Актуально = ЛОЖЬ
		//|	И ТаблицаНоменклатуры.Остаток = 0
		//|
		//|ИНДЕКСИРОВАТЬ ПО
		//|	Ссылка
		//|;
		|ВЫБРАТЬ
		|	РСНастройки.НомерСтроки			КАК Приоритет,
		|	РСНастройки.ТоварнаяКатегория	КАК ТоварнаяКатегория,
		|	РСНастройки.ВидЦены             КАК ВидЦены,
		|	РСНастройки.НижняяГраница		КАК НижняяГраница,
		|	РСНастройки.ВерхняяГраница		КАК ВерхняяГраница,
		|	РСНастройки.Содержит			КАК Содержит,
		|	РСНастройки.НеСодержит			КАК НеСодержит,
		|	РСНастройки.Производитель		КАК Производитель
		|ПОМЕСТИТЬ ТаблицаНастройки
		|ИЗ
		|	&ТаблицаНастройки КАК РСНастройки
		|;
		////////////////////////////////////////////////////////////////////////////////
		// Рассчитываем ТК для актуальной номенклатуры согласно настроек  
		|ВЫБРАТЬ
		|	АктуальнаяНоменклатура.Ссылка 					КАК Ссылка,
		|	КешНастройки.Приоритет			 				КАК Приоритет,
		|   АктуальнаяНоменклатура.Наименование 			КАК Наименование
		|   
		|ПОМЕСТИТЬ КешРассчитанныйПриоритетПоЦене
		|ИЗ
		|	АктуальнаяНоменклатура КАК АктуальнаяНоменклатура
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНастройки КАК КешНастройки
		|	ПО  (ВЫБОР
		|			КОГДА КешНастройки.ВидЦены = ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
		|			ТОГДА ИСТИНА ";
		
		Для Каждого ЭлементСписка Из СписокВидовЦен Цикл 
			ТекстЗапроса =ТекстЗапроса +СтрЗаменить(ШаблонВидаЦены,"%Индекс%",ЭлементСписка.ИндексКолонки);
		КонецЦикла;
		
		ТекстЗапроса = ТекстЗапроса+
		"		КОНЕЦ)
		
		|	И	(ВЫБОР
		|			КОГДА КешНастройки.НижняяГраница = 0
		|			ТОГДА ИСТИНА ";
		
		Для Каждого ЭлементСписка Из СписокВидовЦен Цикл
			ТекстЗапроса = ТекстЗапроса+ СтрЗаменить(ШаблонЦеныНижняяГраница,"%Индекс%",ЭлементСписка.ИндексКолонки);
		КонецЦикла;
		
		ТекстЗапроса = ТекстЗапроса+
		"		КОНЕЦ)
		|		
		|	И	(ВЫБОР
		|			КОГДА КешНастройки.ВерхняяГраница = 0
		|			ТОГДА ИСТИНА ";
		
		Для Каждого ЭлементСписка Из СписокВидовЦен Цикл
			ТекстЗапроса = ТекстЗапроса+ СтрЗаменить(ШаблонЦеныВерхняяГраница,"%Индекс%",ЭлементСписка.ИндексКолонки);
		КонецЦикла;
		
		ТекстЗапроса = ТекстЗапроса+
		"		КОНЕЦ)
		|		
		|	И	(ВЫБОР
		|			КОГДА КешНастройки.Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ 
		|			АктуальнаяНоменклатура.Производитель = КешНастройки.Производитель
		|		КОНЕЦ)
		|	И 	(ВЫБОР
		|			КОГДА КешНастройки.ТоварнаяКатегория = ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ 
		|			АктуальнаяНоменклатура.ТоварнаяКатегория = КешНастройки.ТоварнаяКатегория
		|		КОНЕЦ)
		|
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;";
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаСтараяТоварнаяКатегорияНоменклатуры()

Функция ПолучитьТекстРасчитаАналогиНоменклатуры(СписокВидовЦен, ТаблицаНастройки)
	
	ТекстЗапроса = ПолучитьТекстЗапросаСтараяТоварнаяКатегорияНоменклатуры(СписокВидовЦен);

		
	ТекстЗапроса_1 = "ВЫБРАТЬ
			|	РасчПоЦене.Ссылка,
			|	РасчПоЦене.Приоритет
			| ПОМЕСТИТЬ КешРассчитанныйПриоритетПоНаименовании
			|
			|ИЗ
			|	КешРассчитанныйПриоритетПоЦене КАК РасчПоЦене
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНастройки КАК ТаблицаНастройки
			|		ПО РасчПоЦене.Приоритет = ТаблицаНастройки.Приоритет
			|			И (ТаблицаНастройки.Приоритет = 0)
			|";
			
	
	ШаблонТело  = " ОБЪЕДИНИТЬ 
				|
				|ВЫБРАТЬ
				|	РасчПоЦене.Ссылка,
				|	РасчПоЦене.Приоритет
				|ИЗ
				|	КешРассчитанныйПриоритетПоЦене КАК РасчПоЦене
				|	
				|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНастройки КАК ТаблицаНастройки
				|ПО РасчПоЦене.Приоритет = ТаблицаНастройки.Приоритет
				|И (ТаблицаНастройки.Приоритет = &Приоритет%Индекс%)
				|";
				
	ШаблонСодержит =" РасчПоЦене.Наименование ПОДОБНО ";

	ШаблонНеСодержит =" РасчПоЦене.Наименование НЕ ПОДОБНО ";
	
	Шаблон1 = """%"" + ПОДСТРОКА(ТаблицаНастройки.Содержит,%1,%2)+ ""%""";
	Шаблон2 = """%"" + ПОДСТРОКА(ТаблицаНастройки.НеСодержит,%1,%2)+ ""%""";

	Для каждого Строка Из ТаблицаНастройки Цикл  
		
		ТекстЗапроса_1 = ТекстЗапроса_1+ СтрЗаменить(ШаблонТело,"%Индекс%",Строка.НомерСтроки); 	
		СтрСодержит = СокрЛП(Строка.Содержит);
		СтрНеСодержит = СокрЛП(Строка.НеСодержит);

		Если ПустаяСтрока(СтрСодержит) = ЛОЖЬ Тогда
			 ТекстЗапроса_1 = ТекстЗапроса_1 + РазбитьЛогВыражения(СтрСодержит,ШаблонСодержит,Шаблон1);			
		КонецЕсли;    
		 
		Если ПустаяСтрока(СтрНеСодержит) = ЛОЖЬ Тогда
			 ТекстЗапроса_1 = ТекстЗапроса_1 + РазбитьЛогВыражения(СтрНеСодержит,ШаблонНеСодержит,Шаблон2);			
		КонецЕсли;                                      
		
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + ТекстЗапроса_1;
	ТекстЗапроса = ТекстЗапроса + ПолучитьТекстЗапросаРасчитатьТоварнуюКатегориюНоменклатурыВОбщейРезультат(СписокВидовЦен);
	
	Возврат ТекстЗапроса;
	
КонецФункции // РасчитатьНовуюТоварнуюКатегориюТовара(СписокВидовЦен)


Функция ПолучитьТекстЗапросАналоги() 
	
	
	
	ШаблонСодержит =" РасчПоЦене.Наименование ПОДОБНО ";

	ШаблонНеСодержит =" РасчПоЦене.Наименование НЕ ПОДОБНО ";
	
	Шаблон1 = """%"" + ПОДСТРОКА(ТаблицаНастройки.Содержит,%1,%2)+ ""%""";
	Шаблон2 = """%"" + ПОДСТРОКА(ТаблицаНастройки.НеСодержит,%1,%2)+ ""%""";


	
КонецФункции

Функция РазбитьЛогВыражения(СтрСодержит,Шаблон,ШаблонПараметра) Экспорт
	
	ОтборЗП = "";
	Строка = "%1(%2%3)";
	ЛогВыраженияИ = "&AND&";
	ЛогВыраженияИЛИ = "&OR&";
	НачВыражения = СтрСодержит;
			
	Если Найти(СтрСодержит, ЛогВыраженияИ) > 0 
		ИЛИ Найти(СтрСодержит, ЛогВыраженияИЛИ) > 0  Тогда
		
		ЛогВыражения = ПолучитьВыраженияПервоеВСтроке(СтрСодержит,ЛогВыраженияИ,ЛогВыраженияИЛИ);

		НомерЛВ = Найти(СтрСодержит, ЛогВыражения);
		НачВыраз = 0;
		КонецВыраз =НомерЛВ; 
		ОтборЗП = ПолучитьОтборДляЛогВыражения("И (",Шаблон,ШаблонПараметра,1,КонецВыраз,НачВыражения);  
		Отбор = ДобавитьСимвол(Сред (СтрСодержит,НачВыраз,НомерЛВ-1),"%");
		СтрСодержит = Сред(СтрСодержит,НомерЛВ,СтрДлина(СтрСодержит)- НомерЛВ+1);	
				
		Пока Найти(СтрСодержит, ЛогВыраженияИ) > 0 
			ИЛИ Найти(СтрСодержит, ЛогВыраженияИЛИ)> 0  Цикл
			
			ЛогВыражения 	= ПолучитьВыраженияПервоеВСтроке(СтрСодержит,ЛогВыраженияИ,ЛогВыраженияИЛИ);
			ЛогВырНаЗамену	= ?(ЛогВыражения = ЛогВыраженияИ," И "," ИЛИ ");
			
			РазмерСтр		= СтрДлина(СтрСодержит)-СтрДлина(ЛогВыражения); 				
			КонецВыражения 	= ПолучитьНомерСледующегоВыражения(СтрСодержит,ЛогВыраженияИ,ЛогВыраженияИЛИ,РазмерСтр); 
		 			
			СтрОтборСодержит 	= Сред (СтрСодержит,СтрДлина(ЛогВыражения)+1,КонецВыражения); 			
		    Отбор 				= Отбор +  ЛогВырНаЗамену + Шаблон + ДобавитьСимвол(СтрОтборСодержит,"%");
			НачВыраз			= КонецВыраз + СтрДлина(ЛогВыражения);
			КонецВыраз			= НачВыраз + КонецВыражения;
			ОтборЗП 			= ОтборЗП + ПолучитьОтборДляЛогВыражения(ЛогВырНаЗамену,Шаблон,ШаблонПараметра,НачВыраз,КонецВыраз,НачВыражения); 
					
			НачСтрСодержит = КонецВыражения+СтрДлина(ЛогВыражения)+1;
			
			Если НачСтрСодержит < РазмерСтр Тогда
			   СтрСодержит = Сред(СтрСодержит,НачСтрСодержит,РазмерСтр);
			Иначе 
			   СтрСодержит = "";
			КонецЕсли;		
		КонецЦикла;		
	КонецЕсли;
		
	Если ПустаяСтрока(ОтборЗП) = Истина Тогда
		Строка = ПолучитьОтборДляЛогВыражения("И ",Шаблон,ШаблонПараметра,1,СтрДлина(СтрСодержит)+1,НачВыражения); 	
	Иначе
		Строка = ОтборЗП + ")"; 
	КонецЕсли; 
	
	Возврат  Строка; 
КонецФункции // РазбитьЛогВыражения()   

Функция ДобавитьСимвол(СтрВставки,Символ) 
	Шаблон = """%1%2%1""";
	Возврат  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон,Символ,СтрВставки); 
КонецФункции // ДобавитьСимвол()

Функция ПолучитьВыраженияПервоеВСтроке(Строка,Выраз1,Выраз2)
	
	Результат = ""; 	
	РазмерСтр =	СтрДлина(Строка);
		
	НомерВ1	= Найти(Строка, Выраз1);
	НомерВ2 = Найти(Строка, Выраз2);
	
	Если НомерВ1 > 0 ИЛИ НомерВ2 > 0 Тогда
	
		НомерМинПо1	= ?(НомерВ1 = 0	,РазмерСтр	,НомерВ1); 		
		НомерМинПо2	= ?(НомерВ2 = 0	,РазмерСтр	,НомерВ2);
		
		Если НомерМинПо1 < НомерМинПо2  Тогда	
			Результат =  Выраз1;				
		Иначе
			Результат =  Выраз2;	
		КонецЕсли; 		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПолучитьПервоеВхожденияВСтроку(Строка,Выраз1,Выраз2)

Функция ПолучитьНомерСледующегоВыражения(Строка,Выраз1,Выраз2,РазмерМакс)

	НомерВыраз1 = Найти(Прав(Строка,РазмерМакс), Выраз1); 
	НомерВыраз2 = Найти(Прав(Строка,РазмерМакс), Выраз2); 
	
	НомерМинВыраз1 = ?(НомерВыраз1 = 0, РазмерМакс,НомерВыраз1-1); 		
	НомерМинВыраз2 = ?(НомерВыраз2 = 0, РазмерМакс,НомерВыраз2-1); 
	
	Возврат Мин(НомерМинВыраз1,НомерМинВыраз2); 
	

КонецФункции // ПолучитьНомерСледующегоВыражения()

Функция ПолучитьОтборДляЛогВыражения(ЛогВыражения,Шаблон, ШаблонПараметра,НачВыраз,КонецВыраз,СтрСодержит)

	ФДОткр= "&{&";
	ФДЗакр  = "&}&";
	Строка = "%1%2";
	СтрокаЗамена1 = "(";
	СтрокаЗамена2 = ")";
	НачСтроки = НачВыраз;
	КонСтроки = КонецВыраз-НачВыраз; 
	
	ШаблонСтроки  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Строка,Шаблон,ШаблонПараметра); 

	
	СтрокаОтбор = Сред(СтрСодержит,НачСтроки,КонСтроки);
	Пока Найти(СтрокаОтбор, ФДОткр) > 0 
		ИЛИ Найти(СтрокаОтбор, ФДЗакр)> 0 Цикл
			
		ФигурныеДужки = ПолучитьВыраженияПервоеВСтроке(СтрокаОтбор,ФДОткр,ФДЗакр);
		
		НомерЛВ = Найти(СтрокаОтбор, ФигурныеДужки);
		Если   НомерЛВ = 1 Тогда
			  НачСтроки = НачСтроки + 3;
			  КонСтроки = КонСтроки - 3; 
			  ШаблонСтроки  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Строка,СтрокаЗамена1,ШаблонСтроки); 
		ИначеЕсли	НомерЛВ = СтрДлина(СтрокаОтбор)-2	  Тогда
			  КонСтроки = КонСтроки - 3;
			  ШаблонСтроки  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Строка,ШаблонСтроки,СтрокаЗамена2); 
		КонецЕсли;
		 		
		СтрокаОтбор = Сред(СтрСодержит,НачСтроки,КонСтроки);  	
	КонецЦикла;
	
	ОтборПоПараметрам = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки,НачСтроки,КонСтроки);    
	Строка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Строка,ЛогВыражения,ОтборПоПараметрам);
	
	Возврат Строка;

КонецФункции // ПолучитьОтборДляЛогВыражения()



#КонецОбласти 
Функция ПодготовитьТекст(знач ТекстДляПодготовки)
	Шаблон 		= "ru='%1'";
	Результат   = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, ТекстДляПодготовки);
	Возврат НСтр(Результат, ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
КонецФункции // ПодготовитьТекст()

#КонецОбласти

#Область СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	//Версия
	Версия = "0.0.2";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;
    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Настройка аналогов сервис");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Настройка аналогов сервис [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "Настройка аналогов сервис [" + Версия + "]", "ПолучитьТаблицуАналоговНоменклатуры()", "ВызовСерверногоМетода");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;
КонецПроцедуры

#КонецОбласти

#КонецОбласти