
#Область ПрограммныйИнтерфейс

Процедура ПроверитьВозможностьПроведенияДокумента(Источник, Отказ, РежимПроведения) Экспорт
	
	Если ТипЗнч(Источник) <> Тип("ДокументОбъект.ЗаказКлиента") 
	  И	 ТипЗнч(Источник) <> Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда	
		Возврат;
	КонецЕсли; 	
	
	Партнер = Источник.Партнер;	
	Соглашение = Источник.Соглашение;
	Сегмент = Справочники.СегментыПартнеров.ПолучитьСсылку(Новый УникальныйИдентификатор("388032f2-44e0-11e0-9f98-001517115d85")); // Покупці гурту
	ГруппаДоступа = Справочники.ГруппыДоступаПартнеров.ПолучитьСсылку(Новый УникальныйИдентификатор("49392a71-00f2-11e4-80cd-001e676b0174"));  // Покупці гурту
	
	Если КонтрольПроведенияНеИспользуется(Партнер, Сегмент, ГруппаДоступа) Тогда
		Возврат;	
	КонецЕсли; 
	
	СсылкаНаДокумент = Источник.Ссылка;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
		Если Источник.РеализацияПоЗаказам Тогда
			ПроверитьТаблицуТоваровНаОднородность(Источник.ЗаказКлиента.Дата, Соглашение, Источник.Товары, Отказ);		
		Иначе
			ПроверитьТаблицуТоваровНаОднородность(Источник.Дата, Соглашение, Источник.Товары, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ЗаказКлиента") И ЭтоСоглашениеНеБезнальное(Соглашение) Тогда
		ПроверитьТаблицуТоваровНаОднородность(Источник.Дата, Соглашение, Источник.Товары, Отказ);	
	КонецЕсли;
	
КонецПроцедуры // ПроверитьВозможностьПроведенияДокумента()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьТаблицуТоваровНаОднородность(Дата, Соглашение, ТаблицаТовары, Отказ)
	
	ТолькоГривневыйТовар = Ложь;
	ТолькоДоларовыйТовар = Ложь;
	ВалютаУпрУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРеглУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Если Соглашение.Валюта = ВалютаУпрУчета Тогда
		ТолькоДоларовыйТовар = Истина;
	ИначеЕсли Соглашение.Валюта = ВалютаРеглУчета Тогда
		ТолькоГривневыйТовар = Истина;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	Номенклатура
		|ПОМЕСТИТЬ ТаблицаТовары
		|ИЗ
		|	&ТаблицаТовары КАК ТаблицаТовары
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|ВЫБРАТЬ 
		|	ТаблицаТовары.Номенклатура	 КАК Номенклатура,
	    |	IsNull(ГривневыйТовар, Ложь) КАК ГривневыйТовар
	    |ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|
	    |ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АМ_ГривневыйТовар.СрезПоследних(&ДатаДокумента, Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТовары)) КАК АМ_ГривневыйТовар
		|ПО	ТаблицаТовары.Номенклатура = АМ_ГривневыйТовар.Номенклатура
		|
		|ГДЕ
		|	ТаблицаТовары.Номенклатура.ТипНоменклатуры <> Значение(Перечисление.ТипыНоменклатуры.Услуга)	
		|";
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	Запрос.УстановитьПараметр("ТаблицаТовары", ТаблицаТовары.Выгрузить());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ТолькоДоларовыйТовар = Истина И Выборка.ГривневыйТовар = Истина Тогда
			СообщениеПользователю = НСтр("ru = 'Запрещено смешивать гривневый и долларовый товар. Гривневый товар: %Номенклатура%'");
			СообщениеПользователю = СтрЗаменить(СообщениеПользователю, "%Номенклатура%", Строка(Выборка.Номенклатура));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеПользователю, , , , Отказ);	
		КонецЕсли;
		
		Если ТолькоГривневыйТовар = Истина И Выборка.ГривневыйТовар = Ложь Тогда
			СообщениеПользователю = НСтр("ru = 'Запрещено смешивать гривневый и долларовый товар. Долларовый товар: %Номенклатура%'");
			СообщениеПользователю = СтрЗаменить(СообщениеПользователю, "%Номенклатура%", Строка(Выборка.Номенклатура));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеПользователю, , , , Отказ);	
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры // ПроверитьТаблицуТоваровНаОднородность()


Функция ЭтоСоглашениеНеБезнальное(Соглашение)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ
       	|	Ссылка
       	|ИЗ
       	|	Справочник.СоглашенияСКлиентами.ДополнительныеРеквизиты
       	|ГДЕ
	   	|	Ссылка = &Соглашение	
       	|И	Свойство = &Свойство
       	|И  Значение = Истина
		|";
	Запрос.УстановитьПараметр("Соглашение", Соглашение);
	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(Новый УникальныйИдентификатор("2c5efc70-aba8-11e4-80ea-001e676b0175")));	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции // ЭтоСоглашениеНеБезнальное()
	
Функция КонтрольПроведенияНеИспользуется(Партнер, Сегмент, Группа) 
	
	КонтрольНеИспользуется = Истина;
	
	Если Партнер.ГруппаДоступа = Группа Тогда
		КонтрольНеИспользуется = Ложь;
		Возврат КонтрольНеИспользуется;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ
       	|	ПартнерыСегмента.Сегмент,
       	|	ПартнерыСегмента.Партнер
       	|ИЗ
       	|	РегистрСведений.ПартнерыСегмента КАК ПартнерыСегмента
       	|ГДЕ
       	|	ПартнерыСегмента.Сегмент = &Сегмент
       	|И 	ПартнерыСегмента.Партнер = &Партнер
		|";
	Запрос.УстановитьПараметр("Сегмент", Сегмент);
	Запрос.УстановитьПараметр("Партнер", Партнер);
	
	КонтрольНеИспользуется = Запрос.Выполнить().Пустой();
	
	Возврат КонтрольНеИспользуется;
	
КонецФункции // КонтрольПроведенияНеИспользуется()

#КонецОбласти

#Область  СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	Версия = "1.0.4";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Работа с гривневым товаром");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Обработка предназначена для работы с гривневым товаром [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "Работа с гривневым товаром [" + Версия + "]", "АМ_ГР", "ОткрытиеФормы", Ложь, "АМ_ГР");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

#КонецОбласти
