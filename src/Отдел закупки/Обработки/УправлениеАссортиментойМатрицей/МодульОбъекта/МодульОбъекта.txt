
#Область ПрограммныйИнтерфейс

Процедура ВыполнитьКоманду(ИдентификаторКоманды, ПараметрыКоманды) Экспорт
	
	Перем РезультатВыполнения, ОповещениеФорм, МассивДанных;
	
	Если ИдентификаторКоманды = "ОбновитьПродажиПоФорматамВФоне" Тогда
		
		ТекстЗапроса = ПолучитьТекстЗапросаАналитическихДанных();
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%1", "Склады.Ссылка КАК ФорматМагазина, ");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%2", "Склады.Ссылка");
		
	ИначеЕсли ИдентификаторКоманды = "ОбновитьПродажиПоМагазинамВФоне" Тогда
		
		ТекстЗапроса = ПолучитьТекстЗапросаАналитическихДанных();
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%1", "Склады.Склад КАК Склад, ");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%2", "Склады.Склад");
		
	ИначеЕсли ИдентификаторКоманды = "ОбновитьТоварыКОтъездуВФоне" Тогда

		ТекстЗапроса = ПолучитьТекстЗапросаАналитикаТоварыКОтъезду();
		
	ИначеЕсли ИдентификаторКоманды = "ОбновитьТоварыВРезервахВФоне" Тогда

		ТекстЗапроса = ПолучитьТекстЗапросаАналитикаТоварыВРезервах();

	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Склад", 				ПараметрыКоманды.Склад);
	Запрос.УстановитьПараметр("ВидНоменклатуры", 	ПараметрыКоманды.ВидНоменклатуры);
	Запрос.УстановитьПараметр("ТоварнаяКатегория", 	ПараметрыКоманды.ТоварнаяКатегория);
	Запрос.УстановитьПараметр("Производитель", 		ПараметрыКоманды.Производитель);
	Запрос.УстановитьПараметр("Номенклатура", 		ПараметрыКоманды.Номенклатура);
	Запрос.УстановитьПараметр("КонецПериода", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("НачалоПериодаНеделя", НачалоДня(ТекущаяДатаСеанса()) - 7 * 86400);
	Запрос.УстановитьПараметр("НачалоПериода2Недели", НачалоДня(ТекущаяДатаСеанса()) - 14 * 86400);
	Запрос.УстановитьПараметр("НачалоПериодаМесяц", НачалоДня(ТекущаяДатаСеанса()) - 31 * 86400);
	Запрос.УстановитьПараметр("НачалоПериодаКвартал", НачалоДня(ТекущаяДатаСеанса()) - 91 * 86400);
	Выгрузка = Запрос.Выполнить().Выгрузить();
	
	Если ПараметрыКоманды.Свойство("РезультатВыполнения", РезультатВыполнения) Тогда
		
		Если РезультатВыполнения.Свойство("ОповещениеФорм", ОповещениеФорм) Тогда

			ОповещениеФорм.ИмяСобытия = ИдентификаторКоманды;
			ОповещениеФорм.Использование = Истина;
			Если ПараметрыКоманды.Свойство("ДополнительнаяОбработкаСсылка") Тогда
				ОповещениеФорм.Свойство("Источник", ПараметрыКоманды.ДополнительнаяОбработкаСсылка);
			КонецЕсли;
			
			КлючиСтруктуры = "";
			Для Каждого Колонка Из Выгрузка.Колонки Цикл
				КлючиСтруктуры = КлючиСтруктуры + Колонка.Имя + ",";
			КонецЦикла;
			КлючиСтруктуры = Лев(КлючиСтруктуры, СтрДлина(КлючиСтруктуры) - 1);
			
			МассивДанных = Новый Массив;
			Для каждого СтрокаВыгрузки Из Выгрузка Цикл
				
				СтруктураЗаписи = Новый Структура(КлючиСтруктуры);
				ЗаполнитьЗначенияСвойств(СтруктураЗаписи, СтрокаВыгрузки);
				МассивДанных.Добавить(СтруктураЗаписи);
				
			КонецЦикла;
			
			
			Если ИдентификаторКоманды = "ОбновитьТоварыКОтъездуВФоне" Тогда
				ЗаполнитьМассивДанныхТоваровКОтъездуПоАртикулам(Запрос, КлючиСтруктуры, МассивДанных);
			КонецЕсли;
			
			ОповещениеФорм.Параметр = МассивДанных;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьМассивДанныхТоваровКОтъездуПоАртикулам(Запрос, КлючиСтруктуры, МассивДанных)
	
	Запрос.Текст = ПолучитьТекстЗапросаАналитикаТоварыКОтъездуПоАртикулам();
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	ВыгрузкаПоВидамНоменклатуры = РезультатПакета[19].Выгрузить();
	ПараметрыОтбораПоВиду = Новый Структура("Склад, ВидНоменклатуры, ПосадочныеМеста", , , 0);
	
	ВыгрузкаПоТоварнымКатегориям = РезультатПакета[20].Выгрузить();
	ВыгрузкаПоТоварнымКатегориям.Индексы.Добавить("Склад, ВидНоменклатуры");
	ПараметрыОтбораПоТоварнойКатегории = Новый Структура("Склад, ВидНоменклатуры, ТоварнаяКатегория, ПосадочныеМеста", , , , 0);	
	
	ВыгрузкаПоПроизводителям = РезультатПакета[21].Выгрузить();
	ВыгрузкаПоПроизводителям.Индексы.Добавить("Склад, ВидНоменклатуры, ТоварнаяКатегория");
	ПараметрыОтбораПоПроизводителю = Новый Структура("Склад, ВидНоменклатуры, ТоварнаяКатегория, Производитель, ПосадочныеМеста", , , , , 0);
	
	ВыгрузкаПоНоменклатуре = РезультатПакета[22].Выгрузить();
	ВыгрузкаПоНоменклатуре.Индексы.Добавить("Склад, ВидНоменклатуры, ТоварнаяКатегория, Производитель");
	ПараметрыОтбораПоНоменклатуре = Новый Структура("Склад, ВидНоменклатуры, ТоварнаяКатегория, Производитель, ПосадочныеМеста, КоличествоАртикулов", , , , , 0, 1);

	
	ЗаполнитьМассивДанныхТоваровКОтъездуПоАртикуламПоДереву(ВыгрузкаПоВидамНоменклатуры,
															ВыгрузкаПоТоварнымКатегориям,
															ВыгрузкаПоНоменклатуре,
															ПараметрыОтбораПоВиду,
															ПараметрыОтбораПоНоменклатуре,
															КлючиСтруктуры,
															МассивДанных);
															
	ЗаполнитьМассивДанныхТоваровКОтъездуПоАртикуламПоДереву(ВыгрузкаПоТоварнымКатегориям,
															ВыгрузкаПоПроизводителям,
															ВыгрузкаПоНоменклатуре,
															ПараметрыОтбораПоТоварнойКатегории,
															ПараметрыОтбораПоНоменклатуре,
															КлючиСтруктуры,
															МассивДанных);
															
	ЗаполнитьМассивДанныхТоваровКОтъездуПоАртикуламПоДереву(ВыгрузкаПоПроизводителям,
															ВыгрузкаПоНоменклатуре,
															ВыгрузкаПоНоменклатуре,
															ПараметрыОтбораПоПроизводителю,
															ПараметрыОтбораПоНоменклатуре,
															КлючиСтруктуры,
															МассивДанных);

КонецПроцедуры // ЗаполнитьМассивДанныхТоваровКОтъездуПоАртикулам()

Процедура ЗаполнитьМассивДанныхТоваровКОтъездуПоАртикуламПоДереву(ВыгрузкаРодитель,  
																ВыгрузкаПодчиненные, 
																ВыгрузкаДетальныеЗаписи, 
																ПараметрыОтбораРодитель,
																ПараметрыОтбораДетальныеЗаписи,
																КлючиСтруктуры,
																МассивДанных)
																
	Для Каждого СтрокаРодитель ИЗ ВыгрузкаРодитель Цикл
		
		Если СтрокаРодитель.ПосадочныеМеста = 0 Тогда
			Продолжить;
		КонецЕсли;
		 
		ПереборПосадочныхМест = СтрокаРодитель.КоличествоАртикулов - СтрокаРодитель.ПосадочныеМеста - СтрокаРодитель.ОтклонениеКоличествоПМ; 
		Если ПереборПосадочныхМест > 0 Тогда
			
			ЗаполнитьЗначенияСвойств(ПараметрыОтбораРодитель, СтрокаРодитель, , "ПосадочныеМеста");
			РезультатПоискаПодчиненные = ВыгрузкаПодчиненные.НайтиСтроки(ПараметрыОтбораРодитель);
			Для каждого СтрокаПодчиненные Из РезультатПоискаПодчиненные Цикл
						
				ЗаполнитьЗначенияСвойств(ПараметрыОтбораДетальныеЗаписи, СтрокаПодчиненные, , "ПосадочныеМеста, КоличествоАртикулов");	
				ТаблицаПоДетальнымЗаписям = ВыгрузкаДетальныеЗаписи.Скопировать(ПараметрыОтбораДетальныеЗаписи);//ВыгрузкаДетальныеЗаписи.НайтиСтроки(ПараметрыОтбораДетальныеЗаписи);
				ТаблицаПоДетальнымЗаписям.Сортировать("ДНС Убыв");
				Для Каждого СтрокаДетальныеЗаписи ИЗ ТаблицаПоДетальнымЗаписям Цикл
					
					ПереборПосадочныхМест = ПереборПосадочныхМест - 1;
					Если ПереборПосадочныхМест < 0 Тогда
						Прервать;	
					КонецЕсли;
					
					СтруктураЗаписи = Новый Структура(КлючиСтруктуры);
					ЗаполнитьЗначенияСвойств(СтруктураЗаписи, СтрокаДетальныеЗаписи);
					МассивДанных.Вставить(0, СтруктураЗаписи);
					
				КонецЦикла;
				
			КонецЦикла; 
			
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры // ЗаполнитьМассивДанныхТоваровКОтъездуПоАртикуламПоДереву()



Функция ПолучитьТекстЗапросаАналитическихДанных()

	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////////////////
		// 0.
		|ВЫБРАТЬ
		|	АналитикаУчетаНоменклатуры.Ссылка КАК АналитикаУчетаНоменклатуры,
		|	АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	АналитикаУчетаНоменклатуры.Склад КАК Склад
		|ПОМЕСТИТЬ КешАналитикаУчетаНоменклатуры
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|ГДЕ
		|	АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры = &ВидНоменклатуры
		|И	ВЫБОР
		|		КОГДА &ТоварнаяКатегория = ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)
		|			ТОГДА Истина
		|		ИНАЧЕ АналитикаУчетаНоменклатуры.Номенклатура.ТоварнаяКатегория = &ТоварнаяКатегория
		|	КОНЕЦ
		|И	ВЫБОР
		|		КОГДА &Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|			ТОГДА Истина
		|		ИНАЧЕ АналитикаУчетаНоменклатуры.Номенклатура.Производитель = &Производитель
		|	КОНЕЦ
		|И	ВЫБОР
		|		КОГДА &Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА Истина
		|		ИНАЧЕ АналитикаУчетаНоменклатуры.Номенклатура = &Номенклатура
		|	КОНЕЦ			
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаНоменклатуры	
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 1. Продажи по неделям
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	АналитикаУчетаНоменклатуры.Склад 						КАК Склад,
		|	СУММА(КоличествоОборот) 								КАК ПродажиНеделя,
		|	КОЛИЧЕСТВО(АналитикаУчетаНоменклатуры.Номенклатура) 	КАК ПродажиНеделяПоАртикулам
		|ПОМЕСТИТЬ ПродажиПоНеделям
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&НачалоПериодаНеделя, &КонецПериода, , АналитикаУчетаНоменклатуры В (ВЫБРАТЬ АналитикаУчетаНоменклатуры ИЗ КешАналитикаУчетаНоменклатуры)
		|														И  АналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие))
		|
		|СГРУППИРОВАТЬ ПО
		|	АналитикаУчетаНоменклатуры.Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 2. Продажи по неделям "Под заказ"
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Склад							КАК Склад,
		|	Сумма(НаличиеПодЗаказРасход)   	КАК ПродажиНеделяПодЗаказ
		|ПОМЕСТИТЬ ПродажиПоНеделямПодЗаказ
		|ИЗ
		|	РегистрНакопления.ОбеспечениеЗаказов.Обороты(&НачалоПериодаНеделя, 
		|												&КонецПериода, 
		|												Регистратор, 
		|												(Номенклатура, Характеристика, Склад) В (ВЫБРАТЬ 
		|																							Номенклатура, 
		|																							Характеристика, 
		|																							Склад 
		|																						ИЗ 
		|																							КешАналитикаУчетаНоменклатуры))
		|ГДЕ
		|	Регистратор Ссылка Документ.РеализацияТоваровУслуг
		|
		|СГРУППИРОВАТЬ ПО
		| 	Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		| 	Склад
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 3. Продажи по 2-х неделям
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	АналитикаУчетаНоменклатуры.Склад 						КАК Склад,
		|	СУММА(КоличествоОборот) 								КАК Продажи2Недели,
		|	КОЛИЧЕСТВО(АналитикаУчетаНоменклатуры.Номенклатура) 	КАК Продажи2НеделиПоАртикулам
		|ПОМЕСТИТЬ Продажи2ПоНеделям
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&НачалоПериода2Недели, &КонецПериода, , АналитикаУчетаНоменклатуры В (ВЫБРАТЬ АналитикаУчетаНоменклатуры ИЗ КешАналитикаУчетаНоменклатуры)
		|														И  АналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие))
		|
		|СГРУППИРОВАТЬ ПО
		|	АналитикаУчетаНоменклатуры.Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 4. Продажи по 2-х неделям "Под заказ"
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Склад							КАК Склад,
		|	Сумма(НаличиеПодЗаказРасход)   	КАК Продажи2НеделиПодЗаказ
		|ПОМЕСТИТЬ ПродажиПо2НеделямПодЗаказ
		|ИЗ
		|	РегистрНакопления.ОбеспечениеЗаказов.Обороты(&НачалоПериода2Недели, 
		|												&КонецПериода, 
		|												Регистратор, 
		|												(Номенклатура, Характеристика, Склад) В (ВЫБРАТЬ 
		|																							Номенклатура, 
		|																							Характеристика, 
		|																							Склад 
		|																						ИЗ 
		|																							КешАналитикаУчетаНоменклатуры))
		|ГДЕ
		|	Регистратор Ссылка Документ.РеализацияТоваровУслуг
		|
		|СГРУППИРОВАТЬ ПО
		| 	Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		| 	Склад
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 5. Продажи за месяц
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	АналитикаУчетаНоменклатуры.Склад 						КАК Склад,
		|	СУММА(КоличествоОборот) 							КАК ПродажиМесяц,
		|	КОЛИЧЕСТВО(АналитикаУчетаНоменклатуры.Номенклатура) КАК ПродажиМесяцПоАртикулам
		|ПОМЕСТИТЬ ПродажиЗаМесяц
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&НачалоПериодаМесяц, &КонецПериода, ,  АналитикаУчетаНоменклатуры В (ВЫБРАТЬ АналитикаУчетаНоменклатуры ИЗ КешАналитикаУчетаНоменклатуры)
		|														И  АналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие))
		|
		|СГРУППИРОВАТЬ ПО
		|	АналитикаУчетаНоменклатуры.Склад	
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 6. Продажи за месяц "Под заказ"
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Склад							КАК Склад,
		|	Сумма(НаличиеПодЗаказРасход)   	КАК ПродажиМесяцПодЗаказ
		|ПОМЕСТИТЬ ПродажиЗаМесяцПодЗаказ
		|ИЗ
		|	РегистрНакопления.ОбеспечениеЗаказов.Обороты(&НачалоПериодаМесяц, 
		|												&КонецПериода, 
		|												Регистратор, 
		|												(Номенклатура, Характеристика, Склад) В (ВЫБРАТЬ 
		|																							Номенклатура, 
		|																							Характеристика, 
		|																							Склад 
		|																						ИЗ 
		|																							КешАналитикаУчетаНоменклатуры))
		|ГДЕ
		|	Регистратор Ссылка Документ.РеализацияТоваровУслуг
		|
		|СГРУППИРОВАТЬ ПО
		| 	Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		| 	Склад
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 7. Продажи за квартал
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	АналитикаУчетаНоменклатуры.Склад 					КАК Склад,
		|	СУММА(КоличествоОборот) 							КАК ПродажиКвартал,
		|	КОЛИЧЕСТВО(АналитикаУчетаНоменклатуры.Номенклатура) КАК ПродажиКварталПоАртикулам
		|ПОМЕСТИТЬ ПродажиЗаКвартал
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&НачалоПериодаКвартал, &КонецПериода, ,  АналитикаУчетаНоменклатуры В (ВЫБРАТЬ АналитикаУчетаНоменклатуры ИЗ КешАналитикаУчетаНоменклатуры)
		|														И  АналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие))
		|
		|СГРУППИРОВАТЬ ПО
		|	АналитикаУчетаНоменклатуры.Склад	
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 8. Продажи за квартал "Под заказ"
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Склад							КАК Склад,
		|	Сумма(НаличиеПодЗаказРасход)   	КАК ПродажиКварталПодЗаказ
		|ПОМЕСТИТЬ ПродажиЗаКварталПодЗаказ
		|ИЗ
		|	РегистрНакопления.ОбеспечениеЗаказов.Обороты(&НачалоПериодаКвартал, 
		|												&КонецПериода, 
		|												Регистратор, 
		|												(Номенклатура, Характеристика, Склад) В (ВЫБРАТЬ 
		|																							Номенклатура, 
		|																							Характеристика, 
		|																							Склад 
		|																						ИЗ 
		|																							КешАналитикаУчетаНоменклатуры))
		|ГДЕ
		|	Регистратор Ссылка Документ.РеализацияТоваровУслуг
		|
		|СГРУППИРОВАТЬ ПО
		| 	Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		| 	Склад
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 9.
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	%1
		|	СУММА(ОстаткиНаСкладе.ВНаличииОстаток) КАК НаСкладе,
		|
		|	СУММА(ПродажиПоНеделям.ПродажиНеделя) КАК ПродажиНеделя,
		|	СУММА(ПродажиПоНеделям.ПродажиНеделяПоАртикулам) КАК ПродажиНеделяПоАртикулам,
		|	СУММА(ПродажиПоНеделямПодЗаказ.ПродажиНеделяПодЗаказ) КАК ПродажиНеделяПодЗаказ,
		|
		|	СУММА(Продажи2ПоНеделям.Продажи2Недели) КАК Продажи2Недели,
		|	СУММА(Продажи2ПоНеделям.Продажи2НеделиПоАртикулам) КАК Продажи2НеделиПоАртикулам,
		|	СУММА(ПродажиПо2НеделямПодЗаказ.Продажи2НеделиПодЗаказ) КАК Продажи2НеделиПодЗаказ,
		|
		|	СУММА(ПродажиЗаМесяц.ПродажиМесяц) КАК ПродажиМесяц,
		|	СУММА(ПродажиЗаМесяц.ПродажиМесяцПоАртикулам) КАК ПродажиМесяцПоАртикулам,
		|	СУММА(ПродажиЗаМесяцПодЗаказ.ПродажиМесяцПодЗаказ) КАК ПродажиМесяцПодЗаказ,
		|
		|	СУММА(ПродажиЗаКвартал.ПродажиКвартал) КАК ПродажиКвартал,
		|	СУММА(ПродажиЗаКвартал.ПродажиКварталПоАртикулам) КАК ПродажиКварталПоАртикулам,
		|	СУММА(ПродажиЗаКварталПодЗаказ.ПродажиКварталПодЗаказ) КАК ПродажиКварталПодЗаказ
		|
		|ИЗ
		|	Справочник.АМ_ФорматыМагазинов.Склады КАК Склады
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.Остатки(, (Номенклатура, Характеристика, Склад) В (ВЫБРАТЬ 
		|																							Номенклатура, 
		|																							Характеристика, 
		|																							Склад 
		|																						ИЗ 
		|																							КешАналитикаУчетаНоменклатуры)) КАК ОстаткиНаСкладе
		|ПО ОстаткиНаСкладе.Склад = Склады.Склад
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ПродажиПоНеделям КАК ПродажиПоНеделям
		|ПО ПродажиПоНеделям.Склад = Склады.Склад
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ПродажиПоНеделямПодЗаказ КАК ПродажиПоНеделямПодЗаказ
		|ПО ПродажиПоНеделямПодЗаказ.Склад = Склады.Склад
		|
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ Продажи2ПоНеделям КАК Продажи2ПоНеделям
		|ПО Продажи2ПоНеделям.Склад = Склады.Склад
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ПродажиПо2НеделямПодЗаказ КАК ПродажиПо2НеделямПодЗаказ
		|ПО ПродажиПо2НеделямПодЗаказ.Склад = Склады.Склад
		|
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ПродажиЗаМесяц КАК ПродажиЗаМесяц
		|ПО ПродажиЗаМесяц.Склад = Склады.Склад
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ПродажиЗаМесяцПодЗаказ КАК ПродажиЗаМесяцПодЗаказ
		|ПО ПродажиЗаМесяцПодЗаказ.Склад = Склады.Склад
		|
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ПродажиЗаКвартал КАК ПродажиЗаКвартал
		|ПО ПродажиЗаКвартал.Склад = Склады.Склад
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ПродажиЗаКварталПодЗаказ КАК ПродажиЗаКварталПодЗаказ
		|ПО ПродажиЗаКварталПодЗаказ.Склад = Склады.Склад
		|
		|СГРУППИРОВАТЬ ПО
		|	%2	
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПродажиКвартал Убыв
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешАналитикаУчетаНоменклатуры;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПродажиПоНеделям;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПродажиПоНеделямПодЗаказ;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Продажи2ПоНеделям;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПродажиПо2НеделямПодЗаказ;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПродажиЗаМесяц;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПродажиЗаМесяцПодЗаказ;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПродажиЗаКвартал;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПродажиЗаКварталПодЗаказ;
		|";
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапросаАналитическихДанных()

Функция ПолучитьТекстЗапросаАналитикаТоварыКОтъезду()

	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////////////////
		// 0.
		|ВЫБРАТЬ
		|	СвободныеОстатки.Номенклатура КАК Номенклатура,
		|	СвободныеОстатки.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СвободныеОстатки.Номенклатура.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	СвободныеОстатки.Номенклатура.Производитель КАК Производитель,
		|	СвободныеОстатки.Характеристика КАК Характеристика,
		|	СвободныеОстатки.Склад			КАК Склад,
		|	СвободныеОстатки.ВНаличииОстаток 
		|  - СвободныеОстатки.ВРезервеОстаток КАК СвободныйОстаток
		|ПОМЕСТИТЬ КешСвободныхОстатков
		|ИЗ
		|	РегистрНакопления.СвободныеОстатки.Остатки(, Номенклатура.ВидНоменклатуры = &ВидНоменклатуры И Склад = &Склад) КАК СвободныеОстатки
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 1.
		|ВЫБРАТЬ
		|	КешСвободныхОстатков.Склад				КАК Склад,
		|	КешСвободныхОстатков.ВидНоменклатуры	КАК ВидНоменклатуры,
		|	КешСвободныхОстатков.ТоварнаяКатегория 	КАК ТоварнаяКатегория,
		|	КешСвободныхОстатков.Производитель		КАК Производитель,
		|	КешСвободныхОстатков.Номенклатура		КАК Номенклатура,
		|	КешСвободныхОстатков.Характеристика		КАК Характеристика,
		|	КешСвободныхОстатков.СвободныйОстаток	КАК СвободныйОстаток,
		|	""Товарная категория не заполнена!""	КАК	ПричинаОтъезда
		|ПОМЕСТИТЬ КешТкПустая
		|ИЗ
		|	КешСвободныхОстатков КАК КешСвободныхОстатков
		|ГДЕ
		|	КешСвободныхОстатков.ТоварнаяКатегория = ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)
		|И	КешСвободныхОстатков.Производитель	  <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 2.
		|ВЫБРАТЬ
		|	КешСвободныхОстатков.Склад				КАК Склад,
		|	КешСвободныхОстатков.ВидНоменклатуры	КАК ВидНоменклатуры,
		|	КешСвободныхОстатков.ТоварнаяКатегория 	КАК ТоварнаяКатегория,
		|	КешСвободныхОстатков.Производитель		КАК Производитель,
		|	КешСвободныхОстатков.Номенклатура		КАК Номенклатура,
		|	КешСвободныхОстатков.Характеристика		КАК Характеристика,
		|	КешСвободныхОстатков.СвободныйОстаток	КАК СвободныйОстаток,
		|	""Производитель не заполнен!""			КАК	ПричинаОтъезда
		|ПОМЕСТИТЬ КешПрПустая
		|ИЗ
		|	КешСвободныхОстатков КАК КешСвободныхОстатков
		|ГДЕ
		|	КешСвободныхОстатков.ТоварнаяКатегория <> ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)
		|И	КешСвободныхОстатков.Производитель	    = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 3.
		|ВЫБРАТЬ
		|	КешСвободныхОстатков.Склад				КАК Склад,
		|	КешСвободныхОстатков.ВидНоменклатуры	КАК ВидНоменклатуры,
		|	КешСвободныхОстатков.ТоварнаяКатегория 	КАК ТоварнаяКатегория,
		|	КешСвободныхОстатков.Производитель		КАК Производитель,
		|	КешСвободныхОстатков.Номенклатура		КАК Номенклатура,
		|	КешСвободныхОстатков.Характеристика		КАК Характеристика,
		|	КешСвободныхОстатков.СвободныйОстаток	КАК СвободныйОстаток,
		|	""Производитель и товарная категория не заполнены!"" КАК ПричинаОтъезда
		|ПОМЕСТИТЬ КешТкПрПустая
		|ИЗ
		|	КешСвободныхОстатков КАК КешСвободныхОстатков
		|ГДЕ
		|	КешСвободныхОстатков.ТоварнаяКатегория  = ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)
		|И	КешСвободныхОстатков.Производитель	    = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 4.
		|ВЫБРАТЬ
		|	УАМ.Склад,
		|	УАМ.ВидНоменклатуры,
		|	УАМ.ТоварнаяКатегория,
		|	УАМ.Производитель,
		|	УАМ.Номенклатура	
		|ПОМЕСТИТЬ НастройкиИсключеноИзАссортимента
		|ИЗ
		|	РегистрСведений.АМ_УправлениеАссортиментнойМатрицей.СрезПоследних(, Склад = &Склад И ВидНоменклатуры = &ВидНоменклатуры) КАК УАМ
		|ГДЕ
		|	УАМ.Исключено = Истина
		|И	УАМ.КонтрольМатрицейОтключен = Ложь
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 5.
		|ВЫБРАТЬ
		|	КешСвободныхОстатков.Склад				КАК Склад,
		|	КешСвободныхОстатков.ВидНоменклатуры	КАК ВидНоменклатуры,
		|	КешСвободныхОстатков.ТоварнаяКатегория 	КАК ТоварнаяКатегория,
		|	КешСвободныхОстатков.Производитель		КАК Производитель,
		|	КешСвободныхОстатков.Номенклатура		КАК Номенклатура,
		|	КешСвободныхОстатков.Характеристика		КАК Характеристика,
		|	КешСвободныхОстатков.СвободныйОстаток	КАК СвободныйОстаток,
		|	""Товар исключен из матрицы магазина!"" КАК ПричинаОтъезда
		|ПОМЕСТИТЬ КешИсключеноИзАссортимента	
		|ИЗ
		|	КешСвободныхОстатков КАК КешСвободныхОстатков
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ НастройкиИсключеноИзАссортимента КАК КешИсключеноИзАссортимента
		|ПО	КешИсключеноИзАссортимента.Склад = КешСвободныхОстатков.Склад 
		|И	КешИсключеноИзАссортимента.ВидНоменклатуры = КешСвободныхОстатков.ВидНоменклатуры 
		|И	ВЫБОР
		|		КОГДА КешИсключеноИзАссортимента.ТоварнаяКатегория <> ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)
		|			ТОГДА КешИсключеноИзАссортимента.ТоварнаяКатегория = КешСвободныхОстатков.ТоварнаяКатегория 
		|        ИНАЧЕ Истина
		|    КОНЕЦ
		|И	ВЫБОР
		|		КОГДА КешИсключеноИзАссортимента.Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|			ТОГДА КешИсключеноИзАссортимента.Производитель = КешСвободныхОстатков.Производитель 
		|        ИНАЧЕ Истина
		|    КОНЕЦ
		|И	ВЫБОР
		|		КОГДА КешИсключеноИзАссортимента.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА КешИсключеноИзАссортимента.Номенклатура = КешСвободныхОстатков.Номенклатура 
		|        ИНАЧЕ Истина
		|    КОНЕЦ
		|    
		|ГДЕ
		|	КешСвободныхОстатков.ТоварнаяКатегория  <> ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)
		|И	КешСвободныхОстатков.Производитель	    <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)	
		|	
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НастройкиИсключеноИзАссортимента;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 7.
		|ВЫБРАТЬ
		|	Склад,
		|	ВидНоменклатуры,
		|	ТоварнаяКатегория,
		|	Производитель,
		|	Номенклатура,
		|	МинимальныйЗапас,
		|	МаксимальныйЗапас,	
		|	Исключено,
		|	КонтрольМатрицейОтключен
		|ПОМЕСТИТЬ НастройкиПерегрузАссортимента
		|ИЗ                                                                           
		|	РегистрСведений.АМ_УправлениеАссортиментнойМатрицей.СрезПоследних(, Склад = &Склад  И ВидНоменклатуры = &ВидНоменклатуры)	
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 8.
		|ВЫБРАТЬ
		|	КешСвободныхОстатков.Склад				КАК Склад,
		|	КешСвободныхОстатков.ВидНоменклатуры	КАК ВидНоменклатуры,
		|	КешСвободныхОстатков.ТоварнаяКатегория 	КАК ТоварнаяКатегория,
		|	КешСвободныхОстатков.Производитель		КАК Производитель,
		|	КешСвободныхОстатков.Номенклатура		КАК Номенклатура,
		|	КешСвободныхОстатков.Характеристика		КАК Характеристика,
		|	КешСвободныхОстатков.СвободныйОстаток	КАК СвободныйОстаток,
		|	ВЫБОР
		|		КОГДА IsNull(НеВАссортиментеПоНоменклатуре.МинимальныйЗапас, 0) <> 0
		|			ТОГДА НеВАссортиментеПоНоменклатуре.МинимальныйЗапас
		|		КОГДА IsNull(НеВАссортиментеПоПроизводителю.МинимальныйЗапас, 0) <> 0
		|			ТОГДА НеВАссортиментеПоПроизводителю.МинимальныйЗапас
		|		КОГДА IsNull(НеВАссортиментеПоТоварнойКатегории.МинимальныйЗапас, 0) <> 0
		|			ТОГДА НеВАссортиментеПоТоварнойКатегории.МинимальныйЗапас
		|		КОГДА IsNull(НеВАссортиментеПоВидуНоменклатуры.МинимальныйЗапас, 0) <> 0
		|			ТОГДА НеВАссортиментеПоВидуНоменклатуры.МинимальныйЗапас
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ КАК МинимальныйЗапас,
		|	ВЫБОР
		|		КОГДА IsNull(НеВАссортиментеПоНоменклатуре.МаксимальныйЗапас, 0) <> 0
		|			ТОГДА НеВАссортиментеПоНоменклатуре.МаксимальныйЗапас
		|		КОГДА IsNull(НеВАссортиментеПоПроизводителю.МаксимальныйЗапас, 0) <> 0
		|			ТОГДА НеВАссортиментеПоПроизводителю.МаксимальныйЗапас
		|		КОГДА IsNull(НеВАссортиментеПоТоварнойКатегории.МаксимальныйЗапас, 0) <> 0
		|			ТОГДА НеВАссортиментеПоТоварнойКатегории.МаксимальныйЗапас
		|		КОГДА IsNull(НеВАссортиментеПоВидуНоменклатуры.МаксимальныйЗапас, 0) <> 0
		|			ТОГДА НеВАссортиментеПоВидуНоменклатуры.МаксимальныйЗапас
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ КАК МаксимальныйЗапас,
		|	""Товара больше чем мин.\макс. запас в матрице магазина!"" КАК ПричинаОтъезда	
		| 		 
		|ПОМЕСТИТЬ КешПерегрузАссортимента	
		|ИЗ
		|	КешСвободныхОстатков КАК КешСвободныхОстатков
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПерегрузАссортимента КАК НеВАссортиментеПоВидуНоменклатуры
		|ПО	НеВАссортиментеПоВидуНоменклатуры.Склад = КешСвободныхОстатков.Склад 
		|И	НеВАссортиментеПоВидуНоменклатуры.ВидНоменклатуры = КешСвободныхОстатков.ВидНоменклатуры
		|И	НеВАссортиментеПоВидуНоменклатуры.ТоварнаяКатегория = ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)
		|И	НеВАссортиментеПоВидуНоменклатуры.Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|И	НеВАссортиментеПоВидуНоменклатуры.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) 
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПерегрузАссортимента КАК НеВАссортиментеПоТоварнойКатегории
		|ПО	НеВАссортиментеПоТоварнойКатегории.Склад = КешСвободныхОстатков.Склад 
		|И	НеВАссортиментеПоТоварнойКатегории.ВидНоменклатуры = КешСвободныхОстатков.ВидНоменклатуры 
		|И	НеВАссортиментеПоТоварнойКатегории.ТоварнаяКатегория = КешСвободныхОстатков.ТоварнаяКатегория
		|И	НеВАссортиментеПоТоварнойКатегории.Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|И	НеВАссортиментеПоТоварнойКатегории.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) 
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПерегрузАссортимента КАК НеВАссортиментеПоПроизводителю
		|ПО	НеВАссортиментеПоПроизводителю.Склад = КешСвободныхОстатков.Склад 
		|И	НеВАссортиментеПоПроизводителю.ВидНоменклатуры = КешСвободныхОстатков.ВидНоменклатуры 
		|И	НеВАссортиментеПоПроизводителю.ТоварнаяКатегория = КешСвободныхОстатков.ТоварнаяКатегория
		|И	НеВАссортиментеПоПроизводителю.Производитель = КешСвободныхОстатков.Производитель
		|И	НеВАссортиментеПоПроизводителю.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПерегрузАссортимента КАК НеВАссортиментеПоНоменклатуре
		|ПО	НеВАссортиментеПоНоменклатуре.Склад = КешСвободныхОстатков.Склад 
		|И	НеВАссортиментеПоНоменклатуре.ВидНоменклатуры = КешСвободныхОстатков.ВидНоменклатуры 
		|И	НеВАссортиментеПоНоменклатуре.ТоварнаяКатегория = КешСвободныхОстатков.ТоварнаяКатегория
		|И	НеВАссортиментеПоНоменклатуре.Производитель = КешСвободныхОстатков.Производитель
		|И	НеВАссортиментеПоНоменклатуре.Номенклатура = КешСвободныхОстатков.Номенклатура
		|    
		|ГДЕ
		|	КешСвободныхОстатков.ТоварнаяКатегория  <> ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)
		|И	КешСвободныхОстатков.Производитель	    <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|И	ВЫБОР
		|		КОГДА IsNull(НеВАссортиментеПоНоменклатуре.Исключено, ЛОЖЬ) <> ЛОЖЬ
		|			ТОГДА НеВАссортиментеПоНоменклатуре.Исключено
		|		КОГДА IsNull(НеВАссортиментеПоПроизводителю.Исключено, ЛОЖЬ) <> ЛОЖЬ
		|			ТОГДА НеВАссортиментеПоПроизводителю.Исключено
		|		КОГДА IsNull(НеВАссортиментеПоТоварнойКатегории.Исключено, ЛОЖЬ) <> ЛОЖЬ
		|			ТОГДА НеВАссортиментеПоТоварнойКатегории.Исключено
		|		КОГДА IsNull(НеВАссортиментеПоВидуНоменклатуры.Исключено, ЛОЖЬ) <> ЛОЖЬ
		|			ТОГДА НеВАссортиментеПоВидуНоменклатуры.Исключено
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ = ЛОЖЬ
		|И	ВЫБОР
		|		КОГДА IsNull(НеВАссортиментеПоНоменклатуре.КонтрольМатрицейОтключен, ЛОЖЬ) <> ЛОЖЬ
		|			ТОГДА НеВАссортиментеПоНоменклатуре.КонтрольМатрицейОтключен
		|		КОГДА IsNull(НеВАссортиментеПоПроизводителю.КонтрольМатрицейОтключен, ЛОЖЬ) <> ЛОЖЬ
		|			ТОГДА НеВАссортиментеПоПроизводителю.КонтрольМатрицейОтключен
		|		КОГДА IsNull(НеВАссортиментеПоТоварнойКатегории.КонтрольМатрицейОтключен, ЛОЖЬ) <> ЛОЖЬ
		|			ТОГДА НеВАссортиментеПоТоварнойКатегории.КонтрольМатрицейОтключен
		|		КОГДА IsNull(НеВАссортиментеПоВидуНоменклатуры.КонтрольМатрицейОтключен, ЛОЖЬ) <> ЛОЖЬ
		|			ТОГДА НеВАссортиментеПоВидуНоменклатуры.КонтрольМатрицейОтключен
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ = ЛОЖЬ
		|	
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НастройкиПерегрузАссортимента; 
		////////////////////////////////////////////////////////////////////////////////////////////
		// 10.
		|ВЫБРАТЬ
		|	КешПерегрузАссортимента.Склад				КАК Склад,
		|	КешПерегрузАссортимента.ВидНоменклатуры		КАК ВидНоменклатуры,
		|	КешПерегрузАссортимента.ТоварнаяКатегория 	КАК ТоварнаяКатегория,
		|	КешПерегрузАссортимента.Производитель		КАК Производитель,
		|	КешПерегрузАссортимента.Номенклатура		КАК Номенклатура,
		|	КешПерегрузАссортимента.Характеристика		КАК Характеристика,
		|	ВЫБОР
		|		КОГДА КешПерегрузАссортимента.МаксимальныйЗапас = 0
		|			ТОГДА КешПерегрузАссортимента.СвободныйОстаток - КешПерегрузАссортимента.МинимальныйЗапас
		|		ИНАЧЕ КешПерегрузАссортимента.СвободныйОстаток - КешПерегрузАссортимента.МаксимальныйЗапас
		|	КОНЕЦ КАК Количество,
		|	КешПерегрузАссортимента.ПричинаОтъезда		КАК ПричинаОтъезда
		|ИЗ
		|	КешПерегрузАссортимента КАК КешПерегрузАссортимента
		|ГДЕ
		|	(КешПерегрузАссортимента.МаксимальныйЗапас <> 0
		|  И КешПерегрузАссортимента.СвободныйОстаток > КешПерегрузАссортимента.МаксимальныйЗапас)
		|ИЛИ		
		|  	(КешПерегрузАссортимента.МаксимальныйЗапас = 0
		|  И КешПерегрузАссортимента.СвободныйОстаток > КешПерегрузАссортимента.МинимальныйЗапас)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсключеноИзАссортимента.Склад,
		|	ИсключеноИзАссортимента.ВидНоменклатуры,
		|	ИсключеноИзАссортимента.ТоварнаяКатегория,
		|	ИсключеноИзАссортимента.Производитель,
		|	ИсключеноИзАссортимента.Номенклатура,
		|	ИсключеноИзАссортимента.Характеристика,
		|	ИсключеноИзАссортимента.СвободныйОстаток,
		|	ИсключеноИзАссортимента.ПричинаОтъезда
		|ИЗ
		|	КешИсключеноИзАссортимента КАК ИсключеноИзАссортимента	
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КешТкПустая.Склад,
		|	КешТкПустая.ВидНоменклатуры,
		|	КешТкПустая.ТоварнаяКатегория,
		|	КешТкПустая.Производитель,
		|	КешТкПустая.Номенклатура,
		|	КешТкПустая.Характеристика,
		|	КешТкПустая.СвободныйОстаток,
		|	КешТкПустая.ПричинаОтъезда
		|ИЗ
		|	КешТкПустая КАК КешТкПустая	
		|
		|ОБЪЕДИНИТЬ ВСЕ 
		|
		|ВЫБРАТЬ
		|	КешПрПустая.Склад,
		|	КешПрПустая.ВидНоменклатуры,
		|	КешПрПустая.ТоварнаяКатегория,
		|	КешПрПустая.Производитель,
		|	КешПрПустая.Номенклатура,
		|	КешПрПустая.Характеристика,
		|	КешПрПустая.СвободныйОстаток,
		|	КешПрПустая.ПричинаОтъезда
		|ИЗ
		|	КешПрПустая КАК КешПрПустая	
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КешТкПрПустая.Склад,
		|	КешТкПрПустая.ВидНоменклатуры,
		|	КешТкПрПустая.ТоварнаяКатегория,
		|	КешТкПрПустая.Производитель,
		|	КешТкПрПустая.Номенклатура,
		|	КешТкПрПустая.Характеристика,
		|	КешТкПрПустая.СвободныйОстаток,
		|	КешТкПрПустая.ПричинаОтъезда
		|ИЗ
		|	КешТкПрПустая КАК КешТкПрПустая
		|	
		|УПОРЯДОЧИТЬ ПО
		|	ВидНоменклатуры Возр		
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешТкПустая; 
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешПрПустая;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешТкПрПустая;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешСвободныхОстатков;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешПерегрузАссортимента;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешИсключеноИзАссортимента;
		|";
	Возврат ТекстЗапроса

КонецФункции // ПолучитьТекстЗапросаАналитикаТоварыКОтъезду()

Функция ПолучитьТекстЗапросаАналитикаТоварыКОтъездуПоАртикулам()
	
	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////////////////
		// 0. Кеш матрицы 
		|ВЫБРАТЬ
		|	Склад КАК Склад,
		|	ВидНоменклатуры КАК ВидНоменклатуры, 
		|	ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	Производитель КАК Производитель,
		|	Номенклатура КАК Номенклатура,
		|	
		|	НомерПоПорядку КАК НомерПоПорядку,
		|	
		|	ПосадочныеМеста КАК ПосадочныеМеста,
		|	ОтклонениеКоличествоПМ КАК ОтклонениеКоличествоПМ,
		|	
		|	МинимальныйЗапас КАК МинимальныйЗапас,
		|	СтраховойЗапас КАК СтраховойЗапас,
		|	МаксимальныйЗапас КАК МаксимальныйЗапас,
		|
		|	Исключено КАК Исключено,	
		|	КонтрольМатрицейОтключен КАК КонтрольМатрицейОтключен 
		|	
		|ПОМЕСТИТЬ КешМатрицы
		|ИЗ
		|	РегистрСведений.АМ_УправлениеАссортиментнойМатрицей.СрезПоследних(, Склад = &Склад И ВидНоменклатуры = &ВидНоменклатуры)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад,
		|	ВидНоменклатуры,
		|	ТоварнаяКатегория,
		|	Производитель,
		|	Номенклатура	
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 1. Данные по видам номенклатуры
		|ВЫБРАТЬ
		|	&Склад КАК Склад,
		|	ВидыНоменклатуры.Ссылка	КАК ВидНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка) КАК ТоварнаяКатегория
		|ПОМЕСТИТЬ КешДанныхПоВидуНоменклатуры
		|ИЗ
		|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|
		|ГДЕ
		| 	ВидыНоменклатуры.ЭтоГруппа = Ложь	
		|И	ВидыНоменклатуры.ПометкаУдаления = Ложь
		|И	ВидыНоменклатуры.Ссылка = &ВидНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад, 
		|	ВидНоменклатуры, 
		|	ТоварнаяКатегория
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 2. Посадочных мест по виду номенклатуры, отсекаем то что продакт исключил из управления
		|ВЫБРАТЬ
		|	КешДанных.Склад КАК Склад,
		|	КешДанных.ВидНоменклатуры КАК ВидНоменклатуры, 
		|	КешДанных.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	
		|	IsNull(КешМатрицы.ПосадочныеМеста, 0) КАК ПосадочныеМеста
		|		
		|ПОМЕСТИТЬ ПосадочныхМестПоВидуНоменклатуры
		|ИЗ
		|	КешДанныхПоВидуНоменклатуры КАК КешДанных
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ КешМатрицы КАК КешМатрицы
		|ПО	КешМатрицы.Склад = КешДанных.Склад
		|И	КешМатрицы.ВидНоменклатуры = КешДанных.ВидНоменклатуры
		|И	КешМатрицы.ТоварнаяКатегория = КешДанных.ТоварнаяКатегория
		|
		|ГДЕ
		|	IsNull(КешМатрицы.Исключено, Ложь) = Ложь
		|И	IsNull(КешМатрицы.КонтрольМатрицейОтключен, Ложь) = Ложь	
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад,
		|	ВидНоменклатуры,
		|	ТоварнаяКатегория
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 3. Данные по видам товарным категориям, переносим отсечение на подчиненную группу
		|ВЫБРАТЬ
		|	КешДанныхПоВидуНоменклатуры.Склад КАК Склад,
		|	КешДанныхПоВидуНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ТоварныеКатегории.Ссылка КАК ТоварнаяКатегория,
		|	ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка) КАК Производитель
		|	
		|ПОМЕСТИТЬ КешДанныхПоТоварнымКатегориям
		|ИЗ
		|	ПосадочныхМестПоВидуНоменклатуры КАК КешДанныхПоВидуНоменклатуры 	
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТоварныеКатегории КАК ТоварныеКатегории
		|ПО	ТоварныеКатегории.Владелец = КешДанныхПоВидуНоменклатуры.ВидНоменклатуры
		|И	ТоварныеКатегории.ЭтоГруппа = Ложь	
		|И	ТоварныеКатегории.ПометкаУдаления = Ложь
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад, 
		|	ВидНоменклатуры, 
		|	ТоварнаяКатегория, 
		|	Производитель		
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 4. Посадочных мест по товарной категории, отсекаем то что продакт исключил из управления
		|ВЫБРАТЬ
		|	КешДанных.Склад КАК Склад,
		|	КешДанных.ВидНоменклатуры КАК ВидНоменклатуры,
		|	КешДанных.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	КешДанных.Производитель КАК Производитель,
		|	
		|	IsNull(ПосадочныеМеста, 0) КАК ПосадочныеМеста,
		|	IsNull(ОтклонениеКоличествоПМ, 0) КАК ОтклонениеКоличествоПМ,
		|	
		|	IsNull(НомерПоПорядку, 0) КАК НомерПоПорядку
		|		
		|ПОМЕСТИТЬ ПосадочныхМестПоТоварнойКатегории
		|ИЗ
		|	КешДанныхПоТоварнымКатегориям КАК КешДанных
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ КешМатрицы КАК КешМатрицы
		|ПО	КешМатрицы.Склад = КешДанных.Склад
		|И	КешМатрицы.ВидНоменклатуры = КешДанных.ВидНоменклатуры
		|И	КешМатрицы.ТоварнаяКатегория = КешДанных.ТоварнаяКатегория
		|И	КешМатрицы.Производитель = КешДанных.Производитель
		|
		|ГДЕ
		|	IsNull(КешМатрицы.Исключено, Ложь) = Ложь
		|И	IsNull(КешМатрицы.КонтрольМатрицейОтключен, Ложь) = Ложь
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад,
		|	ВидНоменклатуры,
		|	ТоварнаяКатегория,
		|	Производитель
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 5. Данные по производителям, переносим отсечение на подчиненную группу 
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КешДанныхПоТоварнымКатегориям.Склад	КАК Склад,
		|	КешДанныхПоТоварнымКатегориям.ВидНоменклатуры КАК ВидНоменклатуры,
		|	КешДанныхПоТоварнымКатегориям.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	СпрНоменклатура.Производитель КАК Производитель,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура
		|ПОМЕСТИТЬ КешДанныхПоПроизводителям
		|ИЗ
		|	ПосадочныхМестПоТоварнойКатегории КАК КешДанныхПоТоварнымКатегориям
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|ПО СпрНоменклатура.ВидНоменклатуры = КешДанныхПоТоварнымКатегориям.ВидНоменклатуры
		|И	СпрНоменклатура.ТоварнаяКатегория = КешДанныхПоТоварнымКатегориям.ТоварнаяКатегория
		|И	СпрНоменклатура.Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад, 
		|	ВидНоменклатуры, 
		|	ТоварнаяКатегория, 
		|	Производитель,
		|	Номенклатура	
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 6. Посадочных мест по производителю, отсекаем то что продакт исключил из управления
		|ВЫБРАТЬ
		|	КешДанных.Склад КАК Склад,
		|	КешДанных.ВидНоменклатуры КАК ВидНоменклатуры,
		|	КешДанных.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	КешДанных.Производитель КАК Производитель,
		|	КешДанных.Номенклатура КАК Номенклатура,
		|	
		|	IsNull(ПосадочныеМеста, 0) КАК ПосадочныеМеста,
		|	IsNull(ОтклонениеКоличествоПМ, 0) КАК ОтклонениеКоличествоПМ,
		|	
		|	IsNull(НомерПоПорядку, 0) КАК НомерПоПорядку
		|	
		|ПОМЕСТИТЬ ПосадочныхМестПоПроизводителю
		|ИЗ
		|	КешДанныхПоПроизводителям КАК КешДанных	
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешМатрицы КАК КешМатрицы
		|ПО	КешМатрицы.Склад = КешДанных.Склад
		|И	КешМатрицы.ВидНоменклатуры = КешДанных.ВидНоменклатуры
		|И	КешМатрицы.ТоварнаяКатегория = КешДанных.ТоварнаяКатегория
		|И	КешМатрицы.Производитель = КешДанных.Производитель
		|И	КешМатрицы.Номенклатура = КешДанных.Номенклатура
		|
		|ГДЕ
		|	IsNull(КешМатрицы.Исключено, Ложь) = Ложь
		|И	IsNull(КешМатрицы.КонтрольМатрицейОтключен, Ложь) = Ложь
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад,
		|	ВидНоменклатуры,
		|	ТоварнаяКатегория,
		|	Производитель,
		|	Номенклатура
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 7. Посадочных мест по товарной категории, отсекаем то что продакт исключил из управления
		|ВЫБРАТЬ 	
		|	КешДанныхПоПроизводителям.Склад	КАК Склад,
		|	КешДанныхПоПроизводителям.ВидНоменклатуры КАК ВидНоменклатуры,
		|	КешДанныхПоПроизводителям.ТоварнаяКатегория	КАК ТоварнаяКатегория,
		|	КешДанныхПоПроизводителям.Производитель	КАК Производитель,
		|	СпрНоменклатура.Ссылка КАК Номенклатура
		|ПОМЕСТИТЬ КешДанныхПоНоменклатуре
		|ИЗ
		|	ПосадочныхМестПоПроизводителю КАК КешДанныхПоПроизводителям
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|ПО СпрНоменклатура.ВидНоменклатуры = КешДанныхПоПроизводителям.ВидНоменклатуры
		|И	СпрНоменклатура.ТоварнаяКатегория = КешДанныхПоПроизводителям.ТоварнаяКатегория
		|И	СпрНоменклатура.Производитель = КешДанныхПоПроизводителям.Производитель
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад, 
		|	ВидНоменклатуры, 
		|	ТоварнаяКатегория, 
		|	Производитель,
		|	Номенклатура	
		|;    
		////////////////////////////////////////////////////////////////////////////////////////////
		// 8. Посадочных мест по номенклатуре
		|ВЫБРАТЬ
		|	КешДанных.Склад КАК Склад,
		|	КешДанных.ВидНоменклатуры КАК ВидНоменклатуры,
		|	КешДанных.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	КешДанных.Производитель КАК Производитель,
		|	КешДанных.Номенклатура КАК Номенклатура,
		|	
		|	IsNull(ПосадочныеМеста, 0) КАК ПосадочныеМеста,
		|	0 КАК ОтклонениеКоличествоПМ,
		|	
		|	IsNull(НомерПоПорядку, 0) КАК НомерПоПорядку
		| 	
		|ПОМЕСТИТЬ ПосадочныхМестПоНоменклатуре
		|ИЗ
		|	КешДанныхПоНоменклатуре КАК КешДанных	
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешМатрицы КАК КешМатрицы
		|ПО	КешМатрицы.Склад = КешДанных.Склад
		|И	КешМатрицы.ВидНоменклатуры = КешДанных.ВидНоменклатуры
		|И	КешМатрицы.ТоварнаяКатегория = КешДанных.ТоварнаяКатегория
		|И	КешМатрицы.Производитель = КешДанных.Производитель
		|И	КешМатрицы.Номенклатура = КешДанных.Номенклатура
		|
		|ГДЕ
		|	IsNull(КешМатрицы.Исключено, Ложь) = Ложь
		|И	IsNull(КешМатрицы.КонтрольМатрицейОтключен, Ложь) = Ложь
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад,
		|	ВидНоменклатуры,
		|	ТоварнаяКатегория,
		|	Производитель,
		|	Номенклатура
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешМатрицы;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешДанныхПоВидуНоменклатуры;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешДанныхПоТоварнымКатегориям;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешДанныхПоПроизводителям;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешДанныхПоНоменклатуре;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 14.
		|ВЫБРАТЬ
		|	СвободныеОстатки.Склад КАК Склад,
		|   СвободныеОстатки.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СвободныеОстатки.Номенклатура.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|  	КОЛИЧЕСТВО(СвободныеОстатки.Номенклатура) КАК КоличествоАртикулов
		|ПОМЕСТИТЬ РасчетТКА_ТК
		|ИЗ
		|	РегистрНакопления.СвободныеОстатки.Остатки(, Номенклатура.ВидНоменклатуры = &ВидНоменклатуры И Склад = &Склад) КАК СвободныеОстатки
		|
		|ГДЕ
		|	СвободныеОстатки.ВНаличииОстаток - СвободныеОстатки.ВРезервеОстаток > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	СвободныеОстатки.Склад,
		|	СвободныеОстатки.Номенклатура.ВидНоменклатуры,
		|	СвободныеОстатки.Номенклатура.ТоварнаяКатегория
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад, 
		|	ВидНоменклатуры, 
		|	ТоварнаяКатегория 
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 15.
		|ВЫБРАТЬ
		|	СвободныеОстатки.Склад КАК Склад,
		|   	СвободныеОстатки.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СвободныеОстатки.Номенклатура.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	СвободныеОстатки.Номенклатура.Производитель	КАК Производитель,
		|  	КОЛИЧЕСТВО(СвободныеОстатки.Номенклатура) КАК КоличествоАртикулов
		|ПОМЕСТИТЬ РасчетТКА_ПР
		|ИЗ
		|	РегистрНакопления.СвободныеОстатки.Остатки(, Номенклатура.ВидНоменклатуры = &ВидНоменклатуры И Склад = &Склад) КАК СвободныеОстатки
		|
		|ГДЕ
		|	СвободныеОстатки.ВНаличииОстаток - СвободныеОстатки.ВРезервеОстаток > 0
		|	
		|СГРУППИРОВАТЬ ПО
		|	СвободныеОстатки.Склад,
		|	СвободныеОстатки.Номенклатура.ВидНоменклатуры,
		|	СвободныеОстатки.Номенклатура.ТоварнаяКатегория,
		|	СвободныеОстатки.Номенклатура.Производитель
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад, 
		|	ВидНоменклатуры, 
		|	ТоварнаяКатегория, 
		|	Производитель
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 16.
		|ВЫБРАТЬ
		|	СвободныеОстатки.Склад КАК Склад,
		|   СвободныеОстатки.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СвободныеОстатки.Номенклатура.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	СвободныеОстатки.Номенклатура.Производитель	КАК Производитель,
		|	СвободныеОстатки.Номенклатура КАК Номенклатура,
		|	СвободныеОстатки.ВНаличииОстаток - СвободныеОстатки.ВРезервеОстаток КАК Количество,
		|  	КОЛИЧЕСТВО(СвободныеОстатки.Номенклатура) КАК КоличествоАртикулов
		|ПОМЕСТИТЬ РасчетТКА_НМ
		|ИЗ
		|	РегистрНакопления.СвободныеОстатки.Остатки(, Номенклатура.ВидНоменклатуры = &ВидНоменклатуры И Склад = &Склад) КАК СвободныеОстатки
		|ГДЕ
		|	СвободныеОстатки.ВНаличииОстаток - СвободныеОстатки.ВРезервеОстаток > 0 
		|		
		|СГРУППИРОВАТЬ ПО
		|	СвободныеОстатки.Склад,
		|	СвободныеОстатки.Номенклатура,
		|	СвободныеОстатки.ВНаличииОстаток - СвободныеОстатки.ВРезервеОстаток 
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад, 
		|	ВидНоменклатуры, 
		|	ТоварнаяКатегория, 
		|	Производитель,
		|	Номенклатура
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 17.
		|ВЫБРАТЬ
		|	АУН.Склад КАК Склад,
		|	АУН.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	АУН.Номенклатура.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	СРЕДНЕЕ(ДнейНаСкладе.КоличествоДней) КАК ДНС
		|ПОМЕСТИТЬ ДнейНаСкладе_ТК	
		|ИЗ
		|	РегистрСведений.АМ_ДнейНаСкладе КАК ДнейНаСкладе
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АУН
		|ПО АУН.КлючАналитики = ДнейНаСкладе.АналитикаУчетаНоменклатуры
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПосадочныхМестПоТоварнойКатегории КАК КешДанныхДляВывода
		|ПО	КешДанныхДляВывода.Склад = АУН.Склад
		|И  КешДанныхДляВывода.ВидНоменклатуры = АУН.Номенклатура.ВидНоменклатуры
		|И  КешДанныхДляВывода.ТоварнаяКатегория = АУН.Номенклатура.ТоварнаяКатегория
		|
		|СГРУППИРОВАТЬ ПО
		|	АУН.Склад,
		|	АУН.Номенклатура.ВидНоменклатуры,
		|	АУН.Номенклатура.ТоварнаяКатегория
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад,
		|	ВидНоменклатуры,
		|	ТоварнаяКатегория
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 18.
		|ВЫБРАТЬ
		|	АУН.Склад КАК Склад,
		|	АУН.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	АУН.Номенклатура.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	АУН.Номенклатура.Производитель КАК Производитель,
		|	СРЕДНЕЕ(ДнейНаСкладе.КоличествоДней) КАК ДНС
		|ПОМЕСТИТЬ ДнейНаСкладе_ПР	
		|ИЗ
		|	РегистрСведений.АМ_ДнейНаСкладе КАК ДнейНаСкладе
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АУН
		|ПО АУН.КлючАналитики = ДнейНаСкладе.АналитикаУчетаНоменклатуры
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПосадочныхМестПоПроизводителю КАК КешДанныхДляВывода
		|ПО	КешДанныхДляВывода.Склад = АУН.Склад
		|И  КешДанныхДляВывода.ВидНоменклатуры = АУН.Номенклатура.ВидНоменклатуры
		|И  КешДанныхДляВывода.ТоварнаяКатегория = АУН.Номенклатура.ТоварнаяКатегория
		|И  КешДанныхДляВывода.Производитель = АУН.Номенклатура.Производитель
		|
		|СГРУППИРОВАТЬ ПО
		|	АУН.Склад,
		|	АУН.Номенклатура.ВидНоменклатуры,
		|	АУН.Номенклатура.ТоварнаяКатегория,
		|	АУН.Номенклатура.Производитель
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад,
		|	ВидНоменклатуры,
		|	ТоварнаяКатегория,
		|	Производитель
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 19.
		|ВЫБРАТЬ
		|	КешДанных.Склад КАК Склад,
		|	КешДанных.ВидНоменклатуры КАК ВидНоменклатуры, 
		|	КешДанных.ТоварнаяКатегория КАК ТоварнаяКатегория,	
		|	КешДанных.ПосадочныеМеста КАК ПосадочныеМеста,
		|	
		|	СУММА(ПодчиненныйКешДанных.ПосадочныеМеста) КАК ПодчиненныеПосадочныеМеста,
		|	СУММА(ПодчиненныйКешДанных.ОтклонениеКоличествоПМ) КАК ОтклонениеКоличествоПМ,
		|	
		|	СУММА(IsNull(РасчетТКА.КоличествоАртикулов, 0)) КАК КоличествоАртикулов	
		|ИЗ
		|	ПосадочныхМестПоВидуНоменклатуры КАК КешДанных
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ ПосадочныхМестПоТоварнойКатегории КАК ПодчиненныйКешДанных
		|ПО	ПодчиненныйКешДанных.Склад = КешДанных.Склад
		|И	ПодчиненныйКешДанных.ВидНоменклатуры = КешДанных.ВидНоменклатуры
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РасчетТКА_ТК КАК РасчетТКА
		|ПО	РасчетТКА.Склад = ПодчиненныйКешДанных.Склад
		|И	РасчетТКА.ВидНоменклатуры = ПодчиненныйКешДанных.ВидНоменклатуры
		|И	РасчетТКА.ТоварнаяКатегория = ПодчиненныйКешДанных.ТоварнаяКатегория	
		|
		|СГРУППИРОВАТЬ ПО
		|	КешДанных.Склад,
		|	КешДанных.ВидНоменклатуры, 
		|	КешДанных.ТоварнаяКатегория,
		|	КешДанных.ПосадочныеМеста		
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 20.
		|ВЫБРАТЬ
		|	КешДанных.Склад КАК Склад,
		|	КешДанных.ВидНоменклатуры КАК ВидНоменклатуры, 
		|	КешДанных.ТоварнаяКатегория КАК ТоварнаяКатегория,	
		|	КешДанных.ПосадочныеМеста КАК ПосадочныеМеста,
		|	КешДанных.ОтклонениеКоличествоПМ КАК ОтклонениеКоличествоПМ,
		|	КешДанных.НомерПоПорядку КАК НомерПоПорядку,
		|	
		|	СУММА(ПодчиненныйКешДанных.ПосадочныеМеста) КАК ПодчиненныеПосадочныеМеста,
		|	СУММА(ПодчиненныйКешДанных.ОтклонениеКоличествоПМ) КАК ПодчиненныеОтклонения,
		|	
		|	СУММА(IsNull(РасчетТКА.КоличествоАртикулов, 0)) КАК КоличествоАртикулов,
		|	IsNull(ДнейНаСкладе.ДНС, 0) КАК ДНС
		|ИЗ
		|	ПосадочныхМестПоТоварнойКатегории КАК КешДанных
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ ПосадочныхМестПоПроизводителю КАК ПодчиненныйКешДанных
		|ПО	ПодчиненныйКешДанных.Склад = КешДанных.Склад
		|И	ПодчиненныйКешДанных.ВидНоменклатуры = КешДанных.ВидНоменклатуры
		|И	ПодчиненныйКешДанных.ТоварнаяКатегория = КешДанных.ТоварнаяКатегория
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РасчетТКА_ПР КАК РасчетТКА
		|ПО	РасчетТКА.Склад = ПодчиненныйКешДанных.Склад
		|И	РасчетТКА.ВидНоменклатуры = ПодчиненныйКешДанных.ВидНоменклатуры
		|И	РасчетТКА.ТоварнаяКатегория = ПодчиненныйКешДанных.ТоварнаяКатегория
		|И	РасчетТКА.Производитель = ПодчиненныйКешДанных.Производитель
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДнейНаСкладе_ТК КАК ДнейНаСкладе
		|ПО ДнейНаСкладе.Склад = КешДанных.Склад
		|И	ДнейНаСкладе.ВидНоменклатуры = КешДанных.ВидНоменклатуры
		|И	ДнейНаСкладе.ТоварнаяКатегория = КешДанных.ТоварнаяКатегория
		|
		|СГРУППИРОВАТЬ ПО
		|	КешДанных.Склад,
		|	КешДанных.ВидНоменклатуры, 
		|	КешДанных.ТоварнаяКатегория,
		|	КешДанных.ПосадочныеМеста,
		|	КешДанных.ОтклонениеКоличествоПМ,
		|	КешДанных.НомерПоПорядку,
		|	IsNull(ДнейНаСкладе.ДНС, 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	IsNull(ДнейНаСкладе.ДНС, 0) УБЫВ
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 21.
		|ВЫБРАТЬ
		|	КешДанных.Склад КАК Склад,
		|	КешДанных.ВидНоменклатуры КАК ВидНоменклатуры, 
		|	КешДанных.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	КешДанных.Производитель КАК Производитель,	
		|	КешДанных.ПосадочныеМеста КАК ПосадочныеМеста,
		|	КешДанных.ОтклонениеКоличествоПМ КАК ОтклонениеКоличествоПМ,
		|	КешДанных.НомерПоПорядку КАК НомерПоПорядку,
		|	
		|	СУММА(ПодчиненныйКешДанных.ПосадочныеМеста) КАК ПодчиненныеПосадочныеМеста,
		|	СУММА(ПодчиненныйКешДанных.ОтклонениеКоличествоПМ) КАК ПодчиненныеОтклонения,
		|	
		|	СУММА(IsNull(РасчетТКА.КоличествоАртикулов, 0)) КАК КоличествоАртикулов,
		|	IsNull(ДнейНаСкладе.ДНС, 0) КАК ДНС
		|ИЗ
		|	ПосадочныхМестПоПроизводителю КАК КешДанных
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ ПосадочныхМестПоНоменклатуре КАК ПодчиненныйКешДанных
		|ПО	ПодчиненныйКешДанных.Склад = КешДанных.Склад
		|И	ПодчиненныйКешДанных.ВидНоменклатуры = КешДанных.ВидНоменклатуры
		|И	ПодчиненныйКешДанных.ТоварнаяКатегория = КешДанных.ТоварнаяКатегория
		|И	ПодчиненныйКешДанных.Производитель = КешДанных.Производитель
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РасчетТКА_НМ КАК РасчетТКА
		|ПО	РасчетТКА.Склад = ПодчиненныйКешДанных.Склад
		|И	РасчетТКА.ВидНоменклатуры = ПодчиненныйКешДанных.ВидНоменклатуры
		|И	РасчетТКА.ТоварнаяКатегория = ПодчиненныйКешДанных.ТоварнаяКатегория
		|И	РасчетТКА.Производитель = ПодчиненныйКешДанных.Производитель
		|И  РасчетТКА.Номенклатура = ПодчиненныйКешДанных.Номенклатура
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДнейНаСкладе_ПР КАК ДнейНаСкладе
		|ПО ДнейНаСкладе.Склад = КешДанных.Склад
		|И	ДнейНаСкладе.ВидНоменклатуры = КешДанных.ВидНоменклатуры
		|И	ДнейНаСкладе.ТоварнаяКатегория = КешДанных.ТоварнаяКатегория
		|И	ДнейНаСкладе.Производитель = КешДанных.Производитель	
		|
		|СГРУППИРОВАТЬ ПО
		|	КешДанных.Склад,
		|	КешДанных.ВидНоменклатуры, 
		|	КешДанных.ТоварнаяКатегория,
		|	КешДанных.Производитель,
		|	КешДанных.ПосадочныеМеста,
		|	КешДанных.ОтклонениеКоличествоПМ,
		|	КешДанных.НомерПоПорядку,
		|	IsNull(ДнейНаСкладе.ДНС, 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	IsNull(ДнейНаСкладе.ДНС, 0) УБЫВ
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 22.
		|ВЫБРАТЬ
		|	КешДанных.Склад КАК Склад,
		|	КешДанных.ВидНоменклатуры КАК ВидНоменклатуры, 
		|	КешДанных.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	КешДанных.Производитель КАК Производитель,
		|	КешДанных.Номенклатура КАК Номенклатура,	
		|	КешДанных.ПосадочныеМеста КАК ПосадочныеМеста,
		|	КешДанных.ОтклонениеКоличествоПМ КАК ОтклонениеКоличествоПМ,
		|	КешДанных.НомерПоПорядку КАК НомерПоПорядку,
		|	
		|	IsNull(РасчетТКА.Количество, 0) КАК Количество,
		|	""Артикулов больше чем задано в матрице магазина!"" КАК ПричинаОтъезда,
		|	IsNull(РасчетТКА.КоличествоАртикулов, 0) КАК КоличествоАртикулов,
		|	IsNull(ДнейНаСкладе.КоличествоДней, 0) КАК ДНС
		|
		|ИЗ
		|	ПосадочныхМестПоНоменклатуре КАК КешДанных
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ РасчетТКА_НМ КАК РасчетТКА
		|ПО	РасчетТКА.Склад = КешДанных.Склад
		|И	РасчетТКА.ВидНоменклатуры = КешДанных.ВидНоменклатуры
		|И	РасчетТКА.ТоварнаяКатегория = КешДанных.ТоварнаяКатегория
		|И	РасчетТКА.Производитель = КешДанных.Производитель
		|И   РасчетТКА.Номенклатура = КешДанных.Номенклатура
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АМ_ДнейНаСкладе КАК ДнейНаСкладе
		|ПО	ДнейНаСкладе.АналитикаУчетаНоменклатуры.Номенклатура = КешДанных.Номенклатура
		|И  ДнейНаСкладе.АналитикаУчетаНоменклатуры.Склад = КешДанных.Склад
		|
		|УПОРЯДОЧИТЬ ПО
		|	IsNull(ДнейНаСкладе.КоличествоДней, 0) УБЫВ				
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ РасчетТКА_ТК;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ РасчетТКА_ПР;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ РасчетТКА_НМ;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДнейНаСкладе_ТК;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДнейНаСкладе_ПР;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПосадочныхМестПоВидуНоменклатуры;
		//////////////////////////////////////////////////////////////////////////////////////////// 
		|УНИЧТОЖИТЬ ПосадочныхМестПоТоварнойКатегории;
		//////////////////////////////////////////////////////////////////////////////////////////// 
		|УНИЧТОЖИТЬ ПосадочныхМестПоПроизводителю;
		//////////////////////////////////////////////////////////////////////////////////////////// 
		|УНИЧТОЖИТЬ ПосадочныхМестПоНоменклатуре;
		|";
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапросаАналитикаТоварыКОтъездуПоАртикулам() 

Функция ПолучитьТекстЗапросаАналитикаТоварыВРезервах()
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗаказКлиента КАК Заказ,
		|	ЗаказКлиента.Дата КАК Дата,
		|	Номенклатура КАК Номенклатура,
		|	Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	Номенклатура.ТоварнаяКатегория КАК ТоварнаяКатегория,	
		|	Номенклатура.Производитель КАК Производитель,
		|	СуммаОстаток КАК Сумма,
		|	ЗаказаноОстаток КАК Заказано,
		|	КОформлениюОстаток КАК ВРезерве
		|ИЗ                                             
		|	РегистрНакопления.ЗаказыКлиентов.Остатки(, Склад = &Склад)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ЗаказНаВнутреннееПотребление,
		|	ЗаказНаВнутреннееПотребление.Дата,
		|	Номенклатура,
		|	Номенклатура.ВидНоменклатуры,
		|	Номенклатура.ТоварнаяКатегория,	
		|	Номенклатура.Производитель,
		|	0,
		|	ЗаказаноОстаток,
		|	КОформлениюОстаток
		|ИЗ                                             
		|	РегистрНакопления.ЗаказыНаВнутреннееПотребление.Остатки(, Склад = &Склад)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ЗаказНаПеремещение,
		|	ЗаказНаПеремещение.Дата,
		|	Номенклатура,
		|	Номенклатура.ВидНоменклатуры,
		|	Номенклатура.ТоварнаяКатегория,	
		|	Номенклатура.Производитель,
		|	0,
		|	ЗаказаноОстаток,
		|	КОформлениюОстаток
		|ИЗ                                             
		|	РегистрНакопления.ЗаказыНаПеремещение.Остатки(, ЗаказНаПеремещение.СкладОтправитель = &Склад)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ЗаказНаСборку,
		|	ЗаказНаСборку.Дата,
		|	Номенклатура,
		|	Номенклатура.ВидНоменклатуры,
		|	Номенклатура.ТоварнаяКатегория,	
		|	Номенклатура.Производитель,
		|	0,
		|	ЗаказаноОстаток,
		|	КОформлениюОстаток
		|ИЗ                                             
		|	РегистрНакопления.ЗаказыНаСборку.Остатки(, ЗаказНаСборку.Склад = &Склад)
		|
		|УПОРЯДОЧИТЬ ПО 
		|	Дата Возр	
		|";
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапросаАналитикаТоварыВРезервах()

#КонецОбласти

#Область  СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	Версия = "0.0.10";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "УправлениеАссортиментнымиМатрицами");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);
	ПараметрыРегистрации.Вставить("ВерсияБСП", "2.1.3.50");
    ПараметрыРегистрации.Вставить("Информация", "Управление ассортиментными матрицами [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "УправлениеАссортиментнымиМатрицами [" + Версия + "]", "УАМ", "ОткрытиеФормы", Ложь, "УАМ");
	ДобавитьКоманду(ТаблицаКоманд, "ОбновитьПродажиПоФорматамВФоне", "ОбновитьПродажиПоФорматамВФоне", "ВызовСерверногоМетода");
	ДобавитьКоманду(ТаблицаКоманд, "ОбновитьПродажиПоМагазинамВФоне", "ОбновитьПродажиПоМагазинамВФоне", "ВызовСерверногоМетода");
	ДобавитьКоманду(ТаблицаКоманд, "ОбновитьТоварыКОтъездуВФоне", "ОбновитьТоварыКОтъездуВФоне", "ВызовСерверногоМетода");
	ДобавитьКоманду(ТаблицаКоманд, "ОбновитьТоварыВРезервахВФоне", "ОбновитьТоварыВРезервахВФоне", "ВызовСерверногоМетода");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

#КонецОбласти
