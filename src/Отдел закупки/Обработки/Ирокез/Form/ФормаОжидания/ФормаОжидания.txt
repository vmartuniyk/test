
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторЗадания = Параметры.ИдентифкаторЗадания;
	СопровождающийТекст  = Параметры.СопровождающийТекст;
	ИдентфикаторКоманды  = Параметры.ИдентификаторКоманды;
	
	Если ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ИнтервалПроверки = 1;
	ФоновоеЗаданиеПроверитьПриЗакрытии = Истина;
	ПодключитьОбработчикОжидания("ПроверитьВыполнение", ИнтервалПроверки, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если ФоновоеЗаданиеПроверитьПриЗакрытии Тогда
		ФоновоеЗаданиеПроверитьПриЗакрытии = Ложь;
		ПроверитьВыполнениеНаСервере(ИдентификаторЗадания, Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПроверитьВыполнениеНаСервере(ФоновоеЗаданиеИдентификатор, ПрерватьЕслиНеВыполнено)
	Если ДлительныеОперации.ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор) Тогда
		Возврат Истина;
	ИначеЕсли ПрерватьЕслиНеВыполнено Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ФоновоеЗаданиеИдентификатор);
	КонецЕсли;
	Возврат Ложь;
КонецФункции

&НаКлиенте
Процедура ПроверитьВыполнение()
	
	РезультатПроверки = ПроверитьВыполнениеНаСервере(ИдентификаторЗадания, Ложь);
	
	Если РезультатПроверки = Истина Тогда
		ФоновоеЗаданиеПроверитьПриЗакрытии = Ложь;
		Закрыть();
		ОповеститьОВыборе(ИдентфикаторКоманды);
	КонецЕсли;
	
	Если ИнтервалПроверки < 15 Тогда
		ИнтервалПроверки = ИнтервалПроверки + 0.7;
	КонецЕсли;
	ПодключитьОбработчикОжидания("ПроверитьВыполнение", ИнтервалПроверки, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗадание(Команда)
	ПроверитьВыполнениеНаСервере(ИдентификаторЗадания, Истина);
	ФоновоеЗаданиеПроверитьПриЗакрытии = Ложь;
	Закрыть();
КонецПроцедуры

#КонецОбласти