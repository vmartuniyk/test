
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗагрузитьСписокСоответствийНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Ответ = Вопрос(НСтр("ru='Настройки были изменены, сохранить изменения?'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), РежимДиалогаВопрос.ДаНетОтмена);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЗаписатьСписокСоответствийНаСервере();
			Модифицированность = Ложь;
		ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
			Модифицированность = Ложь;
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаСоответствий

&НаКлиенте
Процедура ТаблицаСоответствийПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПолеСортировкиПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Сортировка_НаправлениеПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)
	Модифицированность = Ложь;
	ЗаписатьСписокСоответствийНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьСписокСоответствийНаСервере()

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос			= Новый Запрос;
	Запрос.Текст    = ТекстЗапросаЗагрузкаСпискаСоответствий();
	
	ТаблицаСоответствий.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры // ЗагрузитьСписокСоответствийНаСервере()

&НаСервере
Функция ТекстЗапросаЗагрузкаСпискаСоответствий()

	ТекстЗапроса = "ВЫБРАТЬ
	               |	ПродактМенеджер КАК ПродактМенеджер,
	               |	ВидНоменклатуры КАК ВидНоменклатуры
	               |ИЗ
	               |	РегистрСведений.КТС_СоответствиеВидНоменклатурыПродактМенеджер";
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаЗагрузкаСпискаСоответствий()



&НаСервере
Процедура ЗаписатьСписокСоответствийНаСервере()

	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.КТС_СоответствиеВидНоменклатурыПродактМенеджер.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(ТаблицаСоответствий.Выгрузить());
	
	НаборЗаписей.Записать();
	
КонецПроцедуры // ЗаписатьСписокСоответствийНаСервере()

#КонецОбласти