
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= "
		|ВЫБРАТЬ
		|	""Свободный остаток""				КАК Документ,
	    |	Номенклатура						КАК Номенклатура,
	    |	ВНаличииОстаток - ВРезервеОстаток 	КАК Количество
        |ИЗ
        |	РегистрНакопления.СвободныеОстатки.Остатки(, Номенклатура = &Номенклатура И Склад = &Склад)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументПоступления 	КАК Документ,
		|	Номенклатура			КАК Номенклатура,
        |	КПоступлениюОстаток 	КАК Количество
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.Остатки(, Номенклатура = &Номенклатура И Склад = &Склад)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегистрЗаказы.ЗаказНаПеремещение			КАК Документ,
		|	РегистрЗаказы.Номенклатура 					КАК Номенклатура,
		|	СУММА(ЗаказыТовары.Количество)				КАК Количество
		|ИЗ
		|	РегистрНакопления.ЗаказыНаПеремещение.Остатки(, Номенклатура = &Номенклатура И ЗаказНаПеремещение.СкладПолучатель = &Склад) КАК РегистрЗаказы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПеремещение.Товары КАК ЗаказыТовары
		|		ПО РегистрЗаказы.ЗаказНаПеремещение = ЗаказыТовары.Ссылка
		|			И РегистрЗаказы.Номенклатура 	= ЗаказыТовары.Номенклатура
		|			И РегистрЗаказы.Характеристика	= ЗаказыТовары.Характеристика
		|			И РегистрЗаказы.КодСтроки 		= ЗаказыТовары.КодСтроки
		|ГДЕ
		|	ЗаказыТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ПустаяСсылка)
		|СГРУППИРОВАТЬ ПО
		|	РегистрЗаказы.ЗаказНаПеремещение,
		|	РегистрЗаказы.Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Параметры.Номенклатура);
	Запрос.УстановитьПараметр("Склад", Параметры.Склад);
	
	ОтчетПоОстаткам 		= Запрос.Выполнить().Выгрузить();
	СхемаКомпоновкиДанных 	= РеквизитФормыВЗначение("Объект").ПолучитьМакет("ОтчетПоОстаткам");
	ДанныеРасшифровки	  	= Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	ВнешниеНаборыДанных = Новый Структура;
    ВнешниеНаборыДанных.Вставить("ОтчетПоОстаткам", ОтчетПоОстаткам); 

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, СхемаКомпоновкиДанных.НастройкиПоУмолчанию, ДанныеРасшифровки);

	Если МакетКомпоновки.НаборыДанных.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru= 'Отчет не сформирован. Включите хотя бы одну группировку в ""Элементы оформления и группировки"".'") ;
	КонецЕсли;
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры
