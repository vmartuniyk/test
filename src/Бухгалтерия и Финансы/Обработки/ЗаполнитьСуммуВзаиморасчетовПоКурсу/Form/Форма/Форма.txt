&НаКлиенте
Процедура ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначения) Экспорт
	
	мОбъектыНазначения = ОбъектыНазначения;
	
	Если ТипЗнч(ОбъектыНазначения) <> Тип("Массив") Тогда
		
		мОбъектыНазначения = Новый Массив;
		мОбъектыНазначения.Добавить(ОбъектыНазначения);
		
	КонецЕсли; 
	
	ПоказатьВводЗначения(Новый ОписаниеОповещения("ПриВыбореВалюты", ЭтаФорма, мОбъектыНазначения),, "Выберите валюту взаиморасчетов", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	
КонецПроцедуры	

&НаКлиенте
Процедура ПриВыбореВалюты(ВыбраннаяВалюта, мОбъектыНазначения) Экспорт
	
	Если ВыбраннаяВалюта = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли; 
	
	ФормаДокумента = ЭтаФорма.ВладелецФормы;
	обДокумент = ФормаДокумента.Объект;
	
	ВалютаДокумента = обДокумент.Валюта;
	сКурсДокумента = ПолучитьКурсВалюты(ВалютаДокумента, обДокумент.Дата);
	сКурсВзаиморасчетов = ПолучитьКурсВалюты(ВыбраннаяВалюта, обДокумент.Дата);
	
	Для каждого СтрокаТ Из обДокумент.РасшифровкаПлатежа Цикл
		
		СтрокаТ.ВалютаВзаиморасчетов = ВыбраннаяВалюта;
		СтрокаТ.СуммаВзаиморасчетов = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(СтрокаТ.Сумма, ВалютаДокумента, 
			ВыбраннаяВалюта, сКурсДокумента.Курс, сКурсВзаиморасчетов.Курс, сКурсДокумента.Кратность, сКурсВзаиморасчетов.Кратность);
		
		Если ЗначениеЗаполнено(СтрокаТ.Заказ) Тогда
			
			ИмяРеквизитаВалюты = ?(ТипЗнч(СтрокаТ.Заказ) = Тип("СправочникСсылка.ДоговорыКонтрагентов"), "ВалютаВзаиморасчетов", "Валюта");
			ВалютаЗаказа = ОбщегоНазначения.ПолучитьЗначениеРеквизита(СтрокаТ.Заказ, ИмяРеквизитаВалюты);
			
			Если ВалютаЗаказа <> ВыбраннаяВалюта Тогда
				
				СтрокаТ.Заказ = Неопределено;
					
			КонецЕсли; 
				
		КонецЕсли; 
		
	КонецЦикла;
	
	ФормаДокумента.Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКурсВалюты(Валюта, Дата)
	
	Возврат РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата)
	
КонецФункции
 