
#Область ИнтерфейсАвтоматичекихТестов
	
Перем ЮнитТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮнитТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;
	
	ВсеТесты.Добавить("Тест_ПолучитьАдресДоставкиПоУмолчания");
	ВсеТесты.Добавить("Тест_ПолучитьСписокПеревозчиков");
	ВсеТесты.Добавить("Тест_ПолучитьАдресПартнера");
	ВсеТесты.Добавить("Тест_ПолучитьАдресСклада");
	
	Возврат ВсеТесты;
	
КонецФункции

Процедура Тест_ПолучитьАдресДоставкиПоУмолчания() Экспорт
	 Партнер = Справочники.Партнеры.ПолучитьСсылку(Новый УникальныйИдентификатор("ce6972d7-4747-11e0-9f98-001517115d85")); // Тищук Дима 
	 АдресДоставки = ПолучитьАдресДоставкиПоУмолчания(Партнер);
	 ЮнитТест.ПроверитьТип(АдресДоставки, "Строка", );
КонецПроцедуры

Процедура Тест_ПолучитьСписокПеревозчиков() Экспорт

	МассивПеревозчиков = ПолучитьСписокПеревозчиков();
	
	ЮнитТест.ПроверитьТип(МассивПеревозчиков, "Массив", );
	ЮнитТест.ПроверитьНеРавенство(МассивПеревозчиков.Количество(), 0, );

КонецПроцедуры

Процедура Тест_ПолучитьАдресПартнера() Экспорт
	Партнер 	  = Справочники.Партнеры.ПолучитьСсылку(Новый УникальныйИдентификатор("ce6972d7-4747-11e0-9f98-001517115d85")); // Тищук Дима
	АдресДоставки = ПолучитьАдресДоставкиПоУмолчания(Партнер);
	ЮнитТест.ПроверитьТип(АдресДоставки, "Строка", );
КонецПроцедуры

Процедура Тест_ПолучитьАдресСклада() Экспорт
	Склад 		= Справочники.Склады.ПолучитьСсылку(Новый УникальныйИдентификатор("d10d3409-40c2-11e0-9f98-001517115d85")); // Склад Соборна
	АдресСклада = ПолучитьАдресСклада(Склад);
	ЮнитТест.ПроверитьТип(АдресСклада, "Строка", );
	ЮнитТест.ПроверитьНеРавенство(ПустаяСтрока(АдресСклада), Истина, НСтр("ru='Установите адрес дл склада СОборная'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
КонецПроцедуры

#КонецОбласти  


#Область ПрограммныйИнтерфейс

// Подготавливает управляемую форму документа к работе с модулем доставки 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма документа
//  ВнешнийОбъект 	- Неопределено, ОбработкаОбъект - Внешний объект для переопределения событий формы
//  ИмяОбработки 	- Строка           				- Имя внешней обработки серийных номеров для подключения методов на клиенте
//
Процедура ПодготовитьМодульДоставки(ЭтаФорма, ВнешнийОбъект, ИмяОбработки = "") Экспорт

	Если ВнешнийОбъект = "ИзменениеВыделенныхЗаявок" Тогда
		ПодготовитьОбработкуИзмененияДанных(ЭтаФорма, "МодульДоставки");
		Возврат;
	КонецЕсли;
	
	СсылкаНаОбъект = ЭтаФорма.Объект.Ссылка;
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ПодготовитьРеализацияТоваровУслуг(ЭтаФорма, ВнешнийОбъект, ИмяОбработки);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		ПодготовитьЗаказКлиента(ЭтаФорма, ВнешнийОбъект, ИмяОбработки);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		ПодготовитьВозвратТоваровПоставщику(ЭтаФорма, ВнешнийОбъект, ИмяОбработки);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ВнутреннееПотреблениеТоваров") Тогда
		ПодготовитьВнутреннееПотреблениеТоваров(ЭтаФорма, ВнешнийОбъект, ИмяОбработки);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ГНАТ_ЗаявкаСервисногоЦентра") Тогда
		ПодготовитьЗаявкуСервисногоЦентра(ЭтаФорма, ВнешнийОбъект, ИмяОбработки);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		ПодготовитьЗаказПоставщику(ЭтаФорма, ВнешнийОбъект, ИмяОбработки);
	КонецЕсли;

КонецПроцедуры

// Возвратщает адрес доставки партнера из карточки партнера
//
// Параметры:
//  Партнер - СправочникСсылка.Партнеры - партнер адрес которого нужно получить.
//
// Возвращаемое значение:
//  Строка - адрес партнера (куда нужно севершить доставку).
//
Функция ПолучитьАдресДоставкиПоУмолчания(Партнер) Экспорт
		
	Запрос 			= Новый Запрос;
	Запрос.Текст    = ТекстЗапросаСписокАдресовПартнера();
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	
	МассивВыбора = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Адрес");
	
	Если МассивВыбора.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	СкладСсылка = Справочники.Склады.НайтиПоНаименованию(МассивВыбора[0]);
	Если Не ЗначениеЗаполнено(СкладСсылка) Тогда
		Возврат МассивВыбора[0];
	Иначе
		Возврат СкладСсылка;
	КонецЕсли;
	
КонецФункции

// Возвращает массив перевозчииков
//
// Возвращаемое значение:
//  Массив - содежит следующие элементы:
//   * СправочникСсылка.Партнеры - перевозчик.
//
Функция ПолучитьСписокПеревозчиков() Экспорт

	НастройкаГлобальная = ХранилищеНастроекДанныхФорм.Загрузить("ДополнительныеНастройкиОбработокМодификацииКонфигурации", , , "ДополнительныеНастройкиОбработокМодификацииКонфигурации");
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= ТекстЗапросаСписокПеревозчиков();
	
	Запрос.УстановитьПараметр("Сегмент", НастройкаГлобальная.ДоставкаСегментПеревозчиков);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Партнер")

КонецФункции // ПолучитьСписокПеревозчиков()

// Возвращает адрес партнера из карточки.
//
// Параметры:
//  Партнер - СправочникСсылка.Партнеры - партнер адрес которого нужно получить.
//
// Возвращаемое значение:
//  Строка - строковое представление адреса партнера.
//
Функция ПолучитьАдресПартнера(Партнер) Экспорт
	
	Запрос 		 = Новый Запрос;
	Запрос.Текст = ТекстЗапросаАдресПартнера();	
	Запрос.УстановитьПараметр("Тип", 	Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("Ссылка", Партнер);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка.Представление, "");
	
КонецФункции // ПолучитьАдресПартнера(Партнер)

// Возвращает адрес склада из карочки.
//
// Параметры:
//  Склад - СправочникСсылка.Склады - склад адрес которого нужно получить.
//
// Возвращаемое значение: 
//  Строка - адрес склада.
//
Функция ПолучитьАдресСклада(Склад) Экспорт
	
	Запрос 			= Новый Запрос;
	Запрос.Текст    = ТекстЗапросаАдресСклада();
						  
	Запрос.УстановитьПараметр("Ссылка", Склад);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат ?(Выборка.Следующий(), Выборка.АдресСклада, "");
	
КонецФункции

// Получает город по адресу доставки 
//
// Параметры:
//  АдресДоставки - Строка, СправочникСсылка.Склады - Адрес доставки
//
// Возвращаемое значение:
//    Строка - Город из адреса доставки
//
Функция ПолучитьГородПоАдресу(АдресДоставки, ПеренаправлениеОпределено = Ложь) Экспорт
	
	Если ПеренаправлениеОпределено Тогда
	
		Если ТипЗнч(АдресДоставки) = Тип("СправочникСсылка.Склады") Тогда
			Возврат АдресДоставки;
		КонецЕсли;
		
		МассивКлассификаторов = Новый Массив;
		Буффер = СтрЗаменить(АдресДоставки, "Адрес:", "");
		
		Пока СтрДлина(Буффер) > 0 Цикл
			
			НомерРазделителя = Найти(Буффер, ",");
			Если НомерРазделителя > 0 Тогда
				МассивКлассификаторов.Добавить(Сред(Буффер, 1, НомерРазделителя - 1));
				Буффер = Сред(Буффер, НомерРазделителя + 1);
			Иначе
				МассивКлассификаторов.Добавить(Буффер);
				Буффер = "";
			КонецЕсли;
			
		КонецЦикла;
		
		
		
		Если МассивКлассификаторов.Количество() > 0 Тогда
			Если Найти(МассивКлассификаторов[0], "обл.") > 0 Тогда
				
				Область = СтрЗаменить(СтрЗаменить(МассивКлассификаторов[0], "обл.", ""), " ", "");
				
			КонецЕсли;
		КонецЕсли;
		
		ЗагружатьГород = Истина;
		
		Если МассивКлассификаторов.Количество() > 1 Тогда	
			Если Найти(МассивКлассификаторов[1], "район") > 0 Тогда
				
				Район = СтрЗаменить(СтрЗаменить(МассивКлассификаторов[1], "район", ""), " ", "");;
				
			ИначеЕсли Найти(МассивКлассификаторов[1], "м.") > 0	Тогда
				
				Город = СтрЗаменить(СтрЗаменить(МассивКлассификаторов[1], "м.", ""), " ", "");
				ЗагружатьГород = Ложь;
				
			КонецЕсли;
		КонецЕсли;
		
		Если МассивКлассификаторов.Количество() > 2 Тогда
			Если ЗагружатьГород И Найти(МассивКлассификаторов[2], "м.") > 0 Тогда
				
				Город = СокрЛП(СтрЗаменить(МассивКлассификаторов[2], "м.", ""));
				
			КонецЕсли;
		КонецЕсли;
		
		
		Возврат ?(ПустаяСтрока(Область),"", Область + " обл.") 
				+ ?(ПустаяСтрока(Район),"", ", " + Район + " район") 
				+ ?(ПустаяСтрока(Город),"", ", м. " + Город);
				
	Иначе
		
		Если git_ПереопределениеОбработчиковСервер.НужноПеренаправлениеВызоваВОбщийМодуль("МодульДоставки", "git_МодульДоставкиСервер") Тогда	
			Возврат git_МодульДоставкиСервер.ПолучитьГородПоАдресу(АдресДоставки, Истина);	
		Иначе
			ВнешнийОбъект = git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектПоИмени("МодульДоставки");
			Возврат ВнешнийОбъект.ПолучитьГородПоАдресу(АдресДоставки, Истина);	
		КонецЕсли;
				
	КонецЕсли;
	
КонецФункции // ПолучитьГородПоАдресу() 

// Возвращает курс валюты на дату документа
// 
// Параметры:
//  Дата - Дата - дата на которую нужно получить курс валют.
//  Валюта - СправочникСсылка.Валюта - валюта для получения курса
//
// Возвращаемое значение:
// 	Число, Неопределено - значение курса валюты, неопределено если не удалось получить курс.
//
Функция ПолучитьКурсНаДату(Дата, Валюта) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаКурсНаДату();
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	Запрос.УстановитьПараметр("Валюта", Валюта);     	
	Выборка = Запрос.Выполнить().Выбрать();	
	Возврат ?(Выборка.Следующий(), Выборка.Курс, Неопределено);
КонецФункции

// Возвращает параметры доставки для объекта
//
// Параметры:
//  Объект - ДокументСсылка - документ для которого получаем параметры доставки.
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица значений с колонками:
//	 * КТС_ПД_АдресДоставки - Строка, СправочникСсылка.Склад - адрес доставки места.
//   * КТС_ПД_ОтделениеНовойПочты - Строка - отделение новой почты.
//	 * КТС_ПД_Перевозчик - СправочникСсылка.Партнеры - перевозчик.
//   * КТС_ПД_ПунктНазначения - Строка, СправочникСсылка.Склады - пункт назначения.
//   * КТС_ПД_КонтактноеЛицо - Строка, СправочникСсылка.Партнеры - контакное лицо.
//   * КТС_ПД_ОбъявленнаяСтоимость - Число - объявленная стоимость.
//   * КТС_ПД_ПлатитПолучатель - Булево - платит получатель.
//   * КТС_ПД_ВозвратнаяДоставка - Булево - возвратная доставка.
//
Функция ПолучитьПараметрыДоставки(Объект) Экспорт

	Запрос 		 = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПараметрыДоставки();
	
	Запрос.УстановитьПараметр("Объект", Объект);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ПолучитьПараметрыДоставкиНаПериод()

// Устанавливает следующие этап работ в документах ГНАТ_ЗаявкаСервисногоЦентра из табличной части задачи.
//
// Параметры:
//  Задача - СправочникСсылка.МестаДоставки - задача статусы заявок из которой нужно изменить.
// 
Процедура УстановитьСтатусыЗаявокСЦ(Задача) Экспорт

	НачатьТранзакцию();
	
	Для каждого СтрокаИзТаблицы Из Задача.Товары Цикл
		
		Попытка
			ДокументОбъект = СтрокаИзТаблицы.ЗаявкаСЦ.ПолучитьОбъект();
		Исключение
			ТемаПисьма = НСтр("ru='ВНИМАНИЕ. Не удалось изменить статус заявки сервисного центра после отгрузки водителем.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ТелоПисьма = НСтр("ru='Не удалось получить документ заявки в задаче %1. Номер строки %2.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ТелоПисьма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТелоПисьма, ПолучитьНавигационнуюСсылку(Задача), СтрокаИзТаблицы.НомерСтроки);
			ОповеститьОтделТехническойПоддержки(ТемаПисьма, ТелоПисьма);
			ОтменитьТранзакцию();
			Возврат;
		КонецПопытки;
		
		ХодРабот = ДокументОбъект.ХодРабот;
		
		Если ХодРабот.Количество() = 0 Тогда
			ТемаПисьма = НСтр("ru='ВНИМАНИЕ. Не удалось изменить статус заявки сервисного центра после отгрузки водителем.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ТелоПисьма = НСтр("ru='В документе заявки %1 по задаче %2 не заполнена таблица ход работ.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ТелоПисьма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТелоПисьма, ПолучитьНавигационнуюСсылку(СтрокаИзТаблицы.ЗаявкаСЦ), ПолучитьНавигационнуюСсылку(Задача));
			ОповеститьОтделТехническойПоддержки(ТемаПисьма, ТелоПисьма);
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		ПоследнаяСтрока = ХодРабот[ХодРабот.Количество()-1];
		ЭтапРаботы 		= ПоследнаяСтрока.ЭтапРаботы;
		СледующиеЭтапы  = ЭтапРаботы.СледующиеЭтапы;
		
		Если СледующиеЭтапы.Количество() = 0 Тогда
			ТемаПисьма = НСтр("ru='ВНИМАНИЕ. Не удалось изменить статус заявки сервисного центра после отгрузки водителем.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ТелоПисьма = НСтр("ru='В этапе %1 в документе %2 не заполнена таблица следующих этапов.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ТелоПисьма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТелоПисьма, ПолучитьНавигационнуюСсылку(ЭтапРаботы), ПолучитьНавигационнуюСсылку(СтрокаИзТаблицы.ЗаявкаСЦ));
			ОповеститьОтделТехническойПоддержки(ТемаПисьма, ТелоПисьма);
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		НоваяСтрока 								= ХодРабот.Добавить();
		НоваяСтрока.МесторасположениеФактическое 	= ПоследнаяСтрока.МесторасположениеФактическое;
		НоваяСтрока.Ответственный 					= ПараметрыСеанса.ТекущийПользователь;
		НоваяСтрока.ПартнерАСЦ 						= ПоследнаяСтрока.ПартнерАСЦ;
		НоваяСтрока.Период 							= ТекущаяДатаСеанса();
		НоваяСтрока.ЭтапРаботы 						= СледующиеЭтапы[0].Этап;
		
		Попытка
		    ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);					
		Исключение
		    ТемаПисьма = НСтр("ru='ВНИМАНИЕ. Не удалось изменить статус заявки сервисного центра после отгрузки водителем.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ТелоПисьма = НСтр("ru='В задаче %1 не удалось провести документ %2.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ТелоПисьма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТелоПисьма, ПолучитьНавигационнуюСсылку(Задача), ПолучитьНавигационнуюСсылку(СтрокаИзТаблицы.ЗаявкаСЦ));
			ОповеститьОтделТехническойПоддержки(ТемаПисьма, ТелоПисьма);
			ОтменитьТранзакцию();
			Возврат;
		КонецПопытки;
	
	КонецЦикла; 
	
	ЗафиксироватьТранзакцию();

КонецПроцедуры
 
 



// Выполняет движение по регистру КТС_АналитикаДоставки
Процедура ВыполнитьПроведениеПоДоставке(Источник, Отказ, РежимПроведения) Экспорт
	
	ВыполнятьДвижениеБезОрдерныеСклады = Ложь;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
		ВыполнятьДвижениеБезОрдерныеСклады = Истина;
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ПеремещениеТоваров") Тогда
		ВыполнятьДвижениеБезОрдерныеСклады = Истина;
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
		ВыполнятьДвижениеБезОрдерныеСклады = Истина;
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ВнутреннееПотреблениеТоваров") Тогда
		ВыполнятьДвижениеБезОрдерныеСклады = Истина;
	КонецЕсли;
		
	ПодготовитьНаборыЗаписей(Источник, ВыполнятьДвижениеБезОрдерныеСклады);
    ОтразитьДвижения(Источник, Источник.Движения, Отказ, ВыполнятьДвижениеБезОрдерныеСклады);
	ЗаписатьНаборыЗаписей(Источник);
	
КонецПроцедуры // ВыполнитьПроведениеПоДоставке()

// Удаляет движения из регистра КТС_АналитикаДоставки
Процедура ВыполнитьУдалениеПроведенияПоДоставке(Источник, Отказ) Экспорт
	
	Если ТипЗнч(Источник) <> Тип("ДокументОбъект.РеализацияТоваровУслуг") 
			И ТипЗнч(Источник) <> Тип("ДокументОбъект.ПеремещениеТоваров")
			И ТипЗнч(Источник) <> Тип("ДокументОбъект.ВозвратТоваровПоставщику")
			И ТипЗнч(Источник) <> Тип("ДокументОбъект.ВнутреннееПотреблениеТоваров")
			И ТипЗнч(Источник) <> Тип("ДокументОбъект.ГНАТ_ЗаявкаСервисногоЦентра") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Источник) <> Тип("ДокументОбъект.ГНАТ_ЗаявкаСервисногоЦентра") Тогда
	
		Движения 			   = Источник.Движения;	
		ДополнительныеСвойства = Движения.КТС_ОтгрузкаИзБезордерныхСкладов.ДополнительныеСвойства;
		
		ДополнительныеСвойства.Вставить("ЭтоНовый", Источник.ДополнительныеСвойства.ЭтоНовый);
		ДополнительныеСвойства.Вставить("ДляПроведения", Новый Структура);
		ДополнительныеСвойства.ДляПроведения.Вставить("СтруктураВременныеТаблицы", Новый Структура("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц));
		Движения.КТС_ОтгрузкаИзБезордерныхСкладов.ДополнительныеСвойства.Вставить("РассчитыватьИзмененияОтгрузки", Истина);
		
		Движения.КТС_ОтгрузкаИзБезордерныхСкладов.Записать();
		
		ВыполнитьКонтрольРезультатовМаршруты(Источник, ДополнительныеСвойства ,Отказ);
		
		ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
		
	Иначе	
		
		НаборЗаписей = РегистрыСведений.КТС_АналитикаДоставки.СоздатьНаборЗаписей();	
		НаборЗаписей.Отбор.Объект.Установить(Источник.Ссылка);
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ВыполнитьСозданиеЗадачиПоЗаказуПоставщику(Источник, Отказ, РежимПроведения, ПеренаправлениеОпределено = Ложь) Экспорт
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ПеренаправлениеОпределено Тогда
	
		Если Источник.КТС_Маршрут.Пустая() И НЕ  ПустаяСтрока(Источник.КТС_НомерТТН) И Источник.КТС_СпособДоставки = Перечисления.КТС_СпособДоставки.НаСкладПеревозчика  Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Небходимо указать маршрут, для Новой почты - «Ровно»",, "Объект.КТС_Маршрут");
			Возврат;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(Источник.КТС_НомерТТН)И Источник.КТС_СпособДоставки = Перечисления.КТС_СпособДоставки.НаСкладПеревозчика И  Источник.Статус = Перечисления.СтатусыЗаказовПоставщикам.КПоступлению И НЕ  ПустаяСтрока(Источник.КТС_АдресСкладаПеревозчика) Тогда

			ТДата 			= ТекущаяДата();
			АдресДоставки 	= ПолучитьАдресАбонента(Источник.Склад);
			Коментарии		= ПолучитьКомментарийКЗадачеЗаказаПоставщику(Источник);		
				
			НовоеМесто = Справочники.МестаДоставки.СоздатьЭлемент();
			
			НовоеМесто.Владелец		 					= Источник.Склад;
			НовоеМесто.АдресДоставки					= АдресДоставки;
			НовоеМесто.ЗаказПоставщика					= Источник.Ссылка;
			НовоеМесто.Ответственный 					= ПараметрыСеанса.ТекущийПользователь;
			НовоеМесто.ТекстЗадачи						= Коментарии;
			НовоеМесто.Транзит		 					= Ложь;
			НовоеМесто.ТранзитныйСклад					= Справочники.Склады.ПустаяСсылка();
			НовоеМесто.ЗаполнятьУникальныйИдентификатор = Истина;
			НовоеМесто.ЭтоЗадача	 					= Истина;	
			НовоеМесто.УстановитьНовыйКод();
			
			Попытка
				НовоеМесто.Записать();			
			Исключение
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
				Возврат;
			КонецПопытки;
				
			
			НаборЗаписей = РегистрыСведений.ЗадачиНаМаршрут.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(ТДата);
			НаборЗаписей.Отбор.Место.Установить(НовоеМесто.Ссылка);
			
			Запись 				  	= НаборЗаписей.Добавить();
			Запись.Период		  	= ТДата;
			Запись.Отправитель  	= Источник.КТС_Перевозчик;
			Запись.Место		  	= НовоеМесто.Ссылка;
			Запись.СтатусОтгрузки 	= Перечисления.СтатусыОтгрузкиМест.ЗадачаПоставлена;
			Запись.Пользователь	 	= ПараметрыСеанса.ТекущийПользователь;
			Запись.АдресОтправителя	= Источник.КТС_АдресСкладаПеревозчика;
			Запись.ВариантМаршрута	= Источник.КТС_Маршрут;
			
			Попытка
				НаборЗаписей.Записать();			
			Исключение
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
				Возврат;
			КонецПопытки;
			
			Справочники.МестаДоставки.ЗаписатьКТС_ГеографическиеРасположенияПартнеров(Источник.Склад, АдресДоставки, Отказ);	
			Справочники.МестаДоставки.ЗаписатьКТС_ГеографическиеРасположенияПартнеров(Источник.КТС_Перевозчик, Источник.КТС_АдресСкладаПеревозчика, Отказ);
		
		КонецЕсли;
		
	Иначе 
		
		Если git_ПереопределениеОбработчиковСервер.НужноПеренаправлениеВызоваВОбщийМодуль("МодульДоставки", "git_МодульДоставкиСервер") Тогда	
			git_МодульДоставкиСервер.ВыполнитьСозданиеЗадачиПоЗаказуПоставщику(Источник, Отказ, РежимПроведения, Истина);	
		Иначе
			ВнешнийОбъект = git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектПоИмени("МодульДоставки");
			ВнешнийОбъект.ВыполнитьСозданиеЗадачиПоЗаказуПоставщику(Источник, Отказ, РежимПроведения, Истина);	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Контактная информация

// Процедура добавляет возможность выбора адреса партнера по классификатору в 
// карточке пратнера
//
// Параметры:
//	мФорма - Управляема форма - форма карточки партнера
//
Процедура ПерерисоватьФорму(мФорма, ПеренаправлениеОпределено = Ложь) Экспорт
	
	Если ПеренаправлениеОпределено Тогда
	
		УстановитьПривилегированныйРежим(Истина);
		
		мЭлементы = мФорма.Элементы;
		Объект    = мФорма.Объект;
				
		//Адрес = мЭлементы.Найти("__КИ_Поле1");
		Адрес = мЭлементы.Найти("КонтактнаяИнформацияПоле0");
		Если Адрес <> Неопределено Тогда
			Адрес.РедактированиеТекста = Ложь;
			Адрес.КнопкаОчистки = Истина;
			Адрес.УстановитьДействие("НачалоВыбора", "ВызовКлиентскихПроцедура");
		КонецЕсли;
		
		//ДополнительныйАдрес = мЭлементы.Найти("__КИ_Поле2");
		ДополнительныйАдрес = мЭлементы.Найти("КонтактнаяИнформацияПоле3");
		Если ДополнительныйАдрес <> Неопределено Тогда
			ДополнительныйАдрес.РедактированиеТекста = Ложь;
			ДополнительныйАдрес.КнопкаОчистки = Истина;
			ДополнительныйАдрес.УстановитьДействие("НачалоВыбора", "ВызовКлиентскихПроцедура");
		КонецЕсли;
		
	Иначе 
		
		Если git_ПереопределениеОбработчиковСервер.НужноПеренаправлениеВызоваВОбщийМодуль("МодульДоставки", "git_МодульДоставкиСервер") Тогда	
			git_МодульДоставкиСервер.ПерерисоватьФорму(мФорма, Истина);	
		Иначе
			ВнешнийОбъект = git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектПоИмени("МодульДоставки");
			ВнешнийОбъект.ПерерисоватьФорму(мФорма, Истина);	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

	
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Подготавливает форму документа реализации товаров услуг к работе с модулем доставки. 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма документа реализации товаров и услуг.
//  ВнешнийОбъект 	- Неопределено, ОбработкаОбъект - Внешний объект для переопределения событий формы.
//  ИмяОбработки 	- Строка           				- Имя внешней обработки серийных номеров для подключения методов на клиенте.
//
Процедура ПодготовитьРеализацияТоваровУслуг(ЭтаФорма, ВнешнийОбъект, ИмяОбработки)
	
	ВидимостьЭлементов = Новый Структура("КТС_ПД_ТаблицаЭтапов");
	
	ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, ЭтаФорма, "ПриИзменении", "Партнер", "КТС_ПД_ПартнерПриИзменении");
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить(Новый РеквизитФормы("МодульДоставкиИмяБиблиотеки", Новый ОписаниеТипов("Строка")));
	ЭтаФорма.ИзменитьРеквизиты(Реквизиты);
	ЭтаФорма.МодульДоставкиИмяБиблиотеки = ИмяОбработки;
	
	СоздатьЭлементыФормы(ЭтаФорма, ЭтаФорма.Объект.Ссылка, ЭтаФорма.Элементы.ГруппаСчетФактура, ВидимостьЭлементов, ,Истина);
	
КонецПроцедуры

// Подготавливает форму документа заказ клиента к работе с модулем доставки. 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма документа реализации товаров и услуг.
//  ВнешнийОбъект 	- Неопределено, ОбработкаОбъект - Внешний объект для переопределения событий формы.
//  ИмяОбработки 	- Строка           				- Имя внешней обработки серийных номеров для подключения методов на клиенте.
//
Процедура ПодготовитьЗаказКлиента(ЭтаФорма, ВнешнийОбъект, ИмяОбработки)

	ВидимостьЭлементов = Новый Структура("КТС_ПД_ТаблицаЭтапов");
	
	ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, ЭтаФорма, "ПриИзменении", "Партнер", "КТС_ПД_ПартнерПриИзменении");
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить(Новый РеквизитФормы("МодульДоставкиИмяБиблиотеки", Новый ОписаниеТипов("Строка")));
	ЭтаФорма.ИзменитьРеквизиты(Реквизиты);
	ЭтаФорма.МодульДоставкиИмяБиблиотеки = ИмяОбработки;
	
	СоздатьЭлементыФормы(ЭтаФорма, ЭтаФорма.Объект.Ссылка, ЭтаФорма.Элементы.ГруппаПодвал, ВидимостьЭлементов, ,Истина);

КонецПроцедуры

// Подготавливает форму документа возврат товаров поставщику к работе с модулем доставки. 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма документа реализации товаров и услуг.
//  ВнешнийОбъект 	- Неопределено, ОбработкаОбъект - Внешний объект для переопределения событий формы.
//  ИмяОбработки 	- Строка           				- Имя внешней обработки серийных номеров для подключения методов на клиенте.
//
Процедура ПодготовитьВозвратТоваровПоставщику(ЭтаФорма, ВнешнийОбъект, ИмяОбработки)

	ВидимостьЭлементов = Новый Структура("КТС_ПД_ВозвратнаяДоставка, КТС_ПД_ТаблицаЭтапов");
	
	ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, ЭтаФорма, "ПриИзменении", "Партнер", "КТС_ПД_ПартнерПриИзменении");
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить(Новый РеквизитФормы("МодульДоставкиИмяБиблиотеки", Новый ОписаниеТипов("Строка")));
	ЭтаФорма.ИзменитьРеквизиты(Реквизиты);
	ЭтаФорма.МодульДоставкиИмяБиблиотеки = ИмяОбработки;
	
	СоздатьЭлементыФормы(ЭтаФорма, ЭтаФорма.Объект.Ссылка, ЭтаФорма.Элементы.ГруппаПодвал, ВидимостьЭлементов, ,Истина);

КонецПроцедуры

// Подготавливает форму документа заявка сервисного центра к работе с модулем доставки. 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма документа реализации товаров и услуг.
//  ВнешнийОбъект 	- Неопределено, ОбработкаОбъект - Внешний объект для переопределения событий формы.
//  ИмяОбработки 	- Строка           				- Имя внешней обработки серийных номеров для подключения методов на клиенте.
//
Процедура ПодготовитьЗаявкуСервисногоЦентра(ЭтаФорма, ВнешнийОбъект, ИмяОбработки)

	Объект = ЭтаФорма.Объект;
	
	ВидимостьЭлементов = Новый Структура("КТС_ПД_ВозвратнаяДоставка");
	
	СоздатьДополнительныеРеквизитыФормы(ЭтаФорма, ИмяОбработки, ВидимостьЭлементов);
	
	ЭтаФорма.КТС_ПД_ТаблицаЭтапов.Загрузить(ПолучитьПараметрыДоставки(Объект.Ссылка));
	
	СоздатьЭлементыФормы(ЭтаФорма, ЭтаФорма.Объект.Ссылка, ЭтаФорма.Элементы.ГруппаСтраницы, ВидимостьЭлементов, Истина, Истина);
	
	ЭтаФорма.Элементы.Переместить(ЭтаФорма.Элементы.ГруппаСтраницы, ЭтаФорма, ЭтаФорма.Элементы.КТС_ПараметрыДоставки);

КонецПроцедуры

// Подготавливает форму документа внутреннее потребление товаров к работе с модулем доставки. 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма документа реализации товаров и услуг.
//  ВнешнийОбъект 	- Неопределено, ОбработкаОбъект - Внешний объект для переопределения событий формы.
//  ИмяОбработки 	- Строка           				- Имя внешней обработки серийных номеров для подключения методов на клиенте.
//
Процедура ПодготовитьВнутреннееПотреблениеТоваров(ЭтаФорма, ВнешнийОбъект, ИмяОбработки)

	ВидимостьЭлементов = Новый Структура;
	ВидимостьЭлементов.Вставить("КТС_ПД_ОтделениеНовойПочты");
	ВидимостьЭлементов.Вставить("КТС_ПД_Перевозчик");
	ВидимостьЭлементов.Вставить("КТС_ПД_ПунктНазначения");
	ВидимостьЭлементов.Вставить("КТС_ПД_КонтактноеЛицо");
	ВидимостьЭлементов.Вставить("КТС_ПД_ОбъявленнаяСтоимость");
	ВидимостьЭлементов.Вставить("КТС_ПД_ПлатитПолучатель");
	ВидимостьЭлементов.Вставить("КТС_ПД_ВозвратнаяДоставка");
	ВидимостьЭлементов.Вставить("КТС_ПД_ТаблицаЭтапов");
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить(Новый РеквизитФормы("МодульДоставкиИмяБиблиотеки", Новый ОписаниеТипов("Строка")));
	ЭтаФорма.ИзменитьРеквизиты(Реквизиты);
	ЭтаФорма.МодульДоставкиИмяБиблиотеки = ИмяОбработки;
	
	СоздатьЭлементыФормы(ЭтаФорма, ЭтаФорма.Объект.Ссылка, ЭтаФорма.Элементы.Ответственный, ВидимостьЭлементов, ,Истина);

КонецПроцедуры

// Подготавливает форму документа заказ поставщику к работе с маршрутами 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма документа реализации товаров и услуг
//  ВнешнийОбъект 	- Неопределено, ОбработкаОбъект - Внешний объект для переопределения событий формы
//  ИмяОбработки 	- Строка           				- Имя внешней обработки серийных номеров для подключения методов на клиенте
//
Процедура ПодготовитьЗаказПоставщику(ЭтаФорма, ВнешнийОбъект, ИмяОбработки)
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить(Новый РеквизитФормы("МодульДоставкиИмяБиблиотеки", Новый ОписаниеТипов("Строка")));
	
	ЭтаФорма.ИзменитьРеквизиты(Реквизиты);
	ЭтаФорма.МодульДоставкиИмяБиблиотеки = ИмяОбработки;
	
	ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, ЭтаФорма, "ПередЗаписью", , "ПередЗаписьюСпособДоставки");
	
	Элементы = ЭтаФорма.Элементы;
	Команды  = ЭтаФорма.Команды;
	Объект	 = ЭтаФорма.Объект;
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить(Новый РеквизитФормы("ВремяИДатаВыезда", Новый ОписаниеТипов("Строка")));
	Реквизиты.Добавить(Новый РеквизитФормы("МаршрутыИмяБиблиотеки", Новый ОписаниеТипов("Строка")));
	ЭтаФорма.ИзменитьРеквизиты(Реквизиты);
	ЭтаФорма.МаршрутыИмяБиблиотеки = ИмяОбработки;
	
	
	// ГРУППЫ
	ПараметрыГруппы = Новый Структура;
	ПараметрыГруппы.Вставить("Вид", 						ВидГруппыФормы.ОбычнаяГруппа);
	ПараметрыГруппы.Вставить("Заголовок", 					"КТС доставляет");
	ПараметрыГруппы.Вставить("Группировка", 				ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная);
	ПараметрыГруппы.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	ПараметрыГруппы.Вставить("РастягиватьПоГоризонтали", 	Истина);
	ПараметрыГруппы.Вставить("Отображение", 			ОтображениеОбычнойГруппы.РамкаГруппы);
	ГруппаДоставка = Элементы.Добавить("ГруппаДоставка", 	Тип("ГруппаФормы"));
	ЗаполнитьЗначенияСвойств(ГруппаДоставка, ПараметрыГруппы);
	
	Элементы.Переместить(ГруппаДоставка, ЭтаФорма, Элементы.ГруппаПодвал);	
	
	ПараметрыГруппыЛево = Новый Структура;
	ПараметрыГруппыЛево.Вставить("Вид", 						ВидГруппыФормы.ОбычнаяГруппа);
	ПараметрыГруппыЛево.Вставить("Группировка", 				ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	ПараметрыГруппыЛево.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	ПараметрыГруппыЛево.Вставить("РастягиватьПоГоризонтали", 	Истина);
	ПараметрыГруппыЛево.Вставить("ОтображатьЗаголовок", 		Ложь);
	ПараметрыГруппыЛево.Вставить("Отображение", 				ОтображениеОбычнойГруппы.Нет);
	ГруппаЛево = Элементы.Добавить("КТС_ГруппаЛево", Тип("ГруппаФормы"), ГруппаДоставка);
	ЗаполнитьЗначенияСвойств(ГруппаЛево, ПараметрыГруппыЛево);
	
	ПараметрыГруппыЦентр = Новый Структура;
	ПараметрыГруппыЦентр.Вставить("Вид", 						ВидГруппыФормы.ОбычнаяГруппа);
	ПараметрыГруппыЦентр.Вставить("Группировка", 				ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	ПараметрыГруппыЦентр.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	ПараметрыГруппыЦентр.Вставить("РастягиватьПоГоризонтали", 	Истина);
	ПараметрыГруппыЦентр.Вставить("ОтображатьЗаголовок", 		Ложь);
	ПараметрыГруппыЦентр.Вставить("Отображение", 				ОтображениеОбычнойГруппы.Нет);
	ГруппаЦентр = Элементы.Добавить("КТС_ГруппаЦентр", Тип("ГруппаФормы"), ГруппаДоставка);
	ЗаполнитьЗначенияСвойств(ГруппаЦентр, ПараметрыГруппыЦентр);
	
	ПараметрыГруппыПраво = Новый Структура;
	ПараметрыГруппыПраво.Вставить("Вид", 						ВидГруппыФормы.ОбычнаяГруппа);
	ПараметрыГруппыПраво.Вставить("Группировка", 				ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	ПараметрыГруппыПраво.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	ПараметрыГруппыПраво.Вставить("РастягиватьПоГоризонтали", 	Истина);
	ПараметрыГруппыПраво.Вставить("ОтображатьЗаголовок", 		Ложь);
	ПараметрыГруппыПраво.Вставить("Отображение", 				ОтображениеОбычнойГруппы.Нет);
	ГруппаПраво = Элементы.Добавить("КТС_ГруппаПраво", Тип("ГруппаФормы"), ГруппаДоставка);
	ЗаполнитьЗначенияСвойств(ГруппаПраво, ПараметрыГруппыПраво);
	

	
	// Элементы группы ЛЕВО
	
	ПараметрыСпособДоставки = Новый Структура;
	ПараметрыСпособДоставки.Вставить("Вид", 				ВидПоляФормы.ПолеВвода);
	ПараметрыСпособДоставки.Вставить("ПутьКДанным", 		"Объект.КТС_СпособДоставки");
	ПараметрыСпособДоставки.Вставить("КнопкаОчистки", 		Истина); 
	СпособДоставки = Элементы.Добавить("КТС_СпособДоставки", Тип("ПолеФормы"), ГруппаЛево);
    ЗаполнитьЗначенияСвойств(СпособДоставки, ПараметрыСпособДоставки);
	СпособДоставки.УстановитьДействие("ПриИзменении", 			"СпособДоставки_ПриИзменении");
	
	ПараметрыПеревозчик = Новый Структура;
	ПараметрыПеревозчик.Вставить("Вид", 						ВидПоляФормы.ПолеВвода);
	ПараметрыПеревозчик.Вставить("ПутьКДанным", 				"Объект.КТС_Перевозчик");
	ПараметрыПеревозчик.Вставить("ЦветФона", 					Новый Цвет(135, 206, 250));
	ПараметрыПеревозчик.Вставить("КнопкаВыбора", 				Ложь);
	Если Объект.КТС_СпособДоставки = Перечисления.КТС_СпособДоставки.НаСкладПеревозчика Тогда
		ПараметрыПеревозчик.Вставить("Доступность", Истина);
	Иначе
		ПараметрыПеревозчик.Вставить("Доступность", Ложь);
	КонецЕсли;
	ПараметрыПеревозчик.Вставить("КнопкаСпискаВыбора", 			Ложь);
	ПараметрыПеревозчик.Вставить("КнопкаОчистки", 				Истина);
	ПараметрыПеревозчик.Вставить("РедактированиеТекста", 		Ложь);
	ПараметрыПеревозчик.Вставить("КнопкаВыбора", 				Истина);
	Перевозчик = Элементы.Добавить("КТС_Перевозчик", Тип("ПолеФормы"), ГруппаЛево);
	ЗаполнитьЗначенияСвойств(Перевозчик, ПараметрыПеревозчик);
	Перевозчик.УстановитьДействие("ПриИзменении", 			"Перевозчик_ПриИзменении");
	Перевозчик.УстановитьДействие("НачалоВыбора", 			"ПД_Перевозчик_НачалоВыбора");

	
	
	// Элементы группы ЦЕНТР
	
	ПараметрыНомерТТН = Новый Структура;
	ПараметрыНомерТТН.Вставить("Вид", 						ВидПоляФормы.ПолеВвода);
	ПараметрыНомерТТН.Вставить("ПутьКДанным", 				"Объект.КТС_НомерТТН");
	ПараметрыНомерТТН.Вставить("КнопкаОчистки", 				Истина);
	НомерТТН = Элементы.Добавить("КТС_НомерТТН", Тип("ПолеФормы"), ГруппаЦентр);
	ЗаполнитьЗначенияСвойств(НомерТТН, ПараметрыНомерТТН);
	
	ПараметрыАдресСкладаПеревозчика = Новый Структура;
	ПараметрыАдресСкладаПеревозчика.Вставить("Вид", 					ВидПоляФормы.ПолеВвода);
	ПараметрыАдресСкладаПеревозчика.Вставить("ПутьКДанным", 			"Объект.КТС_АдресСкладаПеревозчика");
	ПараметрыАдресСкладаПеревозчика.Вставить("ЦветФона", 				Новый Цвет(135, 206, 250));
	Если Объект.КТС_СпособДоставки = Перечисления.КТС_СпособДоставки.НаСкладПеревозчика Тогда
		ПараметрыАдресСкладаПеревозчика.Вставить("Доступность", Истина);
	Иначе
		ПараметрыАдресСкладаПеревозчика.Вставить("Доступность", Ложь);
	КонецЕсли;
	ПараметрыАдресСкладаПеревозчика.Вставить("КнопкаВыбора", 			Истина);
	ПараметрыАдресСкладаПеревозчика.Вставить("КнопкаОчистки", 			Истина);
	ПараметрыАдресСкладаПеревозчика.Вставить("РедактированиеТекста", 	Ложь);
	АдресСкладаПеревозчика = Элементы.Добавить("КТС_АдресСкладаПеревозчика", Тип("ПолеФормы"), ГруппаЦентр);
	ЗаполнитьЗначенияСвойств(АдресСкладаПеревозчика, ПараметрыАдресСкладаПеревозчика);
	АдресСкладаПеревозчика.УстановитьДействие("ПриИзменении", "АдресДоставки_ПриИзменении");
	АдресСкладаПеревозчика.УстановитьДействие("НачалоВыбора", "АдресДоставки_НачалоВыбора");
	
	
	
	
	// Элементы группы ПРАВО
	
	ПараметрыНадписи = Новый Структура;
	ПараметрыНадписи.Вставить("Вид", 						ВидПоляФормы.ПолеНадписи);
	ПараметрыНадписи.Вставить("ПутьКДанным", 				"ВремяИДатаВыезда");
	ПараметрыНадписи.Вставить("ПоложениеЗаголовка", 		ПоложениеЗаголовкаЭлементаФормы.Нет);
	ПараметрыНадписи.Вставить("РастягиватьПоГоризонтали", 	Ложь);
	ПараметрыНадписи.Вставить("Ширина", 					30);
	Надпись = Элементы.Добавить("ВремяИДатаВыезда", Тип("ПолеФормы"), ГруппаПраво);
	ЗаполнитьЗначенияСвойств(Надпись, ПараметрыНадписи);

	ПараметрыМаршрут = Новый Структура;
	ПараметрыМаршрут.Вставить("Вид", 						 ВидПоляФормы.ПолеВвода);
	ПараметрыМаршрут.Вставить("ПутьКДанным", 				 "Объект.КТС_Маршрут");
	ПараметрыМаршрут.Вставить("ЦветФона", 					 Новый Цвет(210, 255, 210));
	ПараметрыМаршрут.Вставить("КнопкаВыбора", Истина); 
	Если Объект.КТС_СпособДоставки = Перечисления.КТС_СпособДоставки.НаСкладПеревозчика Тогда
		ПараметрыМаршрут.Вставить("Доступность", Истина);
	Иначе
		ПараметрыМаршрут.Вставить("Доступность", Ложь);
	КонецЕсли;
	ПараметрыМаршрут.Вставить("КнопкаСпискаВыбора", 		 Ложь);
	ПараметрыМаршрут.Вставить("КнопкаОчистки", 				 Истина);
	ПараметрыМаршрут.Вставить("КнопкаОткрытия", 			 Ложь);
	ПараметрыМаршрут.Вставить("РедактированиеТекста", 		 Ложь);
	Маршрут = Элементы.Добавить("Маршрут", Тип("ПолеФормы"), ГруппаПраво);
	ЗаполнитьЗначенияСвойств(Маршрут, ПараметрыМаршрут);
	Маршрут.УстановитьДействие("НачалоВыбора", 			"Маршруты_НачалоВыбора");
	
КонецПроцедуры // ПодготовитьЗаказПоставщику()

// Подготавливает форму обработки изменения реквизитов заявок сервисного центра. 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма документа реализации товаров и услуг..
//
Процедура ПодготовитьОбработкуИзмененияДанных(ЭтаФорма, ИмяОбработки)
	
	ВидимостьЭлементов = Новый Структура("КТС_ПД_ВозвратнаяДоставка");
	
	СоздатьДополнительныеРеквизитыФормы(ЭтаФорма, ИмяОбработки, ВидимостьЭлементов);
	
	СоздатьЭлементыФормы(ЭтаФорма, "ИзменениеВыделенныхЗаявок", ЭтаФорма.Элементы.ГруппаХодРабот, ВидимостьЭлементов, Истина, Истина);
	
	ЭтаФорма.Элементы.Переместить(ЭтаФорма.Элементы.ГруппаХодРабот, ЭтаФорма, ЭтаФорма.Элементы.КТС_ПараметрыДоставки);

КонецПроцедуры
 




// Универсальная процедура создания дополнительных реквизитов на форме документа. 
//
// Параметры:
//  ЭтаФорма 			- УправляемаяФорма 				- Форма документа реализации товаров и услуг.
//  ИмяОбработки 		- Строка           				- Имя внешней обработки серийных номеров для подключения методов на клиенте.
// 
Процедура СоздатьДополнительныеРеквизитыФормы(ЭтаФорма, ИмяОбработки, ВидимостьЭлементов)

	Реквизиты = Новый Массив;
	Реквизиты.Добавить(Новый РеквизитФормы("МодульДоставкиИмяБиблиотеки", Новый ОписаниеТипов("Строка")));
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_АдресДоставки") Тогда
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_АдресДоставки", Новый ОписаниеТипов("Строка, СправочникСсылка.Склады")));
	КонецЕсли;
		
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_ОтделениеНовойПочты") Тогда
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_ОтделениеНовойПочты", Новый ОписаниеТипов("Строка")));
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_Перевозчик") Тогда
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_Перевозчик", Новый ОписаниеТипов("СправочникСсылка.Партнеры")));
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_ПунктНазначения") Тогда
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_ПунктНазначения", Новый ОписаниеТипов("Строка, СправочникСсылка.Склады")));
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_КонтактноеЛицо") Тогда
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_КонтактноеЛицо", Новый ОписаниеТипов("Строка, СправочникСсылка.Партнеры, СправочникСсылка.КонтактныеЛицаПартнеров")));
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_ОбъявленнаяСтоимость") Тогда
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_ОбъявленнаяСтоимость", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный))));
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_ПлатитПолучатель") Тогда
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_ПлатитПолучатель", Новый ОписаниеТипов("Булево")));
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_ВозвратнаяДоставка") Тогда
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_ВозвратнаяДоставка", Новый ОписаниеТипов("Булево")));
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_ТаблицаЭтапов") Тогда
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_ТаблицаЭтапов", Новый ОписаниеТипов("ТаблицаЗначений")));
		Реквизиты.Добавить(Новый РеквизитФормы("Получатель", Новый ОписаниеТипов("СправочникСсылка.Партнеры, СправочникСсылка.Склады"), "КТС_ПД_ТаблицаЭтапов"));
		Реквизиты.Добавить(Новый РеквизитФормы("Период", Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)), "КТС_ПД_ТаблицаЭтапов"));
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_АдресДоставки", Новый ОписаниеТипов("Строка, СправочникСсылка.Склады"), "КТС_ПД_ТаблицаЭтапов"));
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_ОтделениеНовойПочты", Новый ОписаниеТипов("Строка"), "КТС_ПД_ТаблицаЭтапов"));
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_Перевозчик", Новый ОписаниеТипов("СправочникСсылка.Партнеры"), "КТС_ПД_ТаблицаЭтапов"));
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_ПунктНазначения", Новый ОписаниеТипов("Строка, СправочникСсылка.Склады"), "КТС_ПД_ТаблицаЭтапов"));
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_КонтактноеЛицо", Новый ОписаниеТипов("Строка, СправочникСсылка.Партнеры, СправочникСсылка.КонтактныеЛицаПартнеров"), "КТС_ПД_ТаблицаЭтапов"));
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_ОбъявленнаяСтоимость", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)), "КТС_ПД_ТаблицаЭтапов"));
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_ПлатитПолучатель", Новый ОписаниеТипов("Булево"), "КТС_ПД_ТаблицаЭтапов"));
		Реквизиты.Добавить(Новый РеквизитФормы("КТС_ПД_ВозвратнаяДоставка", Новый ОписаниеТипов("Булево"), "КТС_ПД_ТаблицаЭтапов"));
	КонецЕсли;
	
	ЭтаФорма.ИзменитьРеквизиты(Реквизиты);
	ЭтаФорма.МодульДоставкиИмяБиблиотеки = ИмяОбработки;

КонецПроцедуры

// Процедура создает элементы формы для работы из модулем доставки.
//
// Параметры:
//  ЭтаФорма 						- УправляемаяФорма 	- Форма документа реализации товаров и услуг.
//  ГруппаРодитель					- ГуппаФормы		- Гуппа после которой будут рисовться элементы модуля доставки.
//  ВидимостьЭлементов  			- Структура			- Сожержит имена элементов которые не будуть отображены на форме.
//  ИспользоватьРеквизитыФормы 		- Булево            - Если Истина тогда в элементы подключаются реквизиты формы.
//  УстановитьОтображениеЭлементов  - Булево			- Устанавливает по умолчанию видимость элементов формы.
//
Процедура СоздатьЭлементыФормы(ЭтаФорма, Ссылка, ГруппаРодитель, ВидимостьЭлементов, ИспользоватьРеквизитыФормы=Ложь, УстановитьОтображениеЭлементов=Ложь)

	Элементы = ЭтаФорма.Элементы;
	Команды  = ЭтаФорма.Команды;
	Объект   = ЭтаФорма.Объект;
	
	// ГРУППЫ ФОРМЫ
	
	ПараметрыГруппы = Новый Структура;
	ПараметрыГруппы.Вставить("Имя", 						"КТС_ПараметрыДоставки");
	ПараметрыГруппы.Вставить("ТипЭлемента", 				Тип("ГруппаФормы"));
	ПараметрыГруппы.Вставить("Вид", 						ВидГруппыФормы.ОбычнаяГруппа);
	ПараметрыГруппы.Вставить("Заголовок", 					"Параметры доставки");
	ПараметрыГруппы.Вставить("Группировка", 				ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная);
	ПараметрыГруппы.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	ПараметрыГруппы.Вставить("РастягиватьПоГоризонтали", 	Истина);
	ПараметрыГруппы.Вставить("Отображение", 				ОтображениеОбычнойГруппы.СильноеВыделение);
	ПараметрыГруппы.Вставить("Доступность", 				УстановитьОтображениеЭлементов);
	ГруппаДоставка = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыГруппы,);
	Элементы.Переместить(ГруппаДоставка, ЭтаФорма, ГруппаРодитель);
	
	ПараметрыГруппыЛево = Новый Структура;
	ПараметрыГруппыЛево.Вставить("Имя", 						"КТС_ПД_ГруппаЛево");
	ПараметрыГруппыЛево.Вставить("ТипЭлемента", 				Тип("ГруппаФормы"));
	ПараметрыГруппыЛево.Вставить("Вид", 						ВидГруппыФормы.ОбычнаяГруппа);
	ПараметрыГруппыЛево.Вставить("Группировка", 				ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	ПараметрыГруппыЛево.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	ПараметрыГруппыЛево.Вставить("РастягиватьПоГоризонтали", 	Истина);
	ПараметрыГруппыЛево.Вставить("ОтображатьЗаголовок", 		Ложь);
	ПараметрыГруппыЛево.Вставить("Отображение", 				ОтображениеОбычнойГруппы.Нет);
	ГруппаЛево = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыГруппыЛево, ГруппаДоставка);
	
	ПараметрыГруппыЦентр = Новый Структура;
	ПараметрыГруппыЦентр.Вставить("Имя", 						"КТС_ПД_ГруппаЦентр");
	ПараметрыГруппыЦентр.Вставить("ТипЭлемента", 				Тип("ГруппаФормы"));
	ПараметрыГруппыЦентр.Вставить("Вид", 						ВидГруппыФормы.ОбычнаяГруппа);
	ПараметрыГруппыЦентр.Вставить("Группировка", 				ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	ПараметрыГруппыЦентр.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	ПараметрыГруппыЦентр.Вставить("РастягиватьПоГоризонтали", 	Истина);
	ПараметрыГруппыЦентр.Вставить("ОтображатьЗаголовок", 		Ложь);
	ПараметрыГруппыЦентр.Вставить("Отображение", 				ОтображениеОбычнойГруппы.Нет);
	ГруппаЦентр = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыГруппыЦентр, ГруппаДоставка);
	
	ПараметрыГруппыПраво = Новый Структура;
	ПараметрыГруппыПраво.Вставить("Имя", 						"КТС_ПД_ГруппаПраво");
	ПараметрыГруппыПраво.Вставить("ТипЭлемента", 				Тип("ГруппаФормы"));
	ПараметрыГруппыПраво.Вставить("Вид", 						ВидГруппыФормы.ОбычнаяГруппа);
	ПараметрыГруппыПраво.Вставить("Группировка", 				ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	ПараметрыГруппыПраво.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	ПараметрыГруппыПраво.Вставить("РастягиватьПоГоризонтали", 	Истина);
	ПараметрыГруппыПраво.Вставить("ОтображатьЗаголовок", 		Ложь);
	ПараметрыГруппыПраво.Вставить("Отображение", 				ОтображениеОбычнойГруппы.Нет);
	ГруппаПраво = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыГруппыПраво, ГруппаДоставка);
	
	ПараметрыГруппыАдресДоставки = Новый Структура;
	ПараметрыГруппыАдресДоставки.Вставить("Имя", 						"КТС_ПД_ГруппаАдресДоставки");
	ПараметрыГруппыАдресДоставки.Вставить("ТипЭлемента", 				Тип("ГруппаФормы"));
	ПараметрыГруппыАдресДоставки.Вставить("Вид", 						ВидГруппыФормы.ОбычнаяГруппа);
	ПараметрыГруппыАдресДоставки.Вставить("Группировка", 				ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная);
	ПараметрыГруппыАдресДоставки.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	ПараметрыГруппыАдресДоставки.Вставить("РастягиватьПоГоризонтали", 	Истина);
	ПараметрыГруппыАдресДоставки.Вставить("ОтображатьЗаголовок", 		Ложь);
	ПараметрыГруппыАдресДоставки.Вставить("Отображение", 				ОтображениеОбычнойГруппы.Нет);
	ГруппаАдресДоставки = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыГруппыАдресДоставки, ГруппаЦентр);
	
	
	// ЭЛЕМЕНТЫ ФОРМЫ
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_Перевозчик") Тогда
	
		ПараметрыПеревозчик = Новый Структура;
		ПараметрыПеревозчик.Вставить("Имя", 						"КТС_ПД_Перевозчик");
		ПараметрыПеревозчик.Вставить("Заголовок", 					"Перевозчик");
		ПараметрыПеревозчик.Вставить("ТипЭлемента", 				Тип("ПолеФормы"));
		ПараметрыПеревозчик.Вставить("Вид", 						ВидПоляФормы.ПолеВвода);
		ПараметрыПеревозчик.Вставить("ПутьКДанным", 				?(ИспользоватьРеквизитыФормы, "КТС_ПД_Перевозчик", "Объект.КТС_Перевозчик"));
		ПараметрыПеревозчик.Вставить("РастягиватьПоГоризонтали", 	Ложь);
		ПараметрыПеревозчик.Вставить("Ширина", 						30);
		ПараметрыПеревозчик.Вставить("ЦветФона", 					Новый Цвет(135, 206, 250));
		ПараметрыПеревозчик.Вставить("КнопкаВыбора", 				Ложь);
		ПараметрыПеревозчик.Вставить("КнопкаСпискаВыбора", 			Истина);
		ПараметрыПеревозчик.Вставить("КнопкаОчистки", 				Истина);
		ПараметрыПеревозчик.Вставить("РедактированиеТекста", 		Ложь);
		Перевозчик = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыПеревозчик, ГруппаЛево);
		Перевозчик.УстановитьДействие("ПриИзменении", 				"ПД_Перевозчик_ПриИзменении");
		Перевозчик.УстановитьДействие("НачалоВыбора", 				"ПД_Перевозчик_НачалоВыбора");
		
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_АдресДоставки") Тогда
	
		ПараметрыАдресДоставки = Новый Структура;
		ПараметрыАдресДоставки.Вставить("Имя", 						"КТС_ПД_АдресДоставки");
		ПараметрыАдресДоставки.Вставить("Заголовок", 				"Адрес доставки");
		ПараметрыАдресДоставки.Вставить("ТипЭлемента", 				Тип("ПолеФормы"));
		ПараметрыАдресДоставки.Вставить("Вид", 						ВидПоляФормы.ПолеВвода);
		ПараметрыАдресДоставки.Вставить("ПутьКДанным", 				?(ИспользоватьРеквизитыФормы, "КТС_ПД_АдресДоставки", "Объект.КТС_АдресДоставки"));
		ПараметрыАдресДоставки.Вставить("РастягиватьПоГоризонтали", Ложь);
		ПараметрыАдресДоставки.Вставить("Ширина", 					30);
		ПараметрыАдресДоставки.Вставить("ЦветФона", 				Новый Цвет(135, 206, 250));
		ПараметрыАдресДоставки.Вставить("КнопкаВыбора", 			Истина);
		ПараметрыАдресДоставки.Вставить("КнопкаОчистки", 			Истина);
		ПараметрыАдресДоставки.Вставить("РедактированиеТекста", 	Ложь);
		АдресДоставки = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыАдресДоставки, ГруппаАдресДоставки);
		АдресДоставки.УстановитьДействие("ПриИзменении", "ПД_АдресДоставки_ПриИзменении");
		АдресДоставки.УстановитьДействие("НачалоВыбора", "ПД_АдресДоставки_НачалоВыбора");
		
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("ПД_КнопкаАдресПартнера") Тогда
		
		КомандаАдресПартнера = Команды.Добавить("ПД_КнопкаАдресПартнера");
	    КомандаАдресПартнера.Действие = "ПД_ЗаполнитьАдресПартнераПоУмолчанию";
		КомандаАдресПартнера.Заголовок = "Заполнить";
		
		ПараметрыКнопкаАдресПартнера = Новый Структура;
		ПараметрыКнопкаАдресПартнера.Вставить("Имя", 			"ПД_КнопкаАдресПартнера");
		ПараметрыКнопкаАдресПартнера.Вставить("ТипЭлемента", 	Тип("КнопкаФормы"));
		ПараметрыКнопкаАдресПартнера.Вставить("ИмяКоманды", 	"ПД_КнопкаАдресПартнера");
		КнопкаАдресПартнера = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыКнопкаАдресПартнера, ГруппаАдресДоставки);
		
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_ОтделениеНовойПочты") Тогда
		
		ПараметрыОтделениеНовойПочты = Новый Структура;
		ПараметрыОтделениеНовойПочты.Вставить("Имя", 						"КТС_ПД_ОтделениеНовойПочты");
		ПараметрыОтделениеНовойПочты.Вставить("Заголовок", 					"Отделение нововй почты");
		ПараметрыОтделениеНовойПочты.Вставить("ТипЭлемента", 				Тип("ПолеФормы"));
		ПараметрыОтделениеНовойПочты.Вставить("Вид", 						ВидПоляФормы.ПолеВвода);
		ПараметрыОтделениеНовойПочты.Вставить("ПутьКДанным", 				?(ИспользоватьРеквизитыФормы, "КТС_ПД_ОтделениеНовойПочты", "Объект.КТС_ОтделениеНовойПочты"));
		ПараметрыОтделениеНовойПочты.Вставить("РастягиватьПоГоризонтали", 	Ложь);
		ПараметрыОтделениеНовойПочты.Вставить("Ширина", 					30);
		ПараметрыОтделениеНовойПочты.Вставить("ЦветФона", 					Новый Цвет(135, 206, 250));
		ПараметрыОтделениеНовойПочты.Вставить("КнопкаВыбора", 		 		Истина);
		Если ИспользоватьРеквизитыФормы Тогда
			Если ЭтаФорма.КТС_ПД_Перевозчик.Пустая() Тогда
				ПараметрыОтделениеНовойПочты.Вставить("ТолькоПросмотр", 		Истина);
			КонецЕсли;
		Иначе
			Если Объект.КТС_Перевозчик.Пустая() Тогда
				ПараметрыОтделениеНовойПочты.Вставить("ТолькоПросмотр", 		Истина);
			КонецЕсли;
		КонецЕсли;
	    ПараметрыОтделениеНовойПочты.Вставить("КнопкаОчистки", 				Истина);
		ПараметрыОтделениеНовойПочты.Вставить("РедактированиеТекста", 		Ложь);
		ОтделениеНовойПочты = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыОтделениеНовойПочты, ГруппаПраво);
		ОтделениеНовойПочты.УстановитьДействие("НачалоВыбора", "ПД_ОтделениеНовойПочты_НачалоВыбора");
		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ГНАТ_ЗаявкаСервисногоЦентра") Тогда
			ОтделениеНовойПочты.УстановитьДействие("ПриИзменении", "ПД_ОтделениеНовойПочтыПриИзменении");
		КонецЕсли;
		Если Ссылка = "ИзменениеВыделенныхЗаявок" Тогда
			ОтделениеНовойПочты.УстановитьДействие("ПриИзменении", "ПД_ОтделениеНовойПочтыПриИзменении");
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_КонтактноеЛицо") Тогда
	
		ПараметрыКонтактноеЛицо = Новый Структура;
		ПараметрыКонтактноеЛицо.Вставить("Имя", 						"КТС_ПД_КонтактноеЛицо");
		ПараметрыКонтактноеЛицо.Вставить("Вид", 						ВидПоляФормы.ПолеВвода);
		ПараметрыКонтактноеЛицо.Вставить("Заголовок", 					"Контактное лицо");
		ПараметрыКонтактноеЛицо.Вставить("ТипЭлемента", 				Тип("ПолеФормы"));
		ПараметрыКонтактноеЛицо.Вставить("ПутьКДанным", 				?(ИспользоватьРеквизитыФормы, "КТС_ПД_КонтактноеЛицо", "Объект.КТС_КонтактноеЛицо"));
		ПараметрыКонтактноеЛицо.Вставить("РастягиватьПоГоризонтали", 	Ложь);
		ПараметрыКонтактноеЛицо.Вставить("Ширина", 						30);
		ПараметрыКонтактноеЛицо.Вставить("ЦветФона", 					Новый Цвет(217, 211, 185));
		Если ИспользоватьРеквизитыФормы Тогда
			Если ЭтаФорма.КТС_ПД_Перевозчик.Пустая() Тогда
				ПараметрыКонтактноеЛицо.Вставить("ТолькоПросмотр", 		Истина);
			КонецЕсли;
		Иначе
			Если Объект.КТС_Перевозчик.Пустая() Тогда
				ПараметрыКонтактноеЛицо.Вставить("ТолькоПросмотр", 		Истина);
			КонецЕсли;
		КонецЕсли;
		ПараметрыКонтактноеЛицо.Вставить("КнопкаОчистки", 				Истина);
		ПараметрыКонтактноеЛицо.Вставить("РедактированиеТекста", 		Истина);
		КонтактноеЛицо = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыКонтактноеЛицо, ГруппаЛево);
		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ГНАТ_ЗаявкаСервисногоЦентра") Тогда
			КонтактноеЛицо.УстановитьДействие("ПриИзменении", "ПД_КонтактноеЛицоПриИзменении");
			КонтактноеЛицо.УстановитьДействие("ОбработкаВыбора", "ПД_КонтактноеЛицоОбработкаВыбора");
		КонецЕсли;
		Если Ссылка = "ИзменениеВыделенныхЗаявок" Тогда
			КонтактноеЛицо.УстановитьДействие("ПриИзменении", "ПД_КонтактноеЛицоПриИзменении");
			КонтактноеЛицо.УстановитьДействие("ОбработкаВыбора", "ПД_КонтактноеЛицоОбработкаВыбора");
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_ОбъявленнаяСтоимость") Тогда
		
		ПараметрыОбъявленнаяСтоимость = Новый Структура;
		ПараметрыОбъявленнаяСтоимость.Вставить("Имя", 						"КТС_ПД_ОбъявленнаяСтоимость");
		ПараметрыОбъявленнаяСтоимость.Вставить("Заголовок",					"Объявленная стоимость");
		ПараметрыОбъявленнаяСтоимость.Вставить("ТипЭлемента", 				Тип("ПолеФормы"));
		ПараметрыОбъявленнаяСтоимость.Вставить("Вид", 						ВидПоляФормы.ПолеВвода);
		ПараметрыОбъявленнаяСтоимость.Вставить("ПутьКДанным", 				?(ИспользоватьРеквизитыФормы, "КТС_ПД_ОбъявленнаяСтоимость", "Объект.КТС_ОбъявленнаяСтоимость"));
		ПараметрыОбъявленнаяСтоимость.Вставить("РастягиватьПоГоризонтали", 	Ложь);
		ПараметрыОбъявленнаяСтоимость.Вставить("Ширина", 					30);
		ПараметрыОбъявленнаяСтоимость.Вставить("ЦветФона", 					Новый Цвет(217, 211, 185));
		Если ИспользоватьРеквизитыФормы Тогда
			Если ЭтаФорма.КТС_ПД_Перевозчик.Пустая() Тогда
				ПараметрыОбъявленнаяСтоимость.Вставить("ТолькоПросмотр", 		Истина);
			КонецЕсли;
		Иначе
			Если Объект.КТС_Перевозчик.Пустая() Тогда
				ПараметрыОбъявленнаяСтоимость.Вставить("ТолькоПросмотр", 		Истина);
			КонецЕсли;
		КонецЕсли;
		ПараметрыОбъявленнаяСтоимость.Вставить("КнопкаОчистки", 			Истина);
		ПараметрыОбъявленнаяСтоимость.Вставить("РедактированиеТекста", 		Истина);
		ОбъявленнаяСтоимость = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыОбъявленнаяСтоимость, ГруппаЦентр);
		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ГНАТ_ЗаявкаСервисногоЦентра") Тогда
			ОбъявленнаяСтоимость.УстановитьДействие("ПриИзменении", "ПД_ОбъявленнаяСтоимостьПриИзменении");
		КонецЕсли;
		Если Ссылка = "ИзменениеВыделенныхЗаявок" Тогда
			ОбъявленнаяСтоимость.УстановитьДействие("ПриИзменении", "ПД_ОбъявленнаяСтоимостьПриИзменении");
		КонецЕсли;
		
	КонецЕсли;
	
	
	// Дополнительные сведения
	
	ПараметрыГруппыДополнительно = Новый Структура;
	ПараметрыГруппыДополнительно.Вставить("Имя", 						"КТС_ПД_ГруппаДополнительно");
	ПараметрыГруппыДополнительно.Вставить("Вид", 						ВидГруппыФормы.ОбычнаяГруппа);
	ПараметрыГруппыДополнительно.Вставить("ТипЭлемента", 				Тип("ГруппаФормы"));
	ПараметрыГруппыДополнительно.Вставить("Группировка", 				ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная);
	ПараметрыГруппыДополнительно.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	ПараметрыГруппыДополнительно.Вставить("РастягиватьПоГоризонтали", 	Истина);
	ПараметрыГруппыДополнительно.Вставить("ОтображатьЗаголовок", 		Ложь);
	ПараметрыГруппыДополнительно.Вставить("Отображение", 				ОтображениеОбычнойГруппы.Нет);  
	ГруппаДополнительно = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыГруппыДополнительно, ГруппаПраво);
	
	
	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_ПлатитПолучатель") Тогда

		ПараметрыПлатитПолучатель = Новый Структура;
		ПараметрыПлатитПолучатель.Вставить("Имя", 						"КТС_ПД_ПлатитПолучатель");
		ПараметрыПлатитПолучатель.Вставить("Заголовок",					"Платит получатель");
		ПараметрыПлатитПолучатель.Вставить("Вид", 						ВидПоляФормы.ПолеФлажка);
		ПараметрыПлатитПолучатель.Вставить("ТипЭлемента", 				Тип("ПолеФормы"));
		ПараметрыПлатитПолучатель.Вставить("ПутьКДанным", 				?(ИспользоватьРеквизитыФормы, "КТС_ПД_ПлатитПолучатель", "Объект.КТС_ПлатитПолучатель"));
		ПараметрыПлатитПолучатель.Вставить("РастягиватьПоГоризонтали", 	Ложь);
		ПараметрыПлатитПолучатель.Вставить("Ширина", 					30);
		ПараметрыПлатитПолучатель.Вставить("ЦветФона", 					Новый Цвет(217, 211, 185));
		Если ИспользоватьРеквизитыФормы Тогда
			Если ЭтаФорма.КТС_ПД_Перевозчик.Пустая() Тогда
				ПараметрыПлатитПолучатель.Вставить("ТолькоПросмотр", 		Истина);
			КонецЕсли;
		Иначе
			Если Объект.КТС_Перевозчик.Пустая() Тогда
				ПараметрыПлатитПолучатель.Вставить("ТолькоПросмотр", 		Истина);
			КонецЕсли;
		КонецЕсли;
		ПараметрыПлатитПолучатель.Вставить("КнопкаОчистки", 			Истина);
		ПараметрыПлатитПолучатель.Вставить("РедактированиеТекста", 		Ложь);           
		ПлатитПолучатель = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыПлатитПолучатель, ГруппаДополнительно);
		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ГНАТ_ЗаявкаСервисногоЦентра") Тогда
			ПлатитПолучатель.УстановитьДействие("ПриИзменении", "ПД_ПлатитПолучательПриИзменении");
		КонецЕсли;
		Если Ссылка = "ИзменениеВыделенныхЗаявок" Тогда
			ПлатитПолучатель.УстановитьДействие("ПриИзменении", "ПД_ПлатитПолучательПриИзменении");
		КонецЕсли;
		
	КонецЕсли;

	Если НЕ ВидимостьЭлементов.Свойство("КТС_ПД_ВозвратнаяДоставка") Тогда

		ПараметрыВозвратнаяДоставка = Новый Структура;
		ПараметрыВозвратнаяДоставка.Вставить("Имя", 					"КТС_ПД_ВозвратнаяДоставка");
		ПараметрыВозвратнаяДоставка.Вставить("Заголовок",				"Возвратная доставка");
		ПараметрыВозвратнаяДоставка.Вставить("Вид", 					ВидПоляФормы.ПолеФлажка);
		ПараметрыВозвратнаяДоставка.Вставить("ТипЭлемента", 			Тип("ПолеФормы"));
		ПараметрыВозвратнаяДоставка.Вставить("ПутьКДанным", 			?(ИспользоватьРеквизитыФормы, "КТС_ПД_ВозвратнаяДоставка", "Объект.КТС_ВозвратнаяДоставка"));
		ПараметрыВозвратнаяДоставка.Вставить("РастягиватьПоГоризонтали",Ложь);
		ПараметрыВозвратнаяДоставка.Вставить("Ширина", 					30);
		ПараметрыВозвратнаяДоставка.Вставить("ЦветФона", 				Новый Цвет(217, 211, 185));
		Если ИспользоватьРеквизитыФормы Тогда
			Если ЭтаФорма.КТС_ПД_Перевозчик.Пустая() Тогда
				ПараметрыВозвратнаяДоставка.Вставить("ТолькоПросмотр", 		Истина);
			КонецЕсли;
		Иначе
			Если Объект.КТС_Перевозчик.Пустая() Тогда
				ПараметрыВозвратнаяДоставка.Вставить("ТолькоПросмотр", 		Истина);
			КонецЕсли;
		КонецЕсли;
		ПараметрыВозвратнаяДоставка.Вставить("КнопкаОчистки", 			Истина);
		ПараметрыВозвратнаяДоставка.Вставить("РедактированиеТекста", 	Ложь);
		ВозвратнаяДоставка = git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, ПараметрыВозвратнаяДоставка, ГруппаДополнительно);
		
	КонецЕсли;

КонецПроцедуры



Процедура ПодготовитьНаборыЗаписей(Источник, ВыполнятьДвижениеБезОрдерныеСклады)
	
	Движения = Источник.Движения;
	
	Если Движения.Маршруты_ДокументыПоМаршрутам.Количество() > 0 И Не ВыполнятьДвижениеБезОрдерныеСклады Тогда
		Движения.Маршруты_ДокументыПоМаршрутам.Очистить();
	КонецЕсли;
	
	Если ВыполнятьДвижениеБезОрдерныеСклады И Движения.КТС_ОтгрузкаИзБезордерныхСкладов.Количество() > 0 Тогда
		Движения.КТС_ОтгрузкаИзБезордерныхСкладов.Очистить();
	КонецЕсли;
	
	Если Не Источник.ДополнительныеСвойства.ЭтоНовый Тогда
		Движения.Маршруты_ДокументыПоМаршрутам.Записывать = Истина;
		Если ВыполнятьДвижениеБезОрдерныеСклады Тогда
			Движения.КТС_ОтгрузкаИзБезордерныхСкладов.Записывать = Истина;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОтразитьДвижения(Источник, Движения, Отказ, ВыполнятьДвижениеБезОрдерныеСклады)
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ОтразитьДвиженияДокументовПоМаршрутам(Источник, Движения, Отказ);
	
	Если ВыполнятьДвижениеБезОрдерныеСклады Тогда
		ОтразитьДвиженияОтгрузкаМест(Источник, Движения, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтразитьДвиженияДокументовПоМаршрутам(Источник, Движения, Отказ)

	Движения.Маршруты_ДокументыПоМаршрутам.Записывать = Истина;
	НоваяЗапись = Движения.Маршруты_ДокументыПоМаршрутам.Добавить();
	НоваяЗапись.Период		= Источник.Дата;
	НоваяЗапись.Регистратор = Источник.Ссылка;
	
	Если ТипЗнч(Источник.Ссылка) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		НоваяЗапись.Получатель		= Источник.СкладПолучатель;	
		НоваяЗапись.ПунктНазначения	= Источник.СкладПолучатель;	
		НоваяЗапись.АдресДоставки	= ПолучитьАдресСклада(Источник.СкладПолучатель);
	КонецЕсли;
	
	Если ТипЗнч(Источник.Ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда	
		НоваяЗапись.Получатель			= Источник.Партнер;
		НоваяЗапись.ПунктНазначения 	= ?(ПустаяСтрока(Источник.КТС_ПунктНазначения), ПолучитьГородПоАдресу(Источник.КТС_АдресДоставки), Источник.КТС_ПунктНазначения);

		Если ЗначениеЗаполнено(Источник.КТС_Перевозчик) Тогда
			Если НЕ ЗначениеЗаполнено(Источник.КТС_ОтделениеНовойПочты) Тогда
				Отказ = Истина;
				
				СообщениеОшибка = НСтр("ru='Выполнить проведение %Документа% не удалось.Нужно указать отделение новой почты'");
				СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Документа%", Источник.Ссылка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОшибка,, "Объект.КТС_ОтделениеНовойПочты");
                
				Возврат;
				
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Источник.КТС_КонтактноеЛицо) Тогда
				Отказ = Истина;
				СообщениеОшибка = НСтр("ru='Выполнить проведение %Документа% не удалось.Нужно указать контактное лицо'");
				СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Документа%", Источник.Ссылка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОшибка,, "Объект.КТС_КонтактноеЛицо");

				Возврат;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Источник.КТС_ОбъявленнаяСтоимость) Тогда
				Отказ = Истина;
				СообщениеОшибка = НСтр("ru='Выполнить проведение %Документа% не удалось.Нужно указать объявленную стоимость'");
				СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Документа%", Источник.Ссылка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОшибка,, "Объект.КТС_ОбъявленнаяСтоимость");

				Возврат;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Источник.КТС_АдресДоставки) Тогда
				Отказ = Истина;
				СообщениеОшибка = НСтр("ru='Выполнить проведение %Документа% не удалось.Нужно указать адрес доставки'");
				СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Документа%", Источник.Ссылка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОшибка,, "Объект.КТС_АдресДоставки");

				Возврат;
			КонецЕсли;
		    НоваяЗапись.Перевозчик			 = Источник.КТС_Перевозчик;
			НоваяЗапись.ОтделениеНовойПочты  = Источник.КТС_ОтделениеНовойПочты;
			НоваяЗапись.КонтактноеЛицо		 = Источник.КТС_КонтактноеЛицо;
			НоваяЗапись.ОбъявленнаяСтоимость = Источник.КТС_ОбъявленнаяСтоимость;
        	НоваяЗапись.ПлатитПолучатель     = Источник.КТС_ПлатитПолучатель;  
			НоваяЗапись.ВозвратнаяДоставка   = Источник.КТС_ВозвратнаяДоставка;

		Иначе
			НоваяЗапись.Перевозчик			 = Неопределено;
			НоваяЗапись.ОтделениеНовойПочты  = Неопределено;
			НоваяЗапись.КонтактноеЛицо		 = Неопределено;
			НоваяЗапись.ОбъявленнаяСтоимость = Неопределено;
        	НоваяЗапись.ПлатитПолучатель     = Неопределено;
			НоваяЗапись.ВозвратнаяДоставка   = Неопределено;

        		
		КонецЕсли; 

		Если ТипЗнч(Источник.КТС_АдресДоставки) = Тип("СправочникСсылка.Склады") Тогда
			НоваяЗапись.АдресДоставки = ПолучитьАдресСклада(Источник.КТС_АдресДоставки);
		Иначе
			НоваяЗапись.АдресДоставки = ?(Источник.КТС_АдресДоставки=Неопределено, "", Источник.КТС_АдресДоставки);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Источник.Ссылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда	
		НоваяЗапись.Получатель		= Источник.Партнер;
		НоваяЗапись.ПунктНазначения = ?(ПустаяСтрока(Источник.КТС_ПунктНазначения), ПолучитьГородПоАдресу(Источник.КТС_АдресДоставки), Источник.КТС_ПунктНазначения);
		Если ЗначениеЗаполнено(Источник.КТС_Перевозчик) Тогда
			
			Если НЕ ЗначениеЗаполнено(Источник.КТС_ОтделениеНовойПочты) Тогда
				Отказ = Истина;
				
				СообщениеОшибка = НСтр("ru='Выполнить проведение %Документа% не удалось.Нужно указать отделение новой почты'");
				СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Документа%", Источник.Ссылка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОшибка,, "Объект.КТС_ОтделениеНовойПочты");
                
				Возврат;
				
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Источник.КТС_КонтактноеЛицо) Тогда
				Отказ = Истина;
				СообщениеОшибка = НСтр("ru='Выполнить проведение %Документа% не удалось.Нужно указать контактное лицо'");
				СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Документа%", Источник.Ссылка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОшибка,, "Объект.КТС_КонтактноеЛицо");

				Возврат;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Источник.КТС_ОбъявленнаяСтоимость) Тогда
				Отказ = Истина;
				СообщениеОшибка = НСтр("ru='Выполнить проведение %Документа% не удалось.Нужно указать объявленную стоимость'");
				СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Документа%", Источник.Ссылка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОшибка,, "Объект.КТС_ОбъявленнаяСтоимость");

				Возврат;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Источник.КТС_АдресДоставки) Тогда
				Отказ = Истина;
				СообщениеОшибка = НСтр("ru='Выполнить проведение %Документа% не удалось.Нужно указать адрес доставки'");
				СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Документа%", Источник.Ссылка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОшибка,, "Объект.КТС_АдресДоставки");

				Возврат;
			КонецЕсли;

		    НоваяЗапись.Перевозчик			 = Источник.КТС_Перевозчик;
			НоваяЗапись.ОтделениеНовойПочты  = Источник.КТС_ОтделениеНовойПочты;
			НоваяЗапись.КонтактноеЛицо		 = Источник.КТС_КонтактноеЛицо;
			НоваяЗапись.ОбъявленнаяСтоимость = Источник.КТС_ОбъявленнаяСтоимость;
        	НоваяЗапись.ПлатитПолучатель     = Источник.КТС_ПлатитПолучатель;
		Иначе
			НоваяЗапись.Перевозчик			 = Неопределено;
			НоваяЗапись.ОтделениеНовойПочты  = Неопределено;
			НоваяЗапись.КонтактноеЛицо		 = Неопределено;
			НоваяЗапись.ОбъявленнаяСтоимость = Неопределено;
        	НоваяЗапись.ПлатитПолучатель     = Неопределено;
        		
		КонецЕсли; 
	
		Если ТипЗнч(Источник.КТС_АдресДоставки) = Тип("СправочникСсылка.Склады") Тогда
			НоваяЗапись.АдресДоставки = ПолучитьАдресСклада(Источник.КТС_АдресДоставки);
		Иначе
			НоваяЗапись.АдресДоставки = ?(Источник.КТС_АдресДоставки=Неопределено, "", Источник.КТС_АдресДоставки);
		КонецЕсли;
	КонецЕсли;
	
	
	Если ТипЗнч(Источник.Ссылка) = Тип("ДокументСсылка.ВнутреннееПотреблениеТоваров") Тогда
		Если ЗначениеЗаполнено(Источник.КТС_АдресДоставки) Тогда
			НоваяЗапись.Получатель = Источник.КТС_АдресДоставки;
		Иначе
			НоваяЗапись.Получатель = Источник.Склад;
		КонецЕсли;
		НоваяЗапись.ПунктНазначения = Источник.КТС_АдресДоставки;
		НоваяЗапись.АдресДоставки	= ПолучитьАдресСклада(Источник.КТС_АдресДоставки);
	КонецЕсли;

КонецПроцедуры

Процедура ОтразитьДвиженияОтгрузкаМест(Источник, Движения, Отказ)
	
	Ссылка		 = Источник.Ссылка;
	
	Запрос 		 = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаОтгрузкаМест(Источник);

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Приход);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РезультатЗапроса.Выгрузить();
	
	Движения.КТС_ОтгрузкаИзБезордерныхСкладов.ДополнительныеСвойства.Вставить("ЭтоНовый", Источник.ДополнительныеСвойства.ЭтоНовый);
	Движения.КТС_ОтгрузкаИзБезордерныхСкладов.ДополнительныеСвойства.Вставить("РассчитыватьИзмененияОтгрузки", Ложь);
	Движения.КТС_ОтгрузкаИзБезордерныхСкладов.ДополнительныеСвойства.Вставить("ДляПроведения", Новый Структура("СтруктураВременныеТаблицы", Источник.ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы));
	
	Движения.КТС_ОтгрузкаИзБезордерныхСкладов.Очистить();
	Движения.КТС_ОтгрузкаИзБезордерныхСкладов.Загрузить(НаборЗаписей);
	Движения.КТС_ОтгрузкаИзБезордерныхСкладов.Записать();

КонецПроцедуры

Процедура ЗаписатьНаборыЗаписей(Источник)
	Источник.Движения.Записать();	
КонецПроцедуры 



Процедура ВыполнитьКонтрольРезультатовМаршруты(Источник, ДополнительныеСвойства, Отказ)

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Номенклатура КАК Номенклатура,
	                      |	Характеристика КАК Характеристика,
	                      |	Склад КАК Склад,
	                      |	КоличествоОстаток КАК Количество
	                      |ИЗ
	                      |	РегистрНакопления.КТС_ОтгрузкаИзБезОрдерныхСкладов.Остатки(
	                      |			,
	                      |			(Номенклатура, Характеристика, Склад) В
	                      |				(ВЫБРАТЬ
	                      |					Таблица.Номенклатура КАК Номенклатура,
	                      |					Таблица.Характеристика КАК Характеристика,
	                      |					Таблица.Склад КАК Склад
	                      |				ИЗ
	                      |					РегистрНакопления.КТС_ОтгрузкаИзБезОрдерныхСкладов КАК Таблица
	                      |				ГДЕ
	                      |					Таблица.Регистратор = &Ссылка
	                      |			
	                      |				ОБЪЕДИНИТЬ ВСЕ
	                      |			
	                      |				ВЫБРАТЬ
	                      |					Таблица.Номенклатура КАК Номенклатура,
	                      |					Таблица.Характеристика КАК Характеристика,
	                      |					Таблица.Склад КАК Склад
	                      |				ИЗ
	                      |					ДвиженияОтгрузкаИзБезордерныхСкладовИзменение КАК Таблица))
	                      |ГДЕ
	                      |	КоличествоОстаток < 0");
						  
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
				   
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	РезультатыЗапроса = Запрос.Выполнить();
	
	Если РезультатыЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	СообщениеОшибка = НСтр("ru='Выполнить проведение %Документа% не удалось'");
	СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Документа%", Источник.Ссылка);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОшибка);
	
	Выборка = РезультатыЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СообщениеОшибка = НСтр("ru='%Номенклатура% %Характеристика% отгружено больше чем указано в распоряжении на %Количество%'");
		СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Номенклатура%", Выборка.Номенклатура);
		СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Характеристика%", Выборка.Характеристика);
		СообщениеОшибка	= СтрЗаменить(СообщениеОшибка, "%Количество%", Выборка.Количество);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОшибка);
	КонецЦикла;

КонецПроцедуры



// Отправляет сообщение на 777 о проблеме
//
// Параметры:
// ТемаПисьма   	- Строка 		- Тема письма в техническую поддрежку.
// ТекстПисьма  	- Строка 		- Тело письма в техническую поддрежку.
//
Процедура ОповеститьОтделТехническойПоддержки(ТемаПисьма, ТекстПисьма)
	
	БиблиотекаОбработок = git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектПоИмени("Библиотека внешних обработок");
	Кому = Новый Массив;   
	Кому.Добавить(Новый Структура("Адрес, Представление", "777@ktc-ua.com",	"Отдел технической поддержки"));
	ПараметрыПисьма = БиблиотекаОбработок.СформироватьПараметрыПисьма(ТемаПисьма, ТекстПисьма, Кому, , , , , , ТипТекстаПочтовогоСообщения.HTML);
	БиблиотекаОбработок.ОтправитьСообщениеПоSMTP(Справочники.УчетныеЗаписиЭлектроннойПочты.ПолучитьСсылку(Новый УникальныйИдентификатор("5a8d021b-4548-4020-8cab-9ae6f06c8bf7")), ПараметрыПисьма);
	
КонецПроцедуры


Функция ПолучитьАдресАбонента(Абонент)

	Запрос = Новый Запрос;
	
	Если ТипЗнч(Абонент) = Тип("СправочникСсылка.Партнеры") Тогда
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	Представление КАК Адрес
		               |ИЗ
		               |	Справочник.Партнеры.КонтактнаяИнформация
		               |ГДЕ
		               |	Ссылка = &Абонент
		               |	И Представление <> """"
		               |	И Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)";
	Иначе
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	Представление КАК Адрес
		               |ИЗ
		               |	Справочник.Склады.КонтактнаяИнформация
		               |ГДЕ
		               |	Ссылка = &Абонент
		               |	И Представление <> """"
		               |	И Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)";			   
	КонецЕсли;
				   
	Запрос.УстановитьПараметр("Абонент", Абонент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат ?(Выборка.Следующий(), Выборка.Адрес, "");

КонецФункции // ПолучитьАдресАбонента()

Функция ПолучитьКомментарийКЗадачеЗаказаПоставщику(Источник)

	КомментарийКЗадачи = "";
	КомментарийКЗадачи = Источник.Комментарий + Символы.ПС;
	КомментарийКЗадачи = КомментарийКЗадачи + "Номер ТТН: [" + Источник.КТС_НомерТТН + "]" + Символы.ПС;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(Номенклатура.ВидНоменклатуры) КАК ВидНоменклатуры
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары
		|ГДЕ
		|	Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	Номенклатура.ВидНоменклатуры	
		|";
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		КомментарийКЗадачи = КомментарийКЗадачи + Выборка.ВидНоменклатуры + Символы.ПС;			
	КонецЦикла;
	
	Возврат КомментарийКЗадачи;
	
КонецФункции //


#Область Тексты_запросов

Функция ТекстЗапросаСписокАдресовПартнера()

	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Представление КАК Адрес
		|ИЗ
		|	Справочник.Партнеры.КонтактнаяИнформация
		|ГДЕ
		|	Ссылка = &Партнер
		|	И Тип  = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)";
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаСписокАдресовПартнера()

Функция ТекстЗапросаСписокПеревозчиков()

	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Партнер	
		|ИЗ
		|	РегистрСведений.ПартнерыСегмента
		|ГДЕ
		| 	Сегмент = &Сегмент
		|";
		
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаСписокПеревозчиков()

Функция ТекстЗапросаАдресПартнера()

	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Ссылка			КАК Ссылка,	
		|	Представление	КАК Представление
		|ИЗ
		|	Справочник.Партнеры.КонтактнаяИнформация
		|ГДЕ
		|	Ссылка = &Ссылка И Тип = &Тип";
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаАдресПартнера()

Функция ТекстЗапросаАдресСклада()

	ТекстЗапроса = "ВЫБРАТЬ
                  |	Представление КАК АдресСклада
                  |ИЗ
                  |	Справочник.Склады.КонтактнаяИнформация 
				  |ГДЕ
                  |	Ссылка = &Ссылка
                  |	И Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
                  |	И Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресСклада)";
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаАдресСклада()
 

Функция ТекстЗапросаКурсНаДату()

	ТекстЗапроса = "ВЫБРАТЬ
	               |	Курс / Кратность КАК Курс
	               |ИЗ
	               |	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаДокумента, Валюта = &Валюта)";
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаКурсНаДату()
 

Функция ПолучитьТекстЗапросаОтгрузкаМест(Источник)
 	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ПеремещениеТоваров") Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ
						  | &ВидДвижения КАК ВидДвижения,
						  |	Перемещение.Ссылка.Дата КАК Период,
						  |	Перемещение.Ссылка КАК ДокументОтгрузки,
		                  |	Перемещение.Номенклатура КАК Номенклатура,
		                  |	Перемещение.Характеристика КАК Характеристика,
		                  |	Перемещение.Ссылка.СкладОтправитель КАК Склад,
						  |	ВЫБОР
						  |		КОГДА Перемещение.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
						  |			ТОГДА Перемещение.КоличествоУпаковок
						  |		ИНАЧЕ Перемещение.КоличествоУпаковок * Перемещение.Упаковка.Коэффициент
						  |	КОНЕЦ КАК Количество
						  |ПОМЕСТИТЬ КешТоварыПеремещения
		                  |ИЗ
		                  |	Документ.ПеремещениеТоваров.Товары КАК Перемещение
						  |ГДЕ
						  |	Перемещение.Ссылка = &Ссылка
						  |	И Перемещение.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
						  | И НЕ Перемещение.Ссылка.СкладОтправитель.ИспользоватьОрдернуюСхемуПриОтгрузке
						  |;
						  |
						  ///////////////////////////////////////////////////////////////////
						  |ВЫБРАТЬ
						  |	Ссылка			КАК Склад,
						  |	Представление   КАК Представление
						  |ПОМЕСТИТЬ АдресаСкладов
						  |ИЗ
						  |	Справочник.Склады.КонтактнаяИнформация
						  |ГДЕ
						  |	Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
						  |;
						  |
						  ///////////////////////////////////////////////////////////////////
						  |ВЫБРАТЬ
						  |	СкладыОтгрузки.Ссылка КАК Маршрут,
						  |	КешТоварыПеремещения.ДокументОтгрузки КАК Документ
						  |ПОМЕСТИТЬ МаршрутыОтОтправителя	
						  |ИЗ
						  |	Справочник.ВариантыМаршрутов.СкладыОтгрузки КАК СкладыОтгрузки
						  |
						  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешТоварыПеремещения КАК КешТоварыПеремещения
						  |		ПО СкладыОтгрузки.Склад = КешТоварыПеремещения.ДокументОтгрузки.СкладОтправитель
                          |
						  |ОБЪЕДИНИТЬ
                          |
						  |ВЫБРАТЬ
						  |	ВозвратнаяДоставка.Ссылка КАК Маршрут,
						  |	КешТоварыПеремещения.ДокументОтгрузки КАК Документ
						  |	ИЗ
						  |	Справочник.ВариантыМаршрутов.ВозвратнаяДоставка КАК ВозвратнаяДоставка
						  |
						  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешТоварыПеремещения КАК КешТоварыПеремещения
						  |		ПО ВозвратнаяДоставка.СкладОтправитель = КешТоварыПеремещения.ДокументОтгрузки.СкладОтправитель
						  |			И ВозвратнаяДоставка.СкладПолучатель = КешТоварыПеремещения.ДокументОтгрузки.СкладПолучатель	
						  |;
						  |
						  ///////////////////////////////////////////////////////////////////
						  |ВЫБРАТЬ
						  |	СписокАдресов.Ссылка КАК Маршрут,
						  |	КешТоварыПеремещения.ДокументОтгрузки КАК Документ
						  |ПОМЕСТИТЬ МаршрутыКПолучателю
						  |ИЗ
					      |	Справочник.ВариантыМаршрутов.СписокАдресов КАК СписокАдресов
						  |
						  | 	ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешТоварыПеремещения КАК КешТоварыПеремещения
						  |		ПО СписокАдресов.ГородСклад = КешТоварыПеремещения.ДокументОтгрузки.СкладПолучатель
                          |
						  |ОБЪЕДИНИТЬ
                          |
						  |ВЫБРАТЬ
					      |	ВозвратнаяДоставка.Ссылка КАК Маршрут,
						  |	КешТоварыПеремещения.ДокументОтгрузки КАК Документ
						  |ИЗ
						  |	Справочник.ВариантыМаршрутов.ВозвратнаяДоставка КАК ВозвратнаяДоставка
						  |
						  | 	ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешТоварыПеремещения КАК КешТоварыПеремещения
						  |		ПО ВозвратнаяДоставка.СкладОтправитель = КешТоварыПеремещения.ДокументОтгрузки.СкладОтправитель
						  |			И ВозвратнаяДоставка.СкладПолучатель = КешТоварыПеремещения.ДокументОтгрузки.СкладПолучатель
						  |;
						  |
						  ///////////////////////////////////////////////////////////////////
						  |ВЫБРАТЬ РАЗЛИЧНЫЕ
						  |	МаршрутыКПолучателю.Документ КАК Документ						
						  |ПОМЕСТИТЬ МаршрутПрямойДоставки
						  |ИЗ
						  |	МаршрутыКПолучателю КАК МаршрутыКПолучателю
						  |	
						  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаршрутыОтОтправителя КАК МаршрутыОтОтправителя
						  |		ПО МаршрутыОтОтправителя.Маршрут = МаршрутыКПолучателю.Маршрут
						  |			И МаршрутыКПолучателю.Документ = МаршрутыОтОтправителя.Документ
                          |
						  |		ЛЕВОЕ СОЕДИНЕНИЕ АдресаСкладов КАК АдресаСкладаПолучателя 
						  |		ПО АдресаСкладаПолучателя.Склад = МаршрутыОтОтправителя.Документ.СкладПолучатель
                          |
						  |		ЛЕВОЕ СОЕДИНЕНИЕ АдресаСкладов КАК АдресаСкладаОтправителя 
						  |		ПО АдресаСкладаОтправителя.Склад = МаршрутыОтОтправителя.Документ.СкладОтправитель
                          |
						  |ГДЕ
					      |	АдресаСкладаПолучателя.Представление <> АдресаСкладаОтправителя.Представление		 
						  |;
						  |
						  ///////////////////////////////////////////////////////////////////
						  |ВЫБРАТЬ
						  |	КешТоварыПеремещения.ВидДвижения КАК ВидДвижения,
						  |	КешТоварыПеремещения.Период КАК Период,
						  |	КешТоварыПеремещения.ДокументОтгрузки КАК ДокументОтгрузки,
						  |	КешТоварыПеремещения.Номенклатура КАК Номенклатура,
						  |	КешТоварыПеремещения.Характеристика КАК Характеристика,
		                  |	КешТоварыПеремещения.Склад КАК Склад,
						  |	КешТоварыПеремещения.Количество КАК Количество
						  |ИЗ 
						  |	КешТоварыПеремещения КАК КешТоварыПеремещения
						  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаршрутПрямойДоставки КАК МаршрутПрямойДоставки
						  |		ПО КешТоварыПеремещения.ДокументОтгрузки = МаршрутПрямойДоставки.Документ";
						  
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда               
						  
		ТекстЗапроса = "ВЫБРАТЬ
						  | &ВидДвижения КАК ВидДвижения,
						  |	Реализация.Ссылка.Дата КАК Период,
					      |	Реализация.Ссылка КАК ДокументОтгрузки,
					      |	Реализация.Номенклатура КАК Номенклатура,
					      |	Реализация.Характеристика КАК Характеристика,
					      |	ВЫБОР Реализация.Склад
						  |		КОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
						  |			ТОГДА Реализация.Ссылка.Склад
						  |		ИНАЧЕ Реализация.Склад 
						  |	КОНЕЦ КАК Склад,
					      |	ВЫБОР Реализация.Упаковка
						  |		КОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
						  |			ТОГДА Реализация.КоличествоУпаковок
						  |		ИНАЧЕ Реализация.КоличествоУпаковок * Реализация.Упаковка.Коэффициент
						  |	КОНЕЦ КАК Количество
						  |ИЗ
					      |	Документ.РеализацияТоваровУслуг.Товары КАК Реализация
						  |ГДЕ
						  |	Реализация.Ссылка = &Ссылка
						  |	И Реализация.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
						  |	И НЕ Реализация.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
						  | И НЕ Реализация.Ссылка.КТС_АдресДоставки = """"
						  |	И НЕ Реализация.Ссылка.КТС_АдресДоставки = Неопределено";	
						  
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
						  
		ТекстЗапроса = "ВЫБРАТЬ
						  | &ВидДвижения КАК ВидДвижения,
						  |	ВозвратПоставщику.Ссылка.Дата КАК Период,
					      |	ВозвратПоставщику.Ссылка КАК ДокументОтгрузки,
					      |	ВозвратПоставщику.Номенклатура КАК Номенклатура,
					      |	ВозвратПоставщику.Характеристика КАК Характеристика,
					      |	ВозвратПоставщику.Ссылка.Склад КАК Склад,
					      |	ВЫБОР ВозвратПоставщику.Упаковка
						  |		КОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
						  |			ТОГДА ВозвратПоставщику.КоличествоУпаковок
						  |		ИНАЧЕ ВозвратПоставщику.КоличествоУпаковок * ВозвратПоставщику.Упаковка.Коэффициент
						  |	КОНЕЦ КАК Количество
						  |ИЗ
					      |	Документ.ВозвратТоваровПоставщику.Товары КАК ВозвратПоставщику
						  |ГДЕ
						  |	ВозвратПоставщику.Ссылка = &Ссылка
						  |	И ВозвратПоставщику.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
						  |	И НЕ ВозвратПоставщику.Ссылка.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
						  | И НЕ ВозвратПоставщику.Ссылка.КТС_АдресДоставки = """"
						  |	И НЕ ВозвратПоставщику.Ссылка.КТС_АдресДоставки = Неопределено";
						  
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ВнутреннееПотреблениеТоваров") Тогда
						  
		ТекстЗапроса = "ВЫБРАТЬ
						  | &ВидДвижения КАК ВидДвижения,
						  |	ВнутреннееПотребление.Ссылка.Дата КАК Период,
					      |	ВнутреннееПотребление.Ссылка КАК ДокументОтгрузки,
					      |	ВнутреннееПотребление.Номенклатура КАК Номенклатура,
					      |	ВнутреннееПотребление.Характеристика КАК Характеристика,
					      |	ВнутреннееПотребление.Ссылка.Склад КАК Склад,
					      |	ВЫБОР ВнутреннееПотребление.Упаковка
						  |		КОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
						  |			ТОГДА ВнутреннееПотребление.КоличествоУпаковок
						  |		ИНАЧЕ ВнутреннееПотребление.КоличествоУпаковок * ВнутреннееПотребление.Упаковка.Коэффициент
						  |	КОНЕЦ КАК Количество
						  |ИЗ
					      |	Документ.ВнутреннееПотреблениеТоваров.Товары КАК ВнутреннееПотребление
						  |ГДЕ
						  |	ВнутреннееПотребление.Ссылка = &Ссылка
						  |	И ВнутреннееПотребление.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
						  |	И НЕ ВнутреннееПотребление.Ссылка.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
						  | И НЕ ВнутреннееПотребление.Ссылка.КТС_АдресДоставки = """"
						  |	И НЕ ВнутреннееПотребление.Ссылка.КТС_АдресДоставки = Неопределено
						  | И НЕ ВнутреннееПотребление.Ссылка.КТС_АдресДоставки = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)";
						  
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаОтгрузкаМест()


Функция ТекстЗапросаПараметрыДоставки()

	ТекстЗапроса = "ВЫБРАТЬ
	               |	Период								КАК Период,
				   |    Получатель							КАК Получатель,
	               |	КТС_ПД_АдресДоставки				КАК КТС_ПД_АдресДоставки,
	               |	КТС_ПД_ОтделениеНовойПочты			КАК КТС_ПД_ОтделениеНовойПочты,
	               |	КТС_ПД_Перевозчик					КАК КТС_ПД_Перевозчик,
	               |	КТС_ПД_ПунктНазначения				КАК КТС_ПД_ПунктНазначения,
	               |	КТС_ПД_КонтактноеЛицо				КАК КТС_ПД_КонтактноеЛицо,
	               |	КТС_ПД_ОбъявленнаяСтоимость			КАК КТС_ПД_ОбъявленнаяСтоимость,
	               |	КТС_ПД_ПлатитПолучатель				КАК КТС_ПД_ПлатитПолучатель,
	               |	КТС_ПД_ВозвратнаяДоставка			КАК КТС_ПД_ВозвратнаяДоставка
	               |ИЗ
	               |	РегистрСведений.КТС_АналитикаДоставки
	               |ГДЕ
	               |	Объект = &Объект";
	
	Возврат ТекстЗапроса;

КонецФункции // ТекстЗапросаПараметрыДоставки()
 

#КонецОбласти 

#КонецОбласти


#Область СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	//Версия обработки
	Версия = "0.0.045";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "МодульДоставки");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Модуль доставки [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
	ДобавитьКоманду(ТаблицаКоманд, "Модуль доставки [" + Версия + "]", "Форма", "ОткрытиеФормы", Ложь, "МодульДоставки");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункцииПереопределенияОбработчиков

// Только для внутреннего использования
Процедура ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, Форма, ИмяСобытияФормы = "", ПолноеИмяЭлементаФормы = "", НовоеДействие, ОбработкаИсключений = Ложь)

	Если ВнешнийОбъект = Неопределено Тогда
		git_ПереопределениеОбработчиковСервер.ПодменитьДействиеУправляемоеПриложение(Форма, ИмяСобытияФормы, ПолноеИмяЭлементаФормы, НовоеДействие, ОбработкаИсключений);
	Иначе
		ВнешнийОбъект.ПодменитьДействиеУправляемоеПриложение(Форма, ИмяСобытияФормы, ПолноеИмяЭлементаФормы, НовоеДействие, ОбработкаИсключений);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти
