
#Область ИнтерфейсАвтоматическихТестов
	
Перем ЮнитТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮнитТест = ЮнитТестирование;
	
	СпиcокТестов = Новый Массив;
	СпиcокТестов.Добавить("Тест_ПроверитьВерсиюБиблиотеки");
	
	ПараметрыТестов = Новый Структура("ИмяТеста, Транзакция, Параметр", "Тест_ЗаписатьДанныеВРегистрКоефициентПриоритетностиПродаж", Истина, Неопределено);
	СпиcокТестов.Добавить(ПараметрыТестов);

	#Область Задача_4749
	СпиcокТестов.Добавить("Тест_ВыполнитьРассылкуНоменклатурыПоЗаказу");
	#КонецОбласти	


	Возврат СпиcокТестов;
	
КонецФункции


Процедура Тест_ПроверитьВерсиюБиблиотеки() Экспорт

	ВнешниеПараметрыРегистрации = СведенияОВнешнейОбработке();
	ВнутрениеПараметрыРегистрации = git_КоефициентПриоритетностиПродажСервер.СведенияОВнешнейОбработке();	
	ЮнитТест.ПроверитьРавенство(ВнешниеПараметрыРегистрации.Версия, ВнутрениеПараметрыРегистрации.Версия, );

КонецПроцедуры // Тест_ПроверитьВерсиюБиблиотеки()
 
Процедура Тест_ЗаписатьДанныеВРегистрКоефициентПриоритетностиПродаж(Параметр) Экспорт
	
	ТекущаяДата = ТекущаяДата();
	
	WshShell 	= Новый COMobject("wscript.shell"); 
	WshEnvirUsr = WshShell.Environment("USER");
	
	ПутьКРепозиторию 	 = WshEnvirUsr.Item("TEST_REPOSITORY");
	ПутьКВнешнемуОбъекту = ПутьКРепозиторию + "\tests\Справочники\Номенклатура\СоздатьЭлементСправочника.epf";
	
	ВнешнийОбъект = git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектИзФайловойСистемы(ПутьКВнешнемуОбъекту);
	ЭлементНоменклатуры = ВнешнийОбъект.СоздатьЭлементСправочника();

	ЗаписатьДанныеВРегистрКоефициентПриоритетностиПродаж(Новый Структура("Период, Номенклатура, Значение, НеПересчитыватьАвтоматически", 
																		ТекущаяДата, 
																		ЭлементНоменклатуры,
																		Неопределено,
																		Истина), "КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж");
																		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", ЭлементНоменклатуры);
	Запрос.Текст = ПолучитьТекстЗапроса_КПП_Опт();
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.Период <> ТекущаяДата Тогда
			ВызватьИсключение НСтр("ru='Период записи не совпадает!'");		
		ИначеЕсли Выборка.Номенклатура <> ЭлементНоменклатуры Тогда
			ВызватьИсключение НСтр("ru='Номенклатура записи не совпадает!'");
		ИначеЕсли Выборка.НеПересчитыватьАвтоматически_Опт <> Истина Тогда
			ВызватьИсключение НСтр("ru='НеПересчитыватьАвтоматически записи не совпадает!'");
		КонецЕсли;
	Иначе	
		ВызватьИсключение НСтр("ru='Данные не удалось записать'");	
	КонецЕсли;
																																			
КонецПроцедуры

#Область Задача_4749

Процедура Тест_ВыполнитьРассылкуНоменклатурыПоЗаказу() Экспорт

	ДопСообщениеОшибки = НСтр("ru=' Не верный тип запроса ""ПолучитьТекстЗапросаКППРассылкаНоменклатурыБезКППЗаказПоставщику"" '", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	ЮнитТест.ПроверитьТип(ПолучитьТекстЗапросаКППРассылкаНоменклатурыБезКППЗаказПоставщику(), Тип("Строка"),ДопСообщениеОшибки);

	ДопСообщениеОшибки 	= НСтр("ru=' Не верный тип запроса ""ПолучитьТекстЗапросаКППРассылкаНоменклатурыБезКППЗаказКлиента"" '", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	ЮнитТест.ПроверитьТип(ПолучитьТекстЗапросаКППРассылкаНоменклатурыБезКППЗаказКлиента(), Тип("Строка"),ДопСообщениеОшибки);	
	
	Адрес = "Тест poshta@ktc.rovno.ua Тест"; 
	МасивСтрок = РазобратьСтрокуВМассивПоРазделителю("poshta@ktc.rovno.ua");
	Если МасивСтрок.Количество() Тогда 
		ЮнитТест.ПроверитьРавенство(МасивСтрок[0], "poshta@ktc.rovno.ua");
	КонецЕсли;
	
	ДанныеДляТестирования = СоздатьДанныеДляТестирования();
	
	Если ЗначениеЗаполнено(ДанныеДляТестирования.Заказ) Тогда
		ЗаказКлиента = ДанныеДляТестирования.Заказ.ПолучитьОбъект();
		ЗаказКлиента.Товары[0].Отменено = Истина;
		ЗаказКлиента.Товары[0].ПричинаОтмены = ДанныеДляТестирования.ПричинаОтмены;
		ЗаказКлиента.Записать();
		Запрос = Новый Запрос;
		УстановитьПараметрыЗапросаКППРассылкаНоменклатурыБезКПП(ЗаказКлиента, Запрос);
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		ЮнитТест.ПроверитьЗаполненность(РезультатЗапроса.Найти(ДанныеДляТестирования.Заказ));
	КонецЕсли;
			
	Если ЗначениеЗаполнено(ДанныеДляТестирования.ЗаказНаОсновнойСклад) Тогда
		ЗаказКлиента = ДанныеДляТестирования.ЗаказНаОсновнойСклад.ПолучитьОбъект();
		ЗаказКлиента.Товары[0].Отменено = Истина;
		ЗаказКлиента.Товары[0].ПричинаОтмены = ДанныеДляТестирования.ПричинаОтмены;
		ЗаказКлиента.Записать();
		Запрос = Новый Запрос;
		УстановитьПараметрыЗапросаКППРассылкаНоменклатурыБезКПП(ЗаказКлиента, Запрос);
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		ЮнитТест.ПроверитьЗаполненность(РезультатЗапроса.Найти(ДанныеДляТестирования.ЗаказНаОсновнойСклад));
	КонецЕсли;
	
	УничтожитьДанныеДляТестирования(ДанныеДляТестирования);
	
КонецПроцедуры // Тест_ВыполнитьРассылкуНоменклатурыПоЗаказу()

#КонецОбласти


Функция СоздатьДанныеДляТестирования()
	
	ДанныеДляТестирования = Новый Структура;
	
	Склад 					= СоздатьСклад();
	ОсновнойСклад           = СоздатьСклад();

	Запись 			= РегистрыСведений.Модуль_ДоступныеСклады.СоздатьМенеджерЗаписи();
	Запись.Период	= ТекущаяДата();
	Запись.Склад	= ОсновнойСклад;
	Запись.Основной = Истина;
	Запись.Записать();
	
	
	Номенклатура    		= СоздатьНоменклатуру("Номенклатура");
	СерийныйНомер			= СоздатьСерийныйНомер(Номенклатура);
	
	Сертификат	    		= СоздатьНоменклатуру("Сертификат");
	СертификатКТС			= СоздатьСерийныйНомер(Сертификат);
	
	Организация				= СоздатьОрганизацию();
	Поставщик				= СоздатьПоставщика();
	Клиент					= СоздатьКлиента("Клиент");
	КонтрагентПоставщик 	= СоздатьКонтрагента(Поставщик);
	КонтрагентКлиент 		= СоздатьКонтрагента(Клиент);
	СоглашениеСКлиентом 	= СоздатьСоглашениеСКлиентами(Клиент, КонтрагентКлиент, Организация, Склад);
	СоглашениеСПоставщиком 	= СоздатьСоглашенияСПоставщками(Поставщик, КонтрагентПоставщик, Организация, Склад);
	
	ПричинаОтмены 			= Справочники.ПричиныОтменыЗаказовКлиентов.СоздатьЭлемент();
	ПричинаОтмены.Наименование = ПреобразоватьУникальныйИдентификаторВНаименование("Тест");
	ПричинаОтмены.Записать();
	
	ПоступлениеТоваров 		= СоздатьПоступлениеТоваровИУслуг(Поставщик		,КонтрагентПоставщик	,Организация,
															  Склад			,СоглашениеСПоставщиком	,Номенклатура, 
															  СерийныйНомер);
															  
	Заказ	= СоздатьЗаказКлиента(Клиент, КонтрагентКлиент, Организация, СоглашениеСКлиентом,Склад,Номенклатура);
	
	ЗаказНаОсновнойСклад	= СоздатьЗаказКлиента(Клиент, КонтрагентКлиент, Организация, СоглашениеСКлиентом,ОсновнойСклад,Номенклатура);
	
	Запись.Удалить();

	ДанныеДляТестирования.Вставить("Склад"					, Склад);
	ДанныеДляТестирования.Вставить("ОсновнойСклад"			, ОсновнойСклад);
	ДанныеДляТестирования.Вставить("Номенклатура"			, Номенклатура);
	ДанныеДляТестирования.Вставить("СерийныйНомер"			, СерийныйНомер);
	ДанныеДляТестирования.Вставить("Сертификат"				, Сертификат);
	
	ДанныеДляТестирования.Вставить("Организация"			, Организация);
	ДанныеДляТестирования.Вставить("Поставщик"				, Поставщик);
	ДанныеДляТестирования.Вставить("Клиент"					, Клиент);
	ДанныеДляТестирования.Вставить("КонтрагентПоставщик"	, КонтрагентПоставщик);
	ДанныеДляТестирования.Вставить("КонтрагентКлиент"		, КонтрагентКлиент);
	ДанныеДляТестирования.Вставить("СоглашениеСКлиентом"	, СоглашениеСКлиентом);
	ДанныеДляТестирования.Вставить("СоглашениеСПоставщиком"	, СоглашениеСПоставщиком);
	
	ДанныеДляТестирования.Вставить("ПоступлениеТоваров"		, ПоступлениеТоваров);
	ДанныеДляТестирования.Вставить("ЗаказНаОсновнойСклад"	, ЗаказНаОсновнойСклад);
	ДанныеДляТестирования.Вставить("Заказ"					, Заказ);
	ДанныеДляТестирования.Вставить("ПричинаОтмены"			, ПричинаОтмены.Ссылка);
	
	Возврат ДанныеДляТестирования;
	
КонецФункции

Процедура УничтожитьДанныеДляТестирования(ДанныеДляТестирования)

	Если ТипЗнч(ДанныеДляТестирования) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
#Область ПометкаНаУдалениеДляДокументов


	Если ДанныеДляТестирования.Свойство("ЗаказНаОсновнойСклад") Тогда
		
		ЗаказНаОсновнойСклад = ДанныеДляТестирования.ЗаказНаОсновнойСклад.ПолучитьОбъект();
		
		СправочникОбъект = ЗаказНаОсновнойСклад.Подразделение.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
		ЗаказНаОсновнойСклад.Удалить();
	КонецЕсли;

	Если ДанныеДляТестирования.Свойство("Заказ") Тогда
		
		Заказ = ДанныеДляТестирования.Заказ.ПолучитьОбъект();
		
		СправочникОбъект = Заказ.Подразделение.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
		Заказ.Удалить();
	КонецЕсли;

	Если ДанныеДляТестирования.Свойство("ПоступлениеТоваров") Тогда
		
		ПоступлениеТоваров = ДанныеДляТестирования.ПоступлениеТоваров.ПолучитьОбъект();
		
		СправочникОбъект = ПоступлениеТоваров.Подразделение.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
		
		ПоступлениеТоваров.Удалить();
		
	КонецЕсли;
	
#КонецОбласти 	
	
#Область УничтожениеСправочников

	Если ДанныеДляТестирования.Свойство("СоглашениеСКлиентом") Тогда
	
		СправочникОбъект = ДанныеДляТестирования.СоглашениеСКлиентом.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
		
	КонецЕсли;

	Если ДанныеДляТестирования.Свойство("СоглашениеСПоставщиком") Тогда
	
		СправочникОбъект = ДанныеДляТестирования.СоглашениеСПоставщиком.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
		
	КонецЕсли;

	Если ДанныеДляТестирования.Свойство("КонтрагентКлиент") Тогда
	
		СправочникОбъект = ДанныеДляТестирования.КонтрагентКлиент.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
		
	КонецЕсли;

	Если ДанныеДляТестирования.Свойство("КонтрагентПоставщик") Тогда
	
		СправочникОбъект = ДанныеДляТестирования.КонтрагентПоставщик.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
		
	КонецЕсли;

	Если ДанныеДляТестирования.Свойство("Клиент") Тогда
	
		СправочникОбъект = ДанныеДляТестирования.Клиент.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
		
	КонецЕсли;

	Если ДанныеДляТестирования.Свойство("Поставщик") Тогда
	
		СправочникОбъект = ДанныеДляТестирования.Поставщик.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
		
	КонецЕсли;

	Если ДанныеДляТестирования.Свойство("Организация") Тогда
		
		НаборЗаписей = РегистрыСведений.СистемыНалогообложенияОрганизаций.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(ДанныеДляТестирования.Организация);
		НаборЗаписей.Записать();
	
		СправочникОбъект = ДанныеДляТестирования.Организация.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
		
	КонецЕсли;

	Если ДанныеДляТестирования.Свойство("СерийныйНомер") Тогда
		СправочникОбъект = ДанныеДляТестирования.СерийныйНомер.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
	КонецЕсли;

	Если ДанныеДляТестирования.Свойство("Номенклатура") Тогда
		СправочникОбъект = ДанныеДляТестирования.Номенклатура.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
	КонецЕсли;

	Если ДанныеДляТестирования.Свойство("Склад") Тогда
		СправочникОбъект = ДанныеДляТестирования.Склад.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
	КонецЕсли;
	
	Если ДанныеДляТестирования.Свойство("ОсновнойСклад") Тогда
		СправочникОбъект = ДанныеДляТестирования.ОсновнойСклад.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
	КонецЕсли;
	
	Если ДанныеДляТестирования.Свойство("ПричинаОтмены") Тогда
		СправочникОбъект = ДанныеДляТестирования.ПричинаОтмены.ПолучитьОбъект();
		СправочникОбъект.УстановитьПометкуУдаления(Истина);
		СправочникОбъект.Записать();
		СправочникОбъект.Удалить();
	КонецЕсли;
	
				
#КонецОбласти

КонецПроцедуры // УничтожитьДанныеДляТестирования()
 
Функция ПреобразоватьУникальныйИдентификаторВНаименование(Префикс)
	Идентификатор = Новый УникальныйИдентификатор;
	Возврат Префикс + СтрЗаменить(Строка(Идентификатор), "-", "_");
КонецФункции // ПереобразоватьУникальныйИдентификаторВНаименование()



#Область Справочники

Функция СоздатьСклад()
	
	Наименование = ПреобразоватьУникальныйИдентификаторВНаименование("Склад");
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Наименование", Наименование);
	Бибилиотека = ПолучитьБиблиотекуСклад();
	
	Возврат Бибилиотека.СоздатьЭлементСправочника(ПараметрыЗаполнения);

КонецФункции // СоздатьСклад()

Функция СоздатьНоменклатуру(УИНаименование)
	
	Наименование = ПреобразоватьУникальныйИдентификаторВНаименование(УИНаименование);
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Наименование",				Наименование);   
	ПараметрыЗаполнения.Вставить("ИспользоватьУпаковки",		Ложь);
	ПараметрыЗаполнения.Вставить("ИспользоватьСерийныеНомера",	Истина);
	
	Бибилиотека = ПолучитьБиблиотекуНоменклатуры();
	
	Возврат Бибилиотека.СоздатьЭлементСправочника(ПараметрыЗаполнения);
	
КонецФункции // СоздатьНоменклатуру()

Функция СоздатьСерийныйНомер(НоменклатураСсылка)

	Код = ПреобразоватьУникальныйИдентификаторВНаименование(НоменклатураСсылка.Наименование+"СерийныйНомер");
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Владелец",	НоменклатураСсылка);   
	ПараметрыЗаполнения.Вставить("Код",			Код);
	
	Бибилиотека = ПолучитьБиблиотекуСерийныеНомера();
	
	Возврат Бибилиотека.СоздатьЭлементСправочника(ПараметрыЗаполнения);

КонецФункции // СоздатьСерийныйНомер()

Функция СоздатьОрганизацию()

	Наименование = ПреобразоватьУникальныйИдентификаторВНаименование("Организация");
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Наименование", Наименование);
	Бибилиотека = ПолучитьБиблиотекуОрганизации();
	
	Возврат Бибилиотека.СоздатьЭлементСправочника(ПараметрыЗаполнения);

КонецФункции // СоздатьОрганизацию()

Функция СоздатьПоставщика()

	Наименование = ПреобразоватьУникальныйИдентификаторВНаименование("Поставщик");
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Поставщик", Истина);
	ПараметрыЗаполнения.Вставить("Наименование", Наименование);
	Бибилиотека = ПолучитьБиблиотекуПартнеры();
	
	Возврат Бибилиотека.СоздатьЭлементСправочника(ПараметрыЗаполнения);

КонецФункции // СоздатьПартнера()

Функция СоздатьКлиента(УИНаименование)

	Наименование = ПреобразоватьУникальныйИдентификаторВНаименование(УИНаименование);
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Клиент", Истина);
	ПараметрыЗаполнения.Вставить("Наименование", Наименование);
	Бибилиотека = ПолучитьБиблиотекуПартнеры();
	
	Возврат Бибилиотека.СоздатьЭлементСправочника(ПараметрыЗаполнения);

КонецФункции // СоздатьПартнера()

Функция СоздатьКонтрагента(Партнер)

	Наименование = ПреобразоватьУникальныйИдентификаторВНаименование("Контрагент");
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Партнер", Партнер);
	ПараметрыЗаполнения.Вставить("Наименование", Наименование);
	Бибилиотека = ПолучитьБиблиотекуКонтаргенты();
	
	Возврат Бибилиотека.СоздатьЭлементСправочника(ПараметрыЗаполнения);

КонецФункции // СоздатьКонтрагента()

Функция СоздатьСоглашениеСКлиентами(Партнер, Контрагент, Организация, Склад)

	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Наименование", "Соглашение с клиентом");
	ПараметрыЗаполнения.Вставить("Партнер", Партнер);
	ПараметрыЗаполнения.Вставить("Контрагент", Контрагент);
	ПараметрыЗаполнения.Вставить("Организация", Организация);
	ПараметрыЗаполнения.Вставить("Склад", Склад);
	Бибилиотека = ПолучитьБиблиотекуСоглашениеСКлиентами();
	
	Возврат Бибилиотека.СоздатьЭлементСправочника(ПараметрыЗаполнения);

КонецФункции // СоздатьСоглашениеСКлиентами()

Функция СоздатьСоглашенияСПоставщками(Партнер, Контрагент, Организация, Склад)

	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Наименование", "Соглашение с поставщиком");
	ПараметрыЗаполнения.Вставить("Партнер", Партнер);
	ПараметрыЗаполнения.Вставить("Контрагент", Контрагент);
	ПараметрыЗаполнения.Вставить("Организация", Организация);
	ПараметрыЗаполнения.Вставить("Склад", Склад);
	Бибилиотека = ПолучитьБиблиотекуСоглашенияСПоставщками();
	
	Возврат Бибилиотека.СоздатьЭлементСправочника(Неопределено, ПараметрыЗаполнения);

КонецФункции // СоздатьСоглашениеСКлиентами()

#КонецОбласти

#Область Документы

Функция ИнициализироватьТаблицуТоваров()

	Товары = Новый ТаблицаЗначений;
	                                                            
	Товары.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Товары.Колонки.Добавить("ВариантОбеспечения", Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыОбеспечения"));
	Товары.Колонки.Добавить("ДатаОтгрузки", Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	Товары.Колонки.Добавить("Упаковка", Новый ОписаниеТипов("СправочникСсылка.УпаковкиНоменклатуры"));
	Товары.Колонки.Добавить("КоличествоУпаковок", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Неотрицательный)));
	Товары.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Неотрицательный)));
	Товары.Колонки.Добавить("ВидЦены", Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
	Товары.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	Товары.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	Товары.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	Товары.Колонки.Добавить("СуммаНДС", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	Товары.Колонки.Добавить("СуммаСНДС", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	Товары.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	Товары.Колонки.Добавить("КлючСвязиСерийныхНомеров", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(5, 0, ДопустимыйЗнак.Неотрицательный)));
	
	Возврат Товары;

КонецФункции // ИнициализироватьТаблицуТоваров()

Функция ИнициализироватьТаблицуСерийныхНомеров()

	СерийныеНомера = Новый ТаблицаЗначений;
	СерийныеНомера.Колонки.Добавить("СерийныйНомер", Новый ОписаниеТипов("СправочникСсылка.СерийныеНомераНоменклатуры"));
	СерийныеНомера.Колонки.Добавить("КлючСвязиСерийныхНомеров", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(5, 0, ДопустимыйЗнак.Неотрицательный)));
	
	Возврат СерийныеНомера;

КонецФункции // ИнициализироватьТаблицуСерийныхНомеров()

Функция СоздатьПоступлениеТоваровИУслуг(Партнер, Контрагент, Организация, Склад, Соглашение, Номенклатура, СерийныйНомер)

	Товары 			= ИнициализироватьТаблицуТоваров();
	СерийныеНомера  = ИнициализироватьТаблицуСерийныхНомеров();
	
	НоваяСтрока = Товары.Добавить();
	НоваяСтрока.Номенклатура 					= Номенклатура;
	НоваяСтрока.КоличествоУпаковок 				= 1;
	НоваяСтрока.Количество 						= 1;
	НоваяСтрока.Цена 							= 100;
	НоваяСтрока.Сумма 							= 100;
	НоваяСтрока.СтавкаНДС						= Перечисления.СтавкиНДС.БезНДС;
	НоваяСтрока.СуммаНДС 						= 0;
	НоваяСтрока.СуммаСНДС						= 100;
	НоваяСтрока.Склад 							= Склад;
	НоваяСтрока.КлючСвязиСерийныхНомеров 		= 1;
		
	НоваяСтрока = СерийныеНомера.Добавить();
	НоваяСтрока.СерийныйНомер 					= СерийныйНомер;
	НоваяСтрока.КлючСвязиСерийныхНомеров		= 1;
	
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Партнер"			, Партнер);
	ПараметрыЗаполнения.Вставить("Контрагент"		, Контрагент);
	ПараметрыЗаполнения.Вставить("Организация"		, Организация);
	ПараметрыЗаполнения.Вставить("Склад"			, Склад);
	ПараметрыЗаполнения.Вставить("Соглашение"		, Соглашение);
	ПараметрыЗаполнения.Вставить("Товары"			, Товары);
	ПараметрыЗаполнения.Вставить("СерийныеНомера"	, СерийныеНомера);
	
	Бибилиотека = ПолучитьБиблиотекуПоступлениеТоваров();
	
	Возврат Бибилиотека.СоздатьИПровестиДокумент(Неопределено, ПараметрыЗаполнения);

КонецФункции // СоздатьПоступлениеТоваровИУслуг()

Функция СоздатьЗаказКлиента(Партнер,Контрагент,Организация,Соглашение, Склад, Номенклатура)
	
	Товары 		= ИнициализироватьТаблицуТоваров();
	
	НоваяСтрока = Товары.Добавить();
	НоваяСтрока.Номенклатура 					= Номенклатура;
	НоваяСтрока.ВариантОбеспечения				= Перечисления.ВариантыОбеспечения.СоСклада; 
	НоваяСтрока.ДатаОтгрузки 					= КонецМесяца(ТекущаяДата());
	НоваяСтрока.КоличествоУпаковок 				= 1;
	НоваяСтрока.Количество 						= 1;
	НоваяСтрока.Цена 							= 100;
	НоваяСтрока.Сумма 							= 100;
	НоваяСтрока.СтавкаНДС						= Перечисления.СтавкиНДС.БезНДС;
	НоваяСтрока.СуммаНДС 						= 0;
	НоваяСтрока.СуммаСНДС						= 100;
	НоваяСтрока.Склад 							= Склад;
	
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Партнер"			, Партнер);
	ПараметрыЗаполнения.Вставить("Контрагент"		, Контрагент);
	ПараметрыЗаполнения.Вставить("Организация"		, Организация);
	ПараметрыЗаполнения.Вставить("Склад"			, Склад);
	ПараметрыЗаполнения.Вставить("Соглашение"		, Соглашение);
	ПараметрыЗаполнения.Вставить("Товары"			, Товары);
	ПараметрыЗаполнения.Вставить("Статус"			, Перечисления.СтатусыЗаказовКлиентов.КОтгрузке);
	
	Бибилиотека = ПолучитьБиблиотекуЗаказКлиента();
	
	Возврат Бибилиотека.СоздатьИПровестиДокумент(Неопределено, ПараметрыЗаполнения);

КонецФункции //СоздатьЗаказКлиента()

#КонецОбласти

#Область Библиотеки

Функция ПолучитьБиблиотеку(ПутьКБиблиотеке)

	WshShell 	= Новый COMobject("wscript.shell"); 
	WshEnvirUsr = WshShell.Environment("USER");

	ПутьКРепозиторию 		= WshEnvirUsr.Item("TEST_REPOSITORY");
	ПутьКВнешнемуОбъекту 	= ПутьКРепозиторию + ПутьКБиблиотеке;
	
	Возврат git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектИзФайловойСистемы(ПутьКВнешнемуОбъекту);

КонецФункции // ПолучитьБиблиотеку()

Функция ПолучитьПутьКБиблиотеке(ПутьКБиблиотеке)

	WshShell 	= Новый COMobject("wscript.shell"); 
	WshEnvirUsr = WshShell.Environment("USER");

	ПутьКРепозиторию 		= WshEnvirUsr.Item("TEST_REPOSITORY");
	ПутьКВнешнемуОбъекту 	= ПутьКРепозиторию + ПутьКБиблиотеке;
	
	Возврат ПутьКВнешнемуОбъекту;

КонецФункции // ПолучитьПутьКБиблиотеке()

Функция ПолучитьВерсиюБибилиотеки(ИмяБибилиотеки)
	Возврат git_ПереопределениеОбработчиковСервер.ПолучитьВерсиюВнешнегоОбъектаИзБазыДанных(ИмяБибилиотеки);
КонецФункции // ПолучитьВерсиюБибилиотеки()

Функция ПолучитьБиблиотекуСклад()
	ПутьКБиблиотеке = "\tests\Справочники\Склады\СоздатьЭлементСправочника.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуСклад()

Функция ПолучитьБиблиотекуНоменклатуры()
	ПутьКБиблиотеке = "\tests\Справочники\Номенклатура\СоздатьЭлементСправочника.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуНоменклатуры()

Функция ПолучитьБиблиотекуСерийныеНомера()
	ПутьКБиблиотеке = "\tests\Справочники\СерийныеНомераНоменклатуры\СоздатьЭлементСправочника.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуСерийныеНомера()

Функция ПолучитьБиблиотекуОрганизации()
	ПутьКБиблиотеке = "\tests\Справочники\Организации\СоздатьЭлементСправочника.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуОрганизации()

Функция ПолучитьБиблиотекуПартнеры()
	ПутьКБиблиотеке = "\tests\Справочники\Партнеры\СоздатьЭлементСправочника.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуПартнеры()

Функция ПолучитьБиблиотекуКонтаргенты()
	ПутьКБиблиотеке = "\tests\Справочники\Контрагенты\СоздатьЭлементСправочника.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуКонтаргенты()

Функция ПолучитьБиблиотекуСоглашениеСКлиентами()
	ПутьКБиблиотеке = "\tests\Справочники\СоглашениеСКлиентами\СоздатьЭлементСправочника.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуСоглашениеСКлиентами()

Функция ПолучитьБиблиотекуСоглашенияСПоставщками()
	ПутьКБиблиотеке = "\tests\Справочники\СоглашенияСПоставщками\СоздатьЭлементСправочника.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуСоглашенияСПоставщками()

Функция ПолучитьБиблиотекуПоступлениеТоваров()
	ПутьКБиблиотеке = "\tests\Документы\ПоступлениеТоваровУслуг\СоздатьИПровестиДокумент.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуПоступлениеТоваров()

Функция ПолучитьБиблиотекуЗаказКлиента()
	ПутьКБиблиотеке = "\tests\Документы\ЗаказКлиента\СоздатьИПровестиДокумент.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуЗаказКлиента()

Функция ПолучитьБиблиотекуРеализацияТоваровИУслуг()
	ПутьКБиблиотеке = "\tests\Документы\РеализацияТоваровУслуг\СоздатьИПровестиДокумент.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуРеализацияТоваровИУслуг()

Функция ПолучитьБиблиотекуЗаявкаСервисногоЦентра()
	ПутьКБиблиотеке = "\tests\Документы\ГНАТ_ЗаявкаСервисногоЦентра\СоздатьИПровестиДокумент.epf";
 	Возврат ПолучитьБиблиотеку(ПутьКБиблиотеке);
КонецФункции // ПолучитьБиблиотекуЗаявкаСервисногоЦентра()

#КонецОбласти 


#КонецОбласти

	
#Область ПрограммныйИнтерфейс

// Запись из справочника номеклатуры
Процедура ЗаписатьДанныеВРегистрКоефициентПриоритетностиПродаж(ПараметрыЗаполнения, ИмяРегистра) Экспорт
	
	Если ТипЗнч(ПараметрыЗаполнения) = Тип("Структура") Тогда
		
		НаборЗаписей = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(ПараметрыЗаполнения.Период);
		НаборЗаписей.Отбор.Номенклатура.Установить(ПараметрыЗаполнения.Номенклатура);
		
		ПараметрыЗаполнения.Вставить("Ответственный", Пользователи.ТекущийПользователь());
		
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), ПараметрыЗаполнения);
		НаборЗаписей.Записать();	
		
	КонецЕсли;

КонецПроцедуры // ЗаписатьДанныеВРегистрКоефициентПриоритетностиПродаж() 

// Процедура обновляет заработок менеджера в свойствах номенклатуры при изменении КПП
// 
// Параметры:
//  Источник - НаборЗаписей - 
//
Процедура ОбновитьЗаработокИзРегистровКПП(ТипЗначения, ТаблицаЗаписей) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИсточникМетаданные = Метаданные.НайтиПоТипу(ТипЗначения);
	ЕстьИзмерениеКПП = ИсточникМетаданные.Измерения.Найти("КПП") <> Неопределено;
	ЕстьИзмерениеНоменклатура = ИсточникМетаданные.Измерения.Найти("Номенклатура") <> Неопределено;
	ЕстьИзмерениеВидНоменклатуры = ИсточникМетаданные.Измерения.Найти("ВидНоменклатуры") <> Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;	
	Если ЕстьИзмерениеНоменклатура Тогда
		СписокНоменклатуры = Новый СписокЗначений;
		Для каждого ЭлементЗаписи Из ТаблицаЗаписей Цикл
			СписокНоменклатуры.Добавить(ЭлементЗаписи.Номенклатура);	
		КонецЦикла; 
		Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	КонецЕсли;
	
	Если ЕстьИзмерениеКПП И ЕстьИзмерениеВидНоменклатуры Тогда
		СписокКПП = Новый СписокЗначений;
		СписокВидовНоменклатуры = Новый СписокЗначений;
		Для каждого ЭлементЗаписи Из ТаблицаЗаписей Цикл
			СписокКПП.Добавить(ЭлементЗаписи.КПП);
			СписокВидовНоменклатуры.Добавить(ЭлементЗаписи.ВидНоменклатуры);	
		КонецЦикла; 
		Запрос.УстановитьПараметр("СписокКПП", СписокКПП);
		Запрос.УстановитьПараметр("СписокВидовНоменклатуры", СписокВидовНоменклатуры);
		
		ТекстЗапроса = ПолучитьТекстЗапросаКешПоВидуНоменклатуры(); 
			
		Если ТипЗначения = Тип("РегистрСведенийНаборЗаписей.КПП_КоэффициентыПриоритетностиПоВидуНоменклатурыОпт") Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРегистра%", "КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж");	
		КонецЕсли;
		
		Если ТипЗначения = Тип("РегистрСведенийНаборЗаписей.КПП_КоэффициентыПриоритетностиПоВидуНоменклатурыРозница") Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРегистра%", "КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж");	
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Если ЕстьИзмерениеКПП И ЕстьИзмерениеВидНоменклатуры Тогда
		ТекстЗапроса = СтрЗаменить(ПолучитьТекстЗапросаКешаНоменклатуры(), "%Параметры%", " Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ПредварительныйКеш)");
	КонецЕсли;
	
	Если ЕстьИзмерениеНоменклатура Тогда
		ТекстЗапроса = СтрЗаменить(ПолучитьТекстЗапросаКешаНоменклатуры(), "%Параметры%", " Номенклатура В (&СписокНоменклатуры)");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();	
	ОбработатьИзмененияДополнительныхДанныхНоменклатурыКПП(Запрос, ТипЗначения);
	Запрос.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ВыполнитьОбновлениеЗаработкаМенеджераКПП()

// Процедура обновляет заработок менеджера в свойствах номенклатуры при изменении цены
// 
// Параметры:
//  Источник - ДокументСсылка.УстановкаЦенНоменклатуры - документ установки цен
//
Процедура ОбновитьЗаработокИзДокументаУстановкаЦенНоменклатуры(ДокументСсылка) Экспорт
	
	ВидЦеныРозница = Справочники.ВидыЦен.ПолучитьСсылку(Новый УникальныйИдентификатор("8bdf84ff-404c-11e0-9f98-001517115d85"));
	ВидЦеныПартнер = Справочники.ВидыЦен.ПолучитьСсылку(Новый УникальныйИдентификатор("8bdf84fc-404c-11e0-9f98-001517115d85"));

	Если ДокументСсылка.ВидыЦен.Найти(ВидЦеныРозница, "ВидЦены") <> Неопределено Тогда
		ОбновитьЗаработокИзДокументаУстановкаЦенНоменклатуры_2(ДокументСсылка, ВидЦеныРозница, Тип("РегистрСведенийНаборЗаписей.КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж"));	
	КонецЕсли;
	
	Если ДокументСсылка.ВидыЦен.Найти(ВидЦеныПартнер, "ВидЦены") <> Неопределено Тогда
		ОбновитьЗаработокИзДокументаУстановкаЦенНоменклатуры_2(ДокументСсылка, ВидЦеныПартнер, Тип("РегистрСведенийНаборЗаписей.КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж"));	
	КонецЕсли;
			
КонецПроцедуры // ОбновитьЗаработокИзДокументаУстановкаЦенНоменклатуры()

// Процедура обновляет заработок менеджера в свойствах номенклатуры для всей доступной номенклатуры
// 
// Параметры:
//  Нет
//
Процедура ОбновитьЗаработокКПП() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ТекстЗапроса = СтрЗаменить(ПолучитьТекстЗапросаКешаНоменклатуры(), "%Параметры%", "");
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
	ОбработатьИзмененияДополнительныхДанныхНоменклатурыКПП(Запрос, Тип("РегистрСведенийНаборЗаписей.КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж"));	
	ОбработатьИзмененияДополнительныхДанныхНоменклатурыКПП(Запрос, Тип("РегистрСведенийНаборЗаписей.КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж"));	

	Запрос.МенеджерВременныхТаблиц.Закрыть();
	
	ОбнулитьДополнительныхДанныхНоменклатурыБезКПП();
	
КонецПроцедуры // ОбновитьЗаработокКПП()


// Процедура обновляет старую цену
// 
// Параметры:
//  Нет
//
Процедура ВыполнитьОбновлениеСтарыхЦенНоменклатуры() Экспорт
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ТекстЗапроса = СтрЗаменить(ПолучитьТекстЗапросаКешаНоменклатуры(), "%Параметры%", "");
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
	СвойствоСтараяЦена = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(Новый УникальныйИдентификатор("99ee07fc-c6c9-11e2-b7da-001e673c80fc"));
	НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Свойство.Установить(СвойствоСтараяЦена);
	
	Запрос.Текст = ПолучитьТекстЗапросаСтарыхЦен();
    Запрос.УстановитьПараметр("СвойствоСтараяЦена", СвойствоСтараяЦена);
	Запрос.УстановитьПараметр("Розница", Справочники.ВидыЦен.ПолучитьСсылку(Новый УникальныйИдентификатор("8bdf84ff-404c-11e0-9f98-001517115d85")));
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	Выборка = РезультатПакета[РезультатПакета.ВГраница()].Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);
		Если Выборка.Значение <> 0  Тогда
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЕсли; 
		НаборЗаписей.Записать();
		НаборЗаписей.Очистить();
	КонецЦикла;
	
	Запрос.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ВыполнитьОбновлениеСтарыхЦенНоменклатуры()



// Подготавливает управляемую форму к работе с коефициентом приоритетности продаж 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма документа
//  ВнешнийОбъект 	- Неопределено, ОбработкаОбъект - Внешний объект для переопределения событий формы
//  ИмяОбработки 	- Строка           				- Имя внешней обработки КПП для подключения методов на клиенте
//
Процедура ПодготовитьКоефициентПриоритетностиПродажКРаботе(ЭтаФорма, ВнешнийОбъект, ИмяОбработки = "") Экспорт
	
	СсылкаНаОбъект = ЭтаФорма.Объект.Ссылка;
	Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Номенклатура") Тогда
		ПодготовитьНоменклатуру(ЭтаФорма, ВнешнийОбъект, ИмяОбработки);
		ЗагрузитьЗначения_КПП_ВФормуСправочника(ЭтаФорма);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.ВидыНоменклатуры") Тогда
		ПодготовитьВидыНоменклатуры(ЭтаФорма, ВнешнийОбъект, ИмяОбработки);
	КонецЕсли;
		
КонецПроцедуры // ПодготовитьКоефициентПриоритетностиПродажКРаботе() 

// Рассылка писем по задаче http://r.ktc-ua.com/issues/4749
//
// Параметры:
//  Источник  		 - ДокументОбъект.ЗаказКлиента - ссылка на заказ клиента
//  Отказ  			 - Булево
//  РежимПроведения  - 
//
Процедура ВыполнитьРассылкуНоменклатурыПоЗаказу(Источник, Отказ, РежимПроведения) Экспорт

	Если Отказ Тогда
		Возврат;	
	КонецЕсли;
	
	//http://r.ktc-ua.com/issues/5250
	Если ЭтоДокументИзНеОсновныхСкладов(Источник.Склад) Тогда
		Возврат;		
	КонецЕсли;
	//#5250
		
	УчетнаяЗаписьЭлектроннойПочты = Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты;
	
	Запрос 			= Новый Запрос;
	УстановитьПараметрыЗапросаКППРассылкаНоменклатурыБезКПП(Источник,Запрос);
	ВыборкаЭлАдрес = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		

	Пока ВыборкаЭлАдрес.Следующий() Цикл
		Кому    = Новый Массив;
		РазделительАдресов = РазобратьСтрокуВМассивПоРазделителю(ВыборкаЭлАдрес.ЭлАдрес, ",");
		Для каждого Адреса Из РазделительАдресов Цикл
			Кому.Добавить(Новый Структура("Адрес, Представление", Адреса,""));
		КонецЦикла; 
		
		ТекстПисьма = "";
		ВыборкаЗП = ВыборкаЭлАдрес.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаЗП.Следующий() Цикл
			ТекстПисьма = ТекстПисьма+"<h2>"+ПолучитьНавигационнуюСсылку(ВыборкаЗП.Заказ) +"</h2><BR>";
			ТекстПисьма = ТекстПисьма+"<table border=""1"">";
			Выборка = ВыборкаЗП.Выбрать();
			Пока Выборка.Следующий() Цикл
				ТекстПисьма = ТекстПисьма+"<tr>"+Выборка.Номенклатура+"</tr>";
			КонецЦикла;
			Текст = ТекстПисьма+"</table>";
			
			Тема = "По заказу "+?(ТипЗнч(Источник) = Тип("ДокументОбъект.ЗаказКлиента"),"клиента ","поставщика ")+"№("+Источник.Номер+") "+"не установлен КПП";
			ПараметрыПисьма 	= ktcОбщийМодульПротоколов.СформироватьПараметрыПисьма(Тема,Текст, Кому,,,,,, ТипТекстаПочтовогоСообщения.HTML);
			ktcОбщийМодульПротоколов.ОтправитьСообщениеПоSMTP(УчетнаяЗаписьЭлектроннойПочты, ПараметрыПисьма);
		КонецЦикла;
 	КонецЦикла;
	
КонецПроцедуры // ВыполнитьРозсылкуНоменклатурыПоЗаказуПоставщику()


// Функция для роботы со строками
//
Функция РазобратьСтрокуВМассивПоРазделителю(Строка, Разделитель = ",") 
    МассивСтрок  = Новый Массив;
    СтрокаЗамены = СтрЗаменить(Строка, Разделитель, Символы.ПС);
	Для Индекс=1 По СтрЧислоСтрок(СтрокаЗамены) Цикл 
		МассивСтрок.Добавить(СтрПолучитьСтроку(СтрокаЗамены, Индекс));
	КонецЦикла;
    Возврат МассивСтрок; 
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Подготавливает форму справочника номенклатуры к работе с коефициентом приоритетности продаж 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма справчника номенклатуры
//  ВнешнийОбъект 	- Неопределено, ОбработкаОбъект - Внешний объект для переопределения событий формы
//  ИмяОбработки 	- Строка           				- Имя внешней обработки коефициента приоритетности продаж для подключения методов на клиенте
//
Процедура ПодготовитьНоменклатуру(ЭтаФорма, ВнешнийОбъект, ИмяОбработки)
	
	Элементы = ЭтаФорма.Элементы;	
	ГруппаКартинка = Элементы.Найти("ГруппаКартинка");
		
	ДобавляемыеРеквизиты = Новый Массив; 	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ККП_Опт", Новый ОписаниеТипов("СправочникСсылка.КоефициентПриоритетностиНоменклатуры")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ККП_Розница", Новый ОписаниеТипов("СправочникСсылка.КоефициентПриоритетностиНоменклатуры")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("НеПересчитыватьАвтоматически_Опт", Новый ОписаниеТипов("Булево")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("НеПересчитыватьАвтоматически_Розница", Новый ОписаниеТипов("Булево")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КоефициентПриоритетностиПродажИмяБибилиотеки",  Новый ОписаниеТипов("Строка")));
	
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	ЭтаФорма.КоефициентПриоритетностиПродажИмяБибилиотеки = ИмяОбработки;
			
	Параметры = Новый Структура;
	Параметры.Вставить("Имя", 							"Группа_КПП_Розница");
	Параметры.Вставить("Заголовок",						"Коефициент приоритетности продаж (розница)");
	Параметры.Вставить("Вид", 							ВидГруппыФормы.ОбычнаяГруппа);
	Параметры.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	Параметры.Вставить("Отображение", ОтображениеОбычнойГруппы.СильноеВыделение);
	Параметры.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная);
	Параметры.Вставить("ОтображатьЗаголовок", Истина);
	Параметры.Вставить("ЦветТекстаЗаголовка", Новый Цвет(0, 0, 0));
	Параметры.Вставить("ТипЭлемента", Тип("ГруппаФормы"));
	Группа_КПП_Розница = ДобавитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, Параметры, ГруппаКартинка);
	
	    Параметры = Новый Структура;
		Параметры.Вставить("Имя", "ККП_Розница");
		Параметры.Вставить("Вид", ВидПоляФормы.ПолеВвода);
		Параметры.Вставить("ПутьКДанным", "ККП_Розница");
		Параметры.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Нет);
		Параметры.Вставить("КнопкаОчистки", Истина);
		Параметры.Вставить("РедактированиеТекста", Ложь);
		Параметры.Вставить("БыстрыйВыбор", Истина);
		Параметры.Вставить("ЦветФона", Новый Цвет(255, 239, 213));		
		Параметры.Вставить("РастягиватьПоГоризонтали", Ложь);
		Параметры.Вставить("ТипЭлемента", Тип("ПолеФормы"));
		ПолеВвода_КПП_Розница = ДобавитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, Параметры, Группа_КПП_Розница);
		ПолеВвода_КПП_Розница.УстановитьДействие("ПриИзменении", "КПП_ПриИзмененииДанных_Розница");
		
		
		Параметры = Новый Структура;
		Параметры.Вставить("Имя", "НеПересчитыватьАвтоматически_Розница");
		Параметры.Вставить("Заголовок",	"Не изменять автоматически");
		Параметры.Вставить("Вид", ВидПоляФормы.ПолеФлажка);
		Параметры.Вставить("ПутьКДанным", "НеПересчитыватьАвтоматически_Розница");
		Параметры.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Авто);
		Параметры.Вставить("Подсказка", "Этот флажок позволяет зафиксировать КПП(розница) для данной номенклатуры");
		Параметры.Вставить("ОтображениеПодсказки", ОтображениеПодсказки.Кнопка);
		Параметры.Вставить("ТипЭлемента", Тип("ПолеФормы"));
		ПолеФлажка_КПП_Розница = ДобавитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, Параметры, Группа_КПП_Розница);
		ПолеФлажка_КПП_Розница.УстановитьДействие("ПриИзменении", "КПП_ПриИзмененииДанных_Розница");
		
	Параметры = Новый Структура;
	Параметры.Вставить("Имя", 							"Группа_КПП_Опт");
	Параметры.Вставить("Заголовок",						"Коефициент приоритетности продаж (опт)");
	Параметры.Вставить("Вид", 							ВидГруппыФормы.ОбычнаяГруппа);
	Параметры.Вставить("РазрешитьИзменениеСостава", 	Ложь);
	Параметры.Вставить("Отображение", ОтображениеОбычнойГруппы.СильноеВыделение);
	Параметры.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная);
	Параметры.Вставить("ОтображатьЗаголовок", Истина);
	Параметры.Вставить("ЦветТекстаЗаголовка", Новый Цвет(0, 0, 0));
	Параметры.Вставить("ТипЭлемента", Тип("ГруппаФормы"));
	Группа_КПП_Опт = ДобавитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, Параметры, ГруппаКартинка);
	
	    Параметры = Новый Структура;
		Параметры.Вставить("Имя", "ККП_Опт");
		Параметры.Вставить("Вид", ВидПоляФормы.ПолеВвода);
		Параметры.Вставить("ПутьКДанным", "ККП_Опт");
		Параметры.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Нет);
		Параметры.Вставить("КнопкаОчистки", Истина);
		Параметры.Вставить("РедактированиеТекста", Ложь);
		Параметры.Вставить("БыстрыйВыбор", Истина);
		Параметры.Вставить("ЦветФона", Новый Цвет(255, 239, 213));		
		Параметры.Вставить("РастягиватьПоГоризонтали", Ложь);
		Параметры.Вставить("ТипЭлемента", Тип("ПолеФормы"));
		ПолеВвода_КПП_Розница = ДобавитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, Параметры, Группа_КПП_Опт);
		ПолеВвода_КПП_Розница.УстановитьДействие("ПриИзменении", "КПП_ПриИзмененииДанных_Опт");
		
		
		Параметры = Новый Структура;
		Параметры.Вставить("Имя", "НеПересчитыватьАвтоматически_Опт");
		Параметры.Вставить("Заголовок",	"Не изменять автоматически");
		Параметры.Вставить("Вид", ВидПоляФормы.ПолеФлажка);
		Параметры.Вставить("ПутьКДанным", "НеПересчитыватьАвтоматически_Опт");
		Параметры.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Авто);
		Параметры.Вставить("Подсказка", "Этот флажок позволяет зафиксировать КПП(опт) для данной номенклатуры");
		Параметры.Вставить("ОтображениеПодсказки", ОтображениеПодсказки.Кнопка);
		Параметры.Вставить("ТипЭлемента", Тип("ПолеФормы"));
		ПолеФлажка_КПП_Розница = ДобавитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, Параметры, Группа_КПП_Опт);
		ПолеФлажка_КПП_Розница.УстановитьДействие("ПриИзменении", "КПП_ПриИзмененииДанных_Опт");
	
КонецПроцедуры // ПодготовитьНоменклатуру()

Процедура ЗагрузитьЗначения_КПП_ВФормуСправочника(ЭтаФорма)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", ЭтаФорма.Объект.Ссылка);
	Запрос.Текст = ПолучитьТекстЗапроса_КПП_Розница();
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Выборка, "ККП_Розница, НеПересчитыватьАвтоматически_Розница");		
	КонецЕсли;
	
	Запрос.Текст = ПолучитьТекстЗапроса_КПП_Опт();
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Выборка, "ККП_Опт, НеПересчитыватьАвтоматически_Опт");		
	КонецЕсли;
	
КонецПроцедуры // ЗагрузитьЗначения_КПП_ВФормуСправочника()

// Подготавливает форму справочника виды номенклатуры к работе с коефициентом приоритетности продаж 
//
// Параметры:
//  ЭтаФорма 		- УправляемаяФорма 				- Форма справчника виды номенклатуры
//  ВнешнийОбъект 	- Неопределено, ОбработкаОбъект - Внешний объект для переопределения событий формы
//  ИмяОбработки 	- Строка           				- Имя внешней обработки коефициента приоритетности продаж для подключения методов на клиенте
//
Процедура ПодготовитьВидыНоменклатуры(ЭтаФорма, ВнешнийОбъект, ИмяОбработки)
	
	Элементы = ЭтаФорма.Элементы;	
	ГруппаСтраницаОсновное = Элементы.Найти("СтраницаОсновное");
		
	ДобавляемыеРеквизиты = Новый Массив; 	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КоефициентПриоритетностиПродажИмяБибилиотеки"	, Новый ОписаниеТипов("Строка")));
	
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	ЭтаФорма.КоефициентПриоритетностиПродажИмяБибилиотеки = ИмяОбработки;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Имя"						, "Группа_КПП");
	Параметры.Вставить("Заголовок"					, "Коефициент приоритетности продаж");
	Параметры.Вставить("Вид"						, ВидГруппыФормы.ОбычнаяГруппа);
	Параметры.Вставить("РазрешитьИзменениеСостава"	, Ложь);
	Параметры.Вставить("Отображение"				, ОтображениеОбычнойГруппы.СильноеВыделение);
	Параметры.Вставить("Группировка"				, ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	Параметры.Вставить("ОтображатьЗаголовок"		, Истина);
	Параметры.Вставить("ЦветТекстаЗаголовка"		, Новый Цвет(0, 0, 0));
	Параметры.Вставить("ТипЭлемента"				, Тип("ГруппаФормы"));
	Группа_КПП = ДобавитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, Параметры, ГруппаСтраницаОсновное);
	ПереместитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, "Группа_КПП", "СтраницаОсновное", "ГруппаНаименованиеДляПечатиИДекорация");

		Параметры = Новый Структура;
		Параметры.Вставить("Имя"						, "УстанавливатьАвтоматически_Розница");
		Параметры.Вставить("Заголовок"					, "Устанавливать по умолчанию КПП для товара на остатках, но без КПП (розница)");
		Параметры.Вставить("Вид"						, ВидПоляФормы.ПолеФлажка);
		Параметры.Вставить("ПутьКДанным"				, "Объект.УстанавливатьАвтоматически_Розница");
		Параметры.Вставить("ПоложениеЗаголовка"			, ПоложениеЗаголовкаЭлементаФормы.Авто);
		Параметры.Вставить("Подсказка"					, "Для товару, який є на залишках, але КПП (роздріб) для нього не встановлено буде автоматично встановлюватись вказане Вами КПП (роздріб)");
		Параметры.Вставить("ОтображениеПодсказки"		, ОтображениеПодсказки.Кнопка);
		Параметры.Вставить("ТипЭлемента"				, Тип("ПолеФормы"));
		ПолеФлажка_КПП_Розница = ДобавитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, Параметры, Группа_КПП);
		ПолеФлажка_КПП_Розница.УстановитьДействие("ПриИзменении", "КПП_ПриИзмененииДанных_Розница");

		Параметры = Новый Структура;
		Параметры.Вставить("Имя"						, "УстанавливатьАвтоматически_Опт");
		Параметры.Вставить("Заголовок"					, "Устанавливать по умолчанию КПП для товара на остатках, но без КПП (опт)");
		Параметры.Вставить("Вид"						, ВидПоляФормы.ПолеФлажка);
		Параметры.Вставить("ПутьКДанным"				, "Объект.УстанавливатьАвтоматически_Опт");
		Параметры.Вставить("ПоложениеЗаголовка"			, ПоложениеЗаголовкаЭлементаФормы.Авто);
		Параметры.Вставить("Подсказка"					, "Для товару, який є на залишках, але КПП (опт) для нього не встановлено буде автоматично встановлюватись вказане Вами КПП (опт)");
		Параметры.Вставить("ОтображениеПодсказки"		, ОтображениеПодсказки.Кнопка);
		Параметры.Вставить("ТипЭлемента"				, Тип("ПолеФормы"));
		ПолеФлажка_КПП_Опт = ДобавитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, Параметры, Группа_КПП);
		ПолеФлажка_КПП_Розница.УстановитьДействие("ПриИзменении", "КПП_ПриИзмененииДанных_Опт");
	
КонецПроцедуры // ПодготовитьВидыНоменклатуры()



// Только для внутреннего использования
Процедура ОбновитьЗаработокИзДокументаУстановкаЦенНоменклатуры_2(ДокументСсылка, ВидЦены, ТипЗначения)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ПолучитьТекстЗапросаКешаНоменклатурыИзДокументаУстановкаЦенНоменклатуры();
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	Запрос.Выполнить();
    ОбработатьИзмененияДополнительныхДанныхНоменклатурыКПП(Запрос, ТипЗначения);
	Запрос.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбновитьЗаработокИзДокументаУстановкаЦенНоменклатуры_2()

// Только для внутреннего использования
Процедура ОбработатьИзмененияДополнительныхДанныхНоменклатурыКПП(Запрос, ТипЗначения)
		
	ТекстЗапроса = ПолучитьТекстЗапросаЗаработокМенеджераКПП();
	Если ТипЗначения = Тип("РегистрСведенийНаборЗаписей.КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж")
	 ИЛИ ТипЗначения = Тип("РегистрСведенийНаборЗаписей.КПП_КоэффициентыПриоритетностиПоВидуНоменклатурыОпт") Тогда
		ВидЦены = Справочники.ВидыЦен.ПолучитьСсылку(Новый УникальныйИдентификатор("8bdf84fc-404c-11e0-9f98-001517115d85")); // Партнерська
	  	СвойствоЗаработок = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(Новый УникальныйИдентификатор("904cd00e-c6ba-11e2-b7da-001e673c80fc"));
	  	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРегистраЗКПП%", 	"КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж");
	  	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРегистраКППВНР%", "КПП_КоэффициентыПриоритетностиПоВидуНоменклатурыОпт");	
	КонецЕсли;
	
	Если ТипЗначения = Тип("РегистрСведенийНаборЗаписей.КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж")
	 ИЛИ ТипЗначения = Тип("РегистрСведенийНаборЗаписей.КПП_КоэффициентыПриоритетностиПоВидуНоменклатурыРозница") Тогда
		ВидЦены = Справочники.ВидыЦен.ПолучитьСсылку(Новый УникальныйИдентификатор("8bdf84ff-404c-11e0-9f98-001517115d85")); // Розница 
	 	СвойствоЗаработок = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(Новый УникальныйИдентификатор("ac5acd51-31ad-11e4-80d4-001e676b0175"));
	 	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРегистраЗКПП%", 	"КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРегистраКППВНР%", "КПП_КоэффициентыПриоритетностиПоВидуНоменклатурыРозница");	
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Свойство.Установить(СвойствоЗаработок);
		
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	Запрос.УстановитьПараметр("ВалютаРегламентированогоУчета", 	Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", 	Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("СвойствоЗаработок", СвойствоЗаработок);
		
	РезультатПакета = Запрос.ВыполнитьПакет();
	Выборка = РезультатПакета[14].Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);
		Если Выборка.Значение <> 0  Тогда
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЕсли;
		НаборЗаписей.Записать();
		НаборЗаписей.Очистить();
	КонецЦикла; 
		
КонецПроцедуры // ОбработатьИзмененияДополнительныхДанныхНоменклатуры()




// Процедура обнуляет заработок менеджера в свойствах номенклатуры без КПП
// 
// Параметры:
//  Нет
//
Процедура ОбнулитьДополнительныхДанныхНоменклатурыБезКПП()

	Запрос			= Новый Запрос();
	Запрос.Текст	= ПолучитьТекстЗапросаОбнуленияДополнительныхДанныхНоменклатурыКПП();
	
	УИ = Новый УникальныйИдентификатор("ac5acd51-31ad-11e4-80d4-001e676b0175"); //Заработок менеджера (Розница)
	СвойствоЗаработок = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(УИ);
	Запрос.УстановитьПараметр("Свойство",СвойствоЗаработок);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Количество() Тогда 
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Свойство.Установить(СвойствоЗаработок);
	Пока Выборка.Следующий() Цикл
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		НаборЗаписей.Записать();
		НаборЗаписей.Очистить();
	КонецЦикла;

КонецПроцедуры

// Текст запроса для получения номенклатуры у которой нужно очистить з/п менеджера
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Строка  - текст запроса
//
Функция ПолучитьТекстЗапросаОбнуленияДополнительныхДанныхНоменклатурыКПП()
	
	ТекстЗапроса =
	
	    ////////////////////////////////////////////////////////////////////////////////
		// 0. Получаем не актуальную номенклатуру
		"ВЫБРАТЬ
		|	Номенклатура
		|ПОМЕСТИТЬ ВТ_НеАктуальнаяНоменклатура
		|ИЗ
		|	РегистрСведений.ALPS_АктуальностьНоменклатуры.СрезПоследних(, )
		|ГДЕ
		|	Актуально = ЛОЖЬ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		////////////////////////////////////////////////////////////////////////////////
		//1. Получаем остатки не актуальной номенклатуры
		|ВЫБРАТЬ
		|	Номенклатура,
		|	КоличествоОстаток
		|ПОМЕСТИТЬ ВТ_ОстаткиНеАктуальногоТовара
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций.Остатки(
		|			,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					Номенклатура
		|				ИЗ
		|					ВТ_НеАктуальнаяНоменклатура))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		////////////////////////////////////////////////////////////////////////////////
		//2. Получаем не актуальную номенклатуру, которой нет на остатках 
		//   и с заполненой з/п менеджера
		|ВЫБРАТЬ
		|	ВТ_НеАктуальнаяНоменклатура.Номенклатура
		|ПОМЕСТИТЬ ВТ_НоменклатураСЗаработкомМенеджера
		|ИЗ
		|	ВТ_НеАктуальнаяНоменклатура КАК ВТ_НеАктуальнаяНоменклатура
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|	ПО 
		|		ВТ_НеАктуальнаяНоменклатура.Номенклатура	=	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.Номенклатура)
		|	И 	ДополнительныеСведения.Свойство 			=	&Свойство
		|	И	ДополнительныеСведения.Значение 			<>	0
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиНеАктуальногоТовара КАК ВТ_ОстаткНеАктуальногоТовара
		|	ПО 
		|		ВТ_НеАктуальнаяНоменклатура.Номенклатура = ВТ_ОстаткНеАктуальногоТовара.Номенклатура
		|	
		|ГДЕ
		|	ЕСТЬNULL(ВТ_ОстаткНеАктуальногоТовара.КоличествоОстаток, 0) = 0
		|;
		|
		////////////////////////////////////////////////////////////////////////////////
		//3. Обнуляем заработок менеджера у номенклатуры без КПП
		|ВЫБРАТЬ
		|	Номенклатура	КАК Объект,
		|	&Свойство		КАК Свойство,
		|	0				КАК Значение
		|ИЗ
		|	РегистрСведений.КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж.СрезПоследних(
		|			,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					Номенклатура
		|				ИЗ
		|					ВТ_НоменклатураСЗаработкомМенеджера)) 
		|ГДЕ
		|	Значение = ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка)";	

	Возврат ТекстЗапроса;
		
КонецФункции // ПолучитьТекстЗапросаОбнуленияДополнительныхДанныхНоменклатурыКПП()




// Возвращает текст запроса кеша номенклатуры по виду номеклатуры
// 
// Параметры:
//  Нет
//
// Возвращаемое значение:
//    Строка - Текст запроса
Функция ПолучитьТекстЗапросаКешПоВидуНоменклатуры()
	
	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	Ссылка КАК ВидНоменклатуры
		|ПОМЕСТИТЬ КешВидовНоменклатуры
		|ИЗ
		|	Справочник.ВидыНоменклатуры
		|ГДЕ
		|	Ссылка В (&СписокВидовНоменклатуры)
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидНоменклатуры
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	СпрНоменклатура.Ссылка КАК Номенклатура
		|ПОМЕСТИТЬ КешНоменклатуры
		|ИЗ
		|	Справочник.Номенклатура КАК СпрНоменклатура
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешВидовНоменклатуры КАК КешВидовНоменклатуры
		|ПО	КешВидовНоменклатуры.ВидНоменклатуры = СпрНоменклатура.Ссылка.ВидНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура	
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ПредварительныйКеш
		|ИЗ
		|	РегистрСведений.%ИмяРегистра%.СрезПоследних(, Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ КешНоменклатуры))
		|ГДЕ
		|	Значение В (&СписокКПП)
		|;
		|УНИЧТОЖИТЬ КешВидовНоменклатуры;
		|УНИЧТОЖИТЬ КешНоменклатуры;
		|";
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса кеша номенклатуры
// 
// Параметры:
//  Нет
//
// Возвращаемое значение:
//    Строка - Текст запроса
Функция ПолучитьТекстЗапросаКешаНоменклатуры()
	
	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////////////////
		//Временная таблица номеклатуры в наличии и актуальной номенклатуры
		|ВЫБРАТЬ
		|	Вл.Номенклатура
		|ПОМЕСТИТЬ Cache_Номенклатура	
		|ИЗ
		|	(
		|		ВЫБРАТЬ
		|			Номенклатура	
		|		ИЗ
		|			РегистрНакопления.ТоварыОрганизаций.Остатки(, %Параметры%)
		|			
		|		ОБЪЕДИНИТЬ
		|		
		|		ВЫБРАТЬ
		|			Номенклатура
		|		ИЗ
		|			РегистрСведений.ALPS_АктуальностьНоменклатуры.СрезПоследних(, %Параметры%)
		|       ГДЕ
		|			Актуально
		|	) КАК Вл
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Вл.Номенклатура
		|";
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапросаКешаНоменклатуры()

// Возвращает текст запроса кеша номенклатуры из источника
// 
// Параметры:
//  Нет
//
// Возвращаемое значение:
//    Строка - Текст запроса
Функция ПолучитьТекстЗапросаКешаНоменклатурыИзДокументаУстановкаЦенНоменклатуры()
	
	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////////////////
		//Временная таблица номеклатуры из источника
		|ВЫБРАТЬ
		|	Номенклатура
		|ПОМЕСТИТЬ Cache_Номенклатура	
		|ИЗ
		|	Документ.УстановкаЦенНоменклатуры.Товары
		|
		|ГДЕ
		|	Ссылка  = &Ссылка	
		|И	ВидЦены = &ВидЦены
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;";
	Возврат ТекстЗапроса;			
	
КонецФункции // ПолучитьТекстЗапросаКешаНоменклатурыИзДокументаУстановкаЦенНоменклатуры()


// Возвращает текст запроса заработка менеджера по КПП
// 
// Параметры:
//  Нет
//
// Возвращаемое значение:
//    Строка - Текст запроса
Функция ПолучитьТекстЗапросаЗаработокМенеджераКПП()

	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////
		//0.
		|ВЫБРАТЬ
		|	СпрНоменклатура.Номенклатура 						КАК Номенклатура,
		|	СпрНоменклатура.Номенклатура.ВидНоменклатуры		КАК ВидНоменклатуры,
		|	ЗначениеКоефициентаПриоритетностиПродаж.Значение 	КАК КПП
		|ПОМЕСТИТЬ КешНоменклатура0
		|ИЗ
		|	Cache_Номенклатура КАК СпрНоменклатура
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.%ИмяРегистраЗКПП%.СрезПоследних(, Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ Cache_Номенклатура)) КАК ЗначениеКоефициентаПриоритетностиПродаж
		|ПО	ЗначениеКоефициентаПриоритетностиПродаж.Номенклатура = СпрНоменклатура.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидНоменклатуры,
		|	КПП
		|;
		////////////////////////////////////////////////////////////////////////////////
		//|УНИЧТОЖИТЬ Cache_Номенклатура;
		////////////////////////////////////////////////////////////////////////////////
		//2.
		|ВЫБРАТЬ
		|	СпрНоменклатура.Номенклатура КАК Номенклатура,
		|	IsNull(КППВНР.Значение, 0)   КАК Значение
		|ПОМЕСТИТЬ КешНоменклатура1
		|ИЗ
		|	КешНоменклатура0 КАК СпрНоменклатура		
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.%ИмяРегистраКППВНР%.СрезПоследних(, (ВидНоменклатуры, КПП) В (ВЫБРАТЬ ВидНоменклатуры, КПП ИЗ КешНоменклатура0)) КАК КППВНР
		|ПО КППВНР.ВидНоменклатуры = СпрНоменклатура.ВидНоменклатуры
		|И	КППВНР.КПП = СпрНоменклатура.КПП
		|;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешНоменклатура0;
		////////////////////////////////////////////////////////////////////////////////
		//4.
		|ВЫБРАТЬ
		|	Цены.Номенклатура 		КАК Номенклатура,
		|	Цены.Цена 				КАК Цена,
		|	Цены.ВидЦены.ВалютаЦены КАК Валюта
		|ПОМЕСТИТЬ ТекущиеЦены
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|												&ТекущаяДата,
		|												Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ КешНоменклатура1) И ВидЦены = &ВидЦены 
		|												) КАК Цены
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура	
		|;
		////////////////////////////////////////////////////////////////////////////////
		//5. 
		|ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
		|	КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность КАК Курс
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&ТекущаяДата, ) КАК КурсыВалютСрезПоследних
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		////////////////////////////////////////////////////////////////////////////////
		//6
		|ВЫБРАТЬ
		|	ТекущиеЦены.Номенклатура КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА ТекущиеЦены.Валюта = &ВалютаРегламентированогоУчета
		|			ТОГДА ТекущиеЦены.Цена
		|		ИНАЧЕ ТекущиеЦены.Цена * (ЕСТЬNULL(КурсыВалютЦены.Курс, 0) / ЕСТЬNULL(КурсыВалютРегл.Курс, 1))
		|	КОНЕЦ КАК ЦенаВВалютеРеглУчета
		|ПОМЕСТИТЬ ЦенаВРегламентированомУчете
		|ИЗ
		|	ТекущиеЦены КАК ТекущиеЦены
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютЦены
		|ПО ТекущиеЦены.Валюта = КурсыВалютЦены.Валюта
		|		
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУпр
		|ПО (КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета)
		|		
		|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютРегл
		|ПО (КурсыВалютРегл.Валюта = &ВалютаРегламентированогоУчета)
		|;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КурсыВалют;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТекущиеЦены;
		////////////////////////////////////////////////////////////////////////////////
		//9.
		|ВЫБРАТЬ
		|	СпрНоменклатура.Номенклатура 											КАК Номенклатура,
		|	СпрНоменклатура.Значение * IsNull(Цены.ЦенаВВалютеРеглУчета, 0) * 0.01 	КАК Значение
		|ПОМЕСТИТЬ КешНоменклатура2
		|ИЗ
		|	КешНоменклатура1 КАК СпрНоменклатура
		|		
		|ЛЕВОЕ СОЕДИНЕНИЕ ЦенаВРегламентированомУчете КАК Цены
		|ПО Цены.Номенклатура = СпрНоменклатура.Номенклатура 
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СпрНоменклатура.Номенклатура
		|;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешНоменклатура1;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЦенаВРегламентированомУчете;
		////////////////////////////////////////////////////////////////////////////////
		//12.
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДопСведения.Объект КАК Справочник.Номенклатура) 	КАК Номенклатура,
		|	ДопСведения.Значение 										КАК Значение
		|ПОМЕСТИТЬ ДопСведения
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДопСведения
		|ГДЕ
		|	ВЫРАЗИТЬ(ДопСведения.Объект КАК Справочник.Номенклатура) В (ВЫБРАТЬ Номенклатура ИЗ КешНоменклатура2)
		|И	ДопСведения.Свойство = &СвойствоЗаработок
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура 	
		|;
		////////////////////////////////////////////////////////////////////////////////
		//13.
		|ВЫБРАТЬ
		|	КешЗаработка.Номенклатура 	КАК Объект,
		|	&СвойствоЗаработок 			КАК Свойство,
		|	КешЗаработка.Значение 		КАК Значение
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	КешНоменклатура2 КАК КешЗаработка
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ ДопСведения КАК ДопСведения
		|ПО	ДопСведения.Номенклатура = КешЗаработка.Номенклатура
   		|
		|ГДЕ
		|	IsNull(ДопСведения.Значение, -1) <> КешЗаработка.Значение	
		|;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешНоменклатура2;
		////////////////////////////////////////////////////////////////////////////////
		//15.
		|ВЫБРАТЬ
		| 	Данные.Объект 	КАК Объект,
		|   Данные.Свойство КАК Свойство,
		|   Данные.Значение КАК Значение
		|ИЗ
		|	Данные КАК Данные
		|;
	    ////////////////////////////////////////////////////////////////////////////////
	    |УНИЧТОЖИТЬ Данные;
		////////////////////////////////////////////////////////////////////////////////
	    |УНИЧТОЖИТЬ ДопСведения;
		////////////////////////////////////////////////////////////////////////////////
		|";
	
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаЗаработокМенеджераКПП()

// Возвращает текст запроса старых цен
// 
// Параметры:
//  Нет
//
// Возвращаемое значение:
//    Строка - Текст запроса
Функция ПолучитьТекстЗапросаСтарыхЦен()
	
	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ДействующиеЦены.Период			КАК Период,
		|	ДействующиеЦены.Номенклатура 	КАК Номенклатура,
		|	ДействующиеЦены.ВидЦены			КАК ВидЦены,
		|	ДействующиеЦены.Цена         	КАК Цена
		|ПОМЕСТИТЬ ДействующиеЦены
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ Cache_Номенклатура) И ВидЦены = &Розница) КАК ДействующиеЦены
		|ИНДЕКСИРОВАТЬ ПО 
		|	Период, 
		|	Номенклатура,
		|	ВидЦены	
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	СтарыеЦены.Номенклатура		КАК Номенклатура,
		|	Максимум(СтарыеЦены.Период) КАК Период
		|ПОМЕСТИТЬ СтарыеЦеныПериод			
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры КАК СтарыеЦены
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДействующиеЦены КАК ДействующиеЦены
		|ПО	ДействующиеЦены.Период 		 > СтарыеЦены.Период
		|И	ДействующиеЦены.Номенклатура = СтарыеЦены.Номенклатура
		|И	ДействующиеЦены.ВидЦены		 = СтарыеЦены.ВидЦены	
        |
		|СГРУППИРОВАТЬ ПО
		|	СтарыеЦены.Номенклатура
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Номенклатура
		|;	
		////////////////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	СтарыеЦены.Номенклатура	КАК Номенклатура,
		|	СтарыеЦены.Цена		    КАК Цена
		|ПОМЕСТИТЬ СтарыеЦены
		|ИЗ
		|	СтарыеЦеныПериод КАК СтарыеЦеныПериод
        |
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК СтарыеЦены 
		|ПО	СтарыеЦены.Период 		= СтарыеЦеныПериод.Период
		|И	СтарыеЦены.Номенклатура = СтарыеЦеныПериод.Номенклатура
		|И	СтарыеЦены.ВидЦены		= &Розница
        |
		|ИНДЕКСИРОВАТЬ ПО
		|	СтарыеЦены.Номенклатура	
		|;	
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СтарыеЦеныПериод;
		////////////////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		| 	ДействующиеЦены.Номенклатура КАК Номенклатура,
		| 	ВЫБОР
		| 		КОГДА IsNull(СтарыеЦены.Цена, 0) > ДействующиеЦены.Цена
		| 		ТОГДА СтарыеЦены.Цена
		| 		ИНАЧЕ 0
		| 	КОНЕЦ КАК Значение	
		|ПОМЕСТИТЬ ДанныеКЗаписи
		|ИЗ
		|	ДействующиеЦены КАК ДействующиеЦены
        |
		|ЛЕВОЕ СОЕДИНЕНИЕ СтарыеЦены КАК СтарыеЦены
		|ПО СтарыеЦены.Номенклатура = ДействующиеЦены.Номенклатура	
		|;	
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДействующиеЦены;
		////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СтарыеЦены;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДопСведения.Объект КАК Справочник.Номенклатура) 	КАК Номенклатура,
		|	ДопСведения.Значение 										КАК Значение
		|ПОМЕСТИТЬ ДопСведения
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДопСведения
		|ГДЕ
		|	ДопСведения.Свойство = &СвойствоСтараяЦена
        |
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура 	
		|;
		////////////////////////////////////////////////////////////////////////////////
		//
		|ВЫБРАТЬ
		|	ДанныеКЗаписи.Номенклатура 	КАК Объект,
		| 	&СвойствоСтараяЦена			КАК Свойство,
		| 	ДанныеКЗаписи.Значение		КАК Значение
		|ИЗ
		| 	ДанныеКЗаписи КАК ДанныеКЗаписи
		| 	
		|ЛЕВОЕ СОЕДИНЕНИЕ ДопСведения КАК ДопСведения
		|ПО	ДопСведения.Номенклатура = ДанныеКЗаписи.Номенклатура
		| 
		|ГДЕ
		|	IsNull(ДопСведения.Значение, -1) <> ДанныеКЗаписи.Значение
		|;
 	    |";
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапросаСтарыхЦен()


Функция ПолучитьТекстЗапроса_КПП_Розница()
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Период							КАК Период,
		|	Номенклатура					КАК Номенклатура,
		|	Значение						КАК ККП_Розница,	
		|   НеПересчитыватьАвтоматически    КАК НеПересчитыватьАвтоматически_Розница
		|ИЗ
		|	РегистрСведений.КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж.СрезПоследних(, Номенклатура = &Номенклатура)
		|";
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапроса_КПП_Розница()

Функция ПолучитьТекстЗапроса_КПП_Опт()
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Период							КАК Период,
		|	Номенклатура					КАК Номенклатура, 
		|	Значение						КАК ККП_Опт,	
		|   НеПересчитыватьАвтоматически    КАК НеПересчитыватьАвтоматически_Опт
		|ИЗ
		|	РегистрСведений.КПП_Опт_ЗначениеКоефициентаПриоритетностиПродаж.СрезПоследних(, Номенклатура = &Номенклатура)
		|";
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапроса_КПП_Опт()


//http://r.ktc-ua.com/issues/4749 
#Область Задача_4749

Процедура УстановитьПараметрыЗапросаКППРассылкаНоменклатурыБезКПП(Источник, Запрос)  
	
	ЭтоЗаказКлиента = ?(ТипЗнч(Источник)=Тип("ДокументОбъект.ЗаказКлиента"),Истина,Ложь);
	Документ =?(ЭтоЗаказКлиента,"ЗаказКлиента","ЗаказПоставщику");	
	УсловиеЭтоНовый = ?(ЭтоЗаказКлиента Или Источник.ЭтоНовый(), "", 
		"		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПоставщикам.Остатки(
		|				&ТекущаяДата,
		|				(Номенклатура, КодСтроки) В
		|					(ВЫБРАТЬ
		|						КешСсылок.Номенклатура,
		|						КешСсылок.КодСтроки
		|					ИЗ
		|						КешСсылок КАК КешСсылок)) КАК ЗП_Остатки
		|		ПО КешСсылок.Ссылка = ЗП_Остатки.ЗаказПоставщику
		|			И КешСсылок.Номенклатура = ЗП_Остатки.Номенклатура
		|			И КешСсылок.КодСтроки = ЗП_Остатки.КодСтроки");
	
	ТекстЗапроса 	= ?(ЭтоЗаказКлиента,
						ПолучитьТекстЗапросаКППРассылкаНоменклатурыБезКППЗаказКлиента(),
						ПолучитьТекстЗапросаКППРассылкаНоменклатурыБезКППЗаказПоставщику());
	Запрос.Текст	= СтрЗаменить(ТекстЗапроса, "%2", УсловиеЭтоНовый);
	
	УИ = Новый УникальныйИдентификатор("2cdef421-31b5-11e4-80d4-001e676b0175");
	
	Запрос.УстановитьПараметр("ТекущаяДата"	, ТекущаяДата());
	Запрос.УстановитьПараметр("Ссылка"		, Источник.Ссылка);
	Запрос.УстановитьПараметр("Свойство"	, ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(УИ));
	
КонецПроцедуры // УстановитьПараметрыЗапросаКППРассылкаНоменклатурыБезКПП()

Функция ПолучитьТекстЗапросаКППРассылкаНоменклатурыБезКППЗаказПоставщику()

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Ссылка 							КАК Ссылка,
		|	Номенклатура					КАК Номенклатура,
		|	Номенклатура.ВидНоменклатуры 	КАК ВидНоменклатуры,
		|	КодСтроки						КАК КодСтроки
		|ПОМЕСТИТЬ КешСсылок
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары
		|ГДЕ
		|	Ссылка = &Ссылка
		|	И Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)

		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	Номенклатура,
		|	ВидНоменклатуры
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Значение 						КАК ЭлАдрес,
		|	Объект 							КАК Объект
		|ПОМЕСТИТЬ ДопСведения
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения
		|ГДЕ
		|	Объект ССЫЛКА Справочник.ВидыНоменклатуры
		|	И Объект В
		|			(ВЫБРАТЬ
		|				ВидНоменклатуры
		|			ИЗ
		|				КешСсылок)
		|	И Свойство = &Свойство

		|СГРУППИРОВАТЬ ПО
		|	Значение,
		|	Объект

		|ИНДЕКСИРОВАТЬ ПО
		|	Объект
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(Значение, ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка)) КАК КПП,
		|	Номенклатура																			   КАК Номенклатура
		|ПОМЕСТИТЬ КешКПП
		|ИЗ
		|	РегистрСведений.КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж.СрезПоследних(
		|			&ТекущаяДата,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					Номенклатура
		|				ИЗ
		|					КешСсылок ))
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КешСсылок.Номенклатура																			КАК Номенклатура,
		|	ЕСТЬNULL(ДопСведения.ЭлАдрес, """") 															КАК ЭлАдрес,
		|	ЕСТЬNULL(КешКПП.КПП, ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка)) 	КАК КПП,
		|	КешСсылок.Ссылка 																				КАК Заказ
		|ПОМЕСТИТЬ ОсновнойКеш
		|ИЗ
		|	КешСсылок КАК КешСсылок
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДопСведения КАК ДопСведения
		|		ПО КешСсылок.ВидНоменклатуры = ДопСведения.Объект
		|		ЛЕВОЕ СОЕДИНЕНИЕ КешКПП КАК КешКПП
		|		ПО КешСсылок.Номенклатура = КешКПП.Номенклатура
		|	%2
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Номенклатура			КАК Номенклатура,
		|	ЭлАдрес 				КАК ЭлАдрес,
		|	Заказ 					КАК Заказ
		|ИЗ
		|	ОсновнойКеш 
		|ГДЕ
		|	(КПП = ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка)
		|			ИЛИ КПП = ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.Актуальный))
		|ИТОГИ ПО
		|	ЭлАдрес,
		|	Заказ";
	
	Возврат ТекстЗапроса;
	

КонецФункции // ПолучитьТекстЗапросаКППРассылкуНоменклатуры()

Функция ПолучитьТекстЗапросаКППРассылкаНоменклатурыБезКППЗаказКлиента()

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Ссылка 							КАК Заказ,
		|	Номенклатура		 			КАК Номенклатура,
		|	СУММА(Количество) 				КАК Количество,
		|	Номенклатура.ВидНоменклатуры	КАК ВидНоменклатуры
		|ПОМЕСТИТЬ КешСсылок
		|ИЗ
		|	Документ.ЗаказКлиента.Товары 
		|ГДЕ
		|	Ссылка = &Ссылка
		|	И Отменено

		|СГРУППИРОВАТЬ ПО
		|	Номенклатура,
		|	Ссылка,
		|	Номенклатура.ВидНоменклатуры

		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Значение КАК ЭлАдрес,
		|	ВЫРАЗИТЬ(Объект КАК Справочник.ВидыНоменклатуры) КАК Объект
		|ПОМЕСТИТЬ ДопСведения
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения
		|ГДЕ
		|	Свойство = &Свойство

		|ИНДЕКСИРОВАТЬ ПО
		|	Объект
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(Значение, ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка)) КАК КПП,
		|	Номенклатура
		|ПОМЕСТИТЬ КешКПП
		|ИЗ
		|	РегистрСведений.КПП_Розница_ЗначениеКоефициентаПриоритетностиПродаж.СрезПоследних(
		|																						&ТекущаяДата,
		|																						Номенклатура В (ВЫБРАТЬ	Номенклатура ИЗ КешСсылок)
		|																					  )
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КешСсылок.Заказ							КАК Заказ,
		|	КешСсылок.Номенклатура					КАК Номенклатура,
		|	ЕСТЬNULL(ДопСведения.ЭлАдрес, """") 	КАК ЭлАдрес,
		|	ЕСТЬNULL(КешКПП.КПП, ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка)) КАК КПП,
		|	КешСсылок.Количество					КАК Количество
		|ПОМЕСТИТЬ ОсновнойКеш
		|ИЗ
		|	КешСсылок КАК КешСсылок
		|	ЛЕВОЕ СОЕДИНЕНИЕ КешКПП КАК КешКПП
		|	ПО 
		|		КешСсылок.Номенклатура = КешКПП.Номенклатура
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДопСведения КАК ДопСведения
		|	ПО 
		|		КешСсылок.ВидНоменклатуры = ДопСведения.Объект
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновнойКеш.Заказ 			КАК Заказ,
		|	ОсновнойКеш.Номенклатура 	КАК Номенклатура,
		|	ОсновнойКеш.ЭлАдрес 		КАК ЭлАдрес
		|ИЗ
		|	ОсновнойКеш КАК ОсновнойКеш
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.Остатки(
		|																 &ТекущаяДата,
		|																 Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ОсновнойКеш)
		|																)КАК СвободныеОстаткиОстатки
		|	ПО 
		|		ОсновнойКеш.Номенклатура = СвободныеОстаткиОстатки.Номенклатура
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
		|																&ТекущаяДата,
		|																Номенклатура В (ВЫБРАТЬ	Номенклатура ИЗ	ОсновнойКеш)
		|															   )КАК ТоварыНаСкладахОстатки
		|	ПО 
		|		ОсновнойКеш.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
		|ГДЕ
		|	(ОсновнойКеш.КПП = ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.ПустаяСсылка)
		|			ИЛИ ОсновнойКеш.КПП = ЗНАЧЕНИЕ(Справочник.КоефициентПриоритетностиНоменклатуры.Актуальный))
		|	И ЕСТЬNULL(ТоварыНаСкладахОстатки.ВНаличииОстаток, 0)- ЕСТЬNULL(СвободныеОстаткиОстатки.ВНаличииОстаток, 0) = 0
		|	И ЕСТЬNULL(ТоварыНаСкладахОстатки.ВНаличииОстаток, 0) = ОсновнойКеш.Количество
		|ИТОГИ ПО
		|	ЭлАдрес,
		|	Заказ";	
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаКППРассылкуНоменклатуры()
//#4749 


Функция РазобратьСтрокуСПочтовымиАдресами(знач СтрокаАдресов)
	
	Результат = Новый Массив;
	
	НедопустимыеСимволы = "!#$%^&*()+`~|\/=";
	СимволыРазделители = ";,";
	
	Индекс = 1;               
	Накопитель = "";          
	                          
	ПолноеИмяАдресата = "";   
	ПочтовыйАдрес = "";      
	СтадияРазбора = 1; 
		
	Пока Индекс <= СтрДлина(СтрокаАдресов) Цикл
		
		Символ = Сред(СтрокаАдресов, Индекс, 1);
		
		Если      Символ = " " Тогда
			Индекс = ? ((ПропуститьПробелы(СтрокаАдресов, Индекс, " ") - 1) > Индекс,
			             ПропуститьПробелы(СтрокаАдресов, Индекс, " ") - 1,
			             Индекс);
			Если      СтадияРазбора = 1 Тогда
				ПолноеИмяАдресата = ПолноеИмяАдресата + Накопитель + " ";
			ИначеЕсли СтадияРазбора = 2 Тогда
				ПочтовыйАдрес = Накопитель;
				СтадияРазбора = 3;
			КонецЕсли;
			Накопитель = "";
		ИначеЕсли Символ = "@" Тогда
			Если      СтадияРазбора = 1 Тогда
				СтадияРазбора = 2;
				
				Для ИндексПоискаНС = 1 По СтрДлина(Накопитель) Цикл
					Если Найти(НедопустимыеСимволы, Сред(Накопитель, ИндексПоискаНС, 1)) > 0 Тогда
						Возврат Неопределено;
					КонецЕсли;
				КонецЦикла;
				
				Накопитель = Накопитель + Символ;
			ИначеЕсли СтадияРазбора = 2 Тогда
				Возврат Неопределено;
			ИначеЕсли СтадияРазбора = 3 Тогда
				Возврат Неопределено;
			КонецЕсли;
		ИначеЕсли Найти(СимволыРазделители, Символ) > 0 Тогда
			
			Если      СтадияРазбора = 1 Тогда
				ПолноеИмяАдресата = ПолноеИмяАдресата + Накопитель;
			ИначеЕсли СтадияРазбора = 2 Тогда
				ПочтовыйАдрес = Накопитель;
			КонецЕсли;
			
			СтадияРазбора = 1;
			
			Если НЕ (ПустаяСтрока(ПолноеИмяАдресата) И ПустаяСтрока(ПочтовыйАдрес)) Тогда
				Результат.Добавить(ПроверитьИПодготовитьПочтовыйАдрес(ПолноеИмяАдресата, ПочтовыйАдрес));
			КонецЕсли;
			
			ПочтовыйАдрес = "";
			ПолноеИмяАдресата = "";
			Накопитель = "";
		Иначе
			Если СтадияРазбора = 2 ИЛИ СтадияРазбора = 3 Тогда
				Если Найти(НедопустимыеСимволы, Символ) > 0 Тогда
					Возврат Неопределено;
				КонецЕсли;
			КонецЕсли;
			
			Накопитель = Накопитель + Символ;
		КонецЕсли;
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Если      СтадияРазбора = 1 Тогда
		ПолноеИмяАдресата = ПолноеИмяАдресата + Накопитель;
	ИначеЕсли СтадияРазбора = 2 Тогда
		ПочтовыйАдрес = Накопитель;
	КонецЕсли;

	Если НЕ (ПустаяСтрока(ПолноеИмяАдресата) И ПустаяСтрока(ПочтовыйАдрес)) Тогда
		Результат.Добавить(ПроверитьИПодготовитьПочтовыйАдрес(ПолноеИмяАдресата, ПочтовыйАдрес));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьИПодготовитьПочтовыйАдрес(знач ПолноеИмяАдресата, знач ПочтовыйАдрес)
	
	СимволыОбрамления = "<>[]";
	
	ПочтовыйАдрес     = СокрЛП(ПочтовыйАдрес);
	ПолноеИмяАдресата = СокрЛП(ПолноеИмяАдресата);
	
	Если Лев(ПолноеИмяАдресата, 1) = "<" Тогда
		Если Прав(ПолноеИмяАдресата, 1) = ">" Тогда
			ПолноеИмяАдресата = Сред(ПолноеИмяАдресата, 2, СтрДлина(ПолноеИмяАдресата)-2);
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	ИначеЕсли Лев(ПолноеИмяАдресата, 1) = "[" Тогда
		Если Прав(ПолноеИмяАдресата, 1) = "]" Тогда
			ПолноеИмяАдресата = Сред(ПолноеИмяАдресата, 2, СтрДлина(ПолноеИмяАдресата)-2);
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если Лев(ПочтовыйАдрес, 1) = "<" Тогда
		Если Прав(ПочтовыйАдрес, 1) = ">" Тогда
			ПочтовыйАдрес = Сред(ПочтовыйАдрес, 2, СтрДлина(ПочтовыйАдрес)-2);
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	ИначеЕсли Лев(ПочтовыйАдрес, 1) = "[" Тогда
		Если Прав(ПочтовыйАдрес, 1) = "]" Тогда
			ПочтовыйАдрес = Сред(ПочтовыйАдрес, 2, СтрДлина(ПочтовыйАдрес)-2);
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Для Индекс = 1 По СтрДлина(СимволыОбрамления) Цикл
		Если Найти(ПолноеИмяАдресата, Сред(СимволыОбрамления, Индекс, 1)) <> 0
		 ИЛИ Найти(ПочтовыйАдрес,     Сред(СимволыОбрамления, Индекс, 1)) <> 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура("Адрес, Представление", ПочтовыйАдрес,ПолноеИмяАдресата);
	
КонецФункции

Функция ПропуститьПробелы(знач Строка, знач ТекущийИндекс, знач ПропускаемыйСимвол)	
	Результат = ТекущийИндекс;	
	Пока ТекущийИндекс < СтрДлина(Строка) Цикл
		Если Сред(Строка, ТекущийИндекс, 1) <> ПропускаемыйСимвол Тогда
			Возврат ТекущийИндекс;
		КонецЕсли;
		ТекущийИндекс = ТекущийИндекс + 1;
	КонецЦикла;
	Возврат ТекущийИндекс;	
КонецФункции


//http://r.ktc-ua.com/issues/5250 
#Область Задача_5250

Функция ЭтоДокументИзНеОсновныхСкладов(Склад)

	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаЭтоОсновнойСклад();
	Запрос.УстановитьПараметр("Склад", Склад);	
	Возврат Запрос.Выполнить().Пустой();

КонецФункции // ЭтоДокументИзНеОсновныхСкладов()

Функция ПолучитьТекстЗапросаЭтоОсновнойСклад()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ 
		|	Склад КАК Склад 	
		|ИЗ
		| 	РегистрСведений.Модуль_ДоступныеСклады
		|ГДЕ
		|	Основной = Истина
		|И 	Склад = &Склад";
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапросаЭтоОсновнойСклад()

#КонецОбласти


#КонецОбласти


#КонецОбласти

#Область СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	//Версия обработки
	Версия = "1.0.13";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "КоефициентПриоритетностиПродаж");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "КоефициентПриоритетностиПродаж [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
	ДобавитьКоманду(ТаблицаКоманд, "КоефициентПриоритетностиПродаж [" + Версия + "]", "Форма", "ОткрытиеФормы", Ложь, "КоефициентПриоритетностиПродаж");
	ДобавитьКоманду(ТаблицаКоманд, "Регл. обновление старых цен [" + Версия + "]", "ВыполнитьОбновлениеСтарыхЦенНоменклатуры();", "ВызовСерверногоМетода");
	ДобавитьКоманду(ТаблицаКоманд, "Регл. обновление заработка КПП [" + Версия + "]", "ОбновитьЗаработокКПП();", "ВызовСерверногоМетода");

    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

Процедура ВыполнитьКоманду(Индентификатор) Экспорт
	Выполнить(Индентификатор);	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииПереопределенияОбработчиков
	
// Только для внутреннего использования
Процедура ПодменитьДействиеУправляемоеПриложение(ВнешнийОбъект, Форма, ИмяСобытияФормы = "", ПолноеИмяЭлементаФормы = "", НовоеДействие, ОбработкаИсключений = Ложь)

	Если ВнешнийОбъект = Неопределено Тогда
		git_ПереопределениеОбработчиковСервер.ПодменитьДействиеУправляемоеПриложение(Форма, ИмяСобытияФормы, ПолноеИмяЭлементаФормы, НовоеДействие, ОбработкаИсключений);
	Иначе
		ВнешнийОбъект.ПодменитьДействиеУправляемоеПриложение(Форма, ИмяСобытияФормы, ПолноеИмяЭлементаФормы, НовоеДействие, ОбработкаИсключений);
	КонецЕсли;
		
КонецПроцедуры

// Только для внутреннего использования
Процедура ПереместитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, ИмяЭлемента, ИмяРодителя, МестоРасположения)
	
	Если ВнешнийОбъект = Неопределено Тогда
		git_ПереопределениеОбработчиковСервер.ПереместитьЭлементВКоллекциюЭлементовФормы(Элементы, ИмяЭлемента, ИмяРодителя, МестоРасположения);
	Иначе
		ВнешнийОбъект.ПереместитьЭлементВКоллекциюЭлементовФормы(Элементы, ИмяЭлемента, ИмяРодителя, МестоРасположения);
	КонецЕсли;
	
КонецПроцедуры // ПереместитьЭлементВКоллекциюЭлементовФормы()

// Только для внутреннего использования
Процедура ПереместитьЭлементВКоллекциюЭлементовФормыБезПоиска(ВнешнийОбъект, Элементы, Элемент, Родитель, МестоРасположения = Неопределено)
	
	Если ВнешнийОбъект = Неопределено Тогда
		git_ПереопределениеОбработчиковСервер.ПереместитьЭлементВКоллекциюЭлементовФормыБезПоиска(Элементы, Элемент, Родитель, МестоРасположения);
	Иначе
		ВнешнийОбъект.ПереместитьЭлементВКоллекциюЭлементовФормыБезПоиска(Элементы, Элемент, Родитель, МестоРасположения);
	КонецЕсли;
	
КонецПроцедуры // ПереместитьЭлементВКоллекциюЭлементовФормыБезПоиска()


// Только для внутреннего использования
Функция ДобавитьЭлементВКоллекциюЭлементовФормы(ВнешнийОбъект, Элементы, Параметры, Родитель = Неопределено) Экспорт
	
	Если ВнешнийОбъект = Неопределено Тогда
		Возврат git_ПереопределениеОбработчиковСервер.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, Параметры, Родитель);
	Иначе
		Возврат ВнешнийОбъект.ДобавитьЭлементВКоллекциюЭлементовФормы(Элементы, Параметры, Родитель);
	КонецЕсли;
	
КонецФункции // ДобавитьЭлементВКоллекциюЭлементовФормы()

// Только для внутреннего использования
Функция ДобавитьКомандуВКоллекциюКомандФормы(ВнешнийОбъект, Команды, Параметры) 
		
	Если ВнешнийОбъект = Неопределено Тогда
		Возврат git_ПереопределениеОбработчиковСервер.ДобавитьКомандуВКоллекциюКомандФормы(Команды, Параметры);
	Иначе
		Возврат ВнешнийОбъект.ДобавитьКомандуВКоллекциюКомандФормы(Команды, Параметры);
	КонецЕсли;
		
КонецФункции // ДобавитьКомандуВКоллекциюКомандФормы()

#КонецОбласти

