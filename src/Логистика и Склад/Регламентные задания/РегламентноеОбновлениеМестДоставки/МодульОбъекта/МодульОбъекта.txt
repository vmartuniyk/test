
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура ВыполнитьОбновление() Экспорт
	
	ТДата = ТекущаяДата();
	
	ИмяСобытияЖурналаРегистрации = НСтр("ru = 'Автоматическое обновление мест в поездках'");
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации,
			УровеньЖурналаРегистрации.Информация, 
			,
			,
			НСтр("ru = 'Начало автоматического обновления мест в поездках'"));

	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаНовыеДокументы();
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ТДата));
	РезультатПакета = Запрос.ВыполнитьПакет();
	Выборка = РезультатПакета[13].Выбрать(); 
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Документы.ПоездкаЗаМаршрутом.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Выборка);
		ДокументОбъект.Дата = ТДата;
		ДокументОбъект.УстановитьНовыйНомер();
		ДокументОбъект.Склады.Загрузить(ЗаполнитьСкладыЗагрузки(ДокументОбъект));
        Если ВодительУжеВПоездке(ДокументОбъект) Тогда
			ДокументОбъект.Водитель = Неопределено;	
		КонецЕсли;
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Автоматическое формирование документов Поездка'"),
				УровеньЖурналаРегистрации.Ошибка, 
				,
				,
				ОписаниеОшибки()); 	
		КонецПопытки;
		
	КонецЦикла; 
			
			
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаДокументов();
	Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл
		Документы.ПоездкаЗаМаршрутом.ИнициализироватьДанныеДокумента(Выборка.Ссылка);		
	КонецЦикла; 
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации,
			УровеньЖурналаРегистрации.Информация, 
			,
			,
			НСтр("ru = 'Завершено автоматическое обновление мест в поездках'"));
	
КонецПроцедуры



Процедура ВыполнитьОбновлениеОплатПартнеров() Экспорт
		
	ИмяСобытияЖурналаРегистрации = НСтр("ru = 'Автоматическое обновление оплат партнеров в поездках'");
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации,
			УровеньЖурналаРегистрации.Информация, 
			,
			,
			НСтр("ru = 'Начало автоматического обновления оплат партнеров в поездках'"));			
			
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаДокументов();
	Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл
		Документы.ПоездкаЗаМаршрутом.ИнициализироватьОплатыДокумента(Выборка.Ссылка);		
	КонецЦикла; 
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации,
			УровеньЖурналаРегистрации.Информация, 
			,
			,
			НСтр("ru = 'Завершено автоматическое обновление оплат партнеров в поездках'"));
	
КонецПроцедуры



// Поездка за маршрутом

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ДокументСсылка.Проведен
	 ИЛИ ДокументСсылка.СтатусПоездки = Перечисления.КТС_СтатусыПоездки.Завершена Тогда
		Возврат;
	КонецЕсли;
	
	

	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаПоездки(ДокументСсылка);	
	Запрос.УстановитьПараметр("ДатаДокумента", ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("НачалоДняДокумента", НачалоДня(ДокументСсылка.Дата));
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ВариантМаршрута", ДокументСсылка.ВариантМаршрута);
	Запрос.УстановитьПараметр("ТипКонтактнойИнформацииАдрес", Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("ВидКонтактнойИнформацииАдресСклада", Справочники.ВидыКонтактнойИнформации.АдресСклада);
	РезультатПакета = Запрос.ВыполнитьПакет();
	РезультатЗапроса = РезультатПакета[ПолучитьИндексРезультатаПакетаПоездки(ДокументСсылка)];
	
	// Расчет мест
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
		Блокировка = Новый БлокировкаДанных;
		ПоездкаЗаМаршрутом 		 = Блокировка.Добавить("РегистрСведений.ПоездкаЗаМаршрутом");
		ПоездкаЗаМаршрутом.Режим = РежимБлокировкиДанных.Исключительный;
		ПоездкаЗаМаршрутом.ИсточникДанных = РезультатЗапроса;
		ПоездкаЗаМаршрутом.ИспользоватьИзИсточникаДанных("ДокументПоездки", "ДокументПоездки");		
		Попытка
			Блокировка.Заблокировать();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
			ОтменитьТранзакцию();
			Возврат;		
		КонецПопытки;
		
		ТаблицаДляЗаписи = РезультатЗапроса.Выгрузить();
		Для каждого Строка Из ТаблицаДляЗаписи Цикл
			Строка.OwnerGeoGUID 		= Строка(Строка.OwnerGeoLocation.УникальныйИдентификатор());	
		    Строка.IsPartner			= ТипЗнч(Строка.Owner) = Тип("СправочникСсылка.Партнеры");
			Строка.GUID					= Строка(Строка.МестоДоставки.УникальныйИдентификатор());
			Строка.ManualCode			= Число(Строка.Код);
			Строка.ОтправительGeoGUID   = Строка(Строка.ОтправительGeoLocation.УникальныйИдентификатор());		
		КонецЦикла; 
		
		НаборЗаписей = РегистрыСведений.ПоездкаЗаМаршрутом.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументПоездки.Установить(ДокументСсылка);
		НаборЗаписей.Загрузить(ТаблицаДляЗаписи);
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
			ОтменитьТранзакцию();
			Возврат;
		КонецПопытки;
		
	ЗафиксироватьТранзакцию();

КонецПроцедуры

Процедура ИнициализироватьОплатыДокумента(ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ДокументСсылка.Проведен
	 ИЛИ ДокументСсылка.СтатусПоездки = Перечисления.КТС_СтатусыПоездки.Завершена Тогда
		Возврат;
	КонецЕсли;
	
	
	Запрос 			= Новый Запрос;
	Запрос.Текст    = ПолучитьТекстЗапросаСуммаОплат();
	Запрос.УстановитьПараметр("ДокументПоездки", ДокументСсылка);
	Запрос.УстановитьПараметр("Организация", Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("0fba8a92-4010-11e0-9f98-001517115d85")));
	Запрос.УстановитьПараметр("КонецСегодня", КонецДня(ДокументСсылка.Дата));
	Запрос.УстановитьПараметр("ВалютаПартнеров", Справочники.Валюты.ПолучитьСсылку(Новый УникальныйИдентификатор("95916050-fdb7-11e2-883c-001e673c80fc")));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Расчет суммы, которую нужно забрать в партнера
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
		Блокировка = Новый БлокировкаДанных;
		ПоездкаЗаМаршрутом 		 = Блокировка.Добавить("РегистрСведений.ПоездкаЗаМаршрутомФинансы");
		ПоездкаЗаМаршрутом.Режим = РежимБлокировкиДанных.Исключительный;
		ПоездкаЗаМаршрутом.ИсточникДанных = РезультатЗапроса;
		ПоездкаЗаМаршрутом.ИспользоватьИзИсточникаДанных("ДокументПоездки", "ДокументПоездки");		
		Попытка
			Блокировка.Заблокировать();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
			ОтменитьТранзакцию();
			Возврат;		
		КонецПопытки;
	
		Выборка = РезультатЗапроса.Выбрать();
		Порядок = 1;
		
		Пока Выборка.Следующий() Цикл
			
			НаборЗаписей = РегистрыСведений.ПоездкаЗаМаршрутомФинансы.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ДокументПоездки.Установить(Выборка.ДокументПоездки);
			НаборЗаписей.Отбор.Партнер.Установить(Выборка.Партнер);
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			// Порядок, хз че с ним
			Запись.Порядок = Порядок;
			
			Порядок = Порядок + 1;
			
			НаборЗаписей.Записать();
			
		КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ЗаполнитьСкладыЗагрузки(Объект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаСкладыЗагрузки();
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("ВидПоездки", Объект.ВидПоездки);
	Запрос.УстановитьПараметр("ВариантМаршрута", Объект.ВариантМаршрута);	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	ТаблицаДанных.Сортировать("ВремяКОтгрузке Возр");
    Возврат ТаблицаДанных;
	
КонецФункции

Функция ВодительУжеВПоездке(Объект)

	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаВодительУжеВПоездке();
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Водитель", Объект.Водитель);
	РезультатЗапроса = Запрос.Выполнить();
	РезультатПроверки = НЕ РезультатЗапроса.Пустой();
	Возврат РезультатПроверки;

КонецФункции // ВодительУжеВПоездке()



Функция ПолучитьТекстЗапросаВодительУжеВПоездке()

	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Ссылка
		|ИЗ
		|	Документ.ПоездкаЗаМаршрутом
		|ГДЕ
		|	Проведен
		|И	Ссылка <> &Ссылка
		|И	Водитель = &Водитель
		|И	СтатусПоездки = Значение(Перечисление.КТС_СтатусыПоездки.ВПроцессе)
		|";
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаВодительУжеВПоездке()
 
Функция ПолучитьТекстЗапросаНовыеДокументы()
	
	ТекстЗапроса = "
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//1.
		|ВЫБРАТЬ
		|	ВариантМаршрута
		|ПОМЕСТИТЬ ПоездкаВПроцессе
		|ИЗ
		|	Документ.ПоездкаЗаМаршрутом
		|ГДЕ
		|	Проведен   	  = Истина
		|И	ВидПоездки 	  = ЗНАЧЕНИЕ(Перечисление.КТС_ВидыПоездки.ОсновнаяПоездка)
		|И	СтатусПоездки = ЗНАЧЕНИЕ(Перечисление.КТС_СтатусыПоездки.ВПроцессе)
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//2
		|ВЫБРАТЬ 
		|	ВариантМаршрута	КАК ВариантМаршрута,
		|	МАКСИМУМ(Дата) 	КАК ДатаПоследнейПоездки
		|ПОМЕСТИТЬ ДанныеПоПоездкам
		|ИЗ
		|	Документ.ПоездкаЗаМаршрутом
		|ГДЕ
		|	Проведен   	  = Истина
		|И	ВидПоездки 	  = ЗНАЧЕНИЕ(Перечисление.КТС_ВидыПоездки.ОсновнаяПоездка)
        |
		|СГРУППИРОВАТЬ ПО
		|	ВариантМаршрута	
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//3.
		|ВЫБРАТЬ 
		|	ДанныеПоПоездкам.ВариантМаршрута,
		|	ПоездкаЗаМаршрутом.Водитель,
		|	ДанныеПоПоездкам.ДатаПоследнейПоездки
		|ПОМЕСТИТЬ ЕстьДанныеПоПоездкам
		|ИЗ
		|	ДанныеПоПоездкам КАК ДанныеПоПоездкам
        |
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоездкаЗаМаршрутом КАК ПоездкаЗаМаршрутом	
		|ПО	ПоездкаЗаМаршрутом.Дата = ДанныеПоПоездкам.ДатаПоследнейПоездки
		|И	ПоездкаЗаМаршрутом.ВариантМаршрута = ДанныеПоПоездкам.ВариантМаршрута		
		|И	Проведен   	  = Истина
		|И	ВидПоездки 	  = ЗНАЧЕНИЕ(Перечисление.КТС_ВидыПоездки.ОсновнаяПоездка)	
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//4.
		|ВЫБРАТЬ
		|	ДоступныеМаршруты.Ссылка 								КАК ВариантМаршрута,
		|	ЗНАЧЕНИЕ(Перечисление.КТС_ВидыПоездки.ОсновнаяПоездка) 	КАК ВидПоездки,
		|	ЕстьДанныеПоПоездкам.Водитель							КАК Водитель,	
		|	МИНИМУМ(КалендарныеГрафики.ДатаГрафика) 				КАК ДатаВыезда
		|ПОМЕСТИТЬ КешВариантыМаршрутов
		|ИЗ
		|	Справочник.ВариантыМаршрутов КАК ДоступныеМаршруты
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
		|ПО	КалендарныеГрафики.Календарь = ДоступныеМаршруты.КалендарьМаршрута	
		|И	КалендарныеГрафики.ДеньВключенВГрафик
		|И	КалендарныеГрафики.ДатаГрафика = &НачалоДня
        |
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЕстьДанныеПоПоездкам КАК ЕстьДанныеПоПоездкам
		|ПО 	ЕстьДанныеПоПоездкам.ВариантМаршрута = ДоступныеМаршруты.Ссылка
        |
		|ГДЕ
		|	НеАктивен 		= ЛОЖЬ
		|И	ПометкаУдаления = ЛОЖЬ
		|И	Ссылка НЕ В (ВЫБРАТЬ ВариантМаршрута ИЗ ПоездкаВПроцессе)
        |
		|СГРУППИРОВАТЬ ПО
		|	ДоступныеМаршруты.Ссылка,
		|	ЕстьДанныеПоПоездкам.Водитель		
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПоездкаВПроцессе;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДанныеПоПоездкам;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЕстьДанныеПоПоездкам;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//8.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВариантыМаршрутовСкладыОтгрузки.Ссылка КАК ВариантМаршрута,
		|	Склад								   КАК Склад
		|ПОМЕСТИТЬ КешСклады
		|ИЗ
		|	Справочник.ВариантыМаршрутов.СкладыОтгрузки КАК ВариантыМаршрутовСкладыОтгрузки
        |
		|ГДЕ
		|	ВЫБОР ДЕНЬНЕДЕЛИ(&НачалоДня)
		|		КОГДА 1
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.ПН
		|		КОГДА 2
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.ВТ
		|		КОГДА 3
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.СР
		|		КОГДА 4
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.ЧТ
		|		КОГДА 5
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.ПТ
		|		КОГДА 6
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.СБ
		|		КОГДА 7
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.ВС
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//9.
		|ВЫБРАТЬ
		|	ВариантыМаршрутовСкладыОтгрузки.Ссылка КАК ВариантМаршрута,
		|	КОЛИЧЕСТВО(
		|		ВЫБОР ДЕНЬНЕДЕЛИ(&НачалоДня)
		|			КОГДА 1
		|				ТОГДА ВариантыМаршрутовСкладыОтгрузки.ПН
		|			КОГДА 2
		|				ТОГДА ВариантыМаршрутовСкладыОтгрузки.ВТ
		|			КОГДА 3
		|				ТОГДА ВариантыМаршрутовСкладыОтгрузки.СР
		|			КОГДА 4
		|				ТОГДА ВариантыМаршрутовСкладыОтгрузки.ЧТ
		|			КОГДА 5
		|				ТОГДА ВариантыМаршрутовСкладыОтгрузки.ПТ
		|			КОГДА 6
		|				ТОГДА ВариантыМаршрутовСкладыОтгрузки.СБ
		|			КОГДА 7
		|				ТОГДА ВариантыМаршрутовСкладыОтгрузки.ВС
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ) КАК КолТочек
		|ПОМЕСТИТЬ КешТочки
		|ИЗ
		|	Справочник.ВариантыМаршрутов.СкладыОтгрузки КАК ВариантыМаршрутовСкладыОтгрузки
        |
		|ГДЕ
		|	ВЫБОР ДЕНЬНЕДЕЛИ(&НачалоДня)
		|		КОГДА 1
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.ПН
		|		КОГДА 2
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.ВТ
		|		КОГДА 3
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.СР
		|		КОГДА 4
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.ЧТ
		|		КОГДА 5
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.ПТ
		|		КОГДА 6
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.СБ
		|		КОГДА 7
		|			ТОГДА ВариантыМаршрутовСкладыОтгрузки.ВС
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|	
		|СГРУППИРОВАТЬ ПО
		|	ВариантыМаршрутовСкладыОтгрузки.Ссылка
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//10.
		|ВЫБРАТЬ
		| 	КешТочки.ВариантМаршрута,
		| 	ВЫРАЗИТЬ(КешТочки.КолТочек / КОЛИЧЕСТВО(КешСклады.Склад) + 0.499999 КАК ЧИСЛО(3,0)) КАК Выезды
		|ПОМЕСТИТЬ КоличествоВыездов
		|ИЗ
		|	КешТочки КАК КешТочки
        |
		|ЛЕВОЕ СОЕДИНЕНИЕ КешСклады КАК КешСклады
		|ПО	КешСклады.ВариантМаршрута = КешТочки.ВариантМаршрута
        |
		|СГРУППИРОВАТЬ ПО
		|	КешТочки.ВариантМаршрута,
		|	КешТочки.КолТочек	  
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешТочки;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешСклады;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//13. 
		|ВЫБРАТЬ
		|	ПоездкаЗаМаршрутом.ВариантМаршрута		КАК	ВариантМаршрута,
		|	КОЛИЧЕСТВО(ПоездкаЗаМаршрутом.Ссылка) 	КАК КоличествоЗавершенных
		|ПОМЕСТИТЬ ЗавершенныеПоездки		
		|ИЗ
		|	Документ.ПоездкаЗаМаршрутом КАК ПоездкаЗаМаршрутом
		|ГДЕ
		|	Проведен = ИСТИНА
		|И	НАЧАЛОПЕРИОДА(Дата, ДЕНЬ) = &НачалоДня
		|И  ВидПоездки = ЗНАЧЕНИЕ(Перечисление.КТС_ВидыПоездки.ОсновнаяПоездка)
		|И	СтатусПоездки = ЗНАЧЕНИЕ(Перечисление.КТС_СтатусыПоездки.Завершена)
        |
		|СГРУППИРОВАТЬ ПО
		|	ПоездкаЗаМаршрутом.ВариантМаршрута	
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//14. 
		|ВЫБРАТЬ
		|	ВариантыМаршрутов.ВариантМаршрута,
		|	ВариантыМаршрутов.ВидПоездки,
		|	ВариантыМаршрутов.Водитель,
		|	ЗНАЧЕНИЕ(Перечисление.КТС_СтатусыПоездки.ВПроцессе) КАК СтатусПоездки
		|ИЗ
		|	КешВариантыМаршрутов КАК ВариантыМаршрутов
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ КоличествоВыездов КАК КоличествоВыездов
		|ПО	КоличествоВыездов.ВариантМаршрута = ВариантыМаршрутов.ВариантМаршрута
        |
		|ЛЕВОЕ СОЕДИНЕНИЕ ЗавершенныеПоездки КАК ЗавершенныеПоездки
		|ПО	ЗавершенныеПоездки.ВариантМаршрута = ВариантыМаршрутов.ВариантМаршрута	
        |
		|ГДЕ
		|	IsNull(ЗавершенныеПоездки.КоличествоЗавершенных, 0) < IsNull(КоличествоВыездов.Выезды, 1) 	
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешВариантыМаршрутов;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЗавершенныеПоездки;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КоличествоВыездов;
		|";
   Возврат ТекстЗапроса;
   
КонецФункции

Функция ПолучитьТекстЗапросаДокументов()
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
       	|	Ссылка
       	|ИЗ
       	|	Документ.ПоездкаЗаМаршрутом 
	   	|ГДЕ
       	|	Проведен
       	|И 	СтатусПоездки = ЗНАЧЕНИЕ(Перечисление.КТС_СтатусыПоездки.ВПроцессе)";
   Возврат ТекстЗапроса;
   
КонецФункции

Функция ПолучитьТекстЗапросаСкладыЗагрузки()

	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Склад КАК Склад,
		|	Минимум(ВЫБОР ДЕНЬНЕДЕЛИ(&Дата)
		|		КОГДА 1
		|			ТОГДА ВремяКОтгрузкеПН
		|		КОГДА 2
		|			ТОГДА ВремяКОтгрузкеВТ
		|		КОГДА 3
		|			ТОГДА ВремяКОтгрузкеСР
		|		КОГДА 4
		|			ТОГДА ВремяКОтгрузкеЧТ
		|		КОГДА 5
		|			ТОГДА ВремяКОтгрузкеПТ
		|		КОГДА 6
		|			ТОГДА ВремяКОтгрузкеСБ
		|		КОГДА 7
		|			ТОГДА ВремяКОтгрузкеВС
		|	КОНЕЦ) КАК ВремяКОтгрузке
		|ИЗ
		|	Справочник.ВариантыМаршрутов.СкладыОтгрузки
		|ГДЕ
		|	НЕ Ссылка.НеАктивен
		|И	НЕ Ссылка.ПометкаУдаления
		|И	Ссылка = &ВариантМаршрута
		|И	ВЫБОР ДЕНЬНЕДЕЛИ(&Дата)
		|		КОГДА 1
		|			ТОГДА ПН
		|		КОГДА 2
		|			ТОГДА ВТ
		|		КОГДА 3
		|			ТОГДА СР
		|		КОГДА 4
		|			ТОГДА ЧТ
		|		КОГДА 5
		|			ТОГДА ПТ
		|		КОГДА 6
		|			ТОГДА СБ
		|		КОГДА 7
		|			ТОГДА ВС
		|		ИНАЧЕ Ложь
		|	КОНЕЦ
		|И 	(Склад, Ссылка, ВЫБОР ДЕНЬНЕДЕЛИ(&Дата)
		|						КОГДА 1
		|							ТОГДА ВремяКОтгрузкеПН
		|						КОГДА 2
		|							ТОГДА ВремяКОтгрузкеВТ
		|						КОГДА 3
		|							ТОГДА ВремяКОтгрузкеСР
		|						КОГДА 4
		|							ТОГДА ВремяКОтгрузкеЧТ
		|						КОГДА 5
		|							ТОГДА ВремяКОтгрузкеПТ
		|						КОГДА 6
		|							ТОГДА ВремяКОтгрузкеСБ
		|						КОГДА 7
		|							ТОГДА ВремяКОтгрузкеВС
		|					КОНЕЦ) НЕ В (ВЫБРАТЬ 
		|									Склад, 
		|									Ссылка.ВариантМаршрута, 
		|									ВремяКОтгрузке
		|								ИЗ
		|									Документ.ПоездкаЗаМаршрутом.Склады
		|								ГДЕ
		|									Ссылка 							<> &Ссылка
		|								И	Ссылка.Проведен                  = Истина
		|								И	Ссылка.ВидПоездки				 = &ВидПоездки
		|								И	Ссылка.ВариантМаршрута 			 = &ВариантМаршрута
		|								И	НачалоПериода(Ссылка.Дата, День) = НачалоПериода(&Дата, День))
		|
		|СГРУППИРОВАТЬ ПО
		|	Склад
		|";
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаСкладыЗагрузки()
 



// Поездка за маршрутом

Функция ПолучитьТекстЗапросаПоездки(ДокументСсылка)
	
	Если ДокументСсылка.ВидПоездки = Перечисления.КТС_ВидыПоездки.ОсновнаяПоездка Тогда
		Возврат ПолучитьТекстЗапросаОсновнойПоездки();	
	Иначе
		Возврат ПолучитьТекстЗапросаДополнительнойПоездки();	
	КонецЕсли;	
	
КонецФункции

Функция ПолучитьТекстЗапросаОсновнойПоездки()

	ТекстЗапроса = "
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//1. Получаем список складов с которых может производится отгрузка
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Склад КАК Склад
		|ПОМЕСТИТЬ СкладыОтгрузки
		|ИЗ
		|	Справочник.ВариантыМаршрутов.СкладыОтгрузки
		|ГДЕ
		|	Ссылка = &ВариантМаршрута
		|;
        //////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//2. Получаем склады и время к отгрузке
		|ВЫБРАТЬ
		|	СкладыОтгрузки.Склад 				КАК Склад,	
		|   ПоездкаЗаМаршрутом.ВремяКОтгрузке   КАК ВремяКОтгрузке
		|ПОМЕСТИТЬ СкладыВремяКОтгрузке
		|ИЗ
		|	СкладыОтгрузки КАК СкладыОтгрузки	
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоездкаЗаМаршрутом.Склады КАК ПоездкаЗаМаршрутом
		|ПО ПоездкаЗаМаршрутом.Ссылка = &ДокументСсылка
		|И	ПоездкаЗаМаршрутом.Склад  = СкладыОтгрузки.Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СкладыОтгрузки.Склад
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//3. Получаем документы доставки открытых по данному маршруту
		|ВЫБРАТЬ
		|	Ссылка
		|ПОМЕСТИТЬ АктивныеПоездкиПоВариантуМаршрута
		|ИЗ
		|	Документ.ПоездкаЗаМаршрутом
		|ГДЕ
		|	Проведен
		|И	Ссылка 			<> &ДокументСсылка
		|И	ВариантМаршрута = &ВариантМаршрута
		|И	СтатусПоездки 	= Значение(Перечисление.КТС_СтатусыПоездки.ВПроцессе)
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//4. Получаем места в других доставках
		|ВЫБРАТЬ
		|	МестоДоставки	
		|ПОМЕСТИТЬ АктивныеМестаПоВариантуМаршрута
		|ИЗ
		|	РегистрСведений.ПоездкаЗаМаршрутом 	
		|ГДЕ
		|	ДокументПоездки В (ВЫБРАТЬ Ссылка ИЗ АктивныеПоездкиПоВариантуМаршрута)		
		|И	НЕ ИсключеноИзДоставки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	МестоДоставки	
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//5. Получаем места, которые нужно добавить к маршруту
		|ВЫБРАТЬ
		|	&ДокументСсылка		КАК ДокументПоездки,
		|	ОтгрузкаМест.Место 	КАК МестоДоставки,
		|	ОтгрузкаМест.Склад	КАК Отправитель,
		|	Ложь				КАК ЭтоЗадача
		|ПОМЕСТИТЬ ВозможноНовыеМестаПоМаршруту
		|ИЗ
		|	РегистрСведений.ОтгрузкаМест.СрезПоследних(, Склад В (ВЫБРАТЬ Склад ИЗ СкладыВремяКОтгрузке)
		|												И Место НЕ В (ВЫБРАТЬ МестоДоставки ИЗ АктивныеМестаПоВариантуМаршрута)
		|										     И НЕ Место.Архивный) КАК ОтгрузкаМест
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ СкладыВремяКОтгрузке КАК СкладыВремяКОтгрузке
		|ПО	СкладыВремяКОтгрузке.Склад = ОтгрузкаМест.Склад
		|И	ВЫБОР
		|		КОГДА НачалоПериода(ОтгрузкаМест.Период, День) < &НачалоДняДокумента
		|			ТОГДА ИСТИНА
		|		КОГДА НачалоПериода(ОтгрузкаМест.Период, День) = &НачалоДняДокумента
		|			ТОГДА 	ВЫБОР
		|						КОГДА ЧАС(СкладыВремяКОтгрузке.ВремяКОтгрузке) > ЧАС(ОтгрузкаМест.Период)
		|							ТОГДА ИСТИНА
		|						КОГДА ЧАС(СкладыВремяКОтгрузке.ВремяКОтгрузке) = ЧАС(ОтгрузкаМест.Период)
		|							И МИНУТА(СкладыВремяКОтгрузке.ВремяКОтгрузке) >= МИНУТА(ОтгрузкаМест.Период)
		|							ТОГДА ИСТИНА
		|						ИНАЧЕ ЛОЖЬ
		|					КОНЕЦ
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|
		|ГДЕ
		|	ОтгрузкаМест.ВариантМаршрута = &ВариантМаршрута
		|И	ОтгрузкаМест.СтатусОтгрузки НЕ В (	ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.НачалоКомплектации),
		|										ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ПогруженоВМашину),
		|										ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ОтгруженоПартнеру),
		|										ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.РазукомплектованиеНаСкладе),
		|										ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ВозвращеноНаСклад))
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//6. Получаем задачи, которые нужно добавить к маршруту
		|ВЫБРАТЬ
		|	&ДокументСсылка				КАК ДокументПоездки,
		|	ЗадачиНаМаршрут.Место 		КАК МестоДоставки,
		|	ЗадачиНаМаршрут.Отправитель КАК Отправитель,
		|	Истина						КАК ЭтоЗадача
		|ПОМЕСТИТЬ ВозможноНовыеЗадачиПоМаршруту
		|ИЗ
		|	РегистрСведений.ЗадачиНаМаршрут.СрезПоследних(, Место НЕ В (ВЫБРАТЬ МестоДоставки ИЗ АктивныеМестаПоВариантуМаршрута)
		|											   И НЕ Место.Архивный) КАК ЗадачиНаМаршрут
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ СкладыВремяКОтгрузке КАК СкладыВремяКОтгрузке
		|ПО	СкладыВремяКОтгрузке.Склад = 	ВЫБОР
		|										КОГДА ЗадачиНаМаршрут.Отправитель Ссылка Справочник.Склады
		|											ТОГДА ВЫРАЗИТЬ(ЗадачиНаМаршрут.Отправитель КАК Справочник.Склады)
		|										ИНАЧЕ ВЫРАЗИТЬ(ЗадачиНаМаршрут.Место.Владелец КАК Справочник.Склады)
		|								  	КОНЕЦ
		|И	ВЫБОР
		|		КОГДА НачалоПериода(ЗадачиНаМаршрут.Период, День) < &НачалоДняДокумента
		|			ТОГДА ИСТИНА
		|		КОГДА НачалоПериода(ЗадачиНаМаршрут.Период, День) = &НачалоДняДокумента
		|			ТОГДА 	ВЫБОР
		|						КОГДА ЧАС(СкладыВремяКОтгрузке.ВремяКОтгрузке) > ЧАС(ЗадачиНаМаршрут.Период)
		|							ТОГДА ИСТИНА
		|						КОГДА ЧАС(СкладыВремяКОтгрузке.ВремяКОтгрузке) 	   = ЧАС(ЗадачиНаМаршрут.Период)
		|							И МИНУТА(СкладыВремяКОтгрузке.ВремяКОтгрузке) >= МИНУТА(ЗадачиНаМаршрут.Период)
		|							ТОГДА ИСТИНА
		|						ИНАЧЕ ЛОЖЬ
		|					КОНЕЦ
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|
		|ГДЕ
		|	ЗадачиНаМаршрут.ВариантМаршрута = &ВариантМаршрута
		|И	ЗадачиНаМаршрут.СтатусОтгрузки НЕ В (	ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ЗадачаВПроцессеВыполнения),
		|											ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ЗадачаВыполнена),
		|											ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ЗадачаОтменена))
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//7. 
		|ВЫБРАТЬ
		|	ПоездкаЗаМаршрутом.ДокументПоездки 		КАК ДокументПоездки,	
		|   ПоездкаЗаМаршрутом.МестоДоставки		КАК МестоДоставки,
		|   ПоездкаЗаМаршрутом.Отправитель			КАК Отправитель,
		|	ПоездкаЗаМаршрутом.ЭтоЗадача        	КАК ЭтоЗадача
		|ПОМЕСТИТЬ ПоездкаМестаДоставки
		|ИЗ
		|	РегистрСведений.ПоездкаЗаМаршрутом КАК ПоездкаЗаМаршрутом
		|ГДЕ
		|	ДокументПоездки	= &ДокументСсылка
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	*
		|ИЗ
		|	ВозможноНовыеМестаПоМаршруту
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	*
		|ИЗ
		|	ВозможноНовыеЗадачиПоМаршруту
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументПоездки,	
		|   МестоДоставки
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//8. 
		|ВЫБРАТЬ
		|	ПоездкаМестаДоставки.ДокументПоездки 		КАК ДокументПоездки, 		
		|   ПоездкаМестаДоставки.МестоДоставки			КАК МестоДоставки,
		|	ПоездкаМестаДоставки.ЭтоЗадача        		КАК ЭтоЗадача,
		|   ПоездкаМестаДоставки.МестоДоставки.Архивный КАК Архивное,
		|	ПоездкаМестаДоставки.Отправитель			КАК Отправитель,
		|   ПоездкаЗаМаршрутом.МестоНеВыдано          	КАК МестоНеВыдано,
		|   ПоездкаЗаМаршрутом.ИсключеноИзДоставки   	КАК ИсключеноИзДоставки
		|ПОМЕСТИТЬ ПоездкаАктуальныеМестаДоставки
		|ИЗ
		|	ПоездкаМестаДоставки КАК ПоездкаМестаДоставки
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПоездкаЗаМаршрутом КАК ПоездкаЗаМаршрутом
		|ПО	ПоездкаЗаМаршрутом.ДокументПоездки 	= ПоездкаМестаДоставки.ДокументПоездки
		|И	ПоездкаЗаМаршрутом.МестоДоставки	= ПоездкаМестаДоставки.МестоДоставки
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СкладыВремяКОтгрузке;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ АктивныеПоездкиПоВариантуМаршрута;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ АктивныеМестаПоВариантуМаршрута;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВозможноНовыеМестаПоМаршруту;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВозможноНовыеЗадачиПоМаршруту;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПоездкаМестаДоставки;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//9. Получаем адреса складов
		|ВЫБРАТЬ
		|	СкладыКонтактнаяИнформация.Ссылка 			КАК Склад,
		|	СкладыКонтактнаяИнформация.Представление 	КАК Адрес
		|ПОМЕСТИТЬ АдресаСкладов
		|ИЗ
		|	Справочник.Склады.КонтактнаяИнформация КАК СкладыКонтактнаяИнформация
		|ГДЕ
		|	СкладыКонтактнаяИнформация.Ссылка В
		|			(ВЫБРАТЬ
		|				Склад
		|			ИЗ
		|				СкладыОтгрузки
		|			)
		|	И СкладыКонтактнаяИнформация.Тип = &ТипКонтактнойИнформацииАдрес
		|	И СкладыКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииАдресСклада
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//10. 
		|ВЫБРАТЬ
		|	ОтгрузкаМест.Период 		КАК Период, 		
		|   ОтгрузкаМест.Место			КАК МестоДоставки,
		|	ОтгрузкаМест.СтатусОтгрузки КАК СтатусОтгрузки,
		|	ОтгрузкаМест.Склад			КАК Отправитель,
		|	АдресаСкладов.Адрес			КАК АдресОтправителя
		|ПОМЕСТИТЬ КешОтгрузкаМест
		|ИЗ
		|	РегистрСведений.ОтгрузкаМест КАК ОтгрузкаМест
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоездкаАктуальныеМестаДоставки КАК АктуальныеМеста
		|ПО	АктуальныеМеста.МестоДоставки = ОтгрузкаМест.Место
		|И	АктуальныеМеста.Отправитель	  = ОтгрузкаМест.Склад
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ АдресаСкладов КАК АдресаСкладов
		|ПО	АдресаСкладов.Склад = ОтгрузкаМест.Склад
		|
		|ГДЕ
		|	ОтгрузкаМест.ВариантМаршрута = &ВариантМаршрута
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	МестоДоставки,
		|	СтатусОтгрузки
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//11. 
		|ВЫБРАТЬ
		|	ЗадачиНаМаршрут.Период 				КАК Период, 		
		|   ЗадачиНаМаршрут.Место				КАК МестоДоставки,
		|	ЗадачиНаМаршрут.СтатусОтгрузки 		КАК СтатусОтгрузки,
		|   ЗадачиНаМаршрут.Отправитель     	КАК Отправитель,
		|   ЗадачиНаМаршрут.АдресОтправителя	КАК АдресОтправителя
		|ПОМЕСТИТЬ КешЗадачиНаМаршрут
		|ИЗ
		|	РегистрСведений.ЗадачиНаМаршрут КАК ЗадачиНаМаршрут
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоездкаАктуальныеМестаДоставки КАК АктуальныеМеста
		|ПО	АктуальныеМеста.МестоДоставки = ЗадачиНаМаршрут.Место
		|И	АктуальныеМеста.Отправитель	  = ЗадачиНаМаршрут.Отправитель
		|
		|ГДЕ
		|	ЗадачиНаМаршрут.ВариантМаршрута = &ВариантМаршрута
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	МестоДоставки,
		|	СтатусОтгрузки
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//12. 
		|ВЫБРАТЬ
		|	АктуальныеМеста.ДокументПоездки, 		
		|   АктуальныеМеста.МестоДоставки,
		|	АктуальныеМеста.ЭтоЗадача,
		|   АктуальныеМеста.Архивное,
		|   АктуальныеМеста.МестоНеВыдано,
		|   АктуальныеМеста.ИсключеноИзДоставки,
		|
		|  	НачалоКомплектации.Период 			КАК НачалоКомплектации,
		|   КомплектацияЗавершена.Период		КАК КомплектацияЗавершена,
		|   ПогруженоВМашину.Период				КАК ПогруженоВМашину,
		|   ОтгруженоПартнеру.Период			КАК ОтгруженоПартнеру,
		|	РазукомплектованиеНаСкладе.Период 	КАК РазукомплектованиеНаСкладе,
		|   ВозвращеноНаСклад.Период			КАК ВозвращеноНаСклад,
		|
		|	ЗадачаПоставлена.Период				КАК ЗадачаПоставлена,
		|   ЗадачаВПроцессеВыполнения.Период	КАК ЗадачаВПроцессеВыполнения,
		|   ЗадачаВыполнена.Период				КАК ЗадачаВыполнена,
		|   ЗадачаОтменена.Период				КАК ЗадачаОтменена,
		|
		|  	ВЫБОР
		|		КОГДА АктуальныеМеста.ЭтоЗадача
		|		ТОГДА 	ВЫБОР
		|					КОГДА АктуальныеМеста.МестоДоставки.Транзит
		|					ТОГДА АктуальныеМеста.МестоДоставки.ТранзитныйСклад
		|					ИНАЧЕ АктуальныеМеста.МестоДоставки.Владелец
		|				КОНЕЦ
		|		ИНАЧЕ АктуальныеМеста.МестоДоставки.Владелец
		|	КОНЕЦ   												КАК Owner,
		|  	ВЫБОР
		|		КОГДА АктуальныеМеста.ЭтоЗадача
		|		ТОГДА 	ВЫБОР
		|					КОГДА АктуальныеМеста.МестоДоставки.Транзит
		|					ТОГДА ТранзитыАдресаСкладов.Адрес
		|					ИНАЧЕ АктуальныеМеста.МестоДоставки.АдресДоставки
		|				КОНЕЦ
		|		ИНАЧЕ АктуальныеМеста.МестоДоставки.АдресДоставки
		|	КОНЕЦ													КАК OwnerAdress,
		|	""00000000-0000-0000-0000-000000000000""				КАК OwnerGeoGUID,
		| 	OwnerGeoLocation.Ссылка									КАК OwnerGeoLocation,
		|	Ложь													КАК IsPartner,
		|	""00000000-0000-0000-0000-000000000000""				КАК GUID,
		|   АктуальныеМеста.МестоДоставки.Код          				КАК Код,
		|	0														КАК ManualCode,
		|   АктуальныеМеста.МестоДоставки.УникальныйИдентификатор	КАК AssignedQRCode,
		|   АктуальныеМеста.МестоДоставки.ЗаполнятьУникальныйИдентификатор КАК IsQRCodeScanningNeeded,
		|   АктуальныеМеста.МестоДоставки.Ответственный             КАК Author,
		|   АктуальныеМеста.МестоДоставки.ТекстЗадачи               КАК Comment,
		|   ВЫБОР
		|		КОГДА АктуальныеМеста.ЭтоЗадача
		|		ТОГДА ЗадачаПоставлена.Отправитель
		|		ИНАЧЕ IsNull(НачалоКомплектации.Отправитель, КомплектацияЗавершена.Отправитель)
		|	КОНЕЦ   												КАК Отправитель,
		|   ВЫБОР
		|		КОГДА АктуальныеМеста.ЭтоЗадача
		|		ТОГДА ЗадачаПоставлена.АдресОтправителя
		|		ИНАЧЕ IsNull(НачалоКомплектации.АдресОтправителя, КомплектацияЗавершена.АдресОтправителя) 
		|	КОНЕЦ                                                   КАК АдресОтправителя,
		|	""00000000-0000-0000-0000-000000000000""				КАК ОтправительGeoGUID,
		|   ОтправительGeoLocation.Ссылка                           КАК ОтправительGeoLocation
		|ИЗ
		|	ПоездкаАктуальныеМестаДоставки КАК АктуальныеМеста
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК НачалоКомплектации
		|ПО	НачалоКомплектации.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	НачалоКомплектации.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.НачалоКомплектации)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК КомплектацияЗавершена
		|ПО	КомплектацияЗавершена.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	КомплектацияЗавершена.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.КомплектацияЗавершена)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК ПогруженоВМашину
		|ПО	ПогруженоВМашину.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ПогруженоВМашину.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ПогруженоВМашину)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК ОтгруженоПартнеру
		|ПО	ОтгруженоПартнеру.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ОтгруженоПартнеру.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ОтгруженоПартнеру)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК РазукомплектованиеНаСкладе
		|ПО	РазукомплектованиеНаСкладе.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	РазукомплектованиеНаСкладе.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.РазукомплектованиеНаСкладе)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК ВозвращеноНаСклад
		|ПО	ВозвращеноНаСклад.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ВозвращеноНаСклад.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ВозвращеноНаСклад)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешЗадачиНаМаршрут КАК ЗадачаПоставлена
		|ПО	ЗадачаПоставлена.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ЗадачаПоставлена.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ЗадачаПоставлена)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешЗадачиНаМаршрут КАК ЗадачаВПроцессеВыполнения
		|ПО	ЗадачаВПроцессеВыполнения.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ЗадачаВПроцессеВыполнения.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ЗадачаВПроцессеВыполнения)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешЗадачиНаМаршрут КАК ЗадачаВыполнена
		|ПО	ЗадачаВыполнена.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ЗадачаВыполнена.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ЗадачаВыполнена)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешЗадачиНаМаршрут КАК ЗадачаОтменена
		|ПО	ЗадачаОтменена.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ЗадачаОтменена.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ЗадачаОтменена)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ АдресаСкладов КАК ТранзитыАдресаСкладов
		|ПО	ТранзитыАдресаСкладов.Склад = АктуальныеМеста.МестоДоставки.ТранзитныйСклад 
		|													
		|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КТС_ГеографическиеРасположенияПартнеров КАК OwnerGeoLocation
		|ПО	OwnerGeoLocation.Владелец = ВЫБОР
		|									КОГДА АктуальныеМеста.ЭтоЗадача
		|									ТОГДА 	ВЫБОР
		|												КОГДА АктуальныеМеста.МестоДоставки.Транзит
		|												ТОГДА АктуальныеМеста.МестоДоставки.ТранзитныйСклад
		|												ИНАЧЕ АктуальныеМеста.МестоДоставки.Владелец
		|											КОНЕЦ
		|									ИНАЧЕ АктуальныеМеста.МестоДоставки.Владелец
		|								КОНЕЦ
		|И	OwnerGeoLocation.ГородСклад = 	ВЫБОР
		|										КОГДА АктуальныеМеста.ЭтоЗадача
		|										ТОГДА 	ВЫБОР
		|													КОГДА АктуальныеМеста.МестоДоставки.Транзит
		|													ТОГДА ТранзитыАдресаСкладов.Адрес
		|													ИНАЧЕ АктуальныеМеста.МестоДоставки.АдресДоставки
		|												КОНЕЦ
		|										ИНАЧЕ АктуальныеМеста.МестоДоставки.АдресДоставки
		|									КОНЕЦ
		|И НЕ OwnerGeoLocation.ПометкаУдаления
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КТС_ГеографическиеРасположенияПартнеров КАК ОтправительGeoLocation
		|ПО	ОтправительGeoLocation.Владелец   = ВЫБОР
		|											КОГДА АктуальныеМеста.ЭтоЗадача
		|											ТОГДА ЗадачаПоставлена.Отправитель
		|											ИНАЧЕ IsNull(НачалоКомплектации.Отправитель, КомплектацияЗавершена.Отправитель)
		|										КОНЕЦ	
		|И	ОтправительGeoLocation.ГородСклад = ВЫБОР
		|											КОГДА АктуальныеМеста.ЭтоЗадача
		|											ТОГДА ЗадачаПоставлена.АдресОтправителя
		|											ИНАЧЕ IsNull(НачалоКомплектации.АдресОтправителя, КомплектацияЗавершена.АдресОтправителя) 
		|										КОНЕЦ
		|И НЕ ОтправительGeoLocation.ПометкаУдаления
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СкладыОтгрузки;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ АдресаСкладов;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешОтгрузкаМест;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешЗадачиНаМаршрут;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПоездкаАктуальныеМестаДоставки;
		|";
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаДанныеДокумента()

Функция ПолучитьТекстЗапросаДополнительнойПоездки()

	ТекстЗапроса = "
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//1. Получаем список складов с которых может производится отгрузка
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Склад КАК Склад
		|ПОМЕСТИТЬ СкладыОтгрузки
		|ИЗ
		|	Справочник.ВариантыМаршрутов.СкладыОтгрузки
		|ГДЕ
		|	Ссылка = &ВариантМаршрута
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//2. 
		|ВЫБРАТЬ
		|	ПоездкаМестаДоставки.ДокументПоездки 		КАК ДокументПоездки, 		
		|   ПоездкаМестаДоставки.МестоДоставки			КАК МестоДоставки,
		|	ПоездкаМестаДоставки.ЭтоЗадача        		КАК ЭтоЗадача,
		|   ПоездкаМестаДоставки.МестоДоставки.Архивный КАК Архивное,
		|	ПоездкаМестаДоставки.Отправитель			КАК Отправитель,
		|   ПоездкаМестаДоставки.МестоНеВыдано          КАК МестоНеВыдано,
		|   ПоездкаМестаДоставки.ИсключеноИзДоставки   	КАК ИсключеноИзДоставки
		|ПОМЕСТИТЬ ПоездкаАктуальныеМестаДоставки
		|ИЗ
		|	РегистрСведений.ПоездкаЗаМаршрутом КАК ПоездкаМестаДоставки
		|
		|ГДЕ
		|	ДокументПоездки	= &ДокументСсылка
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//3. Получаем адреса складов
		|ВЫБРАТЬ
		|	СкладыКонтактнаяИнформация.Ссылка 			КАК Склад,
		|	СкладыКонтактнаяИнформация.Представление 	КАК Адрес
		|ПОМЕСТИТЬ АдресаСкладов
		|ИЗ
		|	Справочник.Склады.КонтактнаяИнформация КАК СкладыКонтактнаяИнформация
		|ГДЕ
		|	СкладыКонтактнаяИнформация.Ссылка В
		|			(ВЫБРАТЬ
		|				Склад
		|			ИЗ
		|				СкладыОтгрузки
		|			)
		|	И СкладыКонтактнаяИнформация.Тип = &ТипКонтактнойИнформацииАдрес
		|	И СкладыКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииАдресСклада
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//4. 
		|ВЫБРАТЬ
		|	ОтгрузкаМест.Период 		КАК Период, 		
		|   ОтгрузкаМест.Место			КАК МестоДоставки,
		|	ОтгрузкаМест.СтатусОтгрузки КАК СтатусОтгрузки,
		|	ОтгрузкаМест.Склад			КАК Отправитель,
		|	АдресаСкладов.Адрес			КАК АдресОтправителя
		|ПОМЕСТИТЬ КешОтгрузкаМест
		|ИЗ
		|	РегистрСведений.ОтгрузкаМест КАК ОтгрузкаМест
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоездкаАктуальныеМестаДоставки КАК АктуальныеМеста
		|ПО	АктуальныеМеста.МестоДоставки = ОтгрузкаМест.Место
		|И	АктуальныеМеста.Отправитель	  = ОтгрузкаМест.Склад
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ АдресаСкладов КАК АдресаСкладов
		|ПО	АдресаСкладов.Склад = ОтгрузкаМест.Склад
		|
		|ГДЕ
		|	ОтгрузкаМест.ВариантМаршрута = &ВариантМаршрута
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	МестоДоставки,
		|	СтатусОтгрузки
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//5. 
		|ВЫБРАТЬ
		|	ЗадачиНаМаршрут.Период 				КАК Период, 		
		|   ЗадачиНаМаршрут.Место				КАК МестоДоставки,
		|	ЗадачиНаМаршрут.СтатусОтгрузки 		КАК СтатусОтгрузки,
		|   ЗадачиНаМаршрут.Отправитель     	КАК Отправитель,
		|   ЗадачиНаМаршрут.АдресОтправителя	КАК АдресОтправителя
		|ПОМЕСТИТЬ КешЗадачиНаМаршрут
		|ИЗ
		|	РегистрСведений.ЗадачиНаМаршрут КАК ЗадачиНаМаршрут
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоездкаАктуальныеМестаДоставки КАК АктуальныеМеста
		|ПО	АктуальныеМеста.МестоДоставки = ЗадачиНаМаршрут.Место
		|И	АктуальныеМеста.Отправитель	  = ЗадачиНаМаршрут.Отправитель
		|
		|ГДЕ
		|	ЗадачиНаМаршрут.ВариантМаршрута = &ВариантМаршрута
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	МестоДоставки,
		|	СтатусОтгрузки
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		//6. 
		|ВЫБРАТЬ
		|	АктуальныеМеста.ДокументПоездки, 		
		|   АктуальныеМеста.МестоДоставки,
		|	АктуальныеМеста.ЭтоЗадача,
		|   АктуальныеМеста.Архивное,
		|   АктуальныеМеста.МестоНеВыдано,
		|   АктуальныеМеста.ИсключеноИзДоставки,
		|
		|  	НачалоКомплектации.Период 			КАК НачалоКомплектации,
		|   КомплектацияЗавершена.Период		КАК КомплектацияЗавершена,
		|   ПогруженоВМашину.Период				КАК ПогруженоВМашину,
		|   ОтгруженоПартнеру.Период			КАК ОтгруженоПартнеру,
		|	РазукомплектованиеНаСкладе.Период 	КАК РазукомплектованиеНаСкладе,
		|   ВозвращеноНаСклад.Период			КАК ВозвращеноНаСклад,
		|
		|	ЗадачаПоставлена.Период				КАК ЗадачаПоставлена,
		|   ЗадачаВПроцессеВыполнения.Период	КАК ЗадачаВПроцессеВыполнения,
		|   ЗадачаВыполнена.Период				КАК ЗадачаВыполнена,
		|   ЗадачаОтменена.Период				КАК ЗадачаОтменена,
		|
		|  	ВЫБОР
		|		КОГДА АктуальныеМеста.ЭтоЗадача
		|		ТОГДА 	ВЫБОР
		|					КОГДА АктуальныеМеста.МестоДоставки.Транзит
		|					ТОГДА АктуальныеМеста.МестоДоставки.ТранзитныйСклад
		|					ИНАЧЕ АктуальныеМеста.МестоДоставки.Владелец
		|				КОНЕЦ
		|		ИНАЧЕ АктуальныеМеста.МестоДоставки.Владелец
		|	КОНЕЦ   												КАК Owner,
		|  	ВЫБОР
		|		КОГДА АктуальныеМеста.ЭтоЗадача
		|		ТОГДА 	ВЫБОР
		|					КОГДА АктуальныеМеста.МестоДоставки.Транзит
		|					ТОГДА ТранзитыАдресаСкладов.Адрес
		|					ИНАЧЕ АктуальныеМеста.МестоДоставки.АдресДоставки
		|				КОНЕЦ
		|		ИНАЧЕ АктуальныеМеста.МестоДоставки.АдресДоставки
		|	КОНЕЦ													КАК OwnerAdress,
		|	""00000000-0000-0000-0000-000000000000""				КАК OwnerGeoGUID,
		| 	OwnerGeoLocation.Ссылка									КАК OwnerGeoLocation,
		|	Ложь													КАК IsPartner,
		|	""00000000-0000-0000-0000-000000000000""				КАК GUID,
		|   АктуальныеМеста.МестоДоставки.Код          				КАК Код,
		|	0														КАК ManualCode,
		|   АктуальныеМеста.МестоДоставки.УникальныйИдентификатор	КАК AssignedQRCode,
		|   АктуальныеМеста.МестоДоставки.ЗаполнятьУникальныйИдентификатор КАК IsQRCodeScanningNeeded,
		|   АктуальныеМеста.МестоДоставки.Ответственный             КАК Author,
		|   АктуальныеМеста.МестоДоставки.ТекстЗадачи               КАК Comment,
		|   ВЫБОР
		|		КОГДА АктуальныеМеста.ЭтоЗадача
		|		ТОГДА ЗадачаПоставлена.Отправитель
		|		ИНАЧЕ IsNull(НачалоКомплектации.Отправитель, КомплектацияЗавершена.Отправитель)
		|	КОНЕЦ   												КАК Отправитель,
		|   ВЫБОР
		|		КОГДА АктуальныеМеста.ЭтоЗадача
		|		ТОГДА ЗадачаПоставлена.АдресОтправителя
		|		ИНАЧЕ IsNull(НачалоКомплектации.АдресОтправителя, КомплектацияЗавершена.АдресОтправителя) 
		|	КОНЕЦ                                                   КАК АдресОтправителя,
		|	""00000000-0000-0000-0000-000000000000""				КАК ОтправительGeoGUID,
		|   ОтправительGeoLocation.Ссылка                           КАК ОтправительGeoLocation
		|ИЗ
		|	ПоездкаАктуальныеМестаДоставки КАК АктуальныеМеста
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК НачалоКомплектации
		|ПО	НачалоКомплектации.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	НачалоКомплектации.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.НачалоКомплектации)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК КомплектацияЗавершена
		|ПО	КомплектацияЗавершена.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	КомплектацияЗавершена.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.КомплектацияЗавершена)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК ПогруженоВМашину
		|ПО	ПогруженоВМашину.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ПогруженоВМашину.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ПогруженоВМашину)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК ОтгруженоПартнеру
		|ПО	ОтгруженоПартнеру.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ОтгруженоПартнеру.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ОтгруженоПартнеру)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК РазукомплектованиеНаСкладе
		|ПО	РазукомплектованиеНаСкладе.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	РазукомплектованиеНаСкладе.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.РазукомплектованиеНаСкладе)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешОтгрузкаМест КАК ВозвращеноНаСклад
		|ПО	ВозвращеноНаСклад.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ВозвращеноНаСклад.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ВозвращеноНаСклад)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешЗадачиНаМаршрут КАК ЗадачаПоставлена
		|ПО	ЗадачаПоставлена.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ЗадачаПоставлена.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ЗадачаПоставлена)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешЗадачиНаМаршрут КАК ЗадачаВПроцессеВыполнения
		|ПО	ЗадачаВПроцессеВыполнения.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ЗадачаВПроцессеВыполнения.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ЗадачаВПроцессеВыполнения)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешЗадачиНаМаршрут КАК ЗадачаВыполнена
		|ПО	ЗадачаВыполнена.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ЗадачаВыполнена.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ЗадачаВыполнена)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ КешЗадачиНаМаршрут КАК ЗадачаОтменена
		|ПО	ЗадачаОтменена.МестоДоставки = АктуальныеМеста.МестоДоставки
		|И	ЗадачаОтменена.СтатусОтгрузки = Значение(Перечисление.СтатусыОтгрузкиМест.ЗадачаОтменена)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ АдресаСкладов КАК ТранзитыАдресаСкладов
		|ПО	ТранзитыАдресаСкладов.Склад = АктуальныеМеста.МестоДоставки.ТранзитныйСклад 
		|													
		|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КТС_ГеографическиеРасположенияПартнеров КАК OwnerGeoLocation
		|ПО	OwnerGeoLocation.Владелец = ВЫБОР
		|									КОГДА АктуальныеМеста.ЭтоЗадача
		|									ТОГДА 	ВЫБОР
		|												КОГДА АктуальныеМеста.МестоДоставки.Транзит
		|												ТОГДА АктуальныеМеста.МестоДоставки.ТранзитныйСклад
		|												ИНАЧЕ АктуальныеМеста.МестоДоставки.Владелец
		|											КОНЕЦ
		|									ИНАЧЕ АктуальныеМеста.МестоДоставки.Владелец
		|								КОНЕЦ
		|И	OwnerGeoLocation.ГородСклад = 	ВЫБОР
		|										КОГДА АктуальныеМеста.ЭтоЗадача
		|										ТОГДА 	ВЫБОР
		|													КОГДА АктуальныеМеста.МестоДоставки.Транзит
		|													ТОГДА ТранзитыАдресаСкладов.Адрес
		|													ИНАЧЕ АктуальныеМеста.МестоДоставки.АдресДоставки
		|												КОНЕЦ
		|										ИНАЧЕ АктуальныеМеста.МестоДоставки.АдресДоставки
		|									КОНЕЦ
		|И НЕ OwnerGeoLocation.ПометкаУдаления
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КТС_ГеографическиеРасположенияПартнеров КАК ОтправительGeoLocation
		|ПО	ОтправительGeoLocation.Владелец   = ВЫБОР
		|											КОГДА АктуальныеМеста.ЭтоЗадача
		|											ТОГДА ЗадачаПоставлена.Отправитель
		|											ИНАЧЕ IsNull(НачалоКомплектации.Отправитель, КомплектацияЗавершена.Отправитель)
		|										КОНЕЦ	
		|И	ОтправительGeoLocation.ГородСклад = ВЫБОР
		|											КОГДА АктуальныеМеста.ЭтоЗадача
		|											ТОГДА ЗадачаПоставлена.АдресОтправителя
		|											ИНАЧЕ IsNull(НачалоКомплектации.АдресОтправителя, КомплектацияЗавершена.АдресОтправителя) 
		|										КОНЕЦ
		|И НЕ ОтправительGeoLocation.ПометкаУдаления
		|;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СкладыОтгрузки;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ АдресаСкладов;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешОтгрузкаМест;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешЗадачиНаМаршрут;
		//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПоездкаАктуальныеМестаДоставки;
		|";
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаДанныеДокумента()

Функция ПолучитьТекстЗапросаСуммаОплат()

	ТекстЗапроса = "
		///////////////////////////////////////////////////////////////////////////////////////////
		// 1. Получаем все доступные места по маршруту, где получатель - партнер (только для партнеров нужно рассчитывать сумму)
		|ВЫБРАТЬ
		|	ДокументПоездки	КАК ДокументПоездки,
		|	Owner			КАК Получатель,
		|	МестоДоставки 	КАК МестоДоставки
		|ПОМЕСТИТЬ КешПоездкаЗаМаршрутом
		|ИЗ
		|	РегистрСведений.ПоездкаЗаМаршрутом
		|ГДЕ
		|	ДокументПоездки = &ДокументПоездки
		|И	Owner Ссылка Справочник.Партнеры	
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ДокументПоездки,
		|	Отправитель,
		|	МестоДоставки
		|ИЗ
		|	РегистрСведений.ПоездкаЗаМаршрутом
		|ГДЕ
		|	ДокументПоездки = &ДокументПоездки
		|И	Отправитель Ссылка Справочник.Партнеры
		|;
		///////////////////////////////////////////////////////////////////////////////////////////
		// 2. Получим партнеров, для которых вручную установили сумму отгрузки
		|ВЫБРАТЬ
		|	Финансы.ДокументПоездки КАК ДокументПоездки,
		|	Финансы.Партнер			КАК Партнер
		|ПОМЕСТИТЬ КешПартнерыНазначенныеЦены
		|ИЗ
		|	РегистрСведений.ПоездкаЗаМаршрутомФинансы КАК Финансы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешПоездкаЗаМаршрутом КАК КешПоездкаЗаМаршрутом
		|		ПО Финансы.ДокументПоездки = КешПоездкаЗаМаршрутом.ДокументПоездки
		|			И (Финансы.СуммаНазначеннаяМенеджером > 0
		|				ИЛИ Финансы.СуммаПолученнаяВодителем > 0
		|				ИЛИ Финансы.СуммаПоФакту > 0)
		|;
		///////////////////////////////////////////////////////////////////////////////////////////
		// 3. Выберем партнеров для которых мы можем расчитать сумму отгрузки
		|ВЫБРАТЬ
		|	ДокументПоездки	КАК ДокументПоездки,
		|	Получатель		КАК Получатель,
		|	МестоДоставки 	КАК МестоДоставки
		|ПОМЕСТИТЬ СписокМестИПартнеров
		|ИЗ
		|	КешПоездкаЗаМаршрутом
		|ГДЕ
		|	(ДокументПоездки, Получатель) НЕ В
		|		(ВЫБРАТЬ
		|			ДокументПоездки,
		|			Партнер
		|		ИЗ
		|			КешПартнерыНазначенныеЦены)
		|;
		///////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешПоездкаЗаМаршрутом;	 	
		///////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешПартнерыНазначенныеЦены; 
		///////////////////////////////////////////////////////////////////////////////////////////
		// 4. Получаем всех партнеров для которых должны устанавливаться настройки условий отгрузки
		// список сегментов партнеров доступен в регистре КТС_НастройкиСегментовУсловийОтгрузки
		|ВЫБРАТЬ
		|	ПартнерыСегмента.Партнер КАК Партнер
		|ПОМЕСТИТЬ ТаблицаДопустимыхПартнеровПредварительная
		|ИЗ
		|	РегистрСведений.ПартнерыСегмента КАК ПартнерыСегмента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КТС_НастройкиСегментовУсловийОтгрузки КАК СегментыУсловийОтгрузки
		|		ПО ПартнерыСегмента.Сегмент    = СегментыУсловийОтгрузки.СегментПартнеров
		|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 5.
					|// Сделаем отбор тех партнеров, для которых были сформированы места
					|//
					|ВЫБРАТЬ
					|	СписокМестИПартнеров.ДокументПоездки	КАК ДокументПоездки,
					|	СписокМестИПартнеров.Получатель			КАК Партнер,
					|	СписокМестИПартнеров.МестоДоставки 		КАК Место
					|ПОМЕСТИТЬ ТаблицаДопустимыхПартнеровИМест
					|ИЗ
					|	СписокМестИПартнеров КАК СписокМестИПартнеров
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДопустимыхПартнеровПредварительная КАК ДопустимыеПартнеры
					|		ПО СписокМестИПартнеров.Получатель = ДопустимыеПартнеры.Партнер
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 6. 
					|// Выбираем только партнеров
					|ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ДокументПоездки КАК ДокументПоездки,
					|	Партнер			КАК Партнер
					|ПОМЕСТИТЬ ТаблицаДопустимыхПартнеров
					|ИЗ
					|	ТаблицаДопустимыхПартнеровИМест
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ СписокМестИПартнеров;	 					// 3

					|///////////////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ТаблицаДопустимыхПартнеровПредварительная;	// 4


					|// РАСЧЕТ ДОЛГА


					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 6.
					|//
					|ВЫБРАТЬ
					|	Валюта КАК Валюта,
					|	Курс / Кратность КАК Курс
					|ПОМЕСТИТЬ КурсыВалют	
					|ИЗ
					|	РегистрСведений.КурсыВалют.СрезПоследних
					|	
					|ИНДЕКСИРОВАТЬ ПО
					|	Валюта
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 7.
					|//
					|ВЫБРАТЬ
					|	АналитикаПоПартнерам.Партнер КАК Партнер,
					|	АналитикаПоПартнерам.Организация КАК Организация,
					|	АналитикаПоПартнерам.КлючАналитики КАК КлючАналитики
					|ПОМЕСТИТЬ Аналитика	
					|ИЗ
					|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДопустимыхПартнеров КАК ТаблицаПартнеров
					|		ПО АналитикаПоПартнерам.Партнер = ТаблицаПартнеров.Партнер
					|			И АналитикаПоПартнерам.Организация = &Организация
					|	
					|ИНДЕКСИРОВАТЬ ПО
					|	КлючАналитики		
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 8.
					|//
					|ВЫБРАТЬ
					|	Аналитика.Партнер КАК Партнер,
					|	Сумма(	ВЫБОР ВалютаУправленческогоУчета.Значение
					|				КОГДА Долги.Валюта
					|				ТОГДА Долги.СуммаОстаток
					|				ИНАЧЕ Долги.СуммаОстаток / КурсыВалютУПР.Курс * КурсыВалют.Курс
					|			КОНЕЦ
					|		  ) КАК Долг	

					|ПОМЕСТИТЬ Долги
					|ИЗ
					|	(
					|		ВЫБРАТЬ
					|			АналитикаУчетаПоПартнерам,
					|			Валюта,
					|			СуммаОстаток
					|		ИЗ
					|			РегистрНакопления.РасчетыСКлиентами.Остатки(&КонецСегодня, АналитикаУчетаПоПартнерам В (ВЫБРАТЬ КлючАналитики ИЗ Аналитика)) КАК РасчетыСКлиентами
					|			
					|		ОБЪЕДИНИТЬ ВСЕ
					|								
					|		ВЫБРАТЬ
					|			АналитикаУчетаПоПартнерам,
					|			Валюта,
					|			СуммаОстаток 				
					|		ИЗ
					|			РегистрНакопления.РасчетыСПоставщиками.Остатки(&КонецСегодня, АналитикаУчетаПоПартнерам В (ВЫБРАТЬ КлючАналитики ИЗ Аналитика)) КАК РасчетыСПоставщиками 
					|	) КАК Долги
					|	
					|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитика КАК Аналитика
					|ПО Аналитика.КлючАналитики = Долги.АналитикаУчетаПоПартнерам

					|ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВалютаУправленческогоУчета КАК ВалютаУправленческогоУчета
					|ПО Истина

					|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУПР
					|ПО	КурсыВалютУПР.Валюта = ВалютаУправленческогоУчета.Значение

					|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
					|ПО	КурсыВалют.Валюта = Долги.Валюта						

					|СГРУППИРОВАТЬ ПО
					|	Аналитика.Партнер
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 9.
					|//
					|ВЫБРАТЬ
					|	Ссылка КАК Ссылка,
					|	МИНИМУМ(ДатаПлатежа) КАК ДатаПлатежа
					|	
					|ПОМЕСТИТЬ ГрафикОплат
					|ИЗ
					|	Документ.ЗаказКлиента.ЭтапыГрафикаОплаты КАК ЭтапыОплаты
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДопустимыхПартнеров КАК ТаблицаПартнеров
					|		ПО ЭтапыОплаты.Ссылка.Партнер = ТаблицаПартнеров.Партнер
					| 			И ЭтапыОплаты.Ссылка.Организация = &Организация					

					|СГРУППИРОВАТЬ ПО 
					|	Ссылка
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 10.
					|//
					|ВЫБРАТЬ
					|	АналитикаУчетаПоПартнерам												КАК КлючАналитики,
					|	ВЫРАЗИТЬ(Регистратор КАК Документ.ВозвратТоваровОтКлиента)				КАК Регистратор,
					|	ВЫРАЗИТЬ(Регистратор КАК Документ.ВозвратТоваровОтКлиента).Валюта		КАК Валюта
					|ПОМЕСТИТЬ ПервыйКеш_Возвраты        
					|ИЗ
					|	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(, &КонецСегодня, Регистратор, , АналитикаУчетаПоПартнерам В (ВЫБРАТЬ КлючАналитики ИЗ Аналитика))
					|ГДЕ
					|	Регистратор Ссылка Документ.ВозвратТоваровОтКлиента
					|И	СуммаОборот <> 0

					|ИНДЕКСИРОВАТЬ ПО
					|	Регистратор,
					|	КлючАналитики
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 11.
					|//
					|ВЫБРАТЬ
					|	Возвраты.КлючАналитики,
					|	Возвраты.Валюта,										
					|	ВозвратыТовары.ДокументРеализации,
					|	Сумма(ВозвратыТовары.СуммаСНДС) КАК СуммаРасход
					|ПОМЕСТИТЬ ВторойКеш_Возвраты        
					|ИЗ
					|	ПервыйКеш_Возвраты КАК Возвраты
					|	
					|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ВозвратыТовары
					|ПО	ВозвратыТовары.Ссылка = Возвраты.Регистратор

					|СГРУППИРОВАТЬ ПО 
					|	Возвраты.КлючАналитики,
					|	Возвраты.Валюта,										
					|   	ВозвратыТовары.ДокументРеализации	
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 12.
					|//
					|ВЫБРАТЬ
					|	АналитикаУчетаПоПартнерам												КАК КлючАналитики,
					|	ВЫРАЗИТЬ(Регистратор КАК Документ.РеализацияТоваровУслуг)				КАК Регистратор,
					|	Регистратор.МоментВремени КАК МоментВремени,
					|	ВЫРАЗИТЬ(Регистратор КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента 	КАК ЗаказКлиента,	
					|	ВЫРАЗИТЬ(Регистратор КАК Документ.РеализацияТоваровУслуг).ДатаПлатежа  	КАК ДатаПлатежа,
					|	Период,
					|	Валюта,
					|	СуммаПриход,
					|	СуммаОборот
					|		
					|ПОМЕСТИТЬ Cache_Обороты
					|ИЗ
					|	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(, &КонецСегодня, Регистратор, , АналитикаУчетаПоПартнерам В (ВЫБРАТЬ КлючАналитики ИЗ Аналитика))
					|ГДЕ
					|	Регистратор Ссылка Документ.РеализацияТоваровУслуг
					|И	СуммаОборот <> 0			
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 12.
					|//
					|ВЫБРАТЬ
					| 	Аналитика.Партнер КАК Партнер,
					|	Обороты.Регистратор,
					|	Обороты.Период,
					|	ВЫБОР
					|		КОГДА Обороты.ЗаказКлиента = Значение(Документ.ЗаказКлиента.ПустаяСсылка) 
					|		  ИЛИ Обороты.ЗаказКлиента = Неопределено
					|		  ИЛИ Обороты.ЗаказКлиента = NULL
					|		ТОГДА Обороты.ДатаПлатежа
					|		ИНАЧЕ ГрафикОплат.ДатаПлатежа
					|	КОНЕЦ КАК ДатаПлатежа,
					|   	Обороты.МоментВремени,
					|	ВЫБОР 
					|		КОГДА ВалютаУправленческогоУчета.Значение = Обороты.Валюта
					|		ТОГДА Обороты.СуммаПриход 		
					|		ИНАЧЕ Обороты.СуммаПриход / КурсыВалютУПР.Курс * КурсыВалют.Курс
					|	КОНЕЦ -	ВЫБОР
					|			  	КОГДА IsNull(Возвраты.СуммаРасход, 0) = 0 
					|			  	ТОГДА 0
					|				КОГДА ВалютаУправленческогоУчета.Значение = Возвраты.Валюта
					|				ТОГДА Возвраты.СуммаРасход
					|				ИНАЧЕ Возвраты.СуммаРасход / КурсыВалютУПР.Курс * КурсыВозвраты.Курс
					|			КОНЕЦ КАК Сумма,
					|	ВЫБОР 
					|		КОГДА ВалютаУправленческогоУчета.Значение = Обороты.Валюта
					|		ТОГДА Обороты.СуммаОборот
					|		ИНАЧЕ Обороты.СуммаОборот / КурсыВалютУПР.Курс * КурсыВалют.Курс
					|	КОНЕЦ -	ВЫБОР
					|			  	КОГДА IsNull(Возвраты.СуммаРасход, 0) = 0 
					|			  	ТОГДА 0
					|				КОГДА ВалютаУправленческогоУчета.Значение = Возвраты.Валюта
					|				ТОГДА Возвраты.СуммаРасход
					|				ИНАЧЕ Возвраты.СуммаРасход / КурсыВалютУПР.Курс * КурсыВозвраты.Курс
					|			КОНЕЦ КАК СуммаОборот
					|   
					|ПОМЕСТИТЬ ОборотыПоКлиенту
					|ИЗ
					|	Cache_Обороты КАК Обороты
					|						
					|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитика КАК Аналитика
					|ПО Аналитика.КлючАналитики = Обороты.КлючАналитики 						


					|ЛЕВОЕ СОЕДИНЕНИЕ ВторойКеш_Возвраты КАК Возвраты
					|ПО  Возвраты.КлючАналитики 		= Обороты.КлючАналитики
					|И	Возвраты.ДокументРеализации = Обороты.Регистратор

					|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВозвраты
					|ПО	КурсыВозвраты.Валюта = Возвраты.Валюта


					|ЛЕВОЕ СОЕДИНЕНИЕ ГрафикОплат КАК ГрафикОплат
					|ПО ГрафикОплат.Ссылка = Обороты.ЗаказКлиента

					|ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВалютаУправленческогоУчета КАК ВалютаУправленческогоУчета
					|ПО  Истина

					|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУПР
					|ПО	КурсыВалютУПР.Валюта = ВалютаУправленческогоУчета.Значение

					|ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
					|ПО	КурсыВалют.Валюта = Обороты.Валюта
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 13.
					|//
					|ВЫБРАТЬ
					|	Партнер КАК Партнер,
					|	НАЧАЛОПЕРИОДА(ДатаПлатежа, МЕСЯЦ) КАК НачПериода,
					|	КОНЕЦПЕРИОДА(ДатаПлатежа, МЕСЯЦ) КАК КонПериода,
					|	СУММА(СуммаОборот) КАК Сумма
					|	
					|ПОМЕСТИТЬ ОборотыПоМесяцам	
					|ИЗ
					|	ОборотыПоКлиенту 
					|															
					|СГРУППИРОВАТЬ ПО
					|	Партнер,
					|	НАЧАЛОПЕРИОДА(ДатаПлатежа, МЕСЯЦ),
					|	КОНЕЦПЕРИОДА(ДатаПлатежа, МЕСЯЦ)
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 14.
					|//
					|ВЫБРАТЬ
					|	ОборотыПоМесяцам.Партнер КАК Партнер,
					|	ОборотыПоМесяцам.НачПериода КАК НачПериода,
					|	ОборотыПоМесяцам.КонПериода КАК КонПериода,
					|	ОборотыПоМесяцам.Сумма КАК Сумма,
					|	СУММА(ОборотыПоМесяцамКопия.Сумма) КАК СуммаПосле,
					|	СУММА(ОборотыПоМесяцамКопия.Сумма) - ОборотыПоМесяцам.Сумма КАК СуммаДо
					|ПОМЕСТИТЬ ОборотыПоМесяцамНарастающие
					|ИЗ
					|   ОборотыПоМесяцам КАК ОборотыПоМесяцам
					|   
					|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОборотыПоМесяцам КАК ОборотыПоМесяцамКопия
					|ПО ОборотыПоМесяцам.Партнер     = ОборотыПоМесяцамКопия.Партнер
					|И  ОборотыПоМесяцам.НачПериода <= ОборотыПоМесяцамКопия.НачПериода

					|СГРУППИРОВАТЬ ПО
					|	ОборотыПоМесяцам.НачПериода,
					|	ОборотыПоМесяцам.КонПериода,
					|	ОборотыПоМесяцам.Партнер,
					|	ОборотыПоМесяцам.Сумма
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 15.
					|//
					|ВЫБРАТЬ
					|	Долги.Партнер КАК Партнер,
					|	Долги.Долг КАК Долг,
					|	ОборотыПоМесНарастающие.НачПериода КАК НачПериода,
					|	ОборотыПоМесНарастающие.КонПериода КАК КонПериода,
					|	Долги.Долг - ОборотыПоМесНарастающие.СуммаДо КАК ОстатокДолга
					|ПОМЕСТИТЬ ДолгиПоВыбраннымМесяцам
					|ИЗ
					|	Долги КАК Долги
					|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОборотыПоМесяцамНарастающие КАК ОборотыПоМесНарастающие
					|	ПО Долги.Партнер = ОборотыПоМесНарастающие.Партнер
					|		И Долги.Долг > ОборотыПоМесНарастающие.СуммаДо
					|		И Долги.Долг <= ОборотыПоМесНарастающие.СуммаПосле
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 16.
					|//
					|ВЫБРАТЬ
					|	Обороты.Партнер КАК Партнер,
					|	Обороты.МоментВремени КАК МоментВремени,
					|	Обороты.Регистратор КАК Регистратор,
					|	Обороты.Период КАК Период,
					|	ДолгиПоВыбМесяцам.Долг КАК Долг,
					|	ДолгиПоВыбМесяцам.ОстатокДолга КАК ОстатокДолга,
					|	Обороты.Сумма КАК Сумма,
					|	Обороты.ДатаПлатежа КАК ДатаОтсрочки
					|	
					|ПОМЕСТИТЬ ДвиженияПоВыбраннымМесяцам
					|ИЗ
					|	ОборотыПоКлиенту КАК Обороты
					|						
					|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолгиПоВыбраннымМесяцам КАК ДолгиПоВыбМесяцам
					|ПО Обороты.Партнер 		= ДолгиПоВыбМесяцам.Партнер
					|И  Обороты.ДатаПлатежа >= ДолгиПоВыбМесяцам.НачПериода
					|И  Обороты.ДатаПлатежа <= ДолгиПоВыбМесяцам.КонПериода
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 17.
					|//
					|ВЫБРАТЬ
					|	ДвиженияПоВыбМесяцам.Партнер КАК Партнер,
					|	ДвиженияПоВыбМесяцам.Регистратор КАК Регистратор,
					|	ДвиженияПоВыбМесяцам.Период КАК Период,
					|	ДвиженияПоВыбМесяцам.МоментВремени КАК МоментВремени,
					|	ДвиженияПоВыбМесяцам.Сумма КАК Сумма,
					|	СУММА(ДвиженияПоВыбМесяцамКопия.Сумма) КАК СуммаПосле,
					|	СУММА(ДвиженияПоВыбМесяцамКопия.Сумма) - ДвиженияПоВыбМесяцам.Сумма КАК СуммаДо,
					|	ДвиженияПоВыбМесяцам.ОстатокДолга КАК ОстатокДолга,
					|	ДвиженияПоВыбМесяцам.ДатаОтсрочки КАК ДатаОтсрочки
					|ПОМЕСТИТЬ ДвиженияПредварительные
					|ИЗ
					|	ДвиженияПоВыбраннымМесяцам КАК ДвиженияПоВыбМесяцам
					|	   
					|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияПоВыбраннымМесяцам КАК ДвиженияПоВыбМесяцамКопия
					|ПО 	ДвиженияПоВыбМесяцам.Партнер 		= ДвиженияПоВыбМесяцамКопия.Партнер
					|И  	ДвиженияПоВыбМесяцам.ДатаОтсрочки  <= ДвиженияПоВыбМесяцамКопия.ДатаОтсрочки
					|И	ВЫБОР
					|		КОГДА ДвиженияПоВыбМесяцам.ДатаОтсрочки   = ДвиженияПоВыбМесяцамКопия.ДатаОтсрочки
					|		ТОГДА ДвиженияПоВыбМесяцам.МоментВремени <= ДвиженияПоВыбМесяцамКопия.МоментВремени
					|		ИНАЧЕ ИСТИНА
					|	КОНЕЦ
					|	   
					|СГРУППИРОВАТЬ ПО
					|	   ДвиженияПоВыбМесяцам.Партнер,
					|	   ДвиженияПоВыбМесяцам.Регистратор,
					|	   ДвиженияПоВыбМесяцам.Период,
					|	   ДвиженияПоВыбМесяцам.МоментВремени,
					|	   ДвиженияПоВыбМесяцам.Сумма,
					|	   ДвиженияПоВыбМесяцам.ОстатокДолга,
					|	   ДвиженияПоВыбМесяцам.ДатаОтсрочки
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 18.
					|//
					|ВЫБРАТЬ
					|	   ДвиженияПредв.Партнер КАК Партнер,
					|	   ДвиженияПредв.Регистратор КАК Регистратор,
					|	   ДвиженияПредв.Период КАК Период,
					|	   ДвиженияПредв.МоментВремени КАК МоментВремени,
					|	   ДвиженияПредв.ОстатокДолга - ДвиженияПредв.СуммаДо КАК СуммаДолга,
					|	   ДвиженияПредв.Сумма КАК Сумма,
					|	   ДвиженияПредв.ДатаОтсрочки КАК ДатаОтсрочки
					|ПОМЕСТИТЬ ДвиженияОкончательные
					|ИЗ
					|	   ДвиженияПредварительные КАК ДвиженияПредв
					|ГДЕ
					|	ДвиженияПредв.ОстатокДолга > ДвиженияПредв.СуммаДо
					|И 	ДвиженияПредв.ОстатокДолга <= ДвиженияПредв.СуммаПосле
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 19.
					|//
					|ВЫБРАТЬ
					|   Обороты.Партнер КАК Партнер,
					|   Обороты.Регистратор КАК Документ,
					|   ВЫБОР
					|  		КОГДА Обороты.Регистратор = ДвиженияОконч.Регистратор
					|		ТОГДА ДвиженияОконч.СуммаДолга
					|		ИНАЧЕ Обороты.Сумма
					|   КОНЕЦ КАК СуммаОстаток,
					|   ВЫБОР
					|		КОГДА Обороты.ДатаПлатежа < &КонецСегодня
					|		ТОГДА 1
					|		ИНАЧЕ 0
					|	КОНЕЦ КАК Прострочено
					|ПОМЕСТИТЬ ОбщаяТаблицаЗадолженностейПоДокументам
					|ИЗ
					|	   ОборотыПоКлиенту  КАК Обороты

					|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияОкончательные КАК ДвиженияОконч
					|ПО ДвиженияОконч.Партнер 		= Обороты.Партнер
					|И  ДвиженияОконч.ДатаОтсрочки  <= Обороты.ДатаПлатежа 
					|И	ВЫБОР
					|		КОГДА ДвиженияОконч.ДатаОтсрочки = Обороты.ДатаПлатежа 
					|		ТОГДА ДвиженияОконч.МоментВремени <= Обороты.МоментВремени
					|		ИНАЧЕ ИСТИНА
					|	КОНЕЦ
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 20.
					|//
					|ВЫБРАТЬ
					|	Партнер КАК Партнер,
					|	СУММА(СуммаОстаток) КАК СуммаПрострочки
					|ПОМЕСТИТЬ ТаблицаПрострочек
					|ИЗ
					|	ОбщаяТаблицаЗадолженностейПоДокументам
					|ГДЕ
					|	Прострочено = 1

					|СГРУППИРОВАТЬ ПО
					|	Партнер
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 21.
					|//
					|ВЫБРАТЬ
					|	ВЫБОР
					|		КОГДА МестаПоДокументам.Документ ССЫЛКА Документ.РасходныйОрдерНаТовары
					|			ТОГДА ВЫРАЗИТЬ(МестаПоДокументам.Документ КАК Документ.РасходныйОрдерНаТовары).Распоряжение
					|		КОГДА МестаПоДокументам.Документ ССЫЛКА Документ.КТС_ОтгрузкаТоваров
					|			ТОГДА ВЫРАЗИТЬ(МестаПоДокументам.Документ КАК Документ.КТС_ОтгрузкаТоваров).Распоряжение
					|	КОНЕЦ КАК Документ,
					|	ТаблицаПартнеров.Партнер КАК Партнер
					|ПОМЕСТИТЬ ТаблицаДокументовПоМестам
					|ИЗ
					|	РегистрСведений.МестаПоДокументам КАК МестаПоДокументам
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДопустимыхПартнеровИМест КАК ТаблицаПартнеров
					|		ПО МестаПоДокументам.Место = ТаблицаПартнеров.Место
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 22.
					|//
					|ВЫБРАТЬ
					|	ОбщаяТаблица.Партнер КАК Партнер,
					|	СУММА(ОбщаяТаблица.СуммаОстаток) КАК СуммаОстаток
					|ПОМЕСТИТЬ ДолгиПоДокументам
					|ИЗ
					|	ОбщаяТаблицаЗадолженностейПоДокументам КАК ОбщаяТаблица
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументовПоМестам КАК ДокументыПоМестам
					|		ПО ОбщаяТаблица.Партнер = ДокументыПоМестам.Партнер
					|			И ОбщаяТаблица.Документ = ДокументыПоМестам.Документ

					|СГРУППИРОВАТЬ ПО
					|	ОбщаяТаблица.Партнер
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 23.
					|//
					|ВЫБРАТЬ
					|	УсловиеОтгрузки КАК УсловиеОтгрузки,
					|	КредитныйЛимит КАК КредитныйЛимит,
					|	ДополнительныйПроцент КАК ДополнительныйПроцент
					|ПОМЕСТИТЬ УсловияОтгрузкиПоУмолчанию
					|ИЗ
					|	РегистрСведений.КТС_НастройкиУсловийОтгрузки.СрезПоследних(, Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 24.
					|//
					|ВЫБРАТЬ
					|	Партнер КАК Партнер,
					|	УсловиеОтгрузки КАК УсловиеОтгрузки,
					|	КредитныйЛимит КАК КредитныйЛимит,
					|	ДополнительныйПроцент КАК ДополнительныйПроцент
					|ПОМЕСТИТЬ УсловияОтгрузкиПоПартнерамПредварительные
					|ИЗ
					|	РегистрСведений.КТС_НастройкиУсловийОтгрузки.СрезПоследних(, Партнер В (ВЫБРАТЬ Партнер ИЗ ТаблицаДопустимыхПартнеровИМест))
					|;

					|///////////////////////////////////////////////////////////////////////////////////////////
					|// 25.
					|//
					|ВЫБРАТЬ
					|	ТаблицаПартнеров.Партнер КАК Партнер,
					|	ВЫБОР
					|		КОГДА ЕСТЬNULL(УсловияОтгрузкиПоПартнерамПредварительные.УсловиеОтгрузки, 0) = 0
					|			ТОГДА УсловияОтгрузкиПоУмолчанию.УсловиеОтгрузки
					|		ИНАЧЕ УсловияОтгрузкиПоПартнерамПредварительные.УсловиеОтгрузки
					|	КОНЕЦ КАК УсловиеОтгрузки,
					|	ВЫБОР
					|		КОГДА ЕСТЬNULL(УсловияОтгрузкиПоПартнерамПредварительные.КредитныйЛимит, 0) = 0
					|			ТОГДА УсловияОтгрузкиПоУмолчанию.КредитныйЛимит
					|		ИНАЧЕ УсловияОтгрузкиПоПартнерамПредварительные.КредитныйЛимит
					|	КОНЕЦ КАК КредитныйЛимит,
					|	ВЫБОР
					|		КОГДА ЕСТЬNULL(УсловияОтгрузкиПоПартнерамПредварительные.ДополнительныйПроцент, 0) = 0
					|			ТОГДА УсловияОтгрузкиПоУмолчанию.ДополнительныйПроцент
					|		ИНАЧЕ УсловияОтгрузкиПоПартнерамПредварительные.ДополнительныйПроцент
					|	КОНЕЦ КАК ДополнительныйПроцент
					|ПОМЕСТИТЬ УсловияОтгрузкиПоПартнерам
					|ИЗ
					|	ТаблицаДопустимыхПартнеров КАК ТаблицаПартнеров
					|		ЛЕВОЕ СОЕДИНЕНИЕ УсловияОтгрузкиПоУмолчанию КАК УсловияОтгрузкиПоУмолчанию
					|		ПО ИСТИНА
					|		ЛЕВОЕ СОЕДИНЕНИЕ УсловияОтгрузкиПоПартнерамПредварительные КАК УсловияОтгрузкиПоПартнерамПредварительные
					|		ПО ТаблицаПартнеров.Партнер = УсловияОтгрузкиПоПартнерамПредварительные.Партнер
					|;

					|ВЫБРАТЬ
					|	УсловияОтгрузки.Партнер КАК Партнер,
					|	ВЫБОР
					|		КОГДА УсловияОтгрузки.УсловиеОтгрузки = ЗНАЧЕНИЕ(Перечисление.КТС_УсловияОтгрузки.ТоварныйКредит)
					|			ТОГДА 
					|				ВЫБОР
					|					КОГДА ЕСТЬNULL(ТаблицаПрострочек.СуммаПрострочки, 0) < (ЕСТЬNULL(Долги.Долг, 0) - УсловияОтгрузки.КредитныйЛимит)
					|						ТОГДА ЕСТЬNULL(Долги.Долг, 0) - УсловияОтгрузки.КредитныйЛимит
					|					ИНАЧЕ ЕСТЬNULL(ТаблицаПрострочек.СуммаПрострочки, 0)
					|				КОНЕЦ
					|		КОГДА УсловияОтгрузки.УсловиеОтгрузки = ЗНАЧЕНИЕ(Перечисление.КТС_УсловияОтгрузки.РеструктуризацияДолга)
					|			ТОГДА 
					|				ВЫБОР
					|					КОГДА (ЕСТЬNULL(ДолгиПоДокументам.СуммаОстаток,0) * (1+(УсловияОтгрузки.ДополнительныйПроцент*0.01))) < (ЕСТЬNULL(Долги.Долг, 0) - УсловияОтгрузки.КредитныйЛимит)
					|						ТОГДА ЕСТЬNULL(Долги.Долг, 0) - УсловияОтгрузки.КредитныйЛимит
					|					ИНАЧЕ ЕСТЬNULL(ДолгиПоДокументам.СуммаОстаток,0) * (1+(УсловияОтгрузки.ДополнительныйПроцент*0.01))
					|				КОНЕЦ
					|		ИНАЧЕ 0
					|	КОНЕЦ КАК СуммаУчета,
					|	ЕСТЬNULL(ТаблицаДопустимыхПартнеров.ДокументПоездки, НЕОПРЕДЕЛЕНО) КАК ДокументПоездки
					|ИЗ
					|	УсловияОтгрузкиПоПартнерам КАК УсловияОтгрузки
					|	
					|	ЛЕВОЕ СОЕДИНЕНИЕ Долги КАК Долги
					|	ПО УсловияОтгрузки.Партнер = Долги.Партнер

					|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПрострочек КАК ТаблицаПрострочек
					|	ПО УсловияОтгрузки.Партнер = ТаблицаПрострочек.Партнер 

					|	ЛЕВОЕ СОЕДИНЕНИЕ ДолгиПоДокументам КАК ДолгиПоДокументам
					|	ПО УсловияОтгрузки.Партнер = ДолгиПоДокументам.Партнер
					
					|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДопустимыхПартнеров КАК ТаблицаДопустимыхПартнеров
					|	ПО УсловияОтгрузки.Партнер = ТаблицаДопустимыхПартнеров.Партнер
					
					|УПОРЯДОЧИТЬ ПО
					|	УсловияОтгрузки.Партнер.Наименование";
	
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаСуммаОплат()

Функция ПолучитьИндексРезультатаПакетаПоездки(ДокументСсылка)
	
	Если ДокументСсылка.ВидПоездки = Перечисления.КТС_ВидыПоездки.ОсновнаяПоездка Тогда
		Возврат 17;	
	Иначе
		Возврат 5;	
	КонецЕсли;	
	
КонецФункции





////////////////////////////////////////////////////////////////////////////////
// Информация о внешней обработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	Версия = "2.1.10";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Регл. обн. мест доставки");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Регл. обн. мест доставки [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
	ДобавитьКоманду(ТаблицаКоманд, "Регл. обн. мест доставки [" + Версия + "]", "ВыполнитьОбновление();", "ВызовСерверногоМетода");
	ДобавитьКоманду(ТаблицаКоманд, "Регл. обн. оплат партнеров в доставке [" + Версия + "]", "ВыполнитьОбновлениеОплатПартнеров();", "ВызовСерверногоМетода");
                                                                
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

Процедура ВыполнитьКоманду(Индентификатор) Экспорт
	Выполнить(Индентификатор);	
КонецПроцедуры