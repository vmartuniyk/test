Функция РасчитатьДатуОтгрузки(ПараметрыСостоянияОбеспечения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ЗаказКлиентаТовары.НомерСтроки,
	                      |	ЗаказКлиентаТовары.Номенклатура,
	                      |	ЗаказКлиентаТовары.Характеристика,
	                      |	ВЫРАЗИТЬ(ВЫБОР ЗаказКлиентаТовары.Склад
	                      |			КОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	                      |				ТОГДА &Склад
	                      |			ИНАЧЕ ЗаказКлиентаТовары.Склад
	                      |		КОНЕЦ КАК Справочник.Склады) КАК Склад,
	                      |	ЗаказКлиентаТовары.Количество
	                      |ПОМЕСТИТЬ КешТоварыБезКалендаря
	                      |ИЗ
	                      |	&Товары КАК ЗаказКлиентаТовары
	                      |ГДЕ
	                      |	НЕ ЗаказКлиентаТовары.Отменено
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КешТоварыБезКалендаря.НомерСтроки,
	                      |	КешТоварыБезКалендаря.Номенклатура,
	                      |	КешТоварыБезКалендаря.Характеристика,
	                      |	КешТоварыБезКалендаря.Склад,
	                      |	КешТоварыБезКалендаря.Количество,
	                      |	КешТоварыБезКалендаря.Склад.ВариантКонтроля КАК ВариантКонтроля,
	                      |	КешТоварыБезКалендаря.Склад.СрокПоставки КАК СрокПоставки,
	                      |	ЕСТЬNULL(НастройкаКонтроляОстатков.ГраницаГрафикаДоступности, ДАТАВРЕМЯ(1,1,1)) КАК ГраницаГрафикаДоступности,
	                      |	ВЫБОР КешТоварыБезКалендаря.Склад.Календарь
	                      |		КОГДА ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
	                      |			ТОГДА Константы.ОсновнойКалендарьПредприятия
	                      |		ИНАЧЕ КешТоварыБезКалендаря.Склад.Календарь
	                      |	КОНЕЦ КАК Календарь
	                      |ПОМЕСТИТЬ КешТовары
	                      |ИЗ
	                      |	КешТоварыБезКалендаря КАК КешТоварыБезКалендаря
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	                      |		ПО (ИСТИНА)
								|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаКонтроляОстатков
								|ПО НастройкаКонтроляОстатков.Склад = КешТоварыБезКалендаря.Склад
								|	И НастройкаКонтроляОстатков.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
								|	И НастройкаКонтроляОстатков.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	График.Номенклатура КАК Номенклатура,
	                      |	График.Характеристика КАК Характеристика,
	                      |	График.Склад КАК Склад,
	                      |	-МИНИМУМ(График.КоличествоКонечныйОстаток) КАК Количество
	                      |ПОМЕСТИТЬ ВтРезервыПоГрафику
	                      |ИЗ
	                      |	РегистрНакопления.ГрафикДвиженияТоваров.ОстаткиИОбороты(
	                      |			КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ),
	                      |			,
	                      |			День,
	                      |			ДвиженияИГраницыПериода,
	                      |			(Номенклатура, Характеристика, Склад) В
	                      |				(ВЫБРАТЬ
	                      |					КешТовары.Номенклатура,
	                      |					КешТовары.Характеристика,
	                      |					КешТовары.Склад
	                      |				ИЗ
	                      |					КешТовары КАК КешТовары)) КАК График
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаХарактеристика
	                      |		ПО График.Склад = НастройкаХарактеристика.Склад
	                      |			И График.Номенклатура = НастройкаХарактеристика.Номенклатура
	                      |			И График.Характеристика = НастройкаХарактеристика.Характеристика
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаНоменклатура
	                      |		ПО График.Склад = НастройкаНоменклатура.Склад
	                      |			И График.Номенклатура = НастройкаНоменклатура.Номенклатура
	                      |			И (НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	                      |			И (НастройкаХарактеристика.Склад ЕСТЬ NULL )
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаСклад
	                      |		ПО График.Склад = НастройкаСклад.Склад
	                      |			И (НастройкаСклад.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	                      |			И (НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	                      |			И (НастройкаХарактеристика.Склад ЕСТЬ NULL )
	                      |			И (НастройкаНоменклатура.Склад ЕСТЬ NULL )
	                      |ГДЕ
	                      |	ВЫБОР
	                      |			КОГДА ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля)) = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика)
	                      |				ТОГДА ВЫБОР
	                      |						КОГДА НЕ НастройкаХарактеристика.ВариантКонтроля ЕСТЬ NULL 
	                      |							ТОГДА ВЫБОР
	                      |									КОГДА НастройкаХарактеристика.ГраницаГрафикаДоступности >= &ТекущаяДата
	                      |										ТОГДА График.Период <= НастройкаХарактеристика.ГраницаГрафикаДоступности
	                      |									КОГДА НастройкаХарактеристика.СрокПоставки > 0
	                      |										ТОГДА График.Период <= ДОБАВИТЬКДАТЕ(&ТекущаяДата, ДЕНЬ, НастройкаХарактеристика.СрокПоставки)
	                      |									ИНАЧЕ ЛОЖЬ
	                      |								КОНЕЦ
	                      |						КОГДА НЕ НастройкаНоменклатура.ВариантКонтроля ЕСТЬ NULL 
	                      |							ТОГДА ВЫБОР
	                      |									КОГДА НастройкаНоменклатура.ГраницаГрафикаДоступности >= &ТекущаяДата
	                      |										ТОГДА График.Период <= НастройкаНоменклатура.ГраницаГрафикаДоступности
	                      |									КОГДА НастройкаНоменклатура.СрокПоставки > 0
	                      |										ТОГДА График.Период <= ДОБАВИТЬКДАТЕ(&ТекущаяДата, ДЕНЬ, НастройкаНоменклатура.СрокПоставки)
	                      |									ИНАЧЕ ЛОЖЬ
	                      |								КОНЕЦ
	                      |						ИНАЧЕ ВЫБОР
	                      |								КОГДА НастройкаСклад.ГраницаГрафикаДоступности >= &ТекущаяДата
	                      |									ТОГДА График.Период <= НастройкаСклад.ГраницаГрафикаДоступности
	                      |								КОГДА НастройкаСклад.СрокПоставки > 0
	                      |									ТОГДА График.Период <= ДОБАВИТЬКДАТЕ(&ТекущаяДата, ДЕНЬ, НастройкаСклад.СрокПоставки)
	                      |								ИНАЧЕ ЛОЖЬ
	                      |							КОНЕЦ
	                      |					КОНЕЦ
	                      |			ИНАЧЕ ЛОЖЬ
	                      |		КОНЕЦ
	                      |	И График.КоличествоКонечныйОстаток < 0
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	График.Номенклатура,
	                      |	График.Характеристика,
	                      |	График.Склад
	                      |
	                      |ИНДЕКСИРОВАТЬ ПО
	                      |	Номенклатура,
	                      |	Характеристика,
	                      |	Склад
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СвободныеОстатки.Номенклатура КАК Номенклатура,
	                      |	СвободныеОстатки.Характеристика КАК Характеристика,
	                      |	СвободныеОстатки.Склад КАК Склад,
	                      |	СвободныеОстатки.ВНаличииОстаток - ВЫБОР
	                      |		КОГДА ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля)) = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомРезерва)
	                      |			ТОГДА СвободныеОстатки.ВРезервеОстаток
	                      |		КОГДА ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля)) = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика)
	                      |			ТОГДА ЕСТЬNULL(РезервыПоГрафику.Количество, 0)
	                      |		ИНАЧЕ 0
	                      |	КОНЕЦ КАК Остаток,
	                      |	СвободныеОстатки.ВНаличииОстаток КАК ОстатокБезРезерва
	                      |ПОМЕСТИТЬ КешСвободныеОстатки
	                      |ИЗ
	                      |	РегистрНакопления.СвободныеОстатки.Остатки(
	                      |			,
	                      |			(Номенклатура, Характеристика, Склад) В
	                      |				(ВЫБРАТЬ
	                      |					КешТовары.Номенклатура,
	                      |					КешТовары.Характеристика,
	                      |					КешТовары.Склад
	                      |				ИЗ
	                      |					КешТовары КАК КешТовары)) КАК СвободныеОстатки
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаХарактеристика
	                      |		ПО СвободныеОстатки.Склад = НастройкаХарактеристика.Склад
	                      |			И СвободныеОстатки.Номенклатура = НастройкаХарактеристика.Номенклатура
	                      |			И СвободныеОстатки.Характеристика = НастройкаХарактеристика.Характеристика
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаНоменклатура
	                      |		ПО СвободныеОстатки.Склад = НастройкаНоменклатура.Склад
	                      |			И СвободныеОстатки.Номенклатура = НастройкаНоменклатура.Номенклатура
	                      |			И (НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	                      |			И (НастройкаХарактеристика.Склад ЕСТЬ NULL )
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОстатков КАК НастройкаСклад
	                      |		ПО СвободныеОстатки.Склад = НастройкаСклад.Склад
	                      |			И (НастройкаСклад.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	                      |			И (НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	                      |			И (НастройкаХарактеристика.Склад ЕСТЬ NULL )
	                      |			И (НастройкаНоменклатура.Склад ЕСТЬ NULL )
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ВтРезервыПоГрафику КАК РезервыПоГрафику
	                      |		ПО СвободныеОстатки.Номенклатура = РезервыПоГрафику.Номенклатура
	                      |			И СвободныеОстатки.Характеристика = РезервыПоГрафику.Характеристика
	                      |			И СвободныеОстатки.Склад = РезервыПоГрафику.Склад
	                      |			И (ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля)) = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика))
	                      |ГДЕ
	                      |	(СвободныеОстатки.ВНаличииОстаток <> 0
	                      |			ИЛИ ВЫБОР
	                      |				КОГДА ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля)) = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомРезерва)
	                      |					ТОГДА СвободныеОстатки.ВРезервеОстаток
	                      |				КОГДА ЕСТЬNULL(НастройкаХарактеристика.ВариантКонтроля, ЕСТЬNULL(НастройкаНоменклатура.ВариантКонтроля, НастройкаСклад.ВариантКонтроля)) = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика)
	                      |					ТОГДА ЕСТЬNULL(РезервыПоГрафику.Количество, 0)
	                      |				ИНАЧЕ 0
	                      |			КОНЕЦ <> 0)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КешТовары.НомерСтроки,
	                      |	КешТовары.Номенклатура,
	                      |	КешТовары.Характеристика,
	                      |	КешТовары.Склад,
	                      |	КешТовары.Количество,
	                      |	ЕСТЬNULL(КешСвободныеОстатки.Остаток, 0) КАК СвободныйОстаток,
	                      |	КалендарныеГрафики.ДатаГрафика КАК ДатаГрафика,
	                      |	КешТовары.Календарь,
	                      |	КешТовары.ВариантКонтроля,
	                      |	ВЫБОР КешТовары.СрокПоставки
						  |		КОГДА 0
						  |		ТОГДА 1
						  |		ИНАЧЕ КешТовары.СрокПоставки КОНЕЦ КАК СрокПоставки,
	                      |	ЕСТЬNULL(КешСвободныеОстатки.ОстатокБезРезерва, 0) КАК ОстатокБезРезерва
	                      |ПОМЕСТИТЬ КешПредварительныйИтог
	                      |ИЗ
	                      |	КешТовары КАК КешТовары
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КешСвободныеОстатки КАК КешСвободныеОстатки
	                      |		ПО КешТовары.Номенклатура = КешСвободныеОстатки.Номенклатура
	                      |			И КешТовары.Характеристика = КешСвободныеОстатки.Характеристика
	                      |			И КешТовары.Склад = КешСвободныеОстатки.Склад
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ALPS_АктуальностьНоменклатуры.СрезПоследних КАК АктуальностьНоменклатуры
	                      |		ПО КешТовары.Номенклатура = АктуальностьНоменклатуры.Номенклатура
	                      |			И КешТовары.Характеристика = АктуальностьНоменклатуры.Характеристика
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
	                      |		ПО КешТовары.Календарь = КалендарныеГрафики.Календарь
	                      |			И (КалендарныеГрафики.ДеньВключенВГрафик)
	                      |			И (ВЫБОР
	                      |				КОГДА КешТовары.ГраницаГрафикаДоступности > ЕСТЬNULL(АктуальностьНоменклатуры.ДатаПоставки, ДАТАВРЕМЯ(1, 1, 1))
	                      |					ТОГДА КалендарныеГрафики.ДатаГрафика > ДОБАВИТЬКДАТЕ(КешТовары.ГраницаГрафикаДоступности, ДЕНЬ, ВЫБОР КешТовары.СрокПоставки КОГДА 0 ТОГДА 0 ИНАЧЕ 1 КОНЕЦ)
	                      |				ИНАЧЕ КалендарныеГрафики.ДатаГрафика >= ДОБАВИТЬКДАТЕ(АктуальностьНоменклатуры.ДатаПоставки, ДЕНЬ, ВЫБОР КешТовары.СрокПоставки КОГДА 0 ТОГДА 0 ИНАЧЕ 1 КОНЕЦ)
	                      |			КОНЕЦ)
	                      |			И (КалендарныеГрафики.ДатаГрафика < ДОБАВИТЬКДАТЕ(&ТекущаяДата, ДЕНЬ, 30))
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КешПредварительныйИтог.НомерСтроки,
	                      |	КешПредварительныйИтог.Номенклатура,
	                      |	КешПредварительныйИтог.Характеристика,
	                      |	КешПредварительныйИтог.Склад,
	                      |	КешПредварительныйИтог.Количество,
	                      |	КешПредварительныйИтог.СвободныйОстаток,
	                      |	КешПредварительныйИтог.Календарь,
	                      |	КешПредварительныйИтог.ДатаГрафика КАК ДатаГрафика,
	                      |	КешПредварительныйИтог.ВариантКонтроля,
	                      |	КешПредварительныйИтог.СрокПоставки,
	                      |	КешПредварительныйИтог.ОстатокБезРезерва
	                      |ПОМЕСТИТЬ КешТоварыСГраницейГрафика
	                      |ИЗ
	                      |	КешПредварительныйИтог КАК КешПредварительныйИтог
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КешПредварительныйИтог КАК ИтогДляСтроки
	                      |		ПО КешПредварительныйИтог.НомерСтроки = ИтогДляСтроки.НомерСтроки
	                      |			И КешПредварительныйИтог.Номенклатура = ИтогДляСтроки.Номенклатура
	                      |			И КешПредварительныйИтог.Характеристика = ИтогДляСтроки.Характеристика
	                      |			И КешПредварительныйИтог.Склад = ИтогДляСтроки.Склад
	                      |			И КешПредварительныйИтог.ДатаГрафика >= ИтогДляСтроки.ДатаГрафика
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	КешПредварительныйИтог.НомерСтроки,
	                      |	КешПредварительныйИтог.Номенклатура,
	                      |	КешПредварительныйИтог.Характеристика,
	                      |	КешПредварительныйИтог.Склад,
	                      |	КешПредварительныйИтог.Количество,
	                      |	КешПредварительныйИтог.СвободныйОстаток,
	                      |	КешПредварительныйИтог.Календарь,
	                      |	КешПредварительныйИтог.ВариантКонтроля,
	                      |	КешПредварительныйИтог.СрокПоставки,
	                      |	КешПредварительныйИтог.ОстатокБезРезерва,
	                      |	КешПредварительныйИтог.ДатаГрафика
	                      |
	                      |ИМЕЮЩИЕ
	                      |	КОЛИЧЕСТВО(ИтогДляСтроки.ДатаГрафика) = КешПредварительныйИтог.СрокПоставки
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КешТоварыСГраницейГрафика.НомерСтроки,
	                      |	КешТоварыСГраницейГрафика.Номенклатура КАК Номенклатура,
	                      |	КешТоварыСГраницейГрафика.Характеристика,
	                      |	КешТоварыСГраницейГрафика.Склад КАК Склад,
	                      |	КешТоварыСГраницейГрафика.Количество,
	                      |	ВЫБОР КешТоварыСГраницейГрафика.ВариантКонтроля
	                      |		КОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика)
	                      |			ТОГДА КешТоварыСГраницейГрафика.СвободныйОстаток
	                      |		ИНАЧЕ КешТоварыСГраницейГрафика.ОстатокБезРезерва
	                      |	КОНЕЦ КАК Остаток,
						  |	ВЫБОР КешТоварыСГраницейГрафика.Номенклатура.ТипНоменклатуры
						  |		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
						  |			ТОГДА &ТекущаяДата
	                      |		ИНАЧЕ ВЫБОР КешТоварыСГраницейГрафика.ВариантКонтроля
	                      |			КОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроля.ОстаткиСУчетомГрафика)
	                      |				ТОГДА КешТоварыСГраницейГрафика.ДатаГрафика
	                      |			ИНАЧЕ МИНИМУМ(КалендарныеГрафики.ДатаГрафика)
						  |		КОНЕЦ
	                      |	КОНЕЦ КАК ДатаГрафика
	                      |ИЗ
	                      |	КешТоварыСГраницейГрафика КАК КешТоварыСГраницейГрафика
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
	                      |		ПО КешТоварыСГраницейГрафика.Календарь = КалендарныеГрафики.Календарь
	                      |			И (КалендарныеГрафики.ДеньВключенВГрафик)
	                      |			И (КалендарныеГрафики.ДатаГрафика > &ТекущаяДата)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	КешТоварыСГраницейГрафика.НомерСтроки,
	                      |	КешТоварыСГраницейГрафика.Номенклатура,
	                      |	КешТоварыСГраницейГрафика.Характеристика,
	                      |	КешТоварыСГраницейГрафика.Склад,
	                      |	КешТоварыСГраницейГрафика.Количество,
	                      |	КешТоварыСГраницейГрафика.СвободныйОстаток,
	                      |	КешТоварыСГраницейГрафика.ДатаГрафика,
	                      |	КешТоварыСГраницейГрафика.Календарь,
	                      |	КешТоварыСГраницейГрафика.ВариантКонтроля,
	                      |	КешТоварыСГраницейГрафика.ОстатокБезРезерва
	                      |ИТОГИ
	                      |	МАКСИМУМ(Остаток)
	                      |ПО
	                      |	Номенклатура,
	                      |	Склад");
						  
	ТаблицаТовары = ПолучитьИзВременногоХранилища(ПараметрыСостоянияОбеспечения.АдресТовары);					  
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	Запрос.УстановитьПараметр("Склад", ПараметрыСостоянияОбеспечения.Склад);
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
		
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		ВыборкаСклад = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСклад.Следующий() Цикл
			
			КоличествоОстаток 				= ВыборкаСклад.Остаток;
			ВыборкаДетальныеЗаписи			= ВыборкаСклад.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				Если ВыборкаДетальныеЗаписи.Количество > КоличествоОстаток Тогда
					ТаблицаТовары[ВыборкаДетальныеЗаписи.НомерСтроки-1].ДатаОтгрузки = ?(ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ДатаГрафика), ВыборкаДетальныеЗаписи.ДатаГрафика, ТекущаяДата());
				Иначе
					ТаблицаТовары[ВыборкаДетальныеЗаписи.НомерСтроки-1].ДатаОтгрузки = ТекущаяДата();
				КонецЕсли;
				
				КоличествоОстаток = КоличествоОстаток - ВыборкаДетальныеЗаписи.Количество;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаТовары;
	
КонецФункции

Функция  ДанныеВнутреннейТаблицыЗапроса(Запрос, ИмяВнутреннейТаблицы) Экспорт

     Перем лмПредТекст, Р;

    лмПредТекст=Запрос.Текст;

    Запрос.Текст="

    |ВЫБРАТЬ

    |    *

    |ИЗ

    |    "+ИмяВнутреннейТаблицы+" КАК Таблица";

    Р=Запрос.Выполнить().Выгрузить();

    Запрос.Текст=лмПредТекст;

    Возврат Р; 

КонецФункции


Функция СведенияОВнешнейОбработке() Экспорт
	
	Версия = "1.0.005";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Расчет даты отгрузки");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Расчет даты отгрузки [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
	ДобавитьКоманду(ТаблицаКоманд, "Расчет даты отгрузки [" + Версия + "]", "Форма", "ОткрытиеФормы", Ложь, "РДО");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

Процедура ВыполнитьКоманду(Индентификатор) Экспорт
	Выполнить(Индентификатор);	
КонецПроцедуры

