#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Номенклатура 		= Параметры.Номенклатура;
	ЕдиницаИзмерения    = Номенклатура.ЕдиницаИзмерения;
	Количество			= 1;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЕдНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)	
	Элементы.Ед.СписокВыбора.ЗагрузитьЗначения(ВыбратьУпаковку());	
КонецПроцедуры

&НаКлиенте
Процедура ЕдНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	 
	СтандартнаяОбработка 	= Ложь;
	Обработчик 				= Новый ОписаниеОповещения("ВыборЕдИзВыпадающегоСписка", ЭтаФорма);
	СписокВыбора			= Новый СписокЗначений;

	СписокВыбора.ЗагрузитьЗначения(ВыбратьУпаковку());
	
	НачальныйЭлемент = СписокВыбора.НайтиПоЗначению(ЕдиницаИзмерения);
	Если НачальныйЭлемент = Неопределено Тогда
		ПоказатьВыборИзСписка(Обработчик, СписокВыбора, Элемент);
	Иначе 
		ПоказатьВыборИзСписка(Обработчик, СписокВыбора, Элемент, НачальныйЭлемент);
	КонецЕсли;
	 
КонецПроцедуры

&НаКлиенте
Процедура ВыборЕдИзВыпадающегоСписка(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ЕдиницаИзмерения = ВыбранныйЭлемент.Значение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)	
	Результат = Новый Структура;
	Результат.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	Результат.Вставить("Количество", Количество);
	
	Если ТипЗнч(ЕдиницаИзмерения) = Тип("СправочникСсылка.УпаковкиНоменклатуры") Тогда
		Результат.Вставить("Упаковка",ЕдиницаИзмерения);
	КонецЕсли;    
	
	Закрыть(Результат);
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере 
Функция ВыбратьУпаковку()
	
	Результат 	  = Новый Массив;	
	Запрос 		  = Новый Запрос();
	Запрос.Текст  = ПолучитьТекстЗапросаУпаковки();	
		
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("НаборУпаковок", Номенклатура.НаборУпаковок);
	
 	ТаблицаУпаковок = Запрос.Выполнить().Выгрузить();
	
	Результат = ТаблицаУпаковок.ВыгрузитьКолонку("Значение");
	Результат.Вставить(0, Номенклатура.ЕдиницаИзмерения);	
	
	Возврат Результат;
		
КонецФункции

&НаСервере 
Функция ПолучитьТекстЗапросаУпаковки()
	
	ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Ссылка 			КАК Значение,
		|	Наименование 	КАК Представление
		|ИЗ
		|	Справочник.УпаковкиНоменклатуры
		|ГДЕ
		|	Владелец = ВЫБОР КОГДА &НаборУпаковок =  ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры)
		|                    ТОГДА &Номенклатура
		|                    КОГДА &НаборУпаковок <> ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ПустаяСсылка)
		|                    ТОГДА &НаборУпаковок
		|					 ИНАЧЕ Неопределено
		|			   КОНЕЦ 
		|И  ПометкаУдаления = Ложь";
	 
	 Возврат ТекстЗапроса;
	
 КонецФункции

#КонецОбласти






