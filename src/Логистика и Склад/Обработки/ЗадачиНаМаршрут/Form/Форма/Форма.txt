

#Область ИнтерфейсАвтоматическихТестов

&НаКлиенте
Перем ЮнитТест;

&НаКлиенте
Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮнитТест = ЮнитТестирование;
	
	СписокТестов = Новый Массив;
	СписокТестов.Добавить("Тест_ПроверитьЗагрузкуСпискаЗадач");
	СписокТестов.Добавить("Тест_ПроверитьВалидностьЗапросаЗагрузкиСпискаЗадач"); 
	//СписокТестов.Добавить("Тест_ПроверитьЗагрузкуРеквизитовЗадачи");

	Возврат СписокТестов;
	
КонецФункции

&НаКлиенте
Процедура Тест_ПроверитьЗагрузкуСпискаЗадач()  Экспорт
	ЗагрузитьСписокЗадач();
КонецПроцедуры

&НаКлиенте
Процедура Тест_ПроверитьВалидностьЗапросаЗагрузкиСпискаЗадач()  Экспорт
	ТекстЗапроса =ПолучитьТекстЗапросаСписокЗадач();
	ЮнитТест.ПроверитьТип(ТекстЗапроса, Тип("Строка"));
	ЮнитТест.ПроверитьВалидностьЗапроса(ТекстЗапроса);
КонецПроцедуры

&НаКлиенте
Процедура Тест_ПроверитьЗагрузкуРеквизитовЗадачи()  Экспорт
	Задача = ПредопределенноеЗначение("Справочник.МестаДоставки.ПустаяСсылка");
	ЗагрузитьРеквизитыЗадачи(Задача);
КонецПроцедуры




#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;	
	КонецЕсли;
	СтатусЗагрузки = 1; // Поставленные задачи
	Автор		   = ПараметрыСеанса.ТекущийПользователь;
	ЗагрузитьСписокЗадач();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("ОбновитьСписокЗадач", 30); 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатусЗагрузкиПриИзменении(Элемент)
	Если СтатусЗагрузки = 3 Тогда
		ИспользиваетсяОтборПоДате 	= Истина;
		КешОтборПоДате 				= ОтборПоДате.Вариант;
		ОтборПоДате.Вариант			= ВариантСтандартногоПериода.Месяц;
	ИначеЕсли  ИспользиваетсяОтборПоДате   Тогда
		 ОтборПоДате				=   КешОтборПоДате;
		 ИспользиваетсяОтборПоДате 	= Ложь;
	КонецЕсли;
	
	УстановитьТекущуюЗадачу();
	ЗагрузитьСписокЗадач();
КонецПроцедуры

&НаКлиенте
Процедура АвторПриИзменении(Элемент)
	УстановитьТекущуюЗадачу();
	ЗагрузитьСписокЗадач();
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоДатеПриИзменении(Элемент)
	УстановитьТекущуюЗадачу();
	ЗагрузитьСписокЗадач();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблициФормыСписокЗадач

&НаКлиенте
Процедура СписокЗадачПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.СписокЗадач.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ЗагрузитьРеквизитыЗадачи(ТекущиеДанные.Задача);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокЗадачПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокЗадачПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ 		= Истина;
	Если Копирование Тогда
		Операция    = Элемент.ТекущиеДанные.Операция;
		СоздатьЗадачуНаМаршрутНаКлиенте(Операция,Ложь);	
	КонецЕсли;
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСписокЗадач()
	УстановитьТекущуюЗадачу();
	ЗагрузитьСписокЗадач();
КонецПроцедуры

&НаКлиенте
Процедура ПечатьQRКода(Команда)
	УстановитьТекущуюЗадачу();
	ТабДок = ПечатьQRКодаНаСервере(ТекущаяЗадача);
	Для Каждого ТабличныйДокумент ИЗ ТабДок Цикл
        ТабличныйДокумент.Напечатать(РежимИспользованияДиалогаПечати.Использовать);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПредваритальнойПросмотр(Команда)
	УстановитьТекущуюЗадачу();
	ТекущиеДанные = Элементы.СписокЗадач.ТекущиеДанные;

	Операция		= ТекущиеДанные.Операция;
	
	Если Операция = ПредопределенноеЗначение("Перечисление.КТС_ОперацииМеста.ВозвратНаСервисЦентрОтПартнера") 
		ИЛИ Операция = ПредопределенноеЗначение("Перечисление.КТС_ОперацииМеста.ВозвратНаСервисЦентрОтПеревозчика") Тогда
		НазваничМакета	= "МестаДоставки_ПечатьQRЭтикеткиПредварительнойПросмотр";
	Иначе
		НазваничМакета	= "МестаДоставки_ПечатьQRЭтикетки";
	КонецЕсли;
		
	ТабДок = ПредварительнойПросмотрНаСервере(ТекущаяЗадача,НазваничМакета);
		
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТабДок", ТабДок);
	ОткрытьФорму("ВнешняяОбработка.ЗадачиНаМаршрут.Форма.ФормаТабДок", ПараметрыФормы);	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьЗадачу(Команда)
	УстановитьТекущуюЗадачу();
	Если ТекущаяЗадача <> Неопределено И Не ТекущаяЗадача.Пустая() Тогда
		УстановитьСтатусЗадачиНаСервере(ТекущаяЗадача, ПредопределенноеЗначение("Перечисление.СтатусыОтгрузкиМест.ЗадачаВыполнена"), Истина);		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗадачу(Команда)
	УстановитьТекущуюЗадачу();
	Если ТекущаяЗадача <> Неопределено И Не ТекущаяЗадача.Пустая() Тогда
		ПараметрыМеста = ПроверитьЗадачуНаВозможностьУдаления();
		Если ПараметрыМеста.КоличествоСтатусов>1 Тогда
		    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Нельзя удалить данную задачу. Так как она уже в процесе работы.'"));
			Возврат;
		КонецЕсли;
		
		Если ПараметрыМеста.ЗадачаУкомплектована Тогда  
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Нельзя удалить данную задачу. Так как она уже укомплектована. Разукомплектуйте ее в Отгрузке Мест'"));
			Возврат;
		КонецЕсли;
		
		УстановитьСтатусЗадачиНаСервере(ТекущаяЗадача, ПредопределенноеЗначение("Перечисление.СтатусыОтгрузкиМест.ЗадачаОтменена"), Истина);		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗадачуЗабратьСоСклада(Команда)
	СоздатьЗадачуНаМаршрутНаКлиенте(1,Истина);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗадачуЗабратьУПартнера(Команда)
	СоздатьЗадачуНаМаршрутНаКлиенте(2,Истина);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВозвратНаСервисЦентр(Команда)
	СоздатьЗадачуНаМаршрутНаКлиенте(3,Истина);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗадачуИнформационаяЗадача(Команда)
	СоздатьЗадачуНаМаршрутНаКлиенте(4,Истина);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗадачуВозвратНаСЦОтПеревозчика(Команда)
	СоздатьЗадачуНаМаршрутНаКлиенте(5,Истина);	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗСЦ(Команда)
	ТекущиеДанные = Элементы.СписокЗадач.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ДокументСсылка 		= ТекущиеДанные.Задача;
	
	Если  ТипЗнч(ДокументСсылка) = Тип("СправочникСсылка.МестаДоставки") Тогда 
		ОбъектОснование = ДокументСсылка;		
	Иначе 
		Возврат;
	КонецЕсли;
	
	Если НЕ СтатусЗагрузки = 3 ИЛИ НЕ ПустаяСтрока(ТекущиеДанные.ТранзитныйСклад) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Нельзя вводить документ на основании не завершенной задачи на маршрут'"));
		Возврат;
	КонецЕсли;
	
    ПараметрыФормы = Новый Структура(); 
    ПараметрыФормы.Вставить("Основание", ОбъектОснование);
	ФормаД = ПолучитьФорму("Документ.ГНАТ_ЗаявкаСервисногоЦентра.Форма.ФормаДокумента",ПараметрыФормы);
	ФормаД.Открыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьТекущуюЗадачу()
	ТекущиеДанные = Элементы.СписокЗадач.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущаяЗадача = ТекущиеДанные.Задача;
	Иначе
		ТекущаяЗадача = Неопределено;
	КонецЕсли;
КонецПроцедуры // УстановитьТекущуюЗадачу()

&НаСервере
Процедура ВостановитьТекущуюЗадачу()
	
	Если Не ТекущаяЗадача.Пустая() И Не ТекущаяЗадача = Неопределено Тогда
		СтруктураПоиска = Новый Структура("Задача", ТекущаяЗадача);
		РезультатПоиска = СписокЗадач.НайтиСтроки(СтруктураПоиска);
		Если РезультатПоиска.Количество() > 0 Тогда
			Элементы.СписокЗадач.ТекущаяСтрока = РезультатПоиска[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // УстановитьТекущуюЗадачу()

&НаСервере
Процедура ЗагрузитьСписокЗадач()

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос 		 	= Новый Запрос;
	Запрос.Текст 	= ПолучитьТекстЗапросаСписокЗадач();
	ДатаНачало		= ОтборПоДате.ДатаНачала;
	ДатаОкончания	= ?(ОтборПоДате.ДатаОкончания = Дата(1,1,1), Дата(3999,12,31, 23, 59, 59), ОтборПоДате.ДатаОкончания);
	 
	Запрос.УстановитьПараметр("Автор", Автор);
	Запрос.УстановитьПараметр("ДатаНачало", 	ДатаНачало);
	Запрос.УстановитьПараметр("ДатаОкончания", 	ДатаОкончания);

	СписокЗадач.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ВостановитьТекущуюЗадачу();
	
КонецПроцедуры // ЗагрузитьСписокзадач()
 
&НаСервере
Функция ПолучитьТекстЗапросаСписокЗадач()

	ТекстЗапроса = "ВЫБРАТЬ
				|	ЗадачиНаМаршут.Место КАК Задача,
				|	ПРЕДСТАВЛЕНИЕ(ЗадачиНаМаршут.Место) КАК ЗадачаПредставление,
				|	ЗадачиНаМаршут.ВариантМаршрута КАК ВариантМаршрута,
				|	ПРЕДСТАВЛЕНИЕ(ЗадачиНаМаршут.ВариантМаршрута) КАК ВариантМаршрутаПредставление,
				|	ЗадачиНаМаршут.Место.Владелец КАК Получатель,
				|	ПРЕДСТАВЛЕНИЕ(ЗадачиНаМаршут.Место.Владелец) КАК ПолучательПредставление,
				|	ЗадачиНаМаршут.Отправитель КАК Отправитель2,
				|	ПРЕДСТАВЛЕНИЕ(ЗадачиНаМаршут.Отправитель) КАК ОтправительПредставление2,
				|	ПервоначальныеДанные.Пользователь КАК АвторЗадачи,
				|	ЗадачиНаМаршут.Место.ТекстЗадачи КАК Коментарии,
				|	ВЫБОР
				|		КОГДА ЗадачиНаМаршут.Место.Транзит
				|			ТОГДА ЗадачиНаМаршут.Место.ТранзитныйСклад
				|		ИНАЧЕ """"
				|	КОНЕЦ КАК ТранзитныйСклад,
				|	ВЫБОР
				|		КОГДА ЗадачиНаМаршут.Место.Транзит
				|			ТОГДА ПРЕДСТАВЛЕНИЕ(ЗадачиНаМаршут.Место.ТранзитныйСклад)
				|		ИНАЧЕ """"
				|	КОНЕЦ КАК ТранзитныйСкладПредставления,
				|	ЗадачиНаМаршут.Место.Операция 			КАК Операция,
				|	ЗадачиНаМаршут.Место.ДокументОснования 	КАК ДокументОснования,
				|	ЗадачиНаМаршут.Место.ТипВозвратаТовара 	КАК ТипВозвратаТовара,
				|	ЗадачиНаМаршут.Место.ПричинаВозврата	КАК ПричинаВозврата,
				|	ЗадачиНаМаршут.Место.НомерЗаявки 		КАК НомерЗаявки,
				|	ПервоначальныеДанные.Отправитель КАК Отправитель1,
				|	ПРЕДСТАВЛЕНИЕ(ПервоначальныеДанные.Отправитель) КАК ОтправительПредставление1,
				|	ЗадачиНаМаршут.Пользователь КАК Ответственный
				|ИЗ
				|	РегистрСведений.ЗадачиНаМаршрут.СрезПоследних КАК ЗадачиНаМаршут	
				|		
				|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиНаМаршрут.СрезПервых(, %ОтборПоОтветственному%) КАК ПервоначальныеДанные
				|ПО ЗадачиНаМаршут.Место = ПервоначальныеДанные.Место
				|%УсловияОтбора%";
 				  
	Если Автор.Пустая() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ОтборПоОтветственному%", "");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ОтборПоОтветственному%", "Пользователь = &Автор");
	КонецЕсли;
	
	Если СтатусЗагрузки = 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловияОтбора%", ""+ ?(ПустаяСтрока(Сортировка_Поле) ИЛИ ПустаяСтрока(Сортировка_Направление), " УПОРЯДОЧИТЬ ПО ЗадачиНаМаршут.Период Возр ", " УПОРЯДОЧИТЬ ПО " + Сортировка_Поле + Сортировка_Направление));
	ИначеЕсли СтатусЗагрузки = 1 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловияОтбора%", "
				|ГДЕ
				|	ЗадачиНаМаршут.СтатусОтгрузки = ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ЗадачаПоставлена)
				|И	ЗадачиНаМаршут.Период МЕЖДУ &ДатаНачало И &ДатаОкончания"+ ?(ПустаяСтрока(Сортировка_Поле) ИЛИ ПустаяСтрока(Сортировка_Направление), " УПОРЯДОЧИТЬ ПО ЗадачиНаМаршут.Период Возр ", " УПОРЯДОЧИТЬ ПО " + Сортировка_Поле + Сортировка_Направление));
		
				
	ИначеЕсли СтатусЗагрузки = 2 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловияОтбора%", "
				|ГДЕ
				|	ЗадачиНаМаршут.СтатусОтгрузки = ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ЗадачаВПроцессеВыполнения)
				|И	ЗадачиНаМаршут.Период МЕЖДУ &ДатаНачало И &ДатаОкончания"+ ?(ПустаяСтрока(Сортировка_Поле) ИЛИ ПустаяСтрока(Сортировка_Направление), " УПОРЯДОЧИТЬ ПО ЗадачиНаМаршут.Период Возр ", " УПОРЯДОЧИТЬ ПО " + Сортировка_Поле + Сортировка_Направление));
		
	ИначеЕсли СтатусЗагрузки = 3 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловияОтбора%", "
				|ГДЕ
				|	ЗадачиНаМаршут.СтатусОтгрузки = ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ЗадачаВыполнена)
				|И	ЗадачиНаМаршут.Период МЕЖДУ &ДатаНачало И &ДатаОкончания"+ ?(ПустаяСтрока(Сортировка_Поле) ИЛИ ПустаяСтрока(Сортировка_Направление), " УПОРЯДОЧИТЬ ПО ЗадачиНаМаршут.Период Возр ", " УПОРЯДОЧИТЬ ПО " + Сортировка_Поле + Сортировка_Направление));

	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловияОтбора%", "
				|ГДЕ
				|	ЗадачиНаМаршут.СтатусОтгрузки = ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ЗадачаОтменена)
				|И	ЗадачиНаМаршут.Период МЕЖДУ &ДатаНачало И &ДатаОкончания"+ ?(ПустаяСтрока(Сортировка_Поле) ИЛИ ПустаяСтрока(Сортировка_Направление), " УПОРЯДОЧИТЬ ПО ЗадачиНаМаршут.Период Возр ", " УПОРЯДОЧИТЬ ПО " + Сортировка_Поле + Сортировка_Направление));

	КонецЕсли;
			

				
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаСписокЗадач()

&НаСервере
Процедура УстановитьСтатусЗадачиНаСервере(Задача, СтатусЗадачи, УстановитьБлокировку=Ложь) 
	Справочники.МестаДоставки.УстановитьСтатусЗадачиНаСервере(Задача, СтатусЗадачи, УстановитьБлокировку);
	ЗагрузитьСписокЗадач();
КонецПроцедуры // УстановитьСтатусЗадачиНаСервере()
 
&НаСервере
Функция ЗагрузитьРеквизитыЗадачи(Задача)

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	Период 						КАК НачалоВыполненияЗадачи,
	                      |	Место.ТекстЗадачи 			КАК ТекстЗадачи,
	                      |	Место.ДокументОснования 	КАК ДокументОснования,
						  |	Место.КонтактноеЛицо 		КАК КонтактноеЛицо,
						  |	Место.НомерЗаявки 			КАК НомерЗаявки,
						  |	Место.ТипВозвратаТовара 	КАК ТипВозвратаТовара,
						  |	Место.ПричинаВозврата 		КАК ПричинаВозврата,
						  | Место.ИспользоватьОтображениеКакЗадачи  Как  ИспользоватьОтображениеКакЗадачи
                          |ИЗ
	                      |	РегистрСведений.ЗадачиНаМаршрут
	                      |ГДЕ
	                      |	Место = &Задача
						  |	И СтатусОтгрузки = &СтатусОтгрузки
						  |УПОРЯДОЧИТЬ ПО
						  |	Период УБЫВ");
						  
	Запрос.УстановитьПараметр("Задача", Задача);
	
	Если СтатусЗагрузки = 1 Тогда
		Запрос.УстановитьПараметр("СтатусОтгрузки", Перечисления.СтатусыОтгрузкиМест.ЗадачаПоставлена);
	ИначеЕсли СтатусЗагрузки = 2 Тогда
		Запрос.УстановитьПараметр("СтатусОтгрузки", Перечисления.СтатусыОтгрузкиМест.ЗадачаВПроцессеВыполнения);
	ИначеЕсли СтатусЗагрузки = 3 Тогда
		Запрос.УстановитьПараметр("СтатусОтгрузки", Перечисления.СтатусыОтгрузкиМест.ЗадачаВыполнена);
	ИначеЕсли СтатусЗагрузки = 4 Тогда
		Запрос.УстановитьПараметр("СтатусОтгрузки", Перечисления.СтатусыОтгрузкиМест.ЗадачаОтменена);
	Иначе 
		Запрос.УстановитьПараметр("СтатусОтгрузки", Перечисления.СтатусыОтгрузкиМест.ЗадачаПоставлена);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ТекстЗадачи 						= Выборка.ТекстЗадачи;
		НачалоВыполнения    				= Выборка.НачалоВыполненияЗадачи;
		ДокументОснования					= Выборка.ДокументОснования;
		ИспользоватьОтображениеКакЗадачи	= Выборка.ИспользоватьОтображениеКакЗадачи;
		КонтактноеЛицо						= Выборка.КонтактноеЛицо;
		НомерЗаявки							= Выборка.НомерЗаявки;
		ТипВозвратаТовара					= Выборка.ТипВозвратаТовара;
		ПричинаВозврата						= Выборка.ПричинаВозврата;

	Иначе
		ТекстЗадачи 						= Неопределено;
		НачалоВыполнения    				= Неопределено;
		ДокументОснования					= Неопределено;
		ИспользоватьОтображениеКакЗадачи	= Неопределено;
		КонтактноеЛицо						= Неопределено;
		НомерЗаявки							= Неопределено;
		ТипВозвратаТовара					= Неопределено;
		ПричинаВозврата						= Неопределено;


	КонецЕсли;
	СтруктураЗадачи = Новый Структура("ТекстЗадачи, ИспользоватьОтображениеКакЗадачи", ТекстЗадачи, ИспользоватьОтображениеКакЗадачи);	
   Возврат СтруктураЗадачи;
КонецФункции// ЗагрузитьРеквизитыЗадачи()
 
&НаСервереБезКонтекста
Функция СформироватьКолекциюПечатныхФорм(ИмяИсточникаДанных, ПараметрыИсточника, ОбъектыПечати = Неопределено, ПараметрыВывода = Неопределено) 
	Перем КоллекцияПечатныхФорм;
    ИсточникДанных = git_ПереопределениеОбработчиковСервер.ПолучитьСсылкуНаВнешнийОбъект(ИмяИсточникаДанных);                
    УправлениеПечатьюПереопределяемый.ПечатьПоВнешнемуИсточнику(ИсточникДанных, ПараметрыИсточника, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);        
    Возврат КоллекцияПечатныхФорм.ВыгрузитьКолонку("ТабличныйДокумент");                                        
КонецФункции

&НаКлиенте
Процедура СортироватьПоВозрастанию(Команда)
	ВыполнитьСортировку(" Возр");	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьПоУбыванию(Команда)
	ВыполнитьСортировку(" Убыв");
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьСортировку(НаправлениеСортировки)
	
	ТекущиеДанные    = Элементы.СписокЗадач.ТекущиеДанные;
	ТекущийЭлементТЗ = Элементы.СписокЗадач.ТекущийЭлемент;
	Если НЕ ТекущиеДанные = Неопределено И НЕ ТекущийЭлементТЗ = Неопределено Тогда
		ИмяЭлемента = ТекущийЭлементТЗ.Имя;
		Если НЕ НаправлениеСортировки = Неопределено Тогда
			УстановитьЗначенияСортировки(Сортировка_Поле, ИмяЭлемента);
			Сортировка_Направление = НаправлениеСортировки;
			ЗагрузитьСписокЗадач();

			//СписокЗадач.Сортировать(ИмяЭлемента + Сортировка_Направление);
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияСортировки(ПолеСортировки, ИмяЭлемента)
	
	Если 	  ИмяЭлемента = "СписокЗадачЗадачаПредставление" 		   	Тогда ПолеСортировки = "Задача";
	ИначеЕсли ИмяЭлемента = "СписокЗадачАвторЗадачи"           			Тогда ПолеСортировки = "АвторЗадачи";
	ИначеЕсли ИмяЭлемента = "СписокЗадачМаршрутПредставление"      		Тогда ПолеСортировки = "ВариантМаршрута";
	ИначеЕсли ИмяЭлемента = "СписокЗадачОтправительПредставление" 	    Тогда ПолеСортировки = "Отправитель";
	ИначеЕсли ИмяЭлемента = "СписокЗадачТранзитныйСкладПредставления"   Тогда ПолеСортировки = "ТранзитныйСклад";
	ИначеЕсли ИмяЭлемента = "СписокЗадачПолучательПредставление"        Тогда ПолеСортировки = "Получатель";
	ИначеЕсли ИмяЭлемента = "СписокЗадачКоментарии" 					Тогда ПолеСортировки = "Коментарии";
	КонецЕсли;
	
КонецПроцедуры

// Получить Предварительной просмотр QR-етикетки
//
// Параметры:
//  ТекущаяЗадача  - СправочникСсылка.МестаДоставки - текущая задача,выделена пользователем
&НаСервере
Функция ПредварительнойПросмотрНаСервере(ТекущаяЗадача,НазваничМакета)
	Возврат Справочники.МестаДоставки.ПредваритальнойПросмотр(ТекущаяЗадача,НазваничМакета);
КонецФункции // ()

// Получить Печать QR-етикетки задачи
//
// Параметры:
//  ТекущаяЗадача  - СправочникСсылка.МестаДоставки - текущая задача,выделена пользователем
&НаСервере
Функция ПечатьQRКодаНаСервере(ТекущаяЗадача)
	Возврат РеквизитФормыВЗначение("Объект").ПечатьQRКодаНаСервере(ТекущаяЗадача);
КонецФункции  // ПечатьQRКодаНаСервере()

// Выполнить действия после создания задачи
//
// Параметры:
//  Результат  - СправочникСсылка.МестаДоставки - текущая задача,выделена пользователем
Процедура ВыполнитьПослеСозданияЗадачи(Результат,Параметры) Экспорт
	Если Результат <> Неопределено Тогда
		ТекущаяЗадача = Результат.Задача;
		ЗагрузитьСписокЗадач();
	КонецЕсли;
КонецПроцедуры

// Проверить задачу на отправку ее на АСЦ
//
// Параметры:
//  НЕТ
//
&НаСервере
Функция ПроверитьЗадачуНаОтправкуПоАСЦ()
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КонтактноеЛицо КАК КонтактноеЛицо
	               |ИЗ
	               |	Справочник.МестаДоставки 
	               |ГДЕ
	               |	Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",ТекущаяЗадача);
	РезЗапроса = Запрос.Выполнить().Выгрузить();
	КонтактноеЛицо      = РезЗапроса[0].КонтактноеЛицо;
	Возврат КонтактноеЛицо;
	 
КонецФункции // ПроверитьЗадачуНаОтправкуПоАСЦ()

// Создать задачу на маршрут
//
// Параметры:
//  Операция  		- Строка - наименовании операции создания задачи
//
//  Копирование  	- Булево - Истина,если задачу копируют, Ложь - если новая задача
&НаКлиенте
Процедура СоздатьЗадачуНаМаршрутНаКлиенте(ОперацияЗадачи,НоваяЗадача)
	ОписанияПослеСозданияЗадачи = Новый ОписаниеОповещения("ВыполнитьПослеСозданияЗадачи",ЭтаФорма);
	
	ПараметрыФормы   = Новый Структура;

	Если НоваяЗадача Тогда
		Операция		=  ПолучитьОперациюЗадачи(ОперацияЗадачи);
	Иначе
		ТекущиеДанные 	 = Элементы.СписокЗадач.ТекущиеДанные;
		Операция		 = ОперацияЗадачи;
		ПараметрыФормы.Вставить("Место" ,		ТекущиеДанные.Задача);
		ПараметрыФормы.Вставить("Отправитель",	ТекущиеДанные.Отправитель1);
		ПараметрыФормы.Вставить("Получатель" ,	ТекущиеДанные.Получатель);

	КонецЕсли;
	
	ПараметрыФормы.Вставить("НоваяЗадача" ,НоваяЗадача);
	ПараметрыФормы.Вставить("Операция" ,	Операция);

	ОткрытьФорму("ВнешняяОбработка.ЗадачиНаМаршрут.Форма.ФормаСозданияЗадачи",
				ПараметрыФормы,ЭтаФорма,
				,
				,
				,
				ОписанияПослеСозданияЗадачи,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
				);


КонецПроцедуры

//Получить значения операции задачи
//
// Параметры:
//  ОперацияЗадачи  - Число - номер операции
//
// Возвращаемое значение:
//  Перечисления.КТС_ОперацииМеста   - предопределенные значения задачи соотвествие номера операци задачи
//
&НаСервере
Функция ПолучитьОперациюЗадачи(ОперацияЗадачи)
	Если ОперацияЗадачи=1 Тогда
		Операция = Перечисления.КТС_ОперацииМеста.ЗабратьСоСклада;
	ИначеЕсли  ОперацияЗадачи=2 Тогда
		Операция = Перечисления.КТС_ОперацииМеста.ЗабратьОтПартнера;
	ИначеЕсли  ОперацияЗадачи=3 Тогда
		Операция = Перечисления.КТС_ОперацииМеста.ВозвратНаСервисЦентрОтПартнера;
	ИначеЕсли  ОперацияЗадачи=4 Тогда
		Операция = Перечисления.КТС_ОперацииМеста.ИнформационаяЗадача;
	ИначеЕсли  ОперацияЗадачи=5 Тогда
		Операция = Перечисления.КТС_ОперацииМеста.ВозвратНаСервисЦентрОтПеревозчика;
	Иначе 
		 Операция = Неопределено;
	КонецЕсли;
	
	Возврат Операция;
КонецФункции //ПолучитьОперациюЗадачи()

// Получить значения статуса и возможность удаления задачи
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Структура  - Статус задачи, возможность удаления
//
&НаСервере
Функция ПроверитьЗадачуНаВозможностьУдаления()
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыМеста = Новый Структура("КоличествоСтатусов, ЗадачаУкомплектована",1,ЛОЖЬ);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗадачиНаМаршрут.Место,
		|	КОЛИЧЕСТВО(ЗадачиНаМаршрут.СтатусОтгрузки) КАК СтатусОтгрузки
		|ПОМЕСТИТЬ КешЗаписиПоЗадаче
		|ИЗ
		|	РегистрСведений.ЗадачиНаМаршрут КАК ЗадачиНаМаршрут
		|ГДЕ
		|	ЗадачиНаМаршрут.Место = &Место
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗадачиНаМаршрут.Место
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КешЗаписиПоЗадаче.Место,
		|	КешЗаписиПоЗадаче.Место.ЗадачаУкомплектована КАК ЗадачаУкомплектована,
		|	КешЗаписиПоЗадаче.СтатусОтгрузки КАК КоличествоСтатусов
		|ИЗ
		|	КешЗаписиПоЗадаче КАК КешЗаписиПоЗадаче";
	
	Запрос.УстановитьПараметр("Место", ТекущаяЗадача);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ПараметрыМеста,Выборка);		
	КонецЦикла;
	
	Возврат ПараметрыМеста;	

КонецФункции // ПроверитьЗадачуНаВозможностьУдаления()


#КонецОбласти

                                              
 