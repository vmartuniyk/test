#Область ИнтерфейсАвтоматическихТестов

Перем ЮнитТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮнитТест = ЮнитТестирование;
	
	СписокТестов = Новый Массив;
	
	ПараметрыТеста = Новый Структура("ИмяТеста, Транзакция, Параметр", "Тест_Заглушка");
	СписокТестов.Добавить(ПараметрыТеста);
	
	Возврат СписокТестов;
	
КонецФункции

Функция Тест_Заглушка(Параметр) Экспорт
	ЮнитТест.ПроверитьИстину(Истина,);
КонецФункции // Тест_Заглушка()


#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура ПровестиРассылкуМаршрутногоЛиста() Экспорт
	
	ТекущаяДата = ТекущаяДата();
	
	Если ДеньНедели(ТекущаяДата) >=1 И ДеньНедели(ТекущаяДата) <= 5 Тогда
		
		Если Час(ТекущаяДата) = 11 Тогда
			ПровестиУтренноюРассылку();
		КонецЕсли;
		
		Если Час(ТекущаяДата) = 17 И Минута(ТекущаяДата) >= 30 Тогда
			ПровестиВечернююРассылку();
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДеньНедели(ТекущаяДата) = 6 Тогда
		Если Час(ТекущаяДата) = 10 Тогда
			ПровестиУтренноюРассылку();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьОписаниеИзменений() Экспорт

	ТекстИзменений = НСтр("ru='[0.4.036] Отключена поддержка комплектации места с помощью мышки.
	|[0.4.019] Добавлена поддержка работы из задачами без реквизита места «Начало выполнения задачи», задача http://r.ktc-ua.com/issues/3133.'");
	
	Возврат ТекстИзменений;

КонецФункции // ПолучитьОписаниеИзменений()
 

Функция ПолучитьШаблонМакета(ПараметрыЗаполнения = Неопределено) Экспорт
	
	Таб	= Новый ТабличныйДокумент;
	ПутьКФайлуШаблона	= СтрЗаменить(ПараметрыЗаполнения,"ПФ_","Шаблон");	
	Сообщить(ПутьКФайлуШаблона);
	Таб.Прочитать(ПутьКФайлуШаблона);
	ХешШаблон			=	ПолучитьХешМодуля(ПутьКФайлуШаблона);
	
	Возврат  ХешШаблон;
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьХешМодуля(ПутьКФайлу)  Экспорт
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.SHA1);
	ХешированиеДанных.ДобавитьФайл(ПутьКФайлу);
	Возврат ХешированиеДанных.ХешСумма;
	
КонецФункции

Процедура ПровестиУтренноюРассылку()

	ТабДокумент = Новый ТабличныйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаУтреннейРассылки();
	
	Запрос.УстановитьПараметр("ВидКонтактнойИнформацииМобильныйТелефон", Справочники.ВидыКонтактнойИнформации.ПолучитьСсылку(Новый УникальныйИдентификатор("7da99962-488d-11e0-9f98-001517115d85")));
	Запрос.УстановитьПараметр("ВидКонтактнойИнформацииДополнительныйМобильныйТелефон", Справочники.ВидыКонтактнойИнформации.ПолучитьСсылку(Новый УникальныйИдентификатор("a8ec65fe-c74a-11e1-9d4d-001e673c80fc"))); 
	Запрос.УстановитьПараметр("Маршрут", Справочники.ВариантыМаршрутов.ПолучитьСсылку(Новый УникальныйИдентификатор("bd28d8b3-f5b6-11e2-8f22-001e673c80fa")));
	Запрос.УстановитьПараметр("ДатаДляЗадач", ТекущаяДата());
	Запрос.УстановитьПараметр("ВидКонтактнойИнформацииПользователя", Справочники.ВидыКонтактнойИнформации.ПолучитьСсылку(Новый УникальныйИдентификатор("8dc2ec9e-55b5-11e1-8ee4-001e67162d38")));
	Запрос.УстановитьПараметр("СегментПеревозчики", Справочники.СегментыПартнеров.ПолучитьСсылку(Новый УникальныйИдентификатор("b837dc86-07b1-11e2-9d4d-001e673c80fc")));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СформироватьТабличныйДокументМаршрутныйЛистРассылка(ТабДокумент, РезультатЗапроса);
	
	ВремФайл 					= ПолучитьИмяВременногоФайла();
	ТабДокумент.ОтображатьСетку = Истина;
	ТабДокумент.Записать(ВремФайл, ТипФайлаТабличногоДокумента.HTML4);
	
	ТекстHТМЛ = Новый ТекстовыйДокумент;
	ТекстHТМЛ.Прочитать(ВремФайл);
   	ТекстHТМЛ = ТекстHТМЛ.ПолучитьТекст();
	
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xls");
	ТабДокумент.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.XLS97);
	
	ТекстHТМЛ = Сред(ТекстHТМЛ, 1, Найти(ТекстHТМЛ ,"<TABLE")+6) + "border=2" + Сред(ТекстHТМЛ, Найти(ТекстHТМЛ, "<TABLE") + 6);
	
	Настройки = ХранилищеНастроекДанныхФорм.Загрузить("ДополнительныеНастройкиОтгрузкиМест", , , "ДополнительныеНастройкиОтгрузкиМест");
	
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		Если Настройки.Свойство("НастройкиРассылкиМаршрутногоЛиста") Тогда
			НастройкиРассылкиМаршрутногоЛиста = Настройки.НастройкиРассылкиМаршрутногоЛиста.Получить();
		КонецЕсли;
	КонецЕсли;
	
	Кому = Новый Массив;
	
	//Кому.Добавить(Новый Структура("Адрес, Представление", "akomar@ktc.rovno.ua", "Андрій Комар"));
	
	Если ТипЗнч(НастройкиРассылкиМаршрутногоЛиста) = Тип("ТаблицаЗначений") Тогда
		Для каждого СтрокаТаблицы Из НастройкиРассылкиМаршрутногоЛиста Цикл
			СтруктураЗаполнения = Новый Структура("Адрес, Представление");
			ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, СтрокаТаблицы);
			Кому.Добавить(СтруктураЗаполнения);
		КонецЦикла
	КонецЕсли;
		
	БиблиотекаОбработок = git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектПоИмени("Библиотека внешних обработок");
	ПараметрыПисьма 	= БиблиотекаОбработок.СформироватьПараметрыПисьма("Маршрутный лист", ТекстHТМЛ, Кому,,,,Новый Структура("МаршрутныйЛист", ИмяВременногоФайла),, ТипТекстаПочтовогоСообщения.HTML);
	БиблиотекаОбработок.ОтправитьСообщениеПоSMTP(Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты, ПараметрыПисьма);

КонецПроцедуры
 


Функция ПолучитьТекстЗапросаУтреннейРассылки()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	МестаДоставки.Ссылка КАК Место,
	               |	МестаДоставки.Владелец КАК Получатель,
	               |	МестаДоставки.АдресДоставки КАК АдресПолучателя
	               |ПОМЕСТИТЬ МестаПоМаршруту
	               |ИЗ
	               |	Справочник.МестаДоставки КАК МестаДоставки
	               |ГДЕ
	               |	НЕ МестаДоставки.Архивный
				   |	И МестаДоставки.ЭтоЗадача
				   |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	МестаДоставки.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартнерыСегмента.Партнер КАК Партнер
	               |ПОМЕСТИТЬ Перевозчики
	               |ИЗ
	               |	РегистрСведений.ПартнерыСегмента КАК ПартнерыСегмента
	               |ГДЕ
	               |	ПартнерыСегмента.Сегмент = &СегментПеревозчики
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗадачиНаМаршрут.Место.Владелец КАК Получатель,
	               |	ЗадачиНаМаршрут.Отправитель КАК Отправитель,
	               |	ЗадачиНаМаршрут.Место.ТекстЗадачи КАК Задача,
	               |	ЗадачиНаМаршрут.Место КАК Место,
	               |	ЗадачиНаМаршрут.Пользователь КАК Автор
	               |ПОМЕСТИТЬ ОбъектыДоставки
	               |ИЗ
	               |	РегистрСведений.ЗадачиНаМаршрут.СрезПоследних(,) КАК ЗадачиНаМаршрут
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перевозчики КАК Перевозчики
	               |		ПО ЗадачиНаМаршрут.Отправитель = Перевозчики.Партнер
	               |ГДЕ
	               |	ЗадачиНаМаршрут.СтатусОтгрузки 		= ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ЗадачаПоставлена)
				   |	И ЗадачиНаМаршрут.ВариантМаршрута   = &Маршрут
				   |	И ЗадачиНаМаршрут.Период			< &ДатаДляЗадач
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПользователиКонтактнаяИнформация.Ссылка КАК Пользователь,
	               |	ПользователиКонтактнаяИнформация.Представление КАК НомерТелефона
	               |ПОМЕСТИТЬ КешТелефоныПользователей
	               |ИЗ
	               |	Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
	               |ГДЕ
	               |	ПользователиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И ПользователиКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииПользователя
	               |	И ПользователиКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Автор
	               |			ИЗ
	               |				ОбъектыДоставки)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартнерыКонтактнаяИнформация.Ссылка КАК Партнер,
	               |	ПартнерыКонтактнаяИнформация.Представление КАК НомерТелефона
	               |ПОМЕСТИТЬ НомераТелефоновПартнеров
	               |ИЗ
	               |	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	               |ГДЕ
	               |	ПартнерыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |	И ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И ПартнерыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонПартнера)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартнерыКонтактнаяИнформация.Ссылка КАК Партнер,
	               |	ПартнерыКонтактнаяИнформация.Представление КАК НомерТелефона
	               |ПОМЕСТИТЬ НомераМобильныхТелефоновПартнеров
	               |ИЗ
	               |	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	               |ГДЕ
	               |	ПартнерыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |	И ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И ПартнерыКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииМобильныйТелефон
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартнерыКонтактнаяИнформация.Ссылка КАК Партнер,
	               |	ПартнерыКонтактнаяИнформация.Представление КАК НомерТелефона
	               |ПОМЕСТИТЬ НомераДополнительныхМобильныхТелефоновПартнеров
	               |ИЗ
	               |	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	               |ГДЕ
	               |	ПартнерыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |	И ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И ПартнерыКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииДополнительныйМобильныйТелефон
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартнерыКонтактнаяИнформация.Ссылка КАК Партнер,
	               |	ПартнерыКонтактнаяИнформация.Представление КАК Адрес
	               |ПОМЕСТИТЬ АдресаПартнеров
	               |ИЗ
	               |	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	               |ГДЕ
	               |	ПартнерыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |	И ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	               |	И ПартнерыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресПартнера)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СкладыКонтактнаяИнформация.Ссылка КАК Склад,
	               |	СкладыКонтактнаяИнформация.Представление КАК НомерТелефона
	               |ПОМЕСТИТЬ НомераТелефоновСкладов
	               |ИЗ
	               |	Справочник.Склады.КонтактнаяИнформация КАК СкладыКонтактнаяИнформация
	               |ГДЕ
	               |	СкладыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Склады
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Склады)
	               |	И СкладыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И СкладыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонСклада)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СкладыКонтактнаяИнформация.Ссылка КАК Склад,
	               |	СкладыКонтактнаяИнформация.Представление КАК Адрес
	               |ПОМЕСТИТЬ АдресаСкладов
	               |ИЗ
	               |	Справочник.Склады.КонтактнаяИнформация КАК СкладыКонтактнаяИнформация
	               |ГДЕ
	               |	СкладыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Склады
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Склады)
	               |	И СкладыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	               |	И СкладыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресСклада)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОбъектыДоставки.Отправитель КАК Отправитель,
	               |	ВЫБОР
	               |		КОГДА ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |			ТОГДА ЕСТЬNULL(АдресаПартнеровОтправителей.Адрес, """")
	               |		ИНАЧЕ ЕСТЬNULL(АдресаСкладовОтправителей.Адрес, """")
	               |	КОНЕЦ КАК АдресОтправителя,
	               |	ВЫБОР
	               |		КОГДА ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |			ТОГДА ЕСТЬNULL(НомераТелефоновПартнеровОтправителей.НомерТелефона, """") + "" "" + ЕСТЬNULL(НомераМобильныхТелефоновПартнеровОтправителей.НомерТелефона, """") + "" "" + ЕСТЬNULL(НомераДополнительныхМобильныхТелефоновПартнеровОтправителей.НомерТелефона, """")
	               |		ИНАЧЕ ЕСТЬNULL(НомераТелефоновСкладовОтправителей.НомерТелефона, """")
	               |	КОНЕЦ КАК НомерТелефонаОтправителя,
	               |	ОбъектыДоставки.Получатель КАК Получатель,
	               |	ВЫБОР
	               |		КОГДА ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры
	               |			ТОГДА ЕСТЬNULL(АдресаПартнеровПолучателей.Адрес, """")
	               |		КОГДА ОбъектыДоставки.Получатель ССЫЛКА Справочник.Склады
	               |			ТОГДА ЕСТЬNULL(АдресаСкладовПолучателей.Адрес, """")
	               |	КОНЕЦ КАК АдресПолучатель,
	               |	ВЫБОР
	               |		КОГДА ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры
	               |			ТОГДА ЕСТЬNULL(НомераТелефоновПартнеровПолучателей.НомерТелефона, """") + "" "" + ЕСТЬNULL(НомераМобильныхТелефоновПартнеровПолучателей.НомерТелефона, """") + "" "" + ЕСТЬNULL(НомераДополнительныхМобильныхТелефоновПартнеровПолучателей.НомерТелефона, """")
	               |		ИНАЧЕ ЕСТЬNULL(НомераТелефоновСкладовПолучателей.НомерТелефона, """")
	               |	КОНЕЦ КАК НомерТелефонаПолучателя,
	               |	ОбъектыДоставки.Задача КАК Задача,
	               |	ОбъектыДоставки.Автор КАК Автор,
	               |	ЕСТЬNULL(ТелефоныПользователей.НомерТелефона, """") КАК НомерТелефонаПользователя
	               |ИЗ
	               |	ОбъектыДоставки КАК ОбъектыДоставки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ АдресаПартнеров КАК АдресаПартнеровОтправителей
	               |		ПО (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры)
	               |			И ОбъектыДоставки.Отправитель = АдресаПартнеровОтправителей.Партнер
	               |		ЛЕВОЕ СОЕДИНЕНИЕ АдресаСкладов КАК АдресаСкладовОтправителей
	               |		ПО ОбъектыДоставки.Отправитель = АдресаСкладовОтправителей.Склад
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Склады)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераТелефоновПартнеров КАК НомераТелефоновПартнеровОтправителей
	               |		ПО ОбъектыДоставки.Отправитель = НомераТелефоновПартнеровОтправителей.Партнер
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераМобильныхТелефоновПартнеров КАК НомераМобильныхТелефоновПартнеровОтправителей
	               |		ПО ОбъектыДоставки.Отправитель = НомераМобильныхТелефоновПартнеровОтправителей.Партнер
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераДополнительныхМобильныхТелефоновПартнеров КАК НомераДополнительныхМобильныхТелефоновПартнеровОтправителей
	               |		ПО ОбъектыДоставки.Отправитель = НомераДополнительныхМобильныхТелефоновПартнеровОтправителей.Партнер
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераТелефоновСкладов КАК НомераТелефоновСкладовОтправителей
	               |		ПО ОбъектыДоставки.Отправитель = НомераТелефоновСкладовОтправителей.Склад
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Склады)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ АдресаПартнеров КАК АдресаПартнеровПолучателей
	               |		ПО ОбъектыДоставки.Получатель = АдресаПартнеровПолучателей.Партнер
	               |			И (ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ АдресаСкладов КАК АдресаСкладовПолучателей
	               |		ПО ОбъектыДоставки.Получатель = АдресаСкладовПолучателей.Склад
	               |			И (ОбъектыДоставки.Получатель ССЫЛКА Справочник.Склады)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераТелефоновПартнеров КАК НомераТелефоновПартнеровПолучателей
	               |		ПО ОбъектыДоставки.Получатель = НомераТелефоновПартнеровПолучателей.Партнер
	               |			И (ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераМобильныхТелефоновПартнеров КАК НомераМобильныхТелефоновПартнеровПолучателей
	               |		ПО ОбъектыДоставки.Получатель = НомераМобильныхТелефоновПартнеровПолучателей.Партнер
	               |			И (ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераДополнительныхМобильныхТелефоновПартнеров КАК НомераДополнительныхМобильныхТелефоновПартнеровПолучателей
	               |		ПО ОбъектыДоставки.Отправитель = НомераДополнительныхМобильныхТелефоновПартнеровПолучателей.Партнер
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераТелефоновСкладов КАК НомераТелефоновСкладовПолучателей
	               |		ПО ОбъектыДоставки.Получатель = НомераТелефоновСкладовПолучателей.Склад
	               |			И (ОбъектыДоставки.Получатель ССЫЛКА Справочник.Склады)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КешТелефоныПользователей КАК ТелефоныПользователей
	               |		ПО ОбъектыДоставки.Автор = ТелефоныПользователей.Пользователь
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ОбъектыДоставки.Отправитель.Наименование,
	               |	ОбъектыДоставки.Получатель.Наименование
	               |ИТОГИ ПО
	               |	Получатель,
	               |	Отправитель";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СформироватьТабличныйДокументМаршрутныйЛистРассылка(ТабличныйДокумент, ДанныеДляПечати)

	МакетМаршрутныйЛист     = ПолучитьМакет("ПФ_MXL_МакетМаршрутныйЛист");	
	ОблЗаголовокПолучатель  = МакетМаршрутныйЛист.ПолучитьОбласть("Заголовок|Получатель");
	ОблЗаголовокОтправитель = МакетМаршрутныйЛист.ПолучитьОбласть("Заголовок|Отправитель");
	ОблЗаголовокКомментарий = МакетМаршрутныйЛист.ПолучитьОбласть("Заголовок|Комментарий");
	
	ОблПолучатель  = МакетМаршрутныйЛист.ПолучитьОбласть("Строка|Получатель");
	ОблОтправитель = МакетМаршрутныйЛист.ПолучитьОбласть("Строка|Отправитель");
	ОблКомментарий = МакетМаршрутныйЛист.ПолучитьОбласть("Строка|Комментарий");
	ОблДата		   = МакетМаршрутныйЛист.ПолучитьОбласть("Дата");
	
	МакетСтроки    = "%1" + Символы.ПС + "%2" + Символы.ПС + "%3";
		
	ВыборкаПолучатель   = ДанныеДляПечати.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТабличныйДокумент.Присоединить(ОблЗаголовокОтправитель);
	ТабличныйДокумент.Присоединить(ОблЗаголовокПолучатель);
	ТабличныйДокумент.Присоединить(ОблЗаголовокКомментарий);
		
	Пока ВыборкаПолучатель.Следующий() Цикл
		
		ВыборкаОтправитель = ВыборкаПолучатель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаОтправитель.Следующий() Цикл
			
			СтрокаМаршрутногоЛиста = Новый ТабличныйДокумент;
			ВыборкаДетальныеЗаписи = ВыборкаОтправитель.Выбрать();
					
			НомерМеста = 1;			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				Если НомерМеста = 1 Тогда
					ОблПолучатель.Параметры.Получатель = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(МакетСтроки,
										ВыборкаПолучатель.Получатель,
										ВыборкаДетальныеЗаписи.АдресПолучатель,
										ВыборкаДетальныеЗаписи.НомерТелефонаПолучателя);
					ОблОтправитель.Параметры.Отправитель = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(МакетСтроки,
										ВыборкаОтправитель.Отправитель,
										ВыборкаДетальныеЗаписи.АдресОтправителя,
										ВыборкаДетальныеЗаписи.НомерТелефонаОтправителя);
				Иначе
					ОблПолучатель.Параметры.Получатель   = "";
					ОблОтправитель.Параметры.Отправитель = "";
				КонецЕсли;
				
				ОблКомментарий.Параметры.Комментарий = "";
				
				СтрокаМаршрутногоЛиста.Вывести(ОблОтправитель);
				СтрокаМаршрутногоЛиста.Присоединить(ОблПолучатель);
								
				Если Не ПустаяСтрока(ВыборкаДетальныеЗаписи.Задача) И Не ПустаяСтрока(ВыборкаДетальныеЗаписи.Автор) Тогда
		            Комментарий 				= "%1%2: %3";
					НомерТелефонаПользователя   = СтрЗаменить(ВыборкаДетальныеЗаписи.НомерТелефонаПользователя, " ", "");
					НомерТелефонаПользователя   = СтрЗаменить(НомерТелефонаПользователя, "(", "");
					НомерТелефонаПользователя   = СтрЗаменить(НомерТелефонаПользователя, ")", "");
					НомерТелефонаПользователя   = СтрЗаменить(НомерТелефонаПользователя, "+38", "");
					ОблКомментарий.Параметры.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Комментарий,
																ВыборкаДетальныеЗаписи.Автор,
																?(ПустаяСтрока(НомерТелефонаПользователя), "", "("+НомерТелефонаПользователя+")"),
																ВыборкаДетальныеЗаписи.Задача);																						
				КонецЕсли;
															
				СтрокаМаршрутногоЛиста.Присоединить(ОблКомментарий);
				
				НомерМеста = НомерМеста + 1;
				
			КонецЦикла;
			
			ОбластьКОбъединению = СтрокаМаршрутногоЛиста.Область(1,1,НомерМеста-1,1);
			ОбластьКОбъединению.Объединить();
			ОбластьКОбъединению.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
			ОбластьКОбъединению.РазмещениеТекста	  = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			
			ОбластьКОбъединению = СтрокаМаршрутногоЛиста.Область(1,2,НомерМеста-1,2);
			ОбластьКОбъединению.Объединить();
			ОбластьКОбъединению.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
			ОбластьКОбъединению.РазмещениеТекста	  = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
						
			Если ТабличныйДокумент.ПроверитьВывод(СтрокаМаршрутногоЛиста) Тогда
				ТабличныйДокумент.Вывести(СтрокаМаршрутногоЛиста);
			Иначе
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(СтрокаМаршрутногоЛиста);
			КонецЕсли;

		КонецЦикла;		
		
	КонецЦикла;

КонецПроцедуры




Процедура ПровестиВечернююРассылку()

	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент  =  Новый ТабличныйДокумент;
	             	
	Запрос 		 = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаВечернейРассылки();
	
	Запрос.УстановитьПараметр("ВидКонтактнойИнформацииМобильныйТелефон", Справочники.ВидыКонтактнойИнформации.ПолучитьСсылку(Новый УникальныйИдентификатор("7da99962-488d-11e0-9f98-001517115d85")));
	Запрос.УстановитьПараметр("ВидКонтактнойИнформацииДополнительныйМобильныйТелефон", Справочники.ВидыКонтактнойИнформации.ПолучитьСсылку(Новый УникальныйИдентификатор("a8ec65fe-c74a-11e1-9d4d-001e673c80fc"))); 
	Запрос.УстановитьПараметр("Организация", Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("0fba8a92-4010-11e0-9f98-001517115d85")));
	Запрос.УстановитьПараметр("Маршрут", Справочники.ВариантыМаршрутов.ПолучитьСсылку(Новый УникальныйИдентификатор("bd28d8b3-f5b6-11e2-8f22-001e673c80fa")));
	Запрос.УстановитьПараметр("КонецДня", КонецДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаРегламентированогоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВидКонтактнойИнформацииПользователя", Справочники.ВидыКонтактнойИнформации.ПолучитьСсылку(Новый УникальныйИдентификатор("8dc2ec9e-55b5-11e1-8ee4-001e67162d38")));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СформироватьТабличныйДокументМаршрутныйЛистВечерняяРассылка(ТабДокумент, РезультатЗапроса);
	
	ВремФайл 					= ПолучитьИмяВременногоФайла();
	ТабДокумент.ОтображатьСетку = Истина;
	ТабДокумент.Записать(ВремФайл, ТипФайлаТабличногоДокумента.HTML4);
	
	ТекстHТМЛ = Новый ТекстовыйДокумент;
	ТекстHТМЛ.Прочитать(ВремФайл);
   	ТекстHТМЛ = ТекстHТМЛ.ПолучитьТекст();
	
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xls");
	ТабДокумент.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.XLS97);
	
	ТекстHТМЛ = Сред(ТекстHТМЛ, 1, Найти(ТекстHТМЛ ,"<TABLE")+6) + "border=2" + Сред(ТекстHТМЛ, Найти(ТекстHТМЛ, "<TABLE") + 6);
	
	Настройки = ХранилищеНастроекДанныхФорм.Загрузить("ДополнительныеНастройкиОтгрузкиМест", , , "ДополнительныеНастройкиОтгрузкиМест");
	
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		Если Настройки.Свойство("НастройкиРассылкиМаршрутногоЛиста") Тогда
			НастройкиРассылкиМаршрутногоЛиста = Настройки.НастройкиРассылкиМаршрутногоЛиста.Получить();
		КонецЕсли;
	КонецЕсли;
	
	Кому = Новый Массив;
	
	//Кому.Добавить(Новый Структура("Адрес, Представление", "akomar@ktc.rovno.ua", "Водій Рівне"));
	
	Если ТипЗнч(НастройкиРассылкиМаршрутногоЛиста) = Тип("ТаблицаЗначений") Тогда
		Для каждого СтрокаТаблицы Из НастройкиРассылкиМаршрутногоЛиста Цикл
			СтруктураЗаполнения = Новый Структура("Адрес, Представление");
			ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, СтрокаТаблицы);
			Кому.Добавить(СтруктураЗаполнения);
		КонецЦикла
	КонецЕсли;
		
	БиблиотекаОбработок = git_ПереопределениеОбработчиковСервер.ПолучитьВнешнийОбъектПоИмени("Библиотека внешних обработок");
	ПараметрыПисьма 	= БиблиотекаОбработок.СформироватьПараметрыПисьма("Маршрутный лист", ТекстHТМЛ, Кому,,,,Новый Структура("МаршрутныйЛист", ИмяВременногоФайла),, ТипТекстаПочтовогоСообщения.HTML);
	БиблиотекаОбработок.ОтправитьСообщениеПоSMTP(Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты, ПараметрыПисьма);

КонецПроцедуры


Функция ПолучитьТекстЗапросаВечернейРассылки()

	ТекстЗапроса = "ВЫБРАТЬ
	               |	МестаДоставки.Ссылка КАК Место,
	               |	МестаДоставки.Владелец КАК Получатель,
	               |	МестаДоставки.АдресДоставки КАК АдресПолучателя
	               |ПОМЕСТИТЬ МестаПоМаршруту
	               |ИЗ
	               |	Справочник.МестаДоставки КАК МестаДоставки
	               |ГДЕ
	               |	НЕ МестаДоставки.Архивный
				   |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	МестаДоставки.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
	               |	ЗаказыПоставщикамОстатки.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	               |	ЗаказыПоставщикамОстатки.Склад КАК Получатель,
	               |	ЗаказыПоставщикамОстатки.ЗаказПоставщику.КТС_НомерТТН КАК НомерТТН
	               |ПОМЕСТИТЬ КешЗаказыПоставщику
	               |ИЗ
	               |	РегистрНакопления.ЗаказыПоставщикам.Остатки(&КонецДня, ) КАК ЗаказыПоставщикамОстатки
	               |ГДЕ
	               |	ЗаказыПоставщикамОстатки.КОформлениюОстаток > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОтгрузкаМест.Место КАК Место,
	               |	ОтгрузкаМест.Место.Код КАК КодМеста,
	               |	ОтгрузкаМест.Склад КАК Отправитель,
	               |	МестаПоМаршруту.Получатель КАК Получатель,
	               |	МестаПоМаршруту.АдресПолучателя КАК АдресПолучателя,
	               |	"""" КАК Комментарий,
	               |	ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка) КАК Автор,
	               |	ИСТИНА КАК ЭтоМесто
	               |ПОМЕСТИТЬ ОбъектыДоставки
	               |ИЗ
	               |	РегистрСведений.ОтгрузкаМест.СрезПоследних(
	               |			,
	               |			Место В
	               |				(ВЫБРАТЬ
	               |					МестаПоМаршруту.Место
	               |				ИЗ
	               |					МестаПоМаршруту)) КАК ОтгрузкаМест
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МестаПоМаршруту КАК МестаПоМаршруту
	               |		ПО ОтгрузкаМест.Место = МестаПоМаршруту.Место
	               |ГДЕ
	               |	ОтгрузкаМест.СтатусОтгрузки 	= ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.КомплектацияЗавершена)
				   |	И ОтгрузкаМест.ВариантМаршрута  = &Маршрут
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	Место,
	               |	Место.Код,
	               |	Отправитель,
	               |	Место.Владелец,
	               |	"""",
	               |	Место.ТекстЗадачи,
	               |	Пользователь,
	               |	ЛОЖЬ
	               |ИЗ
	               |	РегистрСведений.ЗадачиНаМаршрут.СрезПоследних(,)
	               |ГДЕ
	               |	СтатусОтгрузки = ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузкиМест.ЗадачаПоставлена)
				   |	И ВариантМаршрута  = &Маршрут
				   |	И Период		   < &ТекущаяДата
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОбъектыДоставки.Получатель КАК Партнер
	               |ПОМЕСТИТЬ ТаблицаПартнеров
	               |ИЗ
	               |	ОбъектыДоставки КАК ОбъектыДоставки
	               |ГДЕ
	               |	ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартнерыСегмента.Партнер КАК Партнер
	               |ПОМЕСТИТЬ ТаблицаДопустимыхПартнеров
	               |ИЗ
	               |	РегистрСведений.ПартнерыСегмента КАК ПартнерыСегмента
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КТС_НастройкиСегментовУсловийОтгрузки КАК СегментыУсловийОтгрузки
	               |		ПО ПартнерыСегмента.Сегмент = СегментыУсловийОтгрузки.СегментПартнеров
	               |			И (ПартнерыСегмента.Партнер В
	               |				(ВЫБРАТЬ
	               |					ТаблицаПартнеров.Партнер
	               |				ИЗ
	               |					ТаблицаПартнеров))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	               |	КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность КАК Курс
	               |ПОМЕСТИТЬ КурсыВалют
	               |ИЗ
	               |	РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалютСрезПоследних
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Валюта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	АналитикаПоПартнерам.Партнер КАК Партнер,
	               |	АналитикаПоПартнерам.Организация КАК Организация,
	               |	АналитикаПоПартнерам.КлючАналитики КАК КлючАналитики
	               |ПОМЕСТИТЬ Аналитика
	               |ИЗ
	               |	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДопустимыхПартнеров КАК ТаблицаПартнеров
	               |		ПО АналитикаПоПартнерам.Партнер = ТаблицаПартнеров.Партнер
	               |			И (АналитикаПоПартнерам.Организация = &Организация)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	КлючАналитики
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Аналитика.Партнер КАК Партнер,
	               |	СУММА(ВЫБОР ВалютаУправленческогоУчета.Значение
	               |			КОГДА Долги.Валюта
	               |				ТОГДА Долги.СуммаОстаток
	               |			ИНАЧЕ Долги.СуммаОстаток / КурсыВалютУПР.Курс * КурсыВалют.Курс
	               |		КОНЕЦ) КАК Долг
	               |ПОМЕСТИТЬ Долги
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	               |		РасчетыСКлиентами.Валюта КАК Валюта,
	               |		РасчетыСКлиентами.СуммаОстаток КАК СуммаОстаток
	               |	ИЗ
	               |		РегистрНакопления.РасчетыСКлиентами.Остатки(
	               |				&КонецДня,
	               |				АналитикаУчетаПоПартнерам В
	               |					(ВЫБРАТЬ
	               |						Аналитика.КлючАналитики
	               |					ИЗ
	               |						Аналитика)) КАК РасчетыСКлиентами
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам,
	               |		РасчетыСПоставщиками.Валюта,
	               |		РасчетыСПоставщиками.СуммаОстаток
	               |	ИЗ
	               |		РегистрНакопления.РасчетыСПоставщиками.Остатки(
	               |				&КонецДня,
	               |				АналитикаУчетаПоПартнерам В
	               |					(ВЫБРАТЬ
	               |						Аналитика.КлючАналитики
	               |					ИЗ
	               |						Аналитика)) КАК РасчетыСПоставщиками) КАК Долги
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитика КАК Аналитика
	               |		ПО (Аналитика.КлючАналитики = Долги.АналитикаУчетаПоПартнерам)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВалютаУправленческогоУчета КАК ВалютаУправленческогоУчета
	               |		ПО (ИСТИНА)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУПР
	               |		ПО (КурсыВалютУПР.Валюта = ВалютаУправленческогоУчета.Значение)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	               |		ПО (КурсыВалют.Валюта = Долги.Валюта)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Аналитика.Партнер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЭтапыОплаты.Ссылка КАК Ссылка,
	               |	МИНИМУМ(ЭтапыОплаты.ДатаПлатежа) КАК ДатаПлатежа
	               |ПОМЕСТИТЬ ГрафикОплат
	               |ИЗ
	               |	Документ.ЗаказКлиента.ЭтапыГрафикаОплаты КАК ЭтапыОплаты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДопустимыхПартнеров КАК ТаблицаПартнеров
	               |		ПО ЭтапыОплаты.Ссылка.Партнер = ТаблицаПартнеров.Партнер
	               |			И (ЭтапыОплаты.Ссылка.Организация = &Организация)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЭтапыОплаты.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РасчетыСКлиентамиОстаткиИОбороты.АналитикаУчетаПоПартнерам КАК КлючАналитики,
	               |	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.ВозвратТоваровОтКлиента) КАК Регистратор,
	               |	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.ВозвратТоваровОтКлиента).Валюта КАК Валюта
	               |ПОМЕСТИТЬ ПервыйКеш_Возвраты
	               |ИЗ
	               |	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(
	               |			,
	               |			&КонецДня,
	               |			Регистратор,
	               |			,
	               |			АналитикаУчетаПоПартнерам В
	               |				(ВЫБРАТЬ
	               |					Аналитика.КлючАналитики
	               |				ИЗ
	               |					Аналитика)) КАК РасчетыСКлиентамиОстаткиИОбороты
	               |ГДЕ
	               |	РасчетыСКлиентамиОстаткиИОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	               |	И РасчетыСКлиентамиОстаткиИОбороты.СуммаОборот <> 0
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Регистратор,
	               |	КлючАналитики
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Возвраты.КлючАналитики,
	               |	Возвраты.Валюта,
	               |	ВозвратыТовары.ДокументРеализации,
	               |	СУММА(ВозвратыТовары.СуммаСНДС) КАК СуммаРасход
	               |ПОМЕСТИТЬ ВторойКеш_Возвраты
	               |ИЗ
	               |	ПервыйКеш_Возвраты КАК Возвраты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ВозвратыТовары
	               |		ПО (ВозвратыТовары.Ссылка = Возвраты.Регистратор)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Возвраты.КлючАналитики,
	               |	Возвраты.Валюта,
	               |	ВозвратыТовары.ДокументРеализации
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РасчетыСКлиентамиОстаткиИОбороты.АналитикаУчетаПоПартнерам КАК КлючАналитики,
	               |	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг) КАК Регистратор,
	               |	РасчетыСКлиентамиОстаткиИОбороты.Регистратор.МоментВремени КАК МоментВремени,
	               |	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента КАК ЗаказКлиента,
	               |	ВЫРАЗИТЬ(РасчетыСКлиентамиОстаткиИОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).ДатаПлатежа КАК ДатаПлатежа,
	               |	РасчетыСКлиентамиОстаткиИОбороты.Период,
	               |	РасчетыСКлиентамиОстаткиИОбороты.Валюта,
	               |	РасчетыСКлиентамиОстаткиИОбороты.СуммаПриход,
	               |	РасчетыСКлиентамиОстаткиИОбороты.СуммаОборот
	               |ПОМЕСТИТЬ Cache_Обороты
	               |ИЗ
	               |	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(
	               |			,
	               |			&КонецДня,
	               |			Регистратор,
	               |			,
	               |			АналитикаУчетаПоПартнерам В
	               |				(ВЫБРАТЬ
	               |					Аналитика.КлючАналитики
	               |				ИЗ
	               |					Аналитика)) КАК РасчетыСКлиентамиОстаткиИОбороты
	               |ГДЕ
	               |	РасчетыСКлиентамиОстаткиИОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	               |	И РасчетыСКлиентамиОстаткиИОбороты.СуммаОборот <> 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Аналитика.Партнер КАК Партнер,
	               |	Обороты.Регистратор,
	               |	Обороты.Период,
	               |	ВЫБОР
	               |		КОГДА Обороты.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	               |				ИЛИ Обороты.ЗаказКлиента = НЕОПРЕДЕЛЕНО
	               |				ИЛИ Обороты.ЗаказКлиента = NULL
	               |			ТОГДА Обороты.ДатаПлатежа
	               |		ИНАЧЕ ГрафикОплат.ДатаПлатежа
	               |	КОНЕЦ КАК ДатаПлатежа,
	               |	Обороты.МоментВремени,
	               |	ВЫБОР
	               |		КОГДА ВалютаУправленческогоУчета.Значение = Обороты.Валюта
	               |			ТОГДА Обороты.СуммаПриход
	               |		ИНАЧЕ Обороты.СуммаПриход / КурсыВалютУПР.Курс * КурсыВалют.Курс
	               |	КОНЕЦ - ВЫБОР
	               |		КОГДА ЕСТЬNULL(Возвраты.СуммаРасход, 0) = 0
	               |			ТОГДА 0
	               |		КОГДА ВалютаУправленческогоУчета.Значение = Возвраты.Валюта
	               |			ТОГДА Возвраты.СуммаРасход
	               |		ИНАЧЕ Возвраты.СуммаРасход / КурсыВалютУПР.Курс * КурсыВозвраты.Курс
	               |	КОНЕЦ КАК Сумма,
	               |	ВЫБОР
	               |		КОГДА ВалютаУправленческогоУчета.Значение = Обороты.Валюта
	               |			ТОГДА Обороты.СуммаОборот
	               |		ИНАЧЕ Обороты.СуммаОборот / КурсыВалютУПР.Курс * КурсыВалют.Курс
	               |	КОНЕЦ - ВЫБОР
	               |		КОГДА ЕСТЬNULL(Возвраты.СуммаРасход, 0) = 0
	               |			ТОГДА 0
	               |		КОГДА ВалютаУправленческогоУчета.Значение = Возвраты.Валюта
	               |			ТОГДА Возвраты.СуммаРасход
	               |		ИНАЧЕ Возвраты.СуммаРасход / КурсыВалютУПР.Курс * КурсыВозвраты.Курс
	               |	КОНЕЦ КАК СуммаОборот
	               |ПОМЕСТИТЬ ОборотыПоКлиенту
	               |ИЗ
	               |	Cache_Обороты КАК Обороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитика КАК Аналитика
	               |		ПО (Аналитика.КлючАналитики = Обороты.КлючАналитики)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВторойКеш_Возвраты КАК Возвраты
	               |		ПО (Возвраты.КлючАналитики = Обороты.КлючАналитики)
	               |			И (Возвраты.ДокументРеализации = Обороты.Регистратор)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВозвраты
	               |		ПО (КурсыВозвраты.Валюта = Возвраты.Валюта)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ГрафикОплат КАК ГрафикОплат
	               |		ПО (ГрафикОплат.Ссылка = Обороты.ЗаказКлиента)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВалютаУправленческогоУчета КАК ВалютаУправленческогоУчета
	               |		ПО (ИСТИНА)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУПР
	               |		ПО (КурсыВалютУПР.Валюта = ВалютаУправленческогоУчета.Значение)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	               |		ПО (КурсыВалют.Валюта = Обороты.Валюта)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОборотыПоКлиенту.Партнер КАК Партнер,
	               |	НАЧАЛОПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ) КАК НачПериода,
	               |	КОНЕЦПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ) КАК КонПериода,
	               |	СУММА(ОборотыПоКлиенту.СуммаОборот) КАК Сумма
	               |ПОМЕСТИТЬ ОборотыПоМесяцам
	               |ИЗ
	               |	ОборотыПоКлиенту КАК ОборотыПоКлиенту
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОборотыПоКлиенту.Партнер,
	               |	НАЧАЛОПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ),
	               |	КОНЕЦПЕРИОДА(ОборотыПоКлиенту.ДатаПлатежа, МЕСЯЦ)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОборотыПоМесяцам.Партнер КАК Партнер,
	               |	ОборотыПоМесяцам.НачПериода КАК НачПериода,
	               |	ОборотыПоМесяцам.КонПериода КАК КонПериода,
	               |	ОборотыПоМесяцам.Сумма КАК Сумма,
	               |	СУММА(ОборотыПоМесяцамКопия.Сумма) КАК СуммаПосле,
	               |	СУММА(ОборотыПоМесяцамКопия.Сумма) - ОборотыПоМесяцам.Сумма КАК СуммаДо
	               |ПОМЕСТИТЬ ОборотыПоМесяцамНарастающие
	               |ИЗ
	               |	ОборотыПоМесяцам КАК ОборотыПоМесяцам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОборотыПоМесяцам КАК ОборотыПоМесяцамКопия
	               |		ПО ОборотыПоМесяцам.Партнер = ОборотыПоМесяцамКопия.Партнер
	               |			И ОборотыПоМесяцам.НачПериода <= ОборотыПоМесяцамКопия.НачПериода
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОборотыПоМесяцам.НачПериода,
	               |	ОборотыПоМесяцам.КонПериода,
	               |	ОборотыПоМесяцам.Партнер,
	               |	ОборотыПоМесяцам.Сумма
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Долги.Партнер КАК Партнер,
	               |	Долги.Долг КАК Долг,
	               |	ОборотыПоМесНарастающие.НачПериода КАК НачПериода,
	               |	ОборотыПоМесНарастающие.КонПериода КАК КонПериода,
	               |	Долги.Долг - ОборотыПоМесНарастающие.СуммаДо КАК ОстатокДолга
	               |ПОМЕСТИТЬ ДолгиПоВыбраннымМесяцам
	               |ИЗ
	               |	Долги КАК Долги
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОборотыПоМесяцамНарастающие КАК ОборотыПоМесНарастающие
	               |		ПО Долги.Партнер = ОборотыПоМесНарастающие.Партнер
	               |			И Долги.Долг > ОборотыПоМесНарастающие.СуммаДо
	               |			И Долги.Долг <= ОборотыПоМесНарастающие.СуммаПосле
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Обороты.Партнер КАК Партнер,
	               |	Обороты.МоментВремени КАК МоментВремени,
	               |	Обороты.Регистратор КАК Регистратор,
	               |	Обороты.Период КАК Период,
	               |	ДолгиПоВыбМесяцам.Долг КАК Долг,
	               |	ДолгиПоВыбМесяцам.ОстатокДолга КАК ОстатокДолга,
	               |	Обороты.Сумма КАК Сумма,
	               |	Обороты.ДатаПлатежа КАК ДатаОтсрочки
	               |ПОМЕСТИТЬ ДвиженияПоВыбраннымМесяцам
	               |ИЗ
	               |	ОборотыПоКлиенту КАК Обороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолгиПоВыбраннымМесяцам КАК ДолгиПоВыбМесяцам
	               |		ПО Обороты.Партнер = ДолгиПоВыбМесяцам.Партнер
	               |			И Обороты.ДатаПлатежа >= ДолгиПоВыбМесяцам.НачПериода
	               |			И Обороты.ДатаПлатежа <= ДолгиПоВыбМесяцам.КонПериода
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДвиженияПоВыбМесяцам.Партнер КАК Партнер,
	               |	ДвиженияПоВыбМесяцам.Регистратор КАК Регистратор,
	               |	ДвиженияПоВыбМесяцам.Период КАК Период,
	               |	ДвиженияПоВыбМесяцам.МоментВремени КАК МоментВремени,
	               |	ДвиженияПоВыбМесяцам.Сумма КАК Сумма,
	               |	СУММА(ДвиженияПоВыбМесяцамКопия.Сумма) КАК СуммаПосле,
	               |	СУММА(ДвиженияПоВыбМесяцамКопия.Сумма) - ДвиженияПоВыбМесяцам.Сумма КАК СуммаДо,
	               |	ДвиженияПоВыбМесяцам.ОстатокДолга КАК ОстатокДолга,
	               |	ДвиженияПоВыбМесяцам.ДатаОтсрочки КАК ДатаОтсрочки
	               |ПОМЕСТИТЬ ДвиженияПредварительные
	               |ИЗ
	               |	ДвиженияПоВыбраннымМесяцам КАК ДвиженияПоВыбМесяцам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияПоВыбраннымМесяцам КАК ДвиженияПоВыбМесяцамКопия
	               |		ПО ДвиженияПоВыбМесяцам.Партнер = ДвиженияПоВыбМесяцамКопия.Партнер
	               |			И ДвиженияПоВыбМесяцам.ДатаОтсрочки <= ДвиженияПоВыбМесяцамКопия.ДатаОтсрочки
	               |			И (ВЫБОР
	               |				КОГДА ДвиженияПоВыбМесяцам.ДатаОтсрочки = ДвиженияПоВыбМесяцамКопия.ДатаОтсрочки
	               |					ТОГДА ДвиженияПоВыбМесяцам.МоментВремени <= ДвиженияПоВыбМесяцамКопия.МоментВремени
	               |				ИНАЧЕ ИСТИНА
	               |			КОНЕЦ)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ДвиженияПоВыбМесяцам.Партнер,
	               |	ДвиженияПоВыбМесяцам.Регистратор,
	               |	ДвиженияПоВыбМесяцам.Период,
	               |	ДвиженияПоВыбМесяцам.МоментВремени,
	               |	ДвиженияПоВыбМесяцам.Сумма,
	               |	ДвиженияПоВыбМесяцам.ОстатокДолга,
	               |	ДвиженияПоВыбМесяцам.ДатаОтсрочки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДвиженияПредв.Партнер КАК Партнер,
	               |	ДвиженияПредв.Регистратор КАК Регистратор,
	               |	ДвиженияПредв.Период КАК Период,
	               |	ДвиженияПредв.МоментВремени КАК МоментВремени,
	               |	ДвиженияПредв.ОстатокДолга - ДвиженияПредв.СуммаДо КАК СуммаДолга,
	               |	ДвиженияПредв.Сумма КАК Сумма,
	               |	ДвиженияПредв.ДатаОтсрочки КАК ДатаОтсрочки
	               |ПОМЕСТИТЬ ДвиженияОкончательные
	               |ИЗ
	               |	ДвиженияПредварительные КАК ДвиженияПредв
	               |ГДЕ
	               |	ДвиженияПредв.ОстатокДолга > ДвиженияПредв.СуммаДо
	               |	И ДвиженияПредв.ОстатокДолга <= ДвиженияПредв.СуммаПосле
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Обороты.Партнер КАК Партнер,
	               |	Обороты.Регистратор КАК Документ,
	               |	ВЫБОР
	               |		КОГДА Обороты.Регистратор = ДвиженияОконч.Регистратор
	               |			ТОГДА ДвиженияОконч.СуммаДолга
	               |		ИНАЧЕ Обороты.Сумма
	               |	КОНЕЦ КАК СуммаОстаток,
	               |	ВЫБОР
	               |		КОГДА Обороты.ДатаПлатежа < &ТекущаяДата
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Прострочено
	               |ПОМЕСТИТЬ ОбщаяТаблицаЗадолженностейПоДокументам
	               |ИЗ
	               |	ОборотыПоКлиенту КАК Обороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияОкончательные КАК ДвиженияОконч
	               |		ПО (ДвиженияОконч.Партнер = Обороты.Партнер)
	               |			И (ДвиженияОконч.ДатаОтсрочки <= Обороты.ДатаПлатежа)
	               |			И (ВЫБОР
	               |				КОГДА ДвиженияОконч.ДатаОтсрочки = Обороты.ДатаПлатежа
	               |					ТОГДА ДвиженияОконч.МоментВремени <= Обороты.МоментВремени
	               |				ИНАЧЕ ИСТИНА
	               |			КОНЕЦ)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОбщаяТаблицаЗадолженностейПоДокументам.Партнер КАК Партнер,
	               |	СУММА(ОбщаяТаблицаЗадолженностейПоДокументам.СуммаОстаток) КАК СуммаПрострочки
	               |ПОМЕСТИТЬ ТаблицаПрострочек
	               |ИЗ
	               |	ОбщаяТаблицаЗадолженностейПоДокументам КАК ОбщаяТаблицаЗадолженностейПоДокументам
	               |ГДЕ
	               |	ОбщаяТаблицаЗадолженностейПоДокументам.Прострочено = 1
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОбщаяТаблицаЗадолженностейПоДокументам.Партнер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОбъектыДоставки.Место КАК Место,
	               |	ОбъектыДоставки.Место.Владелец КАК Партнер
	               |ПОМЕСТИТЬ ТаблицаМестМаршрутногоЛиста
	               |ИЗ
	               |	ОбъектыДоставки КАК ОбъектыДоставки
	               |ГДЕ
	               |	ОбъектыДоставки.ЭтоМесто
	               |	И ОбъектыДоставки.Место.Владелец ССЫЛКА Справочник.Партнеры
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА МестаПоДокументам.Документ ССЫЛКА Документ.РасходныйОрдерНаТовары
	               |			ТОГДА ВЫРАЗИТЬ(МестаПоДокументам.Документ КАК Документ.РасходныйОрдерНаТовары).Распоряжение
	               |		КОГДА МестаПоДокументам.Документ ССЫЛКА Документ.КТС_ОтгрузкаТоваров
	               |			ТОГДА ВЫРАЗИТЬ(МестаПоДокументам.Документ КАК Документ.КТС_ОтгрузкаТоваров).Распоряжение
	               |	КОНЕЦ КАК Документ,
	               |	ТаблицаМест.Партнер КАК Партнер
	               |ПОМЕСТИТЬ ТаблицаДокументовПоМестам
	               |ИЗ
	               |	РегистрСведений.МестаПоДокументам КАК МестаПоДокументам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаМестМаршрутногоЛиста КАК ТаблицаМест
	               |		ПО МестаПоДокументам.Место = ТаблицаМест.Место
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОбщаяТаблица.Партнер КАК Партнер,
	               |	СУММА(ОбщаяТаблица.СуммаОстаток) КАК СуммаОстаток
	               |ПОМЕСТИТЬ ДолгиПоДокументам
	               |ИЗ
	               |	ОбщаяТаблицаЗадолженностейПоДокументам КАК ОбщаяТаблица
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументовПоМестам КАК ДокументыПоМестам
	               |		ПО ОбщаяТаблица.Партнер = ДокументыПоМестам.Партнер
	               |			И ОбщаяТаблица.Документ = ДокументыПоМестам.Документ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОбщаяТаблица.Партнер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.УсловиеОтгрузки КАК УсловиеОтгрузки,
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.КредитныйЛимит КАК КредитныйЛимит,
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.ДополнительныйПроцент КАК ДополнительныйПроцент
	               |ПОМЕСТИТЬ УсловияОтгрузкиПоУмолчанию
	               |ИЗ
	               |	РегистрСведений.КТС_НастройкиУсловийОтгрузки.СрезПоследних(&КонецДня, Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)) КАК КТС_НастройкиУсловийОтгрузкиСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.Партнер КАК Партнер,
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.УсловиеОтгрузки КАК УсловиеОтгрузки,
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.КредитныйЛимит КАК КредитныйЛимит,
	               |	КТС_НастройкиУсловийОтгрузкиСрезПоследних.ДополнительныйПроцент КАК ДополнительныйПроцент
	               |ПОМЕСТИТЬ УсловияОтгрузкиПоПартнерамПредварительные
	               |ИЗ
	               |	РегистрСведений.КТС_НастройкиУсловийОтгрузки.СрезПоследних(
	               |			&КонецДня,
	               |			Партнер В
	               |				(ВЫБРАТЬ
	               |					ТаблицаДопустимыхПартнеров.Партнер
	               |				ИЗ
	               |					ТаблицаДопустимыхПартнеров)) КАК КТС_НастройкиУсловийОтгрузкиСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаПартнеров.Партнер КАК Партнер,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(УсловияОтгрузкиПоПартнерамПредварительные.УсловиеОтгрузки, 0) = 0
	               |			ТОГДА УсловияОтгрузкиПоУмолчанию.УсловиеОтгрузки
	               |		ИНАЧЕ УсловияОтгрузкиПоПартнерамПредварительные.УсловиеОтгрузки
	               |	КОНЕЦ КАК УсловиеОтгрузки,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(УсловияОтгрузкиПоПартнерамПредварительные.КредитныйЛимит, 0) = 0
	               |			ТОГДА УсловияОтгрузкиПоУмолчанию.КредитныйЛимит
	               |		ИНАЧЕ УсловияОтгрузкиПоПартнерамПредварительные.КредитныйЛимит
	               |	КОНЕЦ КАК КредитныйЛимит,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(УсловияОтгрузкиПоПартнерамПредварительные.ДополнительныйПроцент, 0) = 0
	               |			ТОГДА УсловияОтгрузкиПоУмолчанию.ДополнительныйПроцент
	               |		ИНАЧЕ УсловияОтгрузкиПоПартнерамПредварительные.ДополнительныйПроцент
	               |	КОНЕЦ КАК ДополнительныйПроцент
	               |ПОМЕСТИТЬ УсловияОтгрузкиПоПартнерам
	               |ИЗ
	               |	ТаблицаДопустимыхПартнеров КАК ТаблицаПартнеров
	               |		ЛЕВОЕ СОЕДИНЕНИЕ УсловияОтгрузкиПоУмолчанию КАК УсловияОтгрузкиПоУмолчанию
	               |		ПО (ИСТИНА)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ УсловияОтгрузкиПоПартнерамПредварительные КАК УсловияОтгрузкиПоПартнерамПредварительные
	               |		ПО ТаблицаПартнеров.Партнер = УсловияОтгрузкиПоПартнерамПредварительные.Партнер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	УсловияОтгрузки.Партнер КАК Партнер,
	               |	ВЫБОР
	               |		КОГДА УсловияОтгрузки.УсловиеОтгрузки = ЗНАЧЕНИЕ(Перечисление.КТС_УсловияОтгрузки.ТоварныйКредит)
	               |			ТОГДА ВЫБОР
	               |					КОГДА ЕСТЬNULL(ТаблицаПрострочек.СуммаПрострочки, 0) < ЕСТЬNULL(Долги.Долг, 0) - УсловияОтгрузки.КредитныйЛимит
	               |						ТОГДА ЕСТЬNULL(Долги.Долг, 0) - УсловияОтгрузки.КредитныйЛимит
	               |					ИНАЧЕ ЕСТЬNULL(ТаблицаПрострочек.СуммаПрострочки, 0)
	               |				КОНЕЦ
	               |		КОГДА УсловияОтгрузки.УсловиеОтгрузки = ЗНАЧЕНИЕ(Перечисление.КТС_УсловияОтгрузки.РеструктуризацияДолга)
	               |			ТОГДА ВЫБОР
	               |					КОГДА ЕСТЬNULL(ДолгиПоДокументам.СуммаОстаток, 0) * (1 + УсловияОтгрузки.ДополнительныйПроцент * 0.01) < ЕСТЬNULL(Долги.Долг, 0) - УсловияОтгрузки.КредитныйЛимит
	               |						ТОГДА ЕСТЬNULL(Долги.Долг, 0) - УсловияОтгрузки.КредитныйЛимит
	               |					ИНАЧЕ ЕСТЬNULL(ДолгиПоДокументам.СуммаОстаток, 0) * (1 + УсловияОтгрузки.ДополнительныйПроцент * 0.01)
	               |				КОНЕЦ
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Сумма
	               |ПОМЕСТИТЬ ТаблицаУсловийОтгрузки
	               |ИЗ
	               |	УсловияОтгрузкиПоПартнерам КАК УсловияОтгрузки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Долги КАК Долги
	               |		ПО УсловияОтгрузки.Партнер = Долги.Партнер
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПрострочек КАК ТаблицаПрострочек
	               |		ПО УсловияОтгрузки.Партнер = ТаблицаПрострочек.Партнер
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ДолгиПоДокументам КАК ДолгиПоДокументам
	               |		ПО УсловияОтгрузки.Партнер = ДолгиПоДокументам.Партнер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПользователиКонтактнаяИнформация.Ссылка КАК Пользователь,
	               |	ПользователиКонтактнаяИнформация.Представление КАК НомерТелефона
	               |ПОМЕСТИТЬ КешТелефоныПользователей
	               |ИЗ
	               |	Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
	               |ГДЕ
	               |	ПользователиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И ПользователиКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииПользователя
	               |	И ПользователиКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Автор
	               |			ИЗ
	               |				ОбъектыДоставки)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартнерыКонтактнаяИнформация.Ссылка КАК Партнер,
	               |	ПартнерыКонтактнаяИнформация.Представление КАК НомерТелефона
	               |ПОМЕСТИТЬ НомераТелефоновПартнеров
	               |ИЗ
	               |	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	               |ГДЕ
	               |	ПартнерыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |	И ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И ПартнерыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонПартнера)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартнерыКонтактнаяИнформация.Ссылка КАК Партнер,
	               |	ПартнерыКонтактнаяИнформация.Представление КАК НомерТелефона
	               |ПОМЕСТИТЬ НомераМобильныхТелефоновПартнеров
	               |ИЗ
	               |	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	               |ГДЕ
	               |	ПартнерыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |	И ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И ПартнерыКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииМобильныйТелефон
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартнерыКонтактнаяИнформация.Ссылка КАК Партнер,
	               |	ПартнерыКонтактнаяИнформация.Представление КАК НомерТелефона
	               |ПОМЕСТИТЬ НомераДополнительныхМобильныхТелефоновПартнеров
	               |ИЗ
	               |	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	               |ГДЕ
	               |	ПартнерыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |	И ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И ПартнерыКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииДополнительныйМобильныйТелефон
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартнерыКонтактнаяИнформация.Ссылка КАК Партнер,
	               |	ПартнерыКонтактнаяИнформация.Представление КАК Адрес
	               |ПОМЕСТИТЬ АдресаПартнеров
	               |ИЗ
	               |	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	               |ГДЕ
	               |	ПартнерыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |	И ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	               |	И ПартнерыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресПартнера)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СкладыКонтактнаяИнформация.Ссылка КАК Склад,
	               |	СкладыКонтактнаяИнформация.Представление КАК НомерТелефона
	               |ПОМЕСТИТЬ НомераТелефоновСкладов
	               |ИЗ
	               |	Справочник.Склады.КонтактнаяИнформация КАК СкладыКонтактнаяИнформация
	               |ГДЕ
	               |	СкладыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Склады
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Склады)
	               |	И СкладыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И СкладыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонСклада)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СкладыКонтактнаяИнформация.Ссылка КАК Склад,
	               |	СкладыКонтактнаяИнформация.Представление КАК Адрес
	               |ПОМЕСТИТЬ АдресаСкладов
	               |ИЗ
	               |	Справочник.Склады.КонтактнаяИнформация КАК СкладыКонтактнаяИнформация
	               |ГДЕ
	               |	СкладыКонтактнаяИнформация.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ОбъектыДоставки.Отправитель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Склады
	               |		
	               |			ОБЪЕДИНИТЬ
	               |		
	               |			ВЫБРАТЬ
	               |				ОбъектыДоставки.Получатель
	               |			ИЗ
	               |				ОбъектыДоставки
	               |			ГДЕ
	               |				ОбъектыДоставки.Получатель ССЫЛКА Справочник.Склады)
	               |	И СкладыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	               |	И СкладыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресСклада)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОбъектыДоставки.Место КАК Место,
	               |	ОбъектыДоставки.Отправитель КАК Отправитель,
	               |	ВЫБОР
	               |		КОГДА ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |			ТОГДА ЕСТЬNULL(АдресаПартнеровОтправителей.Адрес, """")
	               |		ИНАЧЕ ЕСТЬNULL(АдресаСкладовОтправителей.Адрес, """")
	               |	КОНЕЦ КАК АдресОтправителя,
	               |	ВЫБОР
	               |		КОГДА ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры
	               |			ТОГДА ЕСТЬNULL(НомераТелефоновПартнеровОтправителей.НомерТелефона, """") + "" "" + ЕСТЬNULL(НомераМобильныхТелефоновПартнеровОтправителей.НомерТелефона, """") + "" "" + ЕСТЬNULL(НомераДополнительныхМобильныхТелефоновПартнеровОтправителей.НомерТелефона, """")
	               |		ИНАЧЕ ЕСТЬNULL(НомераТелефоновСкладовОтправителей.НомерТелефона, """")
	               |	КОНЕЦ КАК НомерТелефонаОтправителя,
	               |	ОбъектыДоставки.Получатель КАК Получатель,
	               |	ВЫБОР
	               |		КОГДА ОбъектыДоставки.ЭтоМесто
	               |			ТОГДА ОбъектыДоставки.АдресПолучателя
	               |		КОГДА ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры
	               |			ТОГДА ЕСТЬNULL(АдресаПартнеровПолучателей.Адрес, """")
	               |		КОГДА ОбъектыДоставки.Получатель ССЫЛКА Справочник.Склады
	               |			ТОГДА ЕСТЬNULL(АдресаСкладовПолучателей.Адрес, """")
	               |	КОНЕЦ КАК АдресПолучатель,
	               |	ВЫБОР
	               |		КОГДА ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры
	               |			ТОГДА ЕСТЬNULL(НомераТелефоновПартнеровПолучателей.НомерТелефона, """") + "" "" + ЕСТЬNULL(НомераМобильныхТелефоновПартнеровПолучателей.НомерТелефона, """") + "" "" + ЕСТЬNULL(НомераДополнительныхМобильныхТелефоновПартнеровПолучателей.НомерТелефона, """")
	               |		ИНАЧЕ ЕСТЬNULL(НомераТелефоновСкладовПолучателей.НомерТелефона, """")
	               |	КОНЕЦ КАК НомерТелефонаПолучателя,
	               |	ОбъектыДоставки.Комментарий КАК Комментарий,
	               |	ОбъектыДоставки.Автор КАК Автор,
	               |	ОбъектыДоставки.КодМеста КАК КодМеста,
	               |	ЕСТЬNULL(ТелефоныПользователей.НомерТелефона, """") КАК НомерТелефонаПользователя,
	               |	ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаУсловийОтгрузки.Сумма, 0) КАК ЧИСЛО(15, 2)) КАК ДолгПартнера,
	               |	ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаУсловийОтгрузки.Сумма, 0) * КурсВалютУпр.Курс / КурсВалютРег.Курс КАК ЧИСЛО(15, 2)) КАК ДолгПартнераРег,
	               |	ОбъектыДоставки.ЭтоМесто КАК ЭтоМесто
	               |ИЗ
	               |	ОбъектыДоставки КАК ОбъектыДоставки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ АдресаПартнеров КАК АдресаПартнеровОтправителей
	               |		ПО ОбъектыДоставки.Отправитель = АдресаПартнеровОтправителей.Партнер
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ АдресаСкладов КАК АдресаСкладовОтправителей
	               |		ПО ОбъектыДоставки.Отправитель = АдресаСкладовОтправителей.Склад
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Склады)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераТелефоновПартнеров КАК НомераТелефоновПартнеровОтправителей
	               |		ПО ОбъектыДоставки.Отправитель = НомераТелефоновПартнеровОтправителей.Партнер
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераМобильныхТелефоновПартнеров КАК НомераМобильныхТелефоновПартнеровОтправителей
	               |		ПО ОбъектыДоставки.Отправитель = НомераМобильныхТелефоновПартнеровОтправителей.Партнер
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераДополнительныхМобильныхТелефоновПартнеров КАК НомераДополнительныхМобильныхТелефоновПартнеровОтправителей
	               |		ПО ОбъектыДоставки.Отправитель = НомераДополнительныхМобильныхТелефоновПартнеровОтправителей.Партнер
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераТелефоновСкладов КАК НомераТелефоновСкладовОтправителей
	               |		ПО ОбъектыДоставки.Отправитель = НомераТелефоновСкладовОтправителей.Склад
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Склады)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ АдресаПартнеров КАК АдресаПартнеровПолучателей
	               |		ПО ОбъектыДоставки.Получатель = АдресаПартнеровПолучателей.Партнер
	               |			И (ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ АдресаСкладов КАК АдресаСкладовПолучателей
	               |		ПО ОбъектыДоставки.Получатель = АдресаСкладовПолучателей.Склад
	               |			И (ОбъектыДоставки.Получатель ССЫЛКА Справочник.Склады)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераТелефоновПартнеров КАК НомераТелефоновПартнеровПолучателей
	               |		ПО ОбъектыДоставки.Получатель = НомераТелефоновПартнеровПолучателей.Партнер
	               |			И (ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераМобильныхТелефоновПартнеров КАК НомераМобильныхТелефоновПартнеровПолучателей
	               |		ПО ОбъектыДоставки.Получатель = НомераМобильныхТелефоновПартнеровПолучателей.Партнер
	               |			И (ОбъектыДоставки.Получатель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераДополнительныхМобильныхТелефоновПартнеров КАК НомераДополнительныхМобильныхТелефоновПартнеровПолучателей
	               |		ПО ОбъектыДоставки.Отправитель = НомераДополнительныхМобильныхТелефоновПартнеровПолучателей.Партнер
	               |			И (ОбъектыДоставки.Отправитель ССЫЛКА Справочник.Партнеры)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НомераТелефоновСкладов КАК НомераТелефоновСкладовПолучателей
	               |		ПО ОбъектыДоставки.Получатель = НомераТелефоновСкладовПолучателей.Склад
	               |			И (ОбъектыДоставки.Получатель ССЫЛКА Справочник.Склады)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КешТелефоныПользователей КАК ТелефоныПользователей
	               |		ПО ОбъектыДоставки.Автор = ТелефоныПользователей.Пользователь
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаУсловийОтгрузки КАК ТаблицаУсловийОтгрузки
	               |		ПО ОбъектыДоставки.Получатель = ТаблицаУсловийОтгрузки.Партнер
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютУпр
	               |		ПО (КурсВалютУпр.Валюта = &ВалютаУправленческогоУчета)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютРег
	               |		ПО (КурсВалютРег.Валюта = &ВалютаРегламентированогоУчета)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ОбъектыДоставки.ЭтоМесто УБЫВ,
	               |	ОбъектыДоставки.Отправитель.Наименование,
	               |	ОбъектыДоставки.Получатель.Наименование
	               |ИТОГИ ПО
	               |	Получатель,
	               |	Отправитель";
	
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаВечернейРассылки()
 
Процедура СформироватьТабличныйДокументМаршрутныйЛистВечерняяРассылка(ТабличныйДокумент, ДанныеДляПечати)
	
	МакетМаршрутныйЛист = ПолучитьМакет("ПФ_MXL_МакетМаршрутныйЛист");	
	ОблЗаголовокПолучатель  = МакетМаршрутныйЛист.ПолучитьОбласть("Заголовок|Получатель");
	ОблЗаголовокОтправитель = МакетМаршрутныйЛист.ПолучитьОбласть("Заголовок|Отправитель");
	ОблЗаголовокКомментарий = МакетМаршрутныйЛист.ПолучитьОбласть("Заголовок|Комментарий");
	
	ОблПолучатель  = МакетМаршрутныйЛист.ПолучитьОбласть("Строка|Получатель");
	ОблОтправитель = МакетМаршрутныйЛист.ПолучитьОбласть("Строка|Отправитель");
	ОблКомментарий = МакетМаршрутныйЛист.ПолучитьОбласть("Место|Комментарий");
	ОблПодпись	   = МакетМаршрутныйЛист.ПолучитьОбласть("Подпись");
	ОблДата		   = МакетМаршрутныйЛист.ПолучитьОбласть("Маршрут_письмом|Отправитель");
	МакетСтроки    = "%1" + Символы.ПС + "%2" + Символы.ПС + "%3";
	
	ОблДата.Параметры.Дата 		= ТекущаяДата();
	ОблДата.Параметры.Маршрут 	= Справочники.ВариантыМаршрутов.ПолучитьСсылку(Новый УникальныйИдентификатор("bd28d8b3-f5b6-11e2-8f22-001e673c80fa"));
	ТабличныйДокумент.Вывести(ОблДата);
	
	ВыборкаПолучатель   = ДанныеДляПечати.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТабличныйДокумент.Вывести(ОблЗаголовокОтправитель);
	ТабличныйДокумент.Присоединить(ОблЗаголовокПолучатель);
	ТабличныйДокумент.Присоединить(ОблЗаголовокКомментарий);

		
	Пока ВыборкаПолучатель.Следующий() Цикл
		
		ВыборкаОтправитель = ВыборкаПолучатель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаОтправитель.Следующий() Цикл
			
			СтрокаМаршрутногоЛиста = Новый ТабличныйДокумент;
			ВыборкаДетальныеЗаписи = ВыборкаОтправитель.Выбрать();
					
			НомерМеста = 1;			
			ЭтоЗадача  = Ложь;
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				Если НомерМеста = 1 Тогда
					ОблПолучатель.Параметры.Получатель = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(МакетСтроки,
										ВыборкаПолучатель.Получатель,
										ВыборкаДетальныеЗаписи.АдресПолучатель,
										ВыборкаДетальныеЗаписи.НомерТелефонаПолучателя);
					ОблОтправитель.Параметры.Отправитель = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(МакетСтроки,
										ВыборкаОтправитель.Отправитель,
										ВыборкаДетальныеЗаписи.АдресОтправителя,
										ВыборкаДетальныеЗаписи.НомерТелефонаОтправителя);
				Иначе
					ОблПолучатель.Параметры.Получатель   = "";
					ОблОтправитель.Параметры.Отправитель = "";
				КонецЕсли;
				
				ОблКомментарий.Параметры.Комментарий = "";
				
				СтрокаМаршрутногоЛиста.Вывести(ОблОтправитель);
				СтрокаМаршрутногоЛиста.Присоединить(ОблПолучатель);
				
				Если Не ПустаяСтрока(ВыборкаДетальныеЗаписи.Комментарий) И Не ПустаяСтрока(ВыборкаДетальныеЗаписи.Автор) Тогда
		            Комментарий 				= "%1%2: %3";
					НомерТелефонаПользователя   = СтрЗаменить(ВыборкаДетальныеЗаписи.НомерТелефонаПользователя, " ", "");
					НомерТелефонаПользователя   = СтрЗаменить(НомерТелефонаПользователя, "(", "");
					НомерТелефонаПользователя   = СтрЗаменить(НомерТелефонаПользователя, ")", "");
					НомерТелефонаПользователя   = СтрЗаменить(НомерТелефонаПользователя, "+38", "");
					ОблКомментарий.Параметры.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Комментарий,
																ВыборкаДетальныеЗаписи.Автор,
																?(ПустаяСтрока(НомерТелефонаПользователя), "", "("+НомерТелефонаПользователя+")"),
																ВыборкаДетальныеЗаписи.Комментарий);																						
				КонецЕсли;
				
				Если ТипЗнч(ВыборкаДетальныеЗаписи.Получатель) = Тип("СправочникСсылка.Партнеры") И ВыборкаДетальныеЗаписи.ДолгПартнера > 0 И ВыборкаДетальныеЗаписи.ЭтоМесто  Тогда
					Комментарий = "УВАГА! НЕ ВІДВАНТАЖУВАТИ ТОВАР БЕЗ %1 USD (%2 грн)";
					ОблКомментарий.Параметры.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Комментарий,
																ВыборкаДетальныеЗаписи.ДолгПартнера,
																ВыборкаДетальныеЗаписи.ДолгПартнераРег);
				КонецЕсли;
				
				Если Не ВыборкаДетальныеЗаписи.ЭтоМесто Тогда
					СтрокаМаршрутногоЛиста.Присоединить(ОблКомментарий);
					ЭтоЗадача = Истина;
				ИначеЕсли НомерМеста = 1 Тогда
					СтрокаМаршрутногоЛиста.Присоединить(ОблКомментарий);
				КонецЕсли;
				
				НомерМеста = НомерМеста + 1;
				
			КонецЦикла;
					
			ОбластьКОбъединению = СтрокаМаршрутногоЛиста.Область(1,1,НомерМеста-1,1);
			ОбластьКОбъединению.Объединить();
			ОбластьКОбъединению.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
			ОбластьКОбъединению.РазмещениеТекста	  = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			ОбластьКОбъединению.ГраницаСнизу		  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
			ОбластьКОбъединению.ГраницаСправа		  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
			
			ОбластьКОбъединению = СтрокаМаршрутногоЛиста.Область(1,2,НомерМеста-1,2);
			ОбластьКОбъединению.Объединить();
			ОбластьКОбъединению.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
			ОбластьКОбъединению.РазмещениеТекста	  = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			ОбластьКОбъединению.ГраницаСнизу		  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
			ОбластьКОбъединению.ГраницаСправа		  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
			
			Если Не ЭтоЗадача Тогда
			
				ОбластьКОбъединению = СтрокаМаршрутногоЛиста.Область(1,3,НомерМеста-1,3);
				ОбластьКОбъединению.Объединить();
				ОбластьКОбъединению.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
				ОбластьКОбъединению.РазмещениеТекста	  = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
				ОбластьКОбъединению.ГраницаСнизу		  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
				ОбластьКОбъединению.ГраницаСправа		  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
				
			Иначе
				
				ОбластьКЗакраске 		  = СтрокаМаршрутногоЛиста.Область(1,1,НомерМеста-1,3);
				ОбластьКЗакраске.ЦветФона = Новый Цвет(220,220,220);
				
			КонецЕсли;

						
			Если ТабличныйДокумент.ПроверитьВывод(СтрокаМаршрутногоЛиста) Тогда
				ТабличныйДокумент.Вывести(СтрокаМаршрутногоЛиста);
			Иначе
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(СтрокаМаршрутногоЛиста);
			КонецЕсли;

		КонецЦикла;		
		
	КонецЦикла;
	
КонецПроцедуры



Функция СформироватьЗаголовокДокументаВодителю(Выборка)

	ТекстЗаголовка = "Транспортна накладна від %1";
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка,
								//ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Выборка.Место.Код, Ложь, Истина),
								Формат(ТекущаяДата(), "ДФ='дд ММММ гггг'"));

КонецФункции // СформироватьЗаголовокДокументаВодителю()
 
 
 
 





Функция СведенияОВнешнейОбработке() Экспорт
	
	//Версия
	Версия = "0.5.130";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Отгрузка мест");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Отгрузка мест [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "Отгрузка мест [" + Версия + "]", "ОМ", "ОткрытиеФормы", Ложь, "ОМ");
	ДобавитьКоманду(ТаблицаКоманд, "Рассылка маршрутного листа [" + Версия + "]", "ПровестиРассылкуМаршрутногоЛиста();", "ВызовСерверногоМетода");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

Процедура ВыполнитьКоманду(Индентификатор) Экспорт
	Выполнить(Индентификатор);	
КонецПроцедуры