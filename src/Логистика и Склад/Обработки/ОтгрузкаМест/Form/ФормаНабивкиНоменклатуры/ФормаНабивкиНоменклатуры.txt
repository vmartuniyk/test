

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Номенклатура 		= Параметры.Номенклатура;
	ЕдиницаИзмерения    = Номенклатура.ЕдиницаИзмерения;
	Количество			= 1;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ЕдНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)	
	Элементы.Ед.СписокВыбора.ЗагрузитьЗначения(ВыбратьУпаковку());	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Добавить(Команда)	
	Результат = Новый Структура;
	Результат.Вставить("Ед", ЕдиницаИзмерения);
	Результат.Вставить("Количество", Количество);
	Закрыть(Результат);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере 
Функция ВыбратьУпаковку()
	
	Результат 	  = Новый Массив;	
	Запрос 		  = Новый Запрос();
	Запрос.Текст  = ПолучитьТекстЗапросаУпаковки();	
		
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("НаборУпаковок", Номенклатура.НаборУпаковок);
	
 	ТаблицаУпаковок = Запрос.Выполнить().Выгрузить();
	
	Результат = ТаблицаУпаковок.ВыгрузитьКолонку("Значение");
	Результат.Вставить(0, Номенклатура.ЕдиницаИзмерения);	
	
	Возврат Результат;
		
КонецФункции

&НаСервере 
Функция ПолучитьТекстЗапросаУпаковки()
	
	ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Ссылка 			КАК Значение,
		|	Наименование 	КАК Представление
		|ИЗ
		|	Справочник.УпаковкиНоменклатуры
		|ГДЕ
		|	Владелец = ВЫБОР КОГДА &НаборУпаковок =  ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры)
		|                    ТОГДА &Номенклатура
		|                    КОГДА &НаборУпаковок <> ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ПустаяСсылка)
		|                    ТОГДА &НаборУпаковок
		|					 ИНАЧЕ Неопределено
		|			   КОНЕЦ 
		|И  ПометкаУдаления = Ложь";
	 
	 Возврат ТекстЗапроса;
	
 КонецФункции








