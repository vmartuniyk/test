
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗапросЦены = Новый Запрос();
	ЗапросЦены.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СправочникНоменклатура.Ссылка КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА РегистрЦен.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|		ТОГДА ЕСТЬNULL(РегистрЦен.Цена, 0) * (КурсВалютыЦены.Курс / КурсВалютыЦены.Кратность / (КурсВалютыОбработки.Курс / КурсВалютыОбработки.Кратность))
	|		ИНАЧЕ (ЕСТЬNULL(РегистрЦен.Цена, 0) / ЕСТЬNULL(РегистрЦен.Упаковка.Коэффициент, 1)) * (КурсВалютыЦены.Курс / КурсВалютыЦены.Кратность / (КурсВалютыОбработки.Курс / КурсВалютыОбработки.Кратность))
	|	КОНЕЦ КАК Цена,
	|	ЕСТЬNULL(РегистрОстатки.ВНаличииОстаток, 0) КАК ВНаличииОстаток,
	|	ЕСТЬNULL(РегистрОстатки.ВНаличииОстаток, 0) - ЕСТЬNULL(РегистрОстатки.ВРезервеОстаток, 0) КАК СвободныйОстаток,
	|	ЕСТЬNULL(РегистрОстатки.ВРезервеОстаток, 0) КАК Резерв
 	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(,
	|										ВидЦены = &ВидЦены
	|									И	Номенклатура = &Номенклатура) КАК РегистрЦен
	|ПО
	|	РегистрЦен.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(,) КАК КурсВалютыЦены
	|ПО
	|	КурсВалютыЦены.Валюта = РегистрЦен.Валюта
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(,Валюта = &Валюта) КАК КурсВалютыОбработки
	|ПО
	|	(ИСТИНА)
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.Остатки(,
	|														Номенклатура = &Номенклатура
	|													И	Склад = &Склад) КАК РегистрОстатки
	|ПО
	|	РегистрОстатки.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|ГДЕ
	|	СправочникНоменклатура.Ссылка = &Номенклатура";
	
	ЗапросЦены.УстановитьПараметр("Номенклатура", Параметры.Номенклатура);
	ЗапросЦены.УстановитьПараметр("ВидЦены", Параметры.ВидЦены);
	ЗапросЦены.УстановитьПараметр("Валюта", Параметры.Валюта);
	ЗапросЦены.УстановитьПараметр("Склад", Параметры.Склад);
	
	Номенклатура = Неопределено;
	Цена = Неопределено;
	ВНаличииОстаток = Неопределено;
	СвободныйОстаток = Неопределено;
	Резерв = Неопределено;
	
	Результат = ЗапросЦены.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Номенклатура = Выборка.Номенклатура;
		Цена = Выборка.Цена;
		ВНаличииОстаток = Выборка.ВНаличииОстаток;
		СвободныйОстаток = Выборка.СвободныйОстаток;
		Резерв = Выборка.Резерв;
		
	КонецЕсли;
	
	ЗапросУпаковки = Новый Запрос();
	ЗапросУпаковки.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(СпрУпаковки.Ссылка, НЕОПРЕДЕЛЕНО) КАК Упаковка,
	|	ЕСТЬNULL(СпрУпаковки.Коэффициент, 1) КАК Коэффициент
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиНоменклатуры КАК СпрУпаковки
	|		ПО (СпрУпаковки.Владелец = ВЫБОР
	|				КОГДА СпрНоменклатура.НаборУпаковок = ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры)
	|					ТОГДА СпрНоменклатура.Ссылка
	|				КОГДА СпрНоменклатура.НаборУпаковок <> ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ПустаяСсылка)
	|					ТОГДА СпрНоменклатура.НаборУпаковок
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|ГДЕ
	|	СпрНоменклатура.Ссылка = &Номенклатура
	|   И НЕ СпрУпаковки.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	СпрУпаковки.Коэффициент,
	|	СпрУпаковки.Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	ЗапросУпаковки.УстановитьПараметр("Номенклатура", Параметры.Номенклатура);
	
	Результат = ЗапросУпаковки.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	НоваяСтрока = ТаблицаНоменклатура.Добавить();
		
	НоваяСтрока.Код = Номенклатура.Код;
	НоваяСтрока.Номенклатура = Номенклатура;
	НоваяСтрока.ЕдиницаИзмерения = Номенклатура.ЕдиницаИзмерения;
	НоваяСтрока.Цена = Цена;
	НоваяСтрока.Валюта = Параметры.Валюта;
	НоваяСтрока.ВНаличииОстаток = ВНаличииОстаток;
	НоваяСтрока.СвободныйОстаток = СвободныйОстаток;
	НоваяСтрока.Резерв = Резерв;
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаНоменклатура.Добавить();
		
		НоваяСтрока.Код = Номенклатура.Код;
		НоваяСтрока.Номенклатура = Номенклатура;
		НоваяСтрока.Упаковка = Выборка.Упаковка;
		НоваяСтрока.Цена = Цена * Выборка.Коэффициент;
		НоваяСтрока.Валюта = Параметры.Валюта;
		НоваяСтрока.ВНаличииОстаток = ВНаличииОстаток / Выборка.Коэффициент;
		НоваяСтрока.СвободныйОстаток = СвободныйОстаток / Выборка.Коэффициент;
		НоваяСтрока.Резерв = Резерв / Выборка.Коэффициент;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНоменклатураВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		СтруктураКВозврату = Новый Структура();
		
		СтруктураКВозврату.Вставить("Код", ТекущиеДанные.Код);
		СтруктураКВозврату.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
		СтруктураКВозврату.Вставить("ЕдиницаИзмерения", ТекущиеДанные.ЕдиницаИзмерения);
		СтруктураКВозврату.Вставить("Упаковка", ТекущиеДанные.Упаковка);
		СтруктураКВозврату.Вставить("Цена", ТекущиеДанные.Цена);
		
		Закрыть(СтруктураКВозврату);
		
	КонецЕсли;
	
КонецПроцедуры
