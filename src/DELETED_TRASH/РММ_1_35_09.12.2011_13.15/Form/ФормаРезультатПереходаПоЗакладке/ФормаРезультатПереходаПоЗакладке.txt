
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//НаименованиеНоменклатуры = Параметры.Номенклатура;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СправочникНоменклатура.Ссылка 											КАК Ссылка,
	|	ВЫРАЗИТЬ(
	|		ВЫБОР 
	|			КОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) > 0 
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА &Валюта <> ЦеныНоменклатуры.Валюта
	|					ТОГДА
	|						ВЫБОР
	|							КОГДА
	|								ЕСТЬNULL(КурсыСрезПоследнихВалютаЦены.Кратность, 0) > 0
	|								И ЕСТЬNULL(КурсыСрезПоследнихВалютаЦены.Курс, 0) > 0
	|								И ЕСТЬNULL(КурсыСрезПоследнихВалютаОбработки.Кратность, 0) > 0
	|								И ЕСТЬNULL(КурсыСрезПоследнихВалютаОбработки.Курс, 0) > 0
	|							
	|							ТОГДА
	|								ЦеныНоменклатуры.Цена*(КурсыСрезПоследнихВалютаЦены.Курс * КурсыСрезПоследнихВалютаОбработки.Кратность / (КурсыСрезПоследнихВалютаОбработки.Курс * КурсыСрезПоследнихВалютаЦены.Кратность))
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|					ИНАЧЕ 
	|						ЦеныНоменклатуры.Цена
	|				КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КАК Число(15,3)) 														КАК Цена,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(СвободныеОстатки.ВНаличииОстаток, 0) КАК Число(15,3)) КАК ОстатокНаСкладе,
	|
	|	ВЫРАЗИТЬ(ЕСТЬNULL(СвободныеОстатки.ВРезервеОстаток, 0) КАК Число(15,3)) КАК НаСкладеВРезерве,
	|
	|	СправочникНоменклатура.ЕдиницаИзмерения									КАК ЕдиницаИзмерения
	|
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, ВидЦены В (&ВидыЦен) И Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ЦеныНоменклатуры
	|	ПО (ЦеныНоменклатуры.Номенклатура = СправочникНоменклатура.Ссылка)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыСрезПоследнихВалютаЦены
	|	ПО (КурсыСрезПоследнихВалютаЦены.Валюта = ЦеныНоменклатуры.Валюта)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта = &Валюта) КАК КурсыСрезПоследнихВалютаОбработки
	|	ПО (ИСТИНА)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.Остатки(, Склад В (&Склад)) КАК СвободныеОстатки
	|	ПО
	|		(СвободныеОстатки.Номенклатура = СправочникНоменклатура.Ссылка)
	|
	|ГДЕ
	|	СправочникНоменклатура.Ссылка = &Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Параметры.Номенклатура);
	Запрос.УстановитьПараметр("Дата", Параметры.Дата);
	Запрос.УстановитьПараметр("ВидыЦен", Параметры.ВидЦен);
	Запрос.УстановитьПараметр("Валюта", Параметры.Валюта);
	Запрос.УстановитьПараметр("Склад", Параметры.Склад);
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.Прямой);
	
	Если Выборка.Следующий() Тогда
	
		НоваяСтрока = Закладка.Добавить();
		
		НоваяСтрока.Номенклатура 		= Выборка.Ссылка;
		НоваяСтрока.Цена 				= Выборка.Цена;
		НоваяСтрока.Остаток 			= Выборка.ОстатокНаСкладе;
		НоваяСтрока.Резерв 				= Выборка.НаСкладеВРезерве;
		НоваяСтрока.ЕдиницаИзмерения 	= Выборка.ЕдиницаИзмерения;
	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВКорзину(Команда)
	
	СтруктураПередачи 	= Новый Структура;
	ТаблицаЗакладки 	= Элементы.Закладка;
	ТекДанные			= ТаблицаЗакладки.ТекущиеДанные;
	
	СтруктураПередачи.Вставить("Номенклатура", ТекДанные.Номенклатура);
	СтруктураПередачи.Вставить("Цена", ТекДанные.Цена);
	
	Закрыть(СтруктураПередачи);
		
КонецПроцедуры
