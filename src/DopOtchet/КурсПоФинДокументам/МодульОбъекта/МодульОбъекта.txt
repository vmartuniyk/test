
#Область АвтоТест
Перем ЮнитТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮнитТест = ЮнитТестирование;
	
	СпиокТестов = Новый Массив;	
	Возврат СпиокТестов;
	
КонецФункции
#КонецОбласти

#Область ПрограмныйИнтерфейс
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)	
	СтандартнаяОбработка = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);	
	
	ОтчетностьФинДок			= ПолучитьНаборДанных();
	ВнешниеНаборыДанных 		= Новый Структура;
	ВнешниеНаборыДанных.Вставить("Объект",ОтчетностьФинДок);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки(); 
	

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	Если МакетКомпоновки.НаборыДанных.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru= 'Отчет не сформирован. Включите хотя бы одну группировку в ""Элементы оформления и группировки"".'") ;
	КонецЕсли;
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедуриИФункции
Функция ПолучитьЗначениеПараметра(ИмяПараметра)
	
	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если ПараметрДанных <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки.Значение;
		Иначе
			Возврат ПараметрДанных.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьНаборДанных()
	УстановитьПривилегированныйРежим(Истина);
	Запрос 		 	= Новый Запрос;
	ТекстЗапроса 	= ПолучитьТекстЗапросаНаборДанных();
	ПериодОтчета 	= ПолучитьЗначениеПараметра("Период");
	Организация		= ПолучитьЗначениеПараметра("Организация");
	Запрос.Текст 	= ТекстЗапроса;
	Запрос.УстановитьПараметр("ДатаНачало", 	ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	ПериодОтчета.ДатаОкончания);
	Запрос.УстановитьПараметр("Организация", 	Организация);


	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьНаборДанных()

Функция ПолучитьТекстЗапросаНаборДанных()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.Дата, МЕСЯЦ) КАК Период,
	               |	ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка КАК Документ,
	               |	ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа.Сумма / ВЫБОР
	               |		КОГДА ЕСТЬNULL(ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа.СуммаВзаиморасчетов, 0) = 0
	               |			ТОГДА 1
	               |		ИНАЧЕ ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа.СуммаВзаиморасчетов
	               |	КОНЕЦ КАК Курс,
	               |	ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.Валюта КАК Валота,
	               |	ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
	               |ИЗ
	               |	Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа
	               |ГДЕ
	               |	ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.Организация = &Организация
	               |	И ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.Дата МЕЖДУ &ДатаНачало И &ДатаОкончания
	               |	И НЕ ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.Валюта = ПоступлениеБезналичныхДенежныхСредствРасшифровкаПлатежа.ВалютаВзаиморасчетов
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Дата, МЕСЯЦ),
	               |	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
	               |	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Сумма / ВЫБОР
	               |		КОГДА ЕСТЬNULL(ПриходныйКассовыйОрдерРасшифровкаПлатежа.СуммаВзаиморасчетов, 0) = 0
	               |			ТОГДА 1
	               |		ИНАЧЕ ПриходныйКассовыйОрдерРасшифровкаПлатежа.СуммаВзаиморасчетов
	               |	КОНЕЦ,
	               |	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Валюта,
	               |	ПриходныйКассовыйОрдерРасшифровкаПлатежа.ВалютаВзаиморасчетов
	               |ИЗ
	               |	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПриходныйКассовыйОрдерРасшифровкаПлатежа
	               |ГДЕ
	               |	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Организация = &Организация
	               |	И ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Дата МЕЖДУ &ДатаНачало И &ДатаОкончания
	               |	И НЕ ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Валюта = ПриходныйКассовыйОрдерРасшифровкаПлатежа.ВалютаВзаиморасчетов
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Дата, МЕСЯЦ),
	               |	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
	               |	РасходныйКассовыйОрдерРасшифровкаПлатежа.Сумма / ВЫБОР
	               |		КОГДА ЕСТЬNULL(РасходныйКассовыйОрдерРасшифровкаПлатежа.СуммаВзаиморасчетов, 0) = 0
	               |			ТОГДА 1
	               |		ИНАЧЕ РасходныйКассовыйОрдерРасшифровкаПлатежа.СуммаВзаиморасчетов
	               |	КОНЕЦ,
	               |	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Валюта,
	               |	РасходныйКассовыйОрдерРасшифровкаПлатежа.ВалютаВзаиморасчетов
	               |ИЗ
	               |	Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасходныйКассовыйОрдерРасшифровкаПлатежа
	               |ГДЕ
	               |	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Организация = &Организация
	               |	И РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Дата МЕЖДУ &ДатаНачало И &ДатаОкончания
	               |	И НЕ РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Валюта = РасходныйКассовыйОрдерРасшифровкаПлатежа.ВалютаВзаиморасчетов
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.Дата, МЕСЯЦ),
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Сумма / ВЫБОР
	               |		КОГДА ЕСТЬNULL(СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.СуммаВзаиморасчетов, 0) = 0
	               |			ТОГДА 1
	               |		ИНАЧЕ СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.СуммаВзаиморасчетов
	               |	КОНЕЦ,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.Валюта,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ВалютаВзаиморасчетов
	               |ИЗ
	               |	Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа
	               |ГДЕ
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.Организация = &Организация
	               |	И СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.Дата МЕЖДУ &ДатаНачало И &ДатаОкончания
	               |	И НЕ СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.Валюта = СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ВалютаВзаиморасчетов";	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти


#Область    СведенияОВнешнемОтчете
Функция 	СведенияОВнешнейОбработке() Экспорт
	
	Версия = "1.0.1";
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
	ПараметрыРегистрации = Новый Структура;
	
	// Объявим еще одну переменную, которая нам потребуется ниже
	МассивНазначений = Новый Массив;
	
	// Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
	// Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
	ПараметрыРегистрации.Вставить("Вид", "ДополнительныйОтчет");
	
	// Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
	// Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
	// которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	
	// Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
	ПараметрыРегистрации.Вставить("Наименование", "Курс по финдокументам");
	
	// Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	
	// Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
	ПараметрыРегистрации.Вставить("Версия", Версия);    
	ПараметрыРегистрации.Вставить("Информация", "Курс по финдокументам'");
	
	// Создадим таблицу команд (подробнее смотрим ниже)
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	
	// Добавим команду в таблицу
	ДобавитьКоманду(ТаблицаКоманд, "Курс по финдокументам", "ФормаОтчета", "ОткрытиеФормы", Ложь, "");
	
	// Сохраним таблицу команд в параметры регистрации обработки
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	
	// Теперь вернем системе наши параметры
	Возврат ПараметрыРегистрации;
КонецФункции

Функция 	ПолучитьТаблицуКоманд()
	
	// Создадим пустую таблицу команд и колонки в ней
	Команды = Новый ТаблицаЗначений;
	
	// Как будет выглядеть описание печатной формы для пользователя
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 
	
	// Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	
	// Тут задается, как должна вызваться команда обработки
	// Возможные варианты:
	// - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
	// - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
	// - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	
	// Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	
	// Для печатной формы должен содержать строку ПечатьMXL 
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	
	Возврат Команды;
	
КонецФункции

Процедура 	ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	// Добавляем команду в таблицу команд по переданному описанию.
	// Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры

#КонецОбласти



