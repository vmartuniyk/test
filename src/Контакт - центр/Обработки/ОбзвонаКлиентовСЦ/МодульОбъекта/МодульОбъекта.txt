
#Область ИнтерфейсАвтоматическихТестов

Перем ЮнитТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮнитТест = ЮнитТестирование;
	
	СписокТестов = Новый Массив;
	ПараметрыТеста = Новый Структура("ИмяТеста", "Тест_ПроверкаВалидностьЗапросаЗаявокСЦ");
	СписокТестов.Добавить(ПараметрыТеста);

	
	Возврат СписокТестов;
	
КонецФункции

Процедура Тест_ПроверкаВалидностьЗапросаЗаявокСЦ() Экспорт
	ТекстЗапроса = ПолучитьТекстЗапросаЗаявокСЦ();
	ЮнитТест.ПроверитьТип(ТекстЗапроса, Тип("Строка"));
	ЮнитТест.ПроверитьВалидностьЗапроса(ТекстЗапроса);
КонецПроцедуры


#КонецОбласти

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// http://r.ktc-ua.com/issues/5081
//
////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// 	Загружаем список заявок сервисного в таблицу значения
//	http://r.ktc-ua.com/issues/5084
//
// Параметры:
//  НЕТ
//
Процедура ЗагрузитьСписокЗаявокСервисногоЦентра(ФормаОбработки) Экспорт
	
	ПолеСортировки =  ФормаОбработки.ПолеСортировки;
	Сортировка_Направление = ФормаОбработки.Сортировка_Направление;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = ПолучитьТекстЗапросаЗаявокСЦ();
	Если НЕ ПустаяСтрока(ПолеСортировки)
		И НЕ ПустаяСтрока(Сортировка_Направление) Тогда
		ТекстЗапроса = ТекстЗапроса
				+ "УПОРЯДОЧИТЬ ПО " 
				+ ПолеСортировки + Сортировка_Направление;
	Иначе
		ТекстЗапроса = ТекстЗапроса
						+" УПОРЯДОЧИТЬ ПО
						  |ДатаВыдачи";
				
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НашПартнер",Справочники.Партнеры.ПолучитьСсылку(Новый УникальныйИдентификатор("9bbd73c0-4748-11e0-9f98-001517115d85")));
	Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
	ФормаОбработки.СписокДокументовЗаявкиСЦ.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получаем  список заявок сервисного центра 
//
// Параметры:
//  НЕТ
//
// Возвращаемое значение:
//  Строка   - текст запроса
//   
Функция ПолучитьТекстЗапросаЗаявокСЦ()
	ТекстЗапроса = "ВЫБРАТЬ
				|	ЗаявкаСервисногоЦентра,
				|	ЭтапРаботы,
				|	ДатаИсполнения КАК ДатаВыдачи
				|ПОМЕСТИТЬ ЗаявкаСЭтапом
				|ИЗ
				|	РегистрСведений.ГНАТ_ЭтапыРаботЗаявокСервисногоЦентра
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	ЗаявкаСервисногоЦентра
				|;
				|
				////////////////////////////////////////////////////////////////////////////////
				//1
				|ВЫБРАТЬ
				|	ЗаявкаСервисногоЦентра
				|ПОМЕСТИТЬ ВсеЗаявкиСРемонтами
				|ИЗ
				|	ЗаявкаСЭтапом
				|ГДЕ
				|	ЭтапРаботы.ЭтоРемонт
				|	
				|ИНДЕКСИРОВАТЬ ПО
				|	ЗаявкаСервисногоЦентра     
				|;
				////////////////////////////////////////////////////////////////////////////////
				//2
				|ВЫБРАТЬ
				|	ЗаявкаСервисногоЦентра,
				|	ЗаявкаСЭтапом.ДатаВыдачи
				|  
				|ПОМЕСТИТЬ ВсеВыданыеЗаявки
				|ИЗ
				|	ЗаявкаСЭтапом
				|ГДЕ
				|	ЗаявкаСервисногоЦентра  В(ВЫБРАТЬ ЗаявкаСервисногоЦентра ИЗ ВсеЗаявкиСРемонтами)
				|	И ЭтапРаботы = ЗНАЧЕНИЕ(Справочник.ГНАТ_ЭтапыРаботы.Выдано)
				|;
				////////////////////////////////////////////////////////////////////////////////
				//3
				|ВЫБРАТЬ
				|	Партнер КАК Партнер
				|ПОМЕСТИТЬ СписокНашихПредприятий
				|ИЗ
				|	РегистрСведений.Модуль_ДоступныеСклады 
				|ГДЕ
				|	НЕ Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
				| 
				|ОБЪЕДИНИТЬ
				|
				|ВЫБРАТЬ
				|	ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
				|
				|ОБЪЕДИНИТЬ
				|   
				|ВЫБРАТЬ
				|	ВЫРАЗИТЬ(&НашПартнер КАК Справочник.Партнеры)
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	Партнер
				|;
				////////////////////////////////////////////////////////////////////////////////
				//4
				|ВЫБРАТЬ
				|	ВсеВыданыеЗаявки.ЗаявкаСервисногоЦентра,
				|	ВсеВыданыеЗаявки.ДатаВыдачи,
				|	ГНАТ_ЗаявкиСервисногоЦентра.Партнер,
				|	ГНАТ_ЗаявкиСервисногоЦентра.NPS,
				|	ГНАТ_ЗаявкиСервисногоЦентра.ДатаЗвонка,
				|	ГНАТ_ЗаявкиСервисногоЦентра.ОтложеннаяДатаЗвонка,
				|	ГНАТ_ЗаявкиСервисногоЦентра.Номенклатура,
				|	ГНАТ_ЗаявкиСервисногоЦентра.Период
				|	
				|ПОМЕСТИТЬ ЗаявкиДляОбзвонкою
				|ИЗ
				|	ВсеВыданыеЗаявки КАК ВсеВыданыеЗаявки
				|		
				|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
				|ПО ВсеВыданыеЗаявки.ЗаявкаСервисногоЦентра = ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
				|;
				////////////////////////////////////////////////////////////////////////////////
				//5				
				|ВЫБРАТЬ
				|	ЗаявкаСОбзвонкоюКлиента.ЗаявкаСервисногоЦентра,
				|	ЗаявкаСОбзвонкоюКлиента.ДатаВыдачи,
				|	ЗаявкаСОбзвонкоюКлиента.Партнер,
				|	ЗаявкаСОбзвонкоюКлиента.NPS,
				|	ЗаявкаСОбзвонкоюКлиента.ДатаЗвонка,
				|	ЗаявкаСОбзвонкоюКлиента.ОтложеннаяДатаЗвонка,
				|	ЗаявкаСОбзвонкоюКлиента.Номенклатура,
				|	ЗаявкаСОбзвонкоюКлиента.Период
				|ПОМЕСТИТЬ ПартнерыДляОбзвонка
				|ИЗ
				|	ЗаявкиДляОбзвонкою КАК ЗаявкаСОбзвонкоюКлиента
				|ГДЕ
				|	НЕ ЗаявкаСОбзвонкоюКлиента.Партнер В
				|				(ВЫБРАТЬ
				|					СписокНашихПредприятий.Партнер
				|				ИЗ
				|					СписокНашихПредприятий)
				|	И НЕ ЗаявкаСОбзвонкоюКлиента.Партнер.КлиентПротивОбзвона
				|;
				////////////////////////////////////////////////////////////////////////////////
				//6				
				|ВЫБРАТЬ
				|	МИНИМУМ(Период) КАК Период,
				|	Партнер,
				|	НАЧАЛОПЕРИОДА(Период, НЕДЕЛЯ) КАК НачНеделя
				|ПОМЕСТИТЬ ПервыеЗаявкиЗаНеделю
				|ИЗ
				|	ПартнерыДляОбзвонка
				|     
				|СГРУППИРОВАТЬ ПО
				|	НАЧАЛОПЕРИОДА(Период, НЕДЕЛЯ),
				|	Партнер
				|	
				|ИНДЕКСИРОВАТЬ ПО
				|	Период,
				|	Партнер
				|;
				////////////////////////////////////////////////////////////////////////////////
				//7	
				|ВЫБРАТЬ
				|	ПартнерыДляОбзвонка.ЗаявкаСервисногоЦентра,
				|	ПартнерыДляОбзвонка.Партнер,
				|	ПартнерыДляОбзвонка.ДатаВыдачи,
				|	ПартнерыДляОбзвонка.NPS,
				|	ПартнерыДляОбзвонка.ДатаЗвонка,
				|	ПартнерыДляОбзвонка.ОтложеннаяДатаЗвонка,
				|	ПартнерыДляОбзвонка.Номенклатура
				|ПОМЕСТИТЬ ВсеЗаявкиНужныеДляОбзвону
				|ИЗ
				|	ПартнерыДляОбзвонка КАК ПартнерыДляОбзвонка
				|		
				|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПервыеЗаявкиЗаНеделю КАК ПервыеЗаявкиЗаНеделю
				|ПО ПартнерыДляОбзвонка.Партнер = ПервыеЗаявкиЗаНеделю.Партнер
				|И ПартнерыДляОбзвонка.Период = ПервыеЗаявкиЗаНеделю.Период

				|ГДЕ
				|	НЕ ПартнерыДляОбзвонка.NPS = НЕОПРЕДЕЛЕНО
				|	
				|;
				////////////////////////////////////////////////////////////////////////////////
				//8
				|ВЫБРАТЬ 
				|	ЗаявкаСервисногоЦентра КАК ЗаявкаСЦ,
				|	Партнер КАК Заказчик,
				|	ДатаЗвонка,
				|	ОтложеннаяДатаЗвонка,
				|	Номенклатура,
				|	ДатаВыдачи,
				|	ЗаявкаСервисногоЦентра.Номер КАК НомерЗСЦ,
				|	ЗаявкаСервисногоЦентра.НомерТелефона КАК Телефон,
				|	ЗаявкаСервисногоЦентра.ВидОперации КАК ТипЗСЦ,
				|   Партнер.Наименование КАК ПартнерПредставления,
				|   Номенклатура.Наименование КАК НоменклатураПредставления
				|ИЗ
				|	ВсеЗаявкиНужныеДляОбзвону
				|ГДЕ 
				|	ОтложеннаяДатаЗвонка >=&ТекущаяДата
				|	
				|ОБЪЕДИНИТЬ	
				|  
				|ВЫБРАТЬ 
				|	ЗаявкаСервисногоЦентра КАК ЗаявкаСЦ,
				|	Партнер КАК Заказчик,
				|	ДатаЗвонка,
				|	ОтложеннаяДатаЗвонка,
				|	Номенклатура,
				|	ДатаВыдачи,
				|	ЗаявкаСервисногоЦентра.Номер КАК НомерЗСЦ,
				|	ЗаявкаСервисногоЦентра.НомерТелефона КАК Телефон,
				|	ЗаявкаСервисногоЦентра.ВидОперации КАК ТипЗСЦ,
				|   Партнер.Наименование КАК ПартнерПредставления,
				|   Номенклатура.Наименование КАК НоменклатураПредставления
				|ИЗ
				|	ВсеЗаявкиНужныеДляОбзвону
				|ГДЕ 
				|	ОтложеннаяДатаЗвонка = ДАТАВРЕМЯ(1,1,1)";
	Возврат ТекстЗапроса;
КонецФункции // ПолучитьТекстЗапросаЗаявокСЦ()


#КонецОбласти