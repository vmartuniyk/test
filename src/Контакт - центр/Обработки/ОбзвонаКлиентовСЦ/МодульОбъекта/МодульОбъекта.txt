
#Область ИнтерфейсАвтоматическихТестов

Перем ЮнитТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮнитТест = ЮнитТестирование;
	
	СписокТестов = Новый Массив;
	ПараметрыТеста = Новый Структура("ИмяТеста", "Тест_ПроверкаВалидностьЗапросаЗаявокСЦ");
	СписокТестов.Добавить(ПараметрыТеста);

	
	Возврат СписокТестов;
	
КонецФункции

Процедура Тест_ПроверкаВалидностьЗапросаЗаявокСЦ() Экспорт
	ТекстЗапроса = ПолучитьТекстЗапросаЗаявокСЦ();
	ЮнитТест.ПроверитьТип(ТекстЗапроса, Тип("Строка"));
	ЮнитТест.ПроверитьВалидностьЗапроса(ТекстЗапроса);
КонецПроцедуры


#КонецОбласти

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// http://r.ktc-ua.com/issues/5081
//
////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// 	Загружаем список заявок сервисного в таблицу значения
//	http://r.ktc-ua.com/issues/5084
//
// Параметры:
//  НЕТ
//
Процедура ЗагрузитьСписокЗаявокСервисногоЦентра(ФормаОбработки) Экспорт
	
	ПолеСортировки =  ФормаОбработки.ПолеСортировки;
	Сортировка_Направление = ФормаОбработки.Сортировка_Направление;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = ПолучитьТекстЗапросаЗаявокСЦ();
	Если НЕ ПустаяСтрока(ПолеСортировки)
		И НЕ ПустаяСтрока(Сортировка_Направление) Тогда
		ТекстЗапроса = ТекстЗапроса
				+ " УПОРЯДОЧИТЬ ПО " 
				+ ПолеСортировки + Сортировка_Направление;
	Иначе
		ТекстЗапроса = ТекстЗапроса
						+" УПОРЯДОЧИТЬ ПО
						  |ДатаВыдачи";
				
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НашПартнер",Справочники.Партнеры.ПолучитьСсылку(Новый УникальныйИдентификатор("9bbd73c0-4748-11e0-9f98-001517115d85")));
	Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
	ФормаОбработки.СписокДокументовЗаявкиСЦ.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получаем  список заявок сервисного центра 
//
// Параметры:
//  НЕТ
//
// Возвращаемое значение:
//  Строка   - текст запроса
//   
Функция ПолучитьТекстЗапросаЗаявокСЦ()
	ТекстЗапроса = " ВЫБРАТЬ
				|	Ссылка КАК ЭтапРаботы
				|ПОМЕСТИТЬ ЭтапиЗаявок
				|ИЗ
				|	Справочник.ГНАТ_ЭтапыРаботы
				|ГДЕ
				|	ЭтоРемонт
				|              
				|ОБЪЕДИНИТЬ
				|
				|ВЫБРАТЬ
				|	ЗНАЧЕНИЕ(Справочник.ГНАТ_ЭтапыРаботы.Выдано)
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	ЭтапРаботы
				|;
				////////////////////////////////////////////////////////////////////////////////
				//1
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	ЗаявкаСервисногоЦентра КАК ЗаявкаСервисногоЦентра,
				|	ЭтапРаботы	КАК ЭтапРаботы,
				|	ДатаИсполнения КАК ДатаВыдачи
				|ПОМЕСТИТЬ ЗаявкаСЭтапомРемонт
				|ИЗ
				|	РегистрСведений.ГНАТ_ЭтапыРаботЗаявокСервисногоЦентра
				|ГДЕ
				|	ЭтапРаботы  В(ВЫБРАТЬ ЭтапРаботы ИЗ ЭтапиЗаявок)
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	ЗаявкаСервисногоЦентра
				|;
				////////////////////////////////////////////////////////////////////////////////
				//2
				|ВЫБРАТЬ
				|	ЗаявкаСервисногоЦентра,
				|	ДатаВыдачи
				|  
				|ПОМЕСТИТЬ ВсеВыданыеЗаявки
				|ИЗ
				|	ЗаявкаСЭтапомРемонт
				|ГДЕ
				|	ЭтапРаботы = ЗНАЧЕНИЕ(Справочник.ГНАТ_ЭтапыРаботы.Выдано)
				|;
				////////////////////////////////////////////////////////////////////////////////
				//3
				|ВЫБРАТЬ
				|	Партнер КАК Партнер
				|ПОМЕСТИТЬ СписокНашихПредприятий
				|ИЗ
				|	РегистрСведений.Модуль_ДоступныеСклады 
				|ГДЕ
				|	НЕ Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
				| 
				|ОБЪЕДИНИТЬ
				|
				|ВЫБРАТЬ
				|	ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
				|
				|ОБЪЕДИНИТЬ
				|   
				|ВЫБРАТЬ
				|	ВЫРАЗИТЬ(&НашПартнер КАК Справочник.Партнеры)
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	Партнер
				|;
				////////////////////////////////////////////////////////////////////////////////
				//4
				|ВЫБРАТЬ
				|	ВсеВыданыеЗаявки.ЗаявкаСервисногоЦентра,
				|	ВсеВыданыеЗаявки.ДатаВыдачи,
				|	ГНАТ_ЗаявкиСервисногоЦентра.Партнер,
				|	ГНАТ_ЗаявкиСервисногоЦентра.NPS,
				|	ГНАТ_ЗаявкиСервисногоЦентра.ДатаЗвонка,
				|	ГНАТ_ЗаявкиСервисногоЦентра.ОтложеннаяДатаЗвонка,
				|	ГНАТ_ЗаявкиСервисногоЦентра.Номенклатура
				|	
				|ПОМЕСТИТЬ ПартнерыДляОбзвонка
				|ИЗ
				|	ВсеВыданыеЗаявки КАК ВсеВыданыеЗаявки
				|		
				|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
				|ПО ВсеВыданыеЗаявки.ЗаявкаСервисногоЦентра = ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
				|
				|ГДЕ
				|	ГНАТ_ЗаявкиСервисногоЦентра.Партнер НЕ В
				|				(ВЫБРАТЬ
				|					СписокНашихПредприятий.Партнер
				|				ИЗ
				|					СписокНашихПредприятий)
				|	И НЕ ГНАТ_ЗаявкиСервисногоЦентра.Партнер.КлиентПротивОбзвона
				|;
				////////////////////////////////////////////////////////////////////////////////
				//5				
				|ВЫБРАТЬ
				|	МИНИМУМ(ДатаВыдачи) КАК ДатаВыдачи,
				|	Партнер,
				|	НАЧАЛОПЕРИОДА(ДатаВыдачи, НЕДЕЛЯ) КАК НачНеделя
				|ПОМЕСТИТЬ ПервыеЗаявкиЗаНеделю
				|ИЗ
				|	ПартнерыДляОбзвонка
				|     
				|СГРУППИРОВАТЬ ПО
				|	НАЧАЛОПЕРИОДА(ДатаВыдачи, НЕДЕЛЯ),
				|	Партнер
				|	
				|ИНДЕКСИРОВАТЬ ПО
				|	ДатаВыдачи,
				|	Партнер
				|;
				////////////////////////////////////////////////////////////////////////////////
				//7	
				|ВЫБРАТЬ
				|	ПартнерыДляОбзвонка.ЗаявкаСервисногоЦентра,
				|	ПартнерыДляОбзвонка.Партнер,
				|	ПартнерыДляОбзвонка.ДатаВыдачи,
				|	ПартнерыДляОбзвонка.NPS,
				|	ПартнерыДляОбзвонка.ДатаЗвонка,
				|	ПартнерыДляОбзвонка.ОтложеннаяДатаЗвонка,
				|	ПартнерыДляОбзвонка.Номенклатура
				|ПОМЕСТИТЬ ВсеЗаявкиНужныеДляОбзвону
				|ИЗ
				|	ПартнерыДляОбзвонка КАК ПартнерыДляОбзвонка
				|		
				|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПервыеЗаявкиЗаНеделю КАК ПервыеЗаявкиЗаНеделю
				|ПО ПартнерыДляОбзвонка.Партнер = ПервыеЗаявкиЗаНеделю.Партнер
				|И ПартнерыДляОбзвонка.ДатаВыдачи = ПервыеЗаявкиЗаНеделю.ДатаВыдачи
				|
				|ГДЕ
				|	ПартнерыДляОбзвонка.NPS = Значение(Перечисление.ГНАТ_NPS.ПустаяСсылка)
				|	
				|;
				////////////////////////////////////////////////////////////////////////////////
				//8
				|ВЫБРАТЬ 
				|	ЗаявкаСервисногоЦентра КАК ЗаявкаСЦ,
				|	Партнер КАК Заказчик,
				|	ДатаЗвонка,
				|	ОтложеннаяДатаЗвонка,
				|	Номенклатура,
				|	ДатаВыдачи,
				|	ЗаявкаСервисногоЦентра.Номер КАК НомерЗСЦ,
				|	ЗаявкаСервисногоЦентра.НомерТелефона КАК Телефон,
				|	ЗаявкаСервисногоЦентра.ВидОперации КАК ТипЗСЦ,
				|   Партнер.Наименование КАК ПартнерПредставления,
				|   Номенклатура.Наименование КАК НоменклатураПредставления
				|ИЗ
				|	ВсеЗаявкиНужныеДляОбзвону
				|ГДЕ 
				|	ОтложеннаяДатаЗвонка <=&ТекущаяДата";
	Возврат ТекстЗапроса;
КонецФункции // ПолучитьТекстЗапросаЗаявокСЦ()


#КонецОбласти