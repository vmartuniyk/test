
#Область ПрограммныйИнтерфейс

Процедура ВыполнитьОбновлениеРентабельности(Период = Неопределено) Экспорт
	
	Если Период <> Неопределено Тогда
		НачалоПериода = НачалоМесяца(Период);
		КонецПериода = КонецМесяца(Период);
	Иначе
		ТекущаяДата = ТекущаяДатаСеанса();
		НачалоПериода = НачалоМесяца(ТекущаяДата);
		КонецПериода = КонецМесяца(ТекущаяДата);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаРентабельностиЗаявокСервисногоЦентра();
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ВидСвязиМеждуПоставщиками", НачалоПериода);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() = Ложь Тогда
		
		ТаблицаКЗаписи = РезультатЗапроса.Выгрузить();
		ТаблицаКЗаписи.Индексы.Добавить("Регистратор");
		ПараметрыПоиска = Новый Структура("Регистратор");
		
		ТаблицаОбхода = ТаблицаКЗаписи.Скопировать(, "Регистратор");
		ТаблицаОбхода.Свернуть("Регистратор");
		Для Каждого СтрокаОбхода Из ТаблицаОбхода Цикл
			
			ПараметрыПоиска.Регистратор = СтрокаОбхода.Регистратор;
			РезультатПоиска = ТаблицаКЗаписи.НайтиСтроки(ПараметрыПоиска);
			Если РезультатПоиска.Количество() Тогда
				
				НаборЗаписей = РегистрыНакопления.РентабельностьЗаявокСервисногоЦентра.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(СтрокаОбхода.Регистратор);

				Для Каждого Результат Из РезультатПоиска Цикл
					ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Результат);	
				КонецЦикла;
				
				НаборЗаписей.Записать();
				
			КонецЕсли;
						
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьОбновлениеРентабельности()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗапросаРентабельностиЗаявокСервисногоЦентра()
	
	ТекстЗапроса = "
		////////////////////////////////////////////////////////////////////////////////////////////
		// 0. Наростающие итоги по курсам валют
		|ВЫБРАТЬ
		|	Период КАК Период,
		|	ПроцентИнженера / 100 КАК ПроцентИнженера
		|ПОМЕСТИТЬ ПроцентыИнженера
		|ИЗ
		|  	РегистрСведений.ПроцентСервисногоИнженераПоЗаявкеСервисногоЦентра
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 1.
		|ВЫБРАТЬ
		|	ПроцентыИнженера.ПроцентИнженера КАК ПроцентИнженера,
		|	ПроцентыИнженера.Период КАК НачалоПериода,
		|  	МИНИМУМ(ЕСТЬNULL(ПроцентыИнженераКопия.Период, ДАТАВРЕМЯ(3999, 1, 1))) КАК КонецПериода
		|ПОМЕСТИТЬ ТаблицаПроцентовИнженера
		|ИЗ
		|  	ПроцентыИнженера КАК ПроцентыИнженера
		|		
		|ЛЕВОЕ СОЕДИНЕНИЕ ПроцентыИнженера КАК ПроцентыИнженераКопия
		|ПО ПроцентыИнженераКопия.Период > ПроцентыИнженера.Период
		|
		|СГРУППИРОВАТЬ ПО
		|  	ПроцентыИнженера.Период,
		|  	ПроцентыИнженера.ПроцентИнженера
		|
		|ИНДЕКСИРОВАТЬ ПО 
		|	НачалоПериода,
		|	КонецПериода	
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПроцентыИнженера;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 3.
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(КурсыВалют.Курс / КурсыВалют.Кратность КАК ЧИСЛО(15, 4)) КАК Курс,
		|	КурсыВалют.Валюта КАК Валюта,
		|	КурсыВалют.Период КАК Период
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют КАК КурсыВалют
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 4.
		|ВЫБРАТЬ
		|	КурсыВалют.Курс,
		|	КурсыВалют.Валюта,
		|	КурсыВалют.Период КАК НачалоПериода,
		|	МИНИМУМ(ЕСТЬNULL(КурсыВалютКопия.Период, ДАТАВРЕМЯ(3999, 1, 1))) КАК КонецПериода
		|ПОМЕСТИТЬ ТаблицаКурсов
		|ИЗ
		|	КурсыВалют КАК КурсыВалют
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютКопия
		|		ПО (КурсыВалютКопия.Период > КурсыВалют.Период)
		|			И (КурсыВалютКопия.Валюта = КурсыВалют.Валюта)
		|
		|СГРУППИРОВАТЬ ПО
		|	КурсыВалют.Период,
		|	КурсыВалют.Курс,
		|	КурсыВалют.Валюта
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КурсыВалют.Валюта
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КурсыВалют;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 6. Удаленные заявки в выборку не попадают
		|ВЫБРАТЬ 
		|	*
		|ПОМЕСТИТЬ ЗаявкиСервисногоЦентраВПериоде
		|ИЗ
		|	РегистрСведений.ГНАТ_ЭтапыРаботЗаявокСервисногоЦентра.СрезПоследних КАК ЭтапыРабот
		|ГДЕ
		|	ЭтапыРабот.Период >= &НачалоПериода
		|И	ЭтапыРабот.Период <= &КонецПериода
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаявкаСервисногоЦентра	
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 7. Получаем партнеров, по которым не считаем рентабельность документа "Заяка сервисного центра"
		|ВЫБРАТЬ
		|	Партнер
		|ПОМЕСТИТЬ КешВендоры
		|ИЗ
		|	РегистрСведений.ГНАТ_ВендорыСервисногоЦентра
		|ГДЕ
		|	УчитыватьПриРасчетеРентабельности = Ложь
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	Партнер	
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 8. Получаем список документов продаж по документам "Заявка сервисного центра"
		|ВЫБРАТЬ
		|	ЗаявкиСервисногоЦентраВПериоде.ЗаявкаСервисногоЦентра КАК ЗаявкаСервисногоЦентра,
		|	РТиУ.Дата КАК Период,
		|	РТиУ.Ссылка	КАК Регистратор,
		|	РТиУ.ВидПодчиненногоДокументаГНАТ КАК ВидПодчиненногоДокументаГНАТ,
		|	Сумма(IsNull(ВИСП.СуммаВыручки, 0)) 	КАК ВыручкаUSD,
		|	Сумма(IsNull(ВИСП.СуммаВыручкиРегл, 0))	КАК ВыручкаUAH,
		|	ВЫБОР
		|		КОГДА IsNull(РТиУ.Соглашение.ИспользуютсяДоговорыКонтрагентов, Ложь) = Истина
		|		ТОГДА 	ВЫБОР
		|					КОГДА РТиУ.Договор.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
		|					ТОГДА РТиУ.Ссылка
		|					КОГДА РТиУ.Соглашение.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным)
		|					ТОГДА  	ВЫБОР
		|								КОГДА РТиУ.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
		|					 			 ИЛИ  РТиУ.ЗаказКлиента = Неопределено
		|								ТОГДА РТиУ.Ссылка
		|								ИНАЧЕ РТиУ.ЗаказКлиента
		|							КОНЕЦ
		|					ИНАЧЕ РТиУ.Договор
		|				КОНЕЦ 
		|		КОГДА РТиУ.Соглашение.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
		|		ТОГДА РТиУ.Ссылка
		|		КОГДА РТиУ.Соглашение.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным)
		|		ТОГДА 	ВЫБОР
		|					КОГДА РТиУ.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
		|					 ИЛИ  РТиУ.ЗаказКлиента = Неопределено
		|					ТОГДА РТиУ.Ссылка
		|					ИНАЧЕ РТиУ.ЗаказКлиента
		|				КОНЕЦ
		|	КОНЕЦ КАК ЗаказКлиента
		|	 
		|ПОМЕСТИТЬ КешДокументовПродаж
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РТиУ
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаявкиСервисногоЦентраВПериоде КАК ЗаявкиСервисногоЦентраВПериоде
		|ПО	ЗаявкиСервисногоЦентраВПериоде.ЗаявкаСервисногоЦентра = РТиУ.ЗаявкаСервисногоЦентра
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВИСП
		|ПО ВЫРАЗИТЬ(ВИСП.Регистратор КАК Документ.РеализацияТоваровУслуг) 	= РТиУ.Ссылка
		|И	ВИСП.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры 	= ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|
		|ГДЕ
		|	РТиУ.Проведен 		 = Истина
		|И	РТиУ.ПометкаУдаления = Ложь
		|И	РТиУ.Партнер НЕ В (ВЫБРАТЬ Партнер ИЗ КешВендоры)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаявкиСервисногоЦентраВПериоде.ЗаявкаСервисногоЦентра,
		|	РТиУ.Ссылка	
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказКлиента		
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешВендоры;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 10.
		|ВЫБРАТЬ
		|	Модуль_ДоступныеСклады.Партнер КАК Партнер
		|ПОМЕСТИТЬ ПарнерыНашегоПредприятия
		|ИЗ
		|	РегистрСведений.Модуль_ДоступныеСклады КАК Модуль_ДоступныеСклады
		|	
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Партнер
		|; 
		////////////////////////////////////////////////////////////////////////////////////////////
		// 11.  		
		////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВозвратТоваровПоставщику.Дата КАК Период,
		|	ВозвратТоваровПоставщику.Ссылка КАК Регистратор,
		|	ВозвратТоваровПоставщику.Валюта,
		|	ВозвратТоваровПоставщику.Партнер,
		|	ВозвратТоваровПоставщику.СуммаДокумента КАК Остаток,
		|	ВозвратТоваровПоставщику.Валюта,
		|	КешЗСЦ.ЗаявкаСервисногоЦентра
		|ПОМЕСТИТЬ КешВозвратПоставщика
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаявкиСервисногоЦентраВПериоде КАК КешЗСЦ
		|ПО ВозвратТоваровПоставщику.ЗаявкаСервисногоЦентра = КешЗСЦ.ЗаявкаСервисногоЦентра
		|
		|ГДЕ
		|	ВозвратТоваровПоставщику.Проведен = ИСТИНА
		|И 	ВозвратТоваровПоставщику.ПометкаУдаления = ЛОЖЬ
		|И  ВозвратТоваровПоставщику.Партнер НЕ В
		|				(ВЫБРАТЬ
		|					ПарнерыНашегоПредприятия.Партнер
		|				ИЗ
		|					ПарнерыНашегоПредприятия)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПарнерыНашегоПредприятия;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 13.
		|ВЫБРАТЬ
		|	КешВозвратПоставщика.Регистратор,
		|	КешВозвратПоставщика.Партнер КАК Партнер,
		|	КешВозвратПоставщика.Период,
		|	КешВозвратПоставщика.Остаток,
		|	КешВозвратПоставщика.Валюта,
		|	КешВозвратПоставщика.ЗаявкаСервисногоЦентра,
		|	АУНоменклатуры.Номенклатура КАК Номенклатура,
		|	СебестоимостьТоваровОбороты.СтоимостьОборот,
		|	СебестоимостьТоваровОбороты.СтоимостьРеглОборот
		|ПОМЕСТИТЬ АУПоНоменклатуреВозвратаПоставщика
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров.Обороты(, , Регистратор, ) КАК СебестоимостьТоваровОбороты
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АУНоменклатуры
		|ПО СебестоимостьТоваровОбороты.АналитикаУчетаНоменклатуры = АУНоменклатуры.КлючАналитики
		|	
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ КешВозвратПоставщика КАК КешВозвратПоставщика
		|ПО (КешВозвратПоставщика.Регистратор = (ВЫРАЗИТЬ(СебестоимостьТоваровОбороты.Регистратор КАК Документ.ВозвратТоваровПоставщику)))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Партнер,
		|	Номенклатура
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 14.
		|ВЫБРАТЬ
		|	АУПоНоменклатуреВозвратаПоставщика.Регистратор КАК Регистратор,
		|	АУПоНоменклатуреВозвратаПоставщика.Партнер КАК Партнер,
		|	АУПоНоменклатуреВозвратаПоставщика.Период,
		|	АУПоНоменклатуреВозвратаПоставщика.ЗаявкаСервисногоЦентра,
		|	АУПоНоменклатуреВозвратаПоставщика.Номенклатура,
		|	АУПоНоменклатуреВозвратаПоставщика.Партнер КАК СвязаныйПартнер
		|ПОМЕСТИТЬ КешСвязаныеПоставщикиПоЗакупке
		|ИЗ
		|	АУПоНоменклатуреВозвратаПоставщика КАК АУПоНоменклатуреВозвратаПоставщика
		|
		|ОБЪЕДИНИТЬ 
		|
		|ВЫБРАТЬ
		|	АУПоНоменклатуреВозвратаПоставщика.Регистратор,
		|	СвязиМеждуПартнерами.ПервыйПартнер,
		|	АУПоНоменклатуреВозвратаПоставщика.Период,
		|	АУПоНоменклатуреВозвратаПоставщика.ЗаявкаСервисногоЦентра,
		|	АУПоНоменклатуреВозвратаПоставщика.Номенклатура,
		|	СвязиМеждуПартнерами.ВторойПартнер
		|ИЗ
		|	АУПоНоменклатуреВозвратаПоставщика КАК АУПоНоменклатуреВозвратаПоставщика
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СвязиМеждуПартнерами КАК СвязиМеждуПартнерами
		|ПО АУПоНоменклатуреВозвратаПоставщика.Партнер = СвязиМеждуПартнерами.ПервыйПартнер
		|И (СвязиМеждуПартнерами.ВидСвязи = &ВидСвязиМеждуПоставщиками)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Партнер,
		|	Регистратор,
		|	СвязаныйПартнер
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 15.
		|ВЫБРАТЬ
		|	КешСвязаныеПоставщикиПоЗакупке.Регистратор,
		|	КешСвязаныеПоставщикиПоЗакупке.Партнер,
		|	КешСвязаныеПоставщикиПоЗакупке.Период,
		|	КешСвязаныеПоставщикиПоЗакупке.ЗаявкаСервисногоЦентра,
		|	МАКСИМУМ(ЕСТЬNULL(СебестоимостьТоваровОбороты.Период, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))) КАК ПериодЗакупка,
		|	КешСвязаныеПоставщикиПоЗакупке.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ СрезПоДатеЗакупкаПоВозвратамПоставщика
		|ИЗ
		|	КешСвязаныеПоставщикиПоЗакупке КАК КешСвязаныеПоставщикиПоЗакупке
		|		
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров.Обороты(
		|				,
		|				,
		|				Регистратор,
		|				АналитикаУчетаНоменклатуры.Номенклатура В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						АУПоНоменклатуре.Номенклатура
		|					ИЗ
		|						АУПоНоменклатуреВозвратаПоставщика КАК АУПоНоменклатуре)) КАК СебестоимостьТоваровОбороты
		|ПО КешСвязаныеПоставщикиПоЗакупке.Номенклатура = СебестоимостьТоваровОбороты.АналитикаУчетаНоменклатуры.Номенклатура
		|	И (СебестоимостьТоваровОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг)
		|	И (СебестоимостьТоваровОбороты.Период < КешСвязаныеПоставщикиПоЗакупке.Период)
		|	И (КешСвязаныеПоставщикиПоЗакупке.СвязаныйПартнер = ВЫРАЗИТЬ(СебестоимостьТоваровОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Партнер)
		|
		|СГРУППИРОВАТЬ ПО
		|	КешСвязаныеПоставщикиПоЗакупке.Регистратор,
		|	КешСвязаныеПоставщикиПоЗакупке.Партнер,
		|	КешСвязаныеПоставщикиПоЗакупке.Период,
		|	КешСвязаныеПоставщикиПоЗакупке.ЗаявкаСервисногоЦентра,
		|	КешСвязаныеПоставщикиПоЗакупке.Номенклатура

		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	ПериодЗакупка
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 17.
		|ВЫБРАТЬ
		|	ЗакупкаПоВозвратамПоставщика.Регистратор,
		|	ЗакупкаПоВозвратамПоставщика.Партнер,
		|	ЗакупкаПоВозвратамПоставщика.Период,
		|	ЗакупкаПоВозвратамПоставщика.ЗаявкаСервисногоЦентра,
		|	СебестоимостьТоваровОбороты.Период КАК ПериодЗакупка,
		|	ЗакупкаПоВозвратамПоставщика.Номенклатура,
		|	ВЫРАЗИТЬ(СебестоимостьТоваровОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг) КАК ДокументПоступления,
		|	ЕСТЬNULL(СебестоимостьТоваровОбороты.СтоимостьОборот, 0) КАК Стоимость,
		|	ЕСТЬNULL(СебестоимостьТоваровОбороты.СтоимостьРеглОборот, 0) КАК СтоимостьРегл
		|ПОМЕСТИТЬ ЗакупкиПоПоставщикамВозвратПоставщика
		|ИЗ
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика КАК ЗакупкаПоВозвратамПоставщика
		|		
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров.Обороты(
		|				,
		|				,
		|				Регистратор,
		|				АналитикаУчетаНоменклатуры.Номенклатура В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						АУПоНоменклатуре.Номенклатура
		|					ИЗ
		|						СрезПоДатеЗакупкаПоВозвратамПоставщика КАК АУПоНоменклатуре)) КАК СебестоимостьТоваровОбороты
		|ПО (СебестоимостьТоваровОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг)
		|	И ЗакупкаПоВозвратамПоставщика.Номенклатура = СебестоимостьТоваровОбороты.АналитикаУчетаНоменклатуры.Номенклатура
		|	И ЗакупкаПоВозвратамПоставщика.ПериодЗакупка = СебестоимостьТоваровОбороты.Период
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		// 18.
		|ВЫБРАТЬ
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика.Регистратор,
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика.Партнер,
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика.Период,
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика.ЗаявкаСервисногоЦентра,
		|	МАКСИМУМ(ЕСТЬNULL(СебестоимостьТоваровОбороты.Период, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))) КАК ПериодЗакупка,
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика.Номенклатура
		|ПОМЕСТИТЬ СрезПоДатеЗакупкаБезПоставщикаПоВозвратамПоставщика
		|ИЗ
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика КАК СрезПоДатеЗакупкаПоВозвратамПоставщика
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров.Обороты(
		|				,
		|				,
		|				Регистратор,
		|				АналитикаУчетаНоменклатуры.Номенклатура В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						АУПоНоменклатуреВозвратаПоставщика.Номенклатура
		|					ИЗ
		|						АУПоНоменклатуреВозвратаПоставщика КАК АУПоНоменклатуреВозвратаПоставщика)) КАК СебестоимостьТоваровОбороты
		|ПО СрезПоДатеЗакупкаПоВозвратамПоставщика.Номенклатура = СебестоимостьТоваровОбороты.АналитикаУчетаНоменклатуры.Номенклатура
		|	И (СебестоимостьТоваровОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг)
		|	И (СебестоимостьТоваровОбороты.Период < СрезПоДатеЗакупкаПоВозвратамПоставщика.Период)
		|
		|ГДЕ
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика.ПериодЗакупка = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|
		|СГРУППИРОВАТЬ ПО
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика.Регистратор,
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика.Партнер,
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика.Период,
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика.ЗаявкаСервисногоЦентра,
		|	СрезПоДатеЗакупкаПоВозвратамПоставщика.Номенклатура
		|;   
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СрезПоДатеЗакупкаПоВозвратамПоставщика; 
		////////////////////////////////////////////////////////////////////////////////////////////
		// 20.
		|ВЫБРАТЬ
		|	ЗакупкаПоставщика.Регистратор,
		|	ЗакупкаПоставщика.Партнер,
		|	ЗакупкаПоставщика.Период,
		|	ЗакупкаПоставщика.ЗаявкаСервисногоЦентра,
		|	СебестоимостьТоваровОбороты.Период КАК ПериодЗакупка,
		|	ЗакупкаПоставщика.Номенклатура,
		|	ВЫРАЗИТЬ(СебестоимостьТоваровОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг) КАК ДокументПоступления,
		|	ЕСТЬNULL(СебестоимостьТоваровОбороты.СтоимостьОборот, 0) КАК Стоимость,
		|	ЕСТЬNULL(СебестоимостьТоваровОбороты.СтоимостьРеглОборот, 0) КАК СтоимостьРегл
		|ПОМЕСТИТЬ ЗакупкиПоДругимПоставщикамВозвратПоставщика
		|ИЗ
		|	СрезПоДатеЗакупкаБезПоставщикаПоВозвратамПоставщика КАК ЗакупкаПоставщика
		|
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров.Обороты(
		|				,
		|				,
		|				Регистратор,
		|				АналитикаУчетаНоменклатуры.Номенклатура В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						АУПоНоменклатуреВозвратаПоставщика.Номенклатура
		|					ИЗ
		|						АУПоНоменклатуреВозвратаПоставщика КАК АУПоНоменклатуреВозвратаПоставщика)) КАК СебестоимостьТоваровОбороты
		|ПО (СебестоимостьТоваровОбороты.АналитикаУчетаНоменклатуры.Номенклатура = ЗакупкаПоставщика.Номенклатура)
		|	И (СебестоимостьТоваровОбороты.Период = ЗакупкаПоставщика.Период)
		|	И (СебестоимостьТоваровОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг)
		|	И (ВЫРАЗИТЬ(СебестоимостьТоваровОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Партнер = ЗакупкаПоставщика.Партнер)
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СрезПоДатеЗакупкаБезПоставщикаПоВозвратамПоставщика;  
		////////////////////////////////////////////////////////////////////////////////////////////
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ АУПоНоменклатуреВозвратаПоставщика; 
		// 23.
		|ВЫБРАТЬ
		|	ЗакупкиПоПоставщикамВозвратПоставщика.Регистратор,
		|	ЗакупкиПоПоставщикамВозвратПоставщика.Партнер,
		|	ЗакупкиПоПоставщикамВозвратПоставщика.Период,
		|	ЗакупкиПоПоставщикамВозвратПоставщика.ЗаявкаСервисногоЦентра,
		|	ЗакупкиПоПоставщикамВозвратПоставщика.ПериодЗакупка,
		|	ЗакупкиПоПоставщикамВозвратПоставщика.Номенклатура,
		|	ЗакупкиПоПоставщикамВозвратПоставщика.ДокументПоступления,
		|	ЗакупкиПоПоставщикамВозвратПоставщика.Стоимость,
		|	ЗакупкиПоПоставщикамВозвратПоставщика.СтоимостьРегл
		|ПОМЕСТИТЬ ЗакупкаТовараПоДокументуВозвратаПоставщика
		|ИЗ
		|	ЗакупкиПоПоставщикамВозвратПоставщика КАК ЗакупкиПоПоставщикамВозвратПоставщика
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ЗакупкиПоДругимПоставщикамВозвратПоставщика.Регистратор,
		|	ЗакупкиПоДругимПоставщикамВозвратПоставщика.Партнер,
		|	ЗакупкиПоДругимПоставщикамВозвратПоставщика.Период,
		|	ЗакупкиПоДругимПоставщикамВозвратПоставщика.ЗаявкаСервисногоЦентра,
		|	ЗакупкиПоДругимПоставщикамВозвратПоставщика.ПериодЗакупка,
		|	ЗакупкиПоДругимПоставщикамВозвратПоставщика.Номенклатура,
		|	ЗакупкиПоДругимПоставщикамВозвратПоставщика.ДокументПоступления,
		|	ЗакупкиПоДругимПоставщикамВозвратПоставщика.Стоимость,
		|	ЗакупкиПоДругимПоставщикамВозвратПоставщика.СтоимостьРегл
		|ИЗ
		|	ЗакупкиПоДругимПоставщикамВозвратПоставщика КАК ЗакупкиПоДругимПоставщикамВозвратПоставщика
		|; 
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЗакупкиПоПоставщикамВозвратПоставщика;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЗакупкиПоДругимПоставщикамВозвратПоставщика; 
		////////////////////////////////////////////////////////////////////////////////////////////
		// 26.
		|ВЫБРАТЬ
		|	ДокументыПродаж.ЗаявкаСервисногоЦентра КАК ЗаявкаСервисногоЦентра,
		|	ДокументыПродаж.Регистратор КАК Регистратор,
		|	ДокументыПродаж.Период КАК Период,
		|	IsNull(ВИСП.СуммаВыручкиОборот, 0)		КАК ВыручкаUSD,
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		ИНАЧЕ IsNull(ВИСП.СебестоимостьОборот, 0)
		|	КОНЕЦ 									КАК СебестоимостьUSD,
		|	IsNull(ВИСП.СуммаВыручкиРеглОборот, 0)	КАК ВыручкаUAH,
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		ИНАЧЕ IsNull(ВИСП.СебестоимостьРеглОборот, 0)
		|	КОНЕЦ КАК СебестоимостьUAH
	//|ПОМЕСТИТЬ ОплаченныеДокументыПоЗаказамНакладным
		|ИЗ
		|	КешДокументовПродаж	КАК ДокументыПродаж
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами.Остатки(, ЗаказКлиента В (	ВЫБРАТЬ 
		|																					ЗаказКлиента 
		|																				ИЗ 
		|																					КешДокументовПродаж
		|																				ГДЕ
		|																					ЗаказКлиента Ссылка Документ.РеализацияТоваровУслуг)) КАК РасчетыСКлиентами
		|ПО	РасчетыСКлиентами.ЗаказКлиента = ДокументыПродаж.ЗаказКлиента
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(, , , ЗаказКлиента В (	ВЫБРАТЬ 
		|																					ЗаказКлиента 
		|																				ИЗ 
		|																					КешДокументовПродаж
		|																				ГДЕ
		|																					ЗаказКлиента Ссылка Документ.РеализацияТоваровУслуг)) КАК ВИСП
		|ПО	ВЫРАЗИТЬ(ВИСП.ЗаказКлиента КАК Документ.РеализацияТоваровУслуг) = ВЫРАЗИТЬ(ДокументыПродаж.ЗаказКлиента КАК Документ.РеализацияТоваровУслуг)	 
		|
		|ГДЕ
		|	ДокументыПродаж.ЗаказКлиента Ссылка Документ.РеализацияТоваровУслуг
		|И	IsNull(РасчетыСКлиентами.КОплатеОстаток, 0) <= 0	
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПродаж.ЗаявкаСервисногоЦентра КАК ЗаявкаСервисногоЦентра,
		|	ДокументыПродаж.Регистратор КАК Регистратор,
		|	ДокументыПродаж.Период КАК Период,
		|	0 КАК ВыручкаUSD,
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.НаВиновноеЛицо)
		|		ТОГДА 0
		|		ИНАЧЕ ДокументыПродаж.ВыручкаUSD * IsNull(ТаблицаПроцентовИнженера.ПроцентИнженера, 0)
		|	КОНЕЦ 									КАК СебестоимостьUSD,
		|	0 КАК ВыручкаUAH,
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.НаВиновноеЛицо)
		|		ТОГДА 0
		|		ИНАЧЕ ДокументыПродаж.ВыручкаUAH * IsNull(ТаблицаПроцентовИнженера.ПроцентИнженера, 0)
		|	КОНЕЦ КАК СебестоимостьUAH
		|ИЗ
		|	КешДокументовПродаж	КАК ДокументыПродаж
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами.Остатки(, ЗаказКлиента В (	ВЫБРАТЬ 
		|																					ЗаказКлиента 
		|																				ИЗ 
		|																					КешДокументовПродаж
		|																				ГДЕ
		|																					ЗаказКлиента Ссылка Документ.РеализацияТоваровУслуг)) КАК РасчетыСКлиентами
		|ПО	РасчетыСКлиентами.ЗаказКлиента = ДокументыПродаж.ЗаказКлиента 
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПроцентовИнженера КАК ТаблицаПроцентовИнженера 
		|ПО	ТаблицаПроцентовИнженера.НачалоПериода <= ДокументыПродаж.Период	
		|И	ТаблицаПроцентовИнженера.КонецПериода  >  ДокументыПродаж.Период
		|
		|ГДЕ
		|	ДокументыПродаж.ЗаказКлиента Ссылка Документ.РеализацияТоваровУслуг
		|И	IsNull(РасчетыСКлиентами.КОплатеОстаток, 0) <= 0	
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПродаж.ЗаявкаСервисногоЦентра,
		|	ДокументыПродаж.Регистратор,
		|	ДокументыПродаж.Период,
		|	IsNull(ВИСП.СуммаВыручкиОборот, 0),
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		ИНАЧЕ IsNull(ВИСП.СебестоимостьОборот, 0)
		|	КОНЕЦ,
		|	IsNull(ВИСП.СуммаВыручкиРеглОборот, 0),
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		ИНАЧЕ IsNull(ВИСП.СебестоимостьРеглОборот, 0)
		|	КОНЕЦ
		|ИЗ
		|	КешДокументовПродаж	КАК ДокументыПродаж
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами.Остатки(, ЗаказКлиента В (	ВЫБРАТЬ 
		|																					ЗаказКлиента 
		|																				ИЗ 
		|																					КешДокументовПродаж
		|																				ГДЕ
		|																					ЗаказКлиента Ссылка Документ.ЗаказКлиента)) КАК РасчетыСКлиентами
		|ПО	РасчетыСКлиентами.ЗаказКлиента = ДокументыПродаж.ЗаказКлиента	 
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(, , , ЗаказКлиента В (	ВЫБРАТЬ 
		|																					ЗаказКлиента 
		|																				ИЗ 
		|																					КешДокументовПродаж
		|																				ГДЕ
		|																					ЗаказКлиента Ссылка Документ.ЗаказКлиента)) КАК ВИСП
		|ПО	ВЫРАЗИТЬ(ВИСП.ЗаказКлиента КАК Документ.ЗаказКлиента) = ВЫРАЗИТЬ(ДокументыПродаж.ЗаказКлиента КАК Документ.ЗаказКлиента)
		|
		|ГДЕ
		|	ДокументыПродаж.ЗаказКлиента Ссылка Документ.ЗаказКлиента
		|И	IsNull(РасчетыСКлиентами.КОплатеОстаток, 0) <= 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПродаж.ЗаявкаСервисногоЦентра,
		|	ДокументыПродаж.Регистратор,
		|	ДокументыПродаж.Период,
		|	0,
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.НаВиновноеЛицо)
		|		ТОГДА 0
		|		ИНАЧЕ ДокументыПродаж.ВыручкаUSD * IsNull(ТаблицаПроцентовИнженера.ПроцентИнженера, 0)
		|	КОНЕЦ,
		|	0,
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.НаВиновноеЛицо)
		|		ТОГДА 0
		|		ИНАЧЕ ДокументыПродаж.ВыручкаUAH * IsNull(ТаблицаПроцентовИнженера.ПроцентИнженера, 0)
		|	КОНЕЦ
		|ИЗ
		|	КешДокументовПродаж	КАК ДокументыПродаж
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами.Остатки(, ЗаказКлиента В (	ВЫБРАТЬ 
		|																					ЗаказКлиента 
		|																				ИЗ 
		|																					КешДокументовПродаж
		|																				ГДЕ
		|																					ЗаказКлиента Ссылка Документ.ЗаказКлиента)) КАК РасчетыСКлиентами
		|ПО	РасчетыСКлиентами.ЗаказКлиента = ДокументыПродаж.ЗаказКлиента	 
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПроцентовИнженера КАК ТаблицаПроцентовИнженера 
		|ПО	ТаблицаПроцентовИнженера.НачалоПериода <= ДокументыПродаж.Период	
		|И	ТаблицаПроцентовИнженера.КонецПериода  >  ДокументыПродаж.Период
		|
		|ГДЕ
		|	ДокументыПродаж.ЗаказКлиента Ссылка Документ.ЗаказКлиента
		|И	IsNull(РасчетыСКлиентами.КОплатеОстаток, 0) <= 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПродаж.ЗаявкаСервисногоЦентра,
		|	ДокументыПродаж.Регистратор,
		|	ДокументыПродаж.Период,
		|	IsNull(ВИСП.СуммаВыручкиОборот, 0),
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		ИНАЧЕ IsNull(ВИСП.СебестоимостьОборот, 0)
		|	КОНЕЦ,
		|	IsNull(ВИСП.СуммаВыручкиРеглОборот, 0),
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		ИНАЧЕ IsNull(ВИСП.СебестоимостьРеглОборот, 0)
		|	КОНЕЦ
		|ИЗ
		|	КешДокументовПродаж	КАК ДокументыПродаж
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КТС_ГрафикПлатежейПартнеров.СрезПоследних(, Документ В (ВЫБРАТЬ 
		|																							Регистратор 
		|																						ИЗ 
		|																							КешДокументовПродаж
		|																						ГДЕ
		|																							ЗаказКлиента Ссылка Справочник.ДоговорыКонтрагентов)
		|																			) КАК ГрафикПлатежей
		|ПО	ГрафикПлатежей.Документ = ДокументыПродаж.Регистратор 		 
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(, , , ЗаказКлиента В (	ВЫБРАТЬ 
		|																					Регистратор 
		|																				ИЗ 
		|																					КешДокументовПродаж
		|																				ГДЕ
		|																					ЗаказКлиента Ссылка Справочник.ДоговорыКонтрагентов)) КАК ВИСП
		|ПО	ВЫРАЗИТЬ(ВИСП.ЗаказКлиента КАК Документ.РеализацияТоваровУслуг) = ВЫРАЗИТЬ(ДокументыПродаж.Регистратор КАК Документ.РеализацияТоваровУслуг)
		|
		|ГДЕ
		|	ДокументыПродаж.ЗаказКлиента Ссылка Справочник.ДоговорыКонтрагентов
		|И	IsNull(ГрафикПлатежей.СуммаОстаток, 0) <= 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПродаж.ЗаявкаСервисногоЦентра,
		|	ДокументыПродаж.Регистратор,
		|	ДокументыПродаж.Период,
		|	0,
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.НаВиновноеЛицо)
		|		ТОГДА 0
		|		ИНАЧЕ ДокументыПродаж.ВыручкаUSD * IsNull(ТаблицаПроцентовИнженера.ПроцентИнженера, 0)
		|	КОНЕЦ,
		|	0,
		|	ВЫБОР
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.ПодменныйФонд)
		|		ТОГДА 0
		|		КОГДА ДокументыПродаж.ВидПодчиненногоДокументаГНАТ = ЗНАЧЕНИЕ(Перечисление.ГНАТ_ВидыПодчиненныхДокументов.НаВиновноеЛицо)
		|		ТОГДА 0
		|		ИНАЧЕ ДокументыПродаж.ВыручкаUAH * IsNull(ТаблицаПроцентовИнженера.ПроцентИнженера, 0)
		|	КОНЕЦ
		|ИЗ
		|	КешДокументовПродаж	КАК ДокументыПродаж
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КТС_ГрафикПлатежейПартнеров.СрезПоследних(, Документ В (ВЫБРАТЬ 
		|																							Регистратор 
		|																						ИЗ 
		|																							КешДокументовПродаж
		|																						ГДЕ
		|																							ЗаказКлиента Ссылка Справочник.ДоговорыКонтрагентов)
		|																			) КАК ГрафикПлатежей
		|ПО	ГрафикПлатежей.Документ = ДокументыПродаж.Регистратор 		 
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПроцентовИнженера КАК ТаблицаПроцентовИнженера 
		|ПО	ТаблицаПроцентовИнженера.НачалоПериода <= ДокументыПродаж.Период	
		|И	ТаблицаПроцентовИнженера.КонецПериода  >  ДокументыПродаж.Период
		|
		|ГДЕ
		|	ДокументыПродаж.ЗаказКлиента Ссылка Справочник.ДоговорыКонтрагентов
		|И	IsNull(ГрафикПлатежей.СуммаОстаток, 0) <= 0
		|                    
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КешВозвратПоставщика.ЗаявкаСервисногоЦентра,
		|	КешВозвратПоставщика.Регистратор,  
		|	КешВозвратПоставщика.Период,   	
		|	ВЫБОР
		|		КОГДА &ВалютаУправленческогоУчета = КешВозвратПоставщика.Валюта
		|			ТОГДА ЕСТЬNULL(КешВозвратПоставщика.Остаток, 0)
		|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(КешВозвратПоставщика.Остаток, 0) * ЕСТЬNULL(ТаблицаКурсов.Курс, 0) / ЕСТЬNULL(ТаблицаКурсовУпр.Курс, 1) КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК ВыручкаUSD,
		|	ЕСТЬNULL(ЗакупкаТовараПоДокументуВозвратаПоставщика.Стоимость, 0) КАК СебестоимостьUSD,  
		|	ВЫБОР
		|		КОГДА &ВалютаРеглУчета = КешВозвратПоставщика.Валюта
		|			ТОГДА ЕСТЬNULL(КешВозвратПоставщика.Остаток, 0)
		|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(КешВозвратПоставщика.Остаток, 0) * ЕСТЬNULL(ТаблицаКурсов.Курс, 0) / ЕСТЬNULL(ТаблицаКурсовРегл.Курс, 1) КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК ВыручкаUAH,  		
		|	ВЫРАЗИТЬ(ЕСТЬNULL(ЗакупкаТовараПоДокументуВозвратаПоставщика.Стоимость,0) * ЕСТЬNULL(ТаблицаКурсовУпр.Курс,0) / ЕСТЬNULL(ТаблицаКурсовРегл.Курс,1) КАК ЧИСЛО(15, 2)) КАК СебестоимостьUAH
		|ИЗ
		|	КешВозвратПоставщика КАК КешВозвратПоставщика
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсов КАК ТаблицаКурсов
		|ПО КешВозвратПоставщика.Валюта = ТаблицаКурсов.Валюта
		|	И КешВозвратПоставщика.Период < ТаблицаКурсов.КонецПериода
		|	И КешВозвратПоставщика.Период >= ТаблицаКурсов.НачалоПериода
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсов КАК ТаблицаКурсовУпр
		|ПО КешВозвратПоставщика.Период >= ТаблицаКурсовУпр.НачалоПериода
		|	И КешВозвратПоставщика.Период < ТаблицаКурсовУпр.КонецПериода
		|	И (ТаблицаКурсовУпр.Валюта = &ВалютаУправленческогоУчета)
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсов КАК ТаблицаКурсовРегл
		|ПО КешВозвратПоставщика.Период >= ТаблицаКурсовРегл.НачалоПериода
		|	И КешВозвратПоставщика.Период < ТаблицаКурсовРегл.КонецПериода
		|	И (ТаблицаКурсовРегл.Валюта = &ВалютаРеглУчета)
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ ЗакупкаТовараПоДокументуВозвратаПоставщика КАК ЗакупкаТовараПоДокументуВозвратаПоставщика
		|	ПО КешВозвратПоставщика.Регистратор = ЗакупкаТовараПоДокументуВозвратаПоставщика.Регистратор
		|	И КешВозвратПоставщика.ЗаявкаСервисногоЦентра = ЗакупкаТовараПоДокументуВозвратаПоставщика.ЗаявкаСервисногоЦентра 
		|
		|;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаПроцентовИнженера;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешДокументовПродаж;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЗаявкиСервисногоЦентраВПериоде;
		////////////////////////////////////////////////////////////////////////////////////////////
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЗакупкаТовараПоДокументуВозвратаПоставщика;
		////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КешВозвратПоставщика; 		
		////////////////////////////////////////////////////////////////////////////////////////////   
		|";
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапросаРентабельностиЗаявокСервисногоЦентра()

#КонецОбласти

#Область  СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	
	Версия = "0.0.2";
	
	// Объявим переменную, в которой мы сохраним и вернем "наружу" необходимые данные
    ПараметрыРегистрации = Новый Структура;

    // Объявим еще одну переменную, которая нам потребуется ниже
    МассивНазначений = Новый Массив;
    
    // Первый параметр, который мы должны указать - это какой вид обработки системе должна зарегистрировать. 
    // Допустимые типы: ДополнительнаяОбработка, ДополнительныйОтчет, ЗаполнениеОбъекта, Отчет, ПечатнаяФорма, СозданиеСвязанныхОбъектов
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");

    // Теперь нам необходимо передать в виде массива имен, к чему будет подключена наша ВПФ
    // Имейте ввиду, что можно задать имя в таком виде: Документ.* - в этом случае обработка будет подключена ко всем документам в системе, 
    // которые поддерживают механизм ВПФ
	//СчитатьДокументыНазначений(МассивНазначений);
	
	
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
    
    // Теперь зададим имя, под которым ВПФ будет зарегистрирована в справочнике внешних обработок
    ПараметрыРегистрации.Вставить("Наименование", "Регл. обновление рентабельности ЗСЦ");
    
    // Зададим право обработке на использование безопасного режима. Более подробно можно узнать в справке к платформе (метод УстановитьБезопасныйРежим)
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);

    // Следующие два параметра играют больше информационную роль, т.е. это то, что будет видеть пользователь в информации к обработке
    ПараметрыРегистрации.Вставить("Версия", Версия);    
    ПараметрыРегистрации.Вставить("Информация", "Обработка используется для регламентного обновление рентабельности ЗСЦ [" + Версия + "]");
    
    // Создадим таблицу команд (подробнее смотрим ниже)
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    
    // Добавим команду в таблицу
    ДобавитьКоманду(ТаблицаКоманд, "Форма обновления рентабельности ЗСЦ [" + Версия + "]", "РЗСЦ", "ОткрытиеФормы", Ложь, "РЗСЦ");
	ДобавитьКоманду(ТаблицаКоманд, "Регл. обновление рентабельности ЗСЦ [" + Версия + "]", "ВыполнитьОбновлениеРентабельности();", "ВызовСерверногоМетода");
    
    // Сохраним таблицу команд в параметры регистрации обработки
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
    
    // Теперь вернем системе наши параметры
    Возврат ПараметрыРегистрации;
КонецФункции

Функция ПолучитьТаблицуКоманд()

   // Создадим пустую таблицу команд и колонки в ней
   Команды = Новый ТаблицаЗначений;

   // Как будет выглядеть описание печатной формы для пользователя
   Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 

   // Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
   Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

   // Тут задается, как должна вызваться команда обработки
   // Возможные варианты:
   // - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
   // - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
   // - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
   Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));

   // Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
   Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));

   // Для печатной формы должен содержать строку ПечатьMXL 
   Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
   
   Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
  // Добавляем команду в таблицу команд по переданному описанию.
  // Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

Процедура ВыполнитьКоманду(Индентификатор) Экспорт
	Выполнить(Индентификатор);	
КонецПроцедуры

#КонецОбласти
