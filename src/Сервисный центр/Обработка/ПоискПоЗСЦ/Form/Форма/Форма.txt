#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    Документы.ГНАТ_ЗаявкаСервисногоЦентра.УсловноеОформленияЗаявокСЦ(ЭтаФорма);   
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаЗаявкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    	ТД = Элемент.ТекущиеДанные;
	Если ТД <> Неопределено Тогда		
		ОткрытьЗначение(ТД.Заявка);		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоНомеруОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
    ПоискПоНомеру = Текст; 
    Если ПустаяСтрока(ПоискПоНомеру) = Истина Тогда
        Возврат;    
    КонецЕсли;                         
   СформироватьНаСервере(ИСТИНА); 
   ОткрытьЗначенияОтбора();
КонецПроцедуры

&НаКлиенте
Процедура ПоискСтрокаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
   ПоискСтрока = Текст;  
   Если ПустаяСтрока(ПоискСтрока) = Истина Тогда    
       Возврат;    
   КонецЕсли;
   СформироватьНаСервере(ЛОЖЬ);
   ОткрытьЗначенияОтбора();  
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоНомеруОчистка(Элемент, СтандартнаяОбработка)
    ПоискПоНомеру = "";
    СписокДокументов.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ПоискСтрокаОчистка(Элемент, СтандартнаяОбработка)
   ПоискСтрока = "";
   СписокДокументов.Очистить();
КонецПроцедуры



#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура НайтиНаКлиенте(Команда)
    Если ПустаяСтрока(ПоискПоНомеру) = Истина Тогда
        Возврат;    
    КонецЕсли;
        
   СформироватьНаСервере(ИСТИНА);
   ОткрытьЗначенияОтбора();
КонецПроцедуры

&НаКлиенте
Процедура ОтменаПоиска(Команда)
    СписокДокументов.Очистить();    
КонецПроцедуры  

&НаКлиенте
Процедура НайтиПохожие(Команда)
    Если ПустаяСтрока(ПоискСтрока) = Истина Тогда    
       Возврат;    
   КонецЕсли;
   СформироватьНаСервере(ЛОЖЬ);
   ОткрытьЗначенияОтбора();
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьНаСервере(ОтборПоНомеруЗСЦ)
    Если ОтборПоНомеруЗСЦ = Истина Тогда
        ТекстЗапросаОтбора = ТекстЗапросаПоОтборуНомера();
    Иначе
        ТекстЗапросаОтбора = ТекстЗапросаПоОтборуСтрока();        
    КонецЕсли;
    
    Если ПустаяСтрока(ТекстЗапросаОтбора)=Истина Тогда
        Возврат;    	
    КонецЕсли;  
    
    ЗаполнитьОтбор(ТекстЗапросаОтбора);
    
    
КонецПроцедуры //СформироватьНаСервере


&НаСервере
Процедура ЗаполнитьОтбор(ТекстОтбора)
    
    УстановитьПривилегированныйРежим(Истина);
    
    МобНомер				= 	Справочники.ВидыКонтактнойИнформации.ПолучитьСсылку(Новый УникальныйИдентификатор("7da99962-488d-11e0-9f98-001517115d85"));  //Мобільний телефон
	МобНомерДоп				= 	Справочники.ВидыКонтактнойИнформации.ПолучитьСсылку(Новый УникальныйИдентификатор("a8ec65fe-c74a-11e1-9d4d-001e673c80fc"));  //Мобільний телефон (додатковий)

    Запрос = Новый Запрос;
    ТекстЗапроса = ПолучитьТекстЗапросаОтбора();
    Запрос.Текст = СтрЗаменить(ТекстЗапроса,"%ОтборПоПоиску%",ТекстОтбора);
    Запрос.УстановитьПараметр("НомерПоиска","%"+ПоискПоНомеру+"%");
    Запрос.УстановитьПараметр("ПоискСтрока","%"+ПоискСтрока+"%");
    Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
	Запрос.УстановитьПараметр("МобНомер",		МобНомер);
	Запрос.УстановитьПараметр("МобНомерДоп",	МобНомерДоп);

    СписокДокументов.Загрузить(Запрос.Выполнить().Выгрузить());
     
КонецПроцедуры //ЗаполнитьОтбор

&НаКлиенте
Процедура ОткрытьЗначенияОтбора()
    Если СписокДокументов.Количество() = 1 Тогда
        ОткрытьЗначение(СписокДокументов[0].Заявка);    
    КонецЕсли;       
КонецПроцедуры //ОткрытьЗначенияОтбора


&НаСервере
Функция ТекстЗапросаПоОтборуНомера()
    ТекстЗапроса = "ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор КАК Ссылка
                   |ПОМЕСТИТЬ КешДокумент
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |ГДЕ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор.Номер ПОДОБНО &НомерПоиска
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |ГДЕ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор.НомерРемонта ПОДОБНО &НомерПоиска
                   |
                   |ИНДЕКСИРОВАТЬ ПО
                   |    Ссылка
                   |;";
    Возврат ТекстЗапроса;	
КонецФункции //ТекстЗапросаПоОтборуНомера

&НаСервере
Функция ТекстЗапросаПоОтборуСтрока()
    ТекстЗапроса = "ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор КАК Ссылка
                   |ПОМЕСТИТЬ КешДокумент
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |ГДЕ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор.Номер ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |ГДЕ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор.НомерРемонта ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |ГДЕ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Партнер.Наименование ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |ГДЕ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Номенклатура.Наименование ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |ГДЕ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.СерийныйНомер.Код ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |ГДЕ
                   |    ПОДСТРОКА(ГНАТ_ЗаявкиСервисногоЦентра.Регистратор.Комплектность, 0, 200) ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |ГДЕ
                   |    ПОДСТРОКА(ГНАТ_ЗаявкиСервисногоЦентра.Регистратор.НомерТелефона, 0, 200) ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |ГДЕ
                   |    ПОДСТРОКА(ГНАТ_ЗаявкиСервисногоЦентра.Регистратор.ЗаявленыйДефект, 0, 200) ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Лояльность_НомераТелефонов КАК Лояльность_НомераТелефонов
                   |        ПО ГНАТ_ЗаявкиСервисногоЦентра.Партнер = Лояльность_НомераТелефонов.Партнер
                   |ГДЕ
                   |    Лояльность_НомераТелефонов.НомерТелефона ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
                   |        ПО ГНАТ_ЗаявкиСервисногоЦентра.Партнер = ПартнерыКонтактнаяИнформация.Ссылка
                   |            И (ПартнерыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонПартнера))
                   |            И (ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон))
                   |ГДЕ
                   |    ПартнерыКонтактнаяИнформация.Представление ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
                   |        ПО ГНАТ_ЗаявкиСервисногоЦентра.Партнер = ПартнерыКонтактнаяИнформация.Ссылка
                   |            И (ПартнерыКонтактнаяИнформация.Вид = &МобНомер)
                   |            И (ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон))
                   |ГДЕ
                   |    ПартнерыКонтактнаяИнформация.Представление ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
                   |        ПО ГНАТ_ЗаявкиСервисногоЦентра.Партнер = ПартнерыКонтактнаяИнформация.Ссылка
                   |            И (ПартнерыКонтактнаяИнформация.Вид = &МобНомерДоп)
                   |            И (ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон))
                   |ГДЕ
                   |    ПартнерыКонтактнаяИнформация.Представление ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаПартнеровКонтактнаяИнформация
                   |        ПО ГНАТ_ЗаявкиСервисногоЦентра.Партнер = КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка.Владелец
                   |            И (КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица))
                   |            И (КонтактныеЛицаПартнеровКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон))
                   |ГДЕ
                   |    КонтактныеЛицаПартнеровКонтактнаяИнформация.Представление ПОДОБНО &ПоискСтрока
                   |
                   |ОБЪЕДИНИТЬ
                   |
                   |ВЫБРАТЬ
                   |    ГНАТ_ЗаявкиСервисногоЦентра.Регистратор
                   |ИЗ
                   |    РегистрСведений.ГНАТ_ЗаявкиСервисногоЦентра КАК ГНАТ_ЗаявкиСервисногоЦентра
                   |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаПартнеровКонтактнаяИнформация
                   |        ПО ГНАТ_ЗаявкиСервисногоЦентра.Партнер = КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка.Владелец
                   |            И (КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.МобильныйТелефонКонтактногоЛица))
                   |            И (КонтактныеЛицаПартнеровКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон))
                   |ГДЕ
                   |    КонтактныеЛицаПартнеровКонтактнаяИнформация.Представление ПОДОБНО &ПоискСтрока
                   |
                   |ИНДЕКСИРОВАТЬ ПО
                   |    Ссылка
                   |; ";
    Возврат ТекстЗапроса;	
КонецФункции  //ТекстЗапросаПоОтборуСтрока



&НаСервере
Функция ПолучитьТекстЗапросаОтбора()
    ТекстЗапроса = "%ОтборПоПоиску%
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.Ссылка КАК Ссылка,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.Номер,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.Дата,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.ДатаПринятия,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.ЗаказКлиента,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.ЗаказчикРемонта,
                   |    ПОДСТРОКА(ДокументГНАТ_ЗаявкаСервисногоЦентра.ЗаявленыйДефект, 0, 200) КАК ЗаявленыйДефект,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.Номенклатура,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.НомерРемонта,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.ОбъектОснование,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.Себестоимость,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.СерийныйНомер,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.Склад,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.МоментВремени,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.ВидОперации,
                   |    ДокументГНАТ_ЗаявкаСервисногоЦентра.NPS,
                   |    ВЫБОР
                   |        КОГДА ДокументГНАТ_ЗаявкаСервисногоЦентра.ПретензияОтработана = ИСТИНА
                   |            ТОГДА ""Претензия отработана""
                   |        КОГДА ДокументГНАТ_ЗаявкаСервисногоЦентра.Претензия = ИСТИНА
                   |            ТОГДА ""Претензия есть""
                   |        ИНАЧЕ ""Претензии нет""
                   |    КОНЕЦ КАК СостояниеПретензии,
                   |    ВЫБОР
                   |        КОГДА ДокументГНАТ_ЗаявкаСервисногоЦентра.Проведен = ИСТИНА
                   |            ТОГДА 1
                   |        КОГДА ДокументГНАТ_ЗаявкаСервисногоЦентра.Проведен = ЛОЖЬ
                   |                И ДокументГНАТ_ЗаявкаСервисногоЦентра.ПометкаУдаления = ЛОЖЬ
                   |            ТОГДА 0
                   |        КОГДА ДокументГНАТ_ЗаявкаСервисногоЦентра.ПометкаУдаления = ИСТИНА
                   |            ТОГДА 2
                   |        ИНАЧЕ 4
                   |    КОНЕЦ КАК СтатусыДокумента
                   |ПОМЕСТИТЬ ДокГнат
                   |ИЗ
                   |    КешДокумент КАК ДокГнат
                   |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ГНАТ_ЗаявкаСервисногоЦентра КАК ДокументГНАТ_ЗаявкаСервисногоЦентра
                   |        ПО ДокГнат.Ссылка = ДокументГНАТ_ЗаявкаСервисногоЦентра.Ссылка
                   |
                   |ИНДЕКСИРОВАТЬ ПО
                   |    Ссылка
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |    ДокГнат.Ссылка КАК Ссылка,
                   |    ДокГнат.ЗаказчикРемонта,
                   |    ПОДСТРОКА(ДокГнат.ЗаявленыйДефект, 0, 200) КАК ЗаявленыйДефект,
                   |    ДокГнат.Номенклатура,
                   |    Этапы.Период КАК ДатаНачалаЭтапа,
                   |    Этапы.ЭтапРаботы КАК ТекущиеЭтап,
                   |    Этапы.ЭтапРаботы.НормативноеВремя КАК НормативноеВремя,
                   |    Этапы.ПартнерАСЦ КАК АСЦ,
                   |    Этапы.ДатаИсполнения КАК ДатаИсполненияЭтапа,
                   |    РАЗНОСТЬДАТ(Этапы.ДатаИсполнения, НАЧАЛОПЕРИОДА(&ТекущаяДата, ДЕНЬ), ДЕНЬ) КАК РазницаДат,
                   |    Этапы.МесторасположениеФактическое КАК Месторасположение,
                   |    Этапы.Комментарий,                    
                   |    ДокГнат.СтатусыДокумента
                   |ПОМЕСТИТЬ ЗСЦПоЭтапу
                   |ИЗ
                   |    ДокГнат КАК ДокГнат
                   |        ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГНАТ_ЭтапыРаботЗаявокСервисногоЦентра.СрезПоследних(
                   |                ,
                   |                ЗаявкаСервисногоЦентра В
                   |                    (ВЫБРАТЬ
                   |                        ДокГнат.Ссылка
                   |                    ИЗ
                   |                        ДокГнат КАК ДокГнат)) КАК Этапы
                   |        ПО ДокГнат.Ссылка = Этапы.ЗаявкаСервисногоЦентра
                   |
                   |ИНДЕКСИРОВАТЬ ПО
                   |    Ссылка
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |    ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Период,
                   |    ВЫБОР
                   |        КОГДА ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие ССЫЛКА Документ.Встреча
                   |            ТОГДА ""Встреча: "" + ПОДСТРОКА(ВЫРАЗИТЬ(ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие КАК Документ.Встреча).Описание, 0, 200)
                   |        КОГДА ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие ССЫЛКА Документ.ЗапланированноеВзаимодействие
                   |            ТОГДА ""ЗапланированноеВзаимодействие: "" + ПОДСТРОКА(ВЫРАЗИТЬ(ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие КАК Документ.ЗапланированноеВзаимодействие).Описание, 0, 200)
                   |        КОГДА ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие ССЫЛКА Документ.СообщениеSMS
                   |            ТОГДА ""СообщениеSMS: "" + ПОДСТРОКА(ВЫРАЗИТЬ(ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие КАК Документ.СообщениеSMS).ТекстСообщения, 0, 200)
                   |        КОГДА ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие ССЫЛКА Документ.ТелефонныйЗвонок
                   |            ТОГДА ""ТелефонныйЗвонок: "" + ПОДСТРОКА(ВЫРАЗИТЬ(ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие КАК Документ.ТелефонныйЗвонок).Описание, 0, 200)
                   |        КОГДА ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие ССЫЛКА Документ.ЭлектронноеПисьмоВходящее
                   |            ТОГДА ""ЭлектронноеПисьмоВходящее: "" + ПОДСТРОКА(ВЫРАЗИТЬ(ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие КАК Документ.ЭлектронноеПисьмоВходящее).Текст, 0, 200)
                   |        КОГДА ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие ССЫЛКА Документ.ЭлектронноеПисьмоИсходящее
                   |            ТОГДА ""ЭлектронноеПисьмоИсходящее: "" + ПОДСТРОКА(ВЫРАЗИТЬ(ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие КАК Документ.ЭлектронноеПисьмоИсходящее).Текст, 0, 200)
                   |    КОНЕЦ КАК КоментарийВзаимодествия,
                   |    ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Взаимодействие КАК Взаимодействие,
                   |    ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Заявка КАК Заявка
                   |ПОМЕСТИТЬ ВсеВзаимодествияПОЗСЦ
                   |ИЗ
                   |    ЗСЦПоЭтапу КАК ЗСЦПоПоследнемуЭтапу
                   |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГНАТ_ВзаимодействияПоЗаявкам.СрезПоследних(
                   |                ,
                   |                Заявка В
                   |                    (ВЫБРАТЬ
                   |                        ДокГнат.Ссылка
                   |                    ИЗ
                   |                        ДокГнат КАК ДокГнат)) КАК ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних
                   |        ПО ЗСЦПоПоследнемуЭтапу.Ссылка = ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Заявка
                   |            И ЗСЦПоПоследнемуЭтапу.ДатаНачалаЭтапа <= ГНАТ_ВзаимодействияПоЗаявкамСрезПоследних.Период
                   |
                   |ИНДЕКСИРОВАТЬ ПО
                   |    Заявка,
                   |    Взаимодействие
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |    ВсеВзаимодествияПОЗСЦ.Заявка,
                   |    МАКСИМУМ(ВсеВзаимодествияПОЗСЦ.Взаимодействие) КАК Взаимодействие
                   |ПОМЕСТИТЬ СрезПоследнихПоВзаимодествиях
                   |ИЗ
                   |    ВсеВзаимодествияПОЗСЦ КАК ВсеВзаимодествияПОЗСЦ
                   |
                   |СГРУППИРОВАТЬ ПО
                   |    ВсеВзаимодествияПОЗСЦ.Заявка
                   |
                   |ИНДЕКСИРОВАТЬ ПО
                   |    ВсеВзаимодествияПОЗСЦ.Заявка,
                   |    Взаимодействие
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |    ВсеВзаимодествияПОЗСЦ.Период,
                   |    ВсеВзаимодествияПОЗСЦ.КоментарийВзаимодествия,
                   |    ВсеВзаимодествияПОЗСЦ.Взаимодействие,
                   |    ВсеВзаимодествияПОЗСЦ.Заявка КАК Заявка
                   |ПОМЕСТИТЬ ПоследнееВзаимодествияПоЗаявке
                   |ИЗ
                   |    ВсеВзаимодествияПОЗСЦ КАК ВсеВзаимодествияПОЗСЦ
                   |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ СрезПоследнихПоВзаимодествиях КАК СрезПоследнихПоВзаимодествиях
                   |        ПО ВсеВзаимодествияПОЗСЦ.Заявка = СрезПоследнихПоВзаимодествиях.Заявка
                   |            И ВсеВзаимодествияПОЗСЦ.Взаимодействие = СрезПоследнихПоВзаимодествиях.Взаимодействие
                   |
                   |ИНДЕКСИРОВАТЬ ПО
                   |    Заявка
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |    ЗСЦПоПоследнемуЭтапу.Ссылка КАК Заявка,
                   |    ПоследнееВзаимодествияПоЗаявке.КоментарийВзаимодествия КАК КоментарийВзаимодествия,   
                   |    ПоследнееВзаимодествияПоЗаявке.Период КАК ДатаВзаимодествия,
                   |    ДокГнат.Номер,
                   |    ДокГнат.Дата,
                   |    ДокГнат.ДатаПринятия,
                   |    ДокГнат.ЗаказчикРемонта,
                   |    ДокГнат.ЗаявленыйДефект,
                   |    ДокГнат.Номенклатура,
                   |    ДокГнат.НомерРемонта,
                   |    ДокГнат.СерийныйНомер,
                   |    ДокГнат.Склад,
                   |    ДокГнат.ВидОперации,
                   |    ЗСЦПоПоследнемуЭтапу.ДатаНачалаЭтапа,
                   |    ЗСЦПоПоследнемуЭтапу.ТекущиеЭтап,
                   |    ЗСЦПоПоследнемуЭтапу.НормативноеВремя,
                   |    ЗСЦПоПоследнемуЭтапу.АСЦ,
                   |    ЗСЦПоПоследнемуЭтапу.ДатаИсполненияЭтапа,
                   |    ЗСЦПоПоследнемуЭтапу.РазницаДат,
                   |    ЗСЦПоПоследнемуЭтапу.Месторасположение,
                   |    ЗСЦПоПоследнемуЭтапу.Комментарий,
                   |    ЗСЦПоПоследнемуЭтапу.СтатусыДокумента,
                   |    ЗСЦПоПоследнемуЭтапу.Ссылка.ПометкаУдаления КАК ПометкаУдаления
                   |ИЗ
                   |    ЗСЦПоЭтапу КАК ЗСЦПоПоследнемуЭтапу
                   |        ЛЕВОЕ СОЕДИНЕНИЕ ПоследнееВзаимодествияПоЗаявке КАК ПоследнееВзаимодествияПоЗаявке
                   |        ПО ЗСЦПоПоследнемуЭтапу.Ссылка = ПоследнееВзаимодествияПоЗаявке.Заявка
                   |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокГнат КАК ДокГнат
                   |        ПО ЗСЦПоПоследнемуЭтапу.Ссылка = ДокГнат.Ссылка";
    Возврат ТекстЗапроса;	
КонецФункции  //ПолучитьТекстЗапросаОтбора     




#КонецОбласти