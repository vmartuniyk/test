
#Область ИнтерфейсАвтоматическихТестов

&НаКлиенте
Перем ЮнитТест;

&НаКлиенте
Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮнитТест = ЮнитТестирование;
	
	СпиокТестов = Новый Массив;	
	Возврат СпиокТестов;
	
КонецФункции

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнитьРеквизитыФормы();
	ЗаполнитьТаблицуУчастников();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Ответ = Вопрос(НСтр("ru='Данные документа были изменены пользователем. Сохранить изменения?'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), РежимДиалогаВопрос.ДаНетОтмена);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			РезультатЗаписи = ЗаписатьДокументНаСервере();
			Если РезультатЗаписи Тогда
				Оповестить("ОбновитьХодРабот");
				Модифицированность = Ложь;
				ПоказатьОповещениеПользователя(НСтр("ru='Документ проведен.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), ПолучитьНавигационнуюСсылку(СсылкаНаВзаимодействие), Строка(СсылкаНаВзаимодействие), БиблиотекаКартинок.Информация32);
			Иначе
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Модифицированность = Ложь;
			Возврат;
		КонецЕсли;
		Если Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтветственныйПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВажностьПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РассмотретьПослеПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РассмотреноПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы_Участники

&НаКлиенте
Процедура УчастникиПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура УчастникиКонтактПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Участники.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ВзаимодействияВызовСервера.ПолучитьПредставлениеИВсюКонтактнуюИнформациюКонтакта(ТекущиеДанные.Контакт, ТекущиеДанные.ПредставлениеКонтакта, ТекущиеДанные.КакСвязаться);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УчастникиКонтактНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные 		 = Элементы.Участники.ТекущиеДанные;
	ВзаимодействияКлиент.ВыбратьКонтакт(Неопределено, ТекущиеДанные.КакСвязаться, ТекущиеДанные.ПредставлениеКонтакта, ТекущиеДанные.Контакт, Ложь, Ложь, Истина);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	РезультатЗаписи = ЗаписатьДокументНаСервере();
	Если РезультатЗаписи Тогда
		Модифицированность = Ложь;
		Оповестить("ОбновитьХодРабот");
		ПоказатьОповещениеПользователя(НСтр("ru='Документ проведен.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), ПолучитьНавигационнуюСсылку(СсылкаНаВзаимодействие), Строка(СсылкаНаВзаимодействие), БиблиотекаКартинок.Информация32);
		Закрыть();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	Важность 		= Перечисления.ВариантыВажностиВзаимодействия.Обычная;
	Ответственный 	= ПараметрыСеанса.ТекущийПользователь;	
КонецПроцедуры // ЗапонитьРеквизитыФормы()

&НаСервере
Процедура ЗаполнитьТаблицуУчастников()

	Партнер = Параметры.ЗаявкаСервисногоЦентра.ЗаказчикРемонта;
	
	Если Партнер = Справочники.Партнеры.НашеПредприятие Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока 		= Участники.Добавить();
	НоваяСтрока.Контакт = Партнер;
	ВзаимодействияВызовСервера.ПолучитьПредставлениеИВсюКонтактнуюИнформациюКонтакта(НоваяСтрока.Контакт, НоваяСтрока.ПредставлениеКонтакта, НоваяСтрока.КакСвязаться);

КонецПроцедуры // ЗаполнитьКонтактнуюИнфорацию()

&НаСервере
Функция ЗаписатьДокументНаСервере()

	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПроверитьЗаполнениеРеквизитовФормы() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	ДокументВзаимодействие = Документы.ЗапланированноеВзаимодействие.СоздатьДокумент();
	ЗаполнитьРеквизитыДокумента(ДокументВзаимодействие);
	
	Попытка
		ДокументВзаимодействие.Записать(РежимЗаписиДокумента.Запись);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		ОтменитьТранзакцию();
		Возврат Ложь;
	КонецПопытки;
	
	Взаимодействия.ПриЗаписиВзаимодействияИзФормы(ДокументВзаимодействие, ДокументВзаимодействие.Предмет);
	
	Попытка
		ДокументВзаимодействие.Записать(РежимЗаписиДокумента.Запись);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		ОтменитьТранзакцию();
		Возврат Ложь;
	КонецПопытки;
	
	РезультатЗаписи = ЗаписатьВзаимодействиеВРегистр(ДокументВзаимодействие.Ссылка);
	Если Не РезультатЗаписи Тогда
		ОтменитьТранзакцию();
		Возврат Ложь;
	КонецЕсли;
	
	СсылкаНаВзаимодействие = ДокументВзаимодействие.Ссылка;
	
	ЗафиксироватьТранзакцию();
	
	Возврат Истина;

КонецФункции // ЗаписатьДокументНаСервере()

&НаСервере
Функция ЗаписатьВзаимодействиеВРегистр(ДокументВзаимодействие)

	Период = ТекущаяДатаСеанса();
	
	НаборЗаписей = РегистрыСведений.ГНАТ_ВзаимодействияПоЗаявкам.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(Период);
	НаборЗаписей.Отбор.Заявка.Установить(Параметры.ЗаявкаСервисногоЦентра);
 	НаборЗаписей.Отбор.Взаимодействие.Установить(ДокументВзаимодействие);
	
	Запись 					= НаборЗаписей.Добавить();
	Запись.Период 			= Период;
	Запись.Заявка 			= Параметры.ЗаявкаСервисногоЦентра;
	Запись.Взаимодействие 	= ДокументВзаимодействие;
	Запись.Количество		= 1;
	
	Попытка
		НаборЗаписей.Записать();    			
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;

КонецФункции // ЗаписатьВзаимодействиеВРегистр()

&НаСервере
Процедура ЗаполнитьРеквизитыДокумента(ДокументВстреча)

	ДокументВстреча.Дата 						= ТекущаяДатаСеанса();	
	ДокументВстреча.Автор 						= ПараметрыСеанса.ТекущийПользователь;
	ДокументВстреча.Важность 					= Важность;
	ДокументВстреча.Описание 					= Описание;
	ДокументВстреча.Ответственный 				= Ответственный;
	ДокументВстреча.Рассмотрено 				= Рассмотрено;
	ДокументВстреча.РассмотретьПосле 			= РассмотретьПосле;
	ДокументВстреча.Тема 						= Тема;
	ДокументВстреча.Комментарий 				= Комментарий;
	
	Для каждого СтрокаТаблицы Из Участники Цикл
	    НоваяСтрока = ДокументВстреча.Участники.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	КонецЦикла; 

КонецПроцедуры // ЗаполнитьРеквизитыДокумента()

&НаСервере
Функция ПроверитьЗаполнениеРеквизитовФормы()

	РезультатПроверки = Истина; // Сначала считаем что все ок.
	
	Если Не ЗначениеЗаполнено(Важность) Тогда
		РезультатПроверки = Ложь;
		ТекстСообщения	  = НСтр("ru='Поле ""Важность"" не заполнено.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Важность");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		РезультатПроверки = Ложь;
		ТекстСообщения	  = НСтр("ru='Поле ""Ответственный"" не заполнено.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Ответственный");
	КонецЕсли;
	
	Если ПустаяСтрока(Тема) Тогда
		РезультатПроверки = Ложь;
		ТекстСообщения	  = НСтр("ru='Поле ""Тема"" не заполнено.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Тема");
	КонецЕсли;
	
	Если ПустаяСтрока(Описание) Тогда
		РезультатПроверки = Ложь;
		ТекстСообщения	  = НСтр("ru='Поле ""Описание"" не заполнено.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Описание");
	КонецЕсли;
	
	Если Участники.Количество() = 0 Тогда
		РезультатПроверки = Ложь;
		ТекстСообщения	  = НСтр("ru='Необходимо указать учатсников встречи.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Участники");
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из Участники Цикл
	
		Если ПустаяСтрока(СтрокаТаблицы.ПредставлениеКонтакта) Тогда
			РезультатПроверки = Ложь;
			ТекстСообщения	  = НСтр("ru='Необходимо задать представление контакта.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ИндексСтроки	  = Участники.Индекс(СтрокаТаблицы);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Участники[" + ИндексСтроки + "].ПредставлениеКонтакта");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Контакт) Тогда
			РезультатПроверки = Ложь;
			ТекстСообщения	  = НСтр("ru='Необходимо задать контакт встречи.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ИндексСтроки	  = Участники.Индекс(СтрокаТаблицы);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Участники[" + ИндексСтроки + "].Контакт");
		КонецЕсли;
	
	КонецЦикла; 
		
	Возврат РезультатПроверки;

КонецФункции // ПроверитьЗаполнениеРеквизитовФормы()

#КонецОбласти