
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Номенклатура = Параметры.Номенклатура;
	СформироватьОтчетНаСервере();
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

// Формирует отчет по остаткам на складах на клиенте
//
// Параметры:
//
&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	СформироватьОтчетНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует отчет по остаткам на складах на сервере
//
// Параметры:
//
&НаСервере
Процедура СформироватьОтчетНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат.Очистить();
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= ПолучитьТекстЗапроса();
	
	Если Не Номенклатура.Пустая() Тогда		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);		
	КонецЕсли;
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	СписокНоменклатуры = РезультатПакета[РезультатПакета.ВГраница()].Выгрузить();
	ВывестиРезультатыЗапроса(СписокНоменклатуры);
	
КонецПроцедуры

// Текст запроса остатки товаров на складах 
//
// Параметры:
//  
//Возвращаемое значение:
//	Строка 			- текст запроса остатки товаров
// 
&НаСервере
Функция ПолучитьТекстЗапроса()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СвободныеОстаткиОстатки.Номенклатура,
	|	СвободныеОстаткиОстатки.Номенклатура.НаименованиеПолное КАК НаименованиеПолное,
	|	СвободныеОстаткиОстатки.Склад
	|ПОМЕСТИТЬ Cache_Номенклатура
	|ИЗ
	|	РегистрНакопления.СвободныеОстатки.Остатки(, " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК СвободныеОстаткиОстатки
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТоварыКПоступлениюОстатки.Номенклатура,
	|	ТоварыКПоступлениюОстатки.Номенклатура.НаименованиеПолное,
	|	ТоварыКПоступлениюОстатки.Склад
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(," + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК ТоварыКПоступлениюОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОбеспечениеЗаказовОстатки.Номенклатура,
	|	ОбеспечениеЗаказовОстатки.Номенклатура.НаименованиеПолное,
	|	ОбеспечениеЗаказовОстатки.Склад
	|ИЗ
	|	РегистрНакопления.ОбеспечениеЗаказов.Остатки(, " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК ОбеспечениеЗаказовОстатки
	|ГДЕ
	|	ОбеспечениеЗаказовОстатки.НаличиеСоСкладаОстаток <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СвободныеОстаткиОстатки.Номенклатура,
	|	СвободныеОстаткиОстатки.Склад
	|;
	|
	|ВЫБРАТЬ
	|	ОбеспечениеЗаказовОстатки.Номенклатура,
	|	ОбеспечениеЗаказовОстатки.Склад,
	|	ОбеспечениеЗаказовОстатки.Назначение.Заказ КАК Заказ,
	|	ОбеспечениеЗаказовОстатки.КЗаказуОстаток КАК ПодОбеспечение,
	|	ОбеспечениеЗаказовОстатки.НаличиеСоСкладаОстаток КАК ПодРезерв
	|ПОМЕСТИТЬ ОбеспечениеЗаказов
	|ИЗ
	|	РегистрНакопления.ОбеспечениеЗаказов.Остатки(, " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК ОбеспечениеЗаказовОстатки
	|ГДЕ
	|	ОбеспечениеЗаказовОстатки.Назначение.Заказ.Склад = ОбеспечениеЗаказовОстатки.Склад
	|;

	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвободныеОстаткиОстаткиИОбороты.Номенклатура,
	|	СвободныеОстаткиОстаткиИОбороты.Склад,
	|	СвободныеОстаткиОстаткиИОбороты.Регистратор КАК Заказ,
	|	0 КАК ПодОбеспечение,
	|	СвободныеОстаткиОстаткиИОбороты.ВРезервеОборот КАК ПодРезерв
	|ПОМЕСТИТЬ ЗаказыКлиентов
	|ИЗ
	|	РегистрНакопления.СвободныеОстатки.ОстаткиИОбороты(, , Регистратор, , " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК СвободныеОстаткиОстаткиИОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыКлиентов.Остатки КАК ЗаказыКлиентовОстатки
	|		ПО СвободныеОстаткиОстаткиИОбороты.Номенклатура = ЗаказыКлиентовОстатки.Номенклатура
	|			И СвободныеОстаткиОстаткиИОбороты.Склад = ЗаказыКлиентовОстатки.Склад
	|			И СвободныеОстаткиОстаткиИОбороты.Регистратор = ЗаказыКлиентовОстатки.ЗаказКлиента
	|ГДЕ
	|	СвободныеОстаткиОстаткиИОбороты.ВРезервеКонечныйОстаток <> 0
	|;

	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвободныеОстаткиОстаткиИОбороты.Номенклатура,
	|	СвободныеОстаткиОстаткиИОбороты.Склад,
	|	СвободныеОстаткиОстаткиИОбороты.Регистратор КАК Заказ,
	|	0 КАК ПодОбеспечение,
	|	СвободныеОстаткиОстаткиИОбороты.ВРезервеОборот КАК ПодРезерв
	|ПОМЕСТИТЬ ЗаказыНаВнутреннееПотребление
	|ИЗ
	|	РегистрНакопления.СвободныеОстатки.ОстаткиИОбороты(, , Регистратор, , " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК СвободныеОстаткиОстаткиИОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыНаВнутреннееПотребление.Остатки(, " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК ЗаказыНаВнутреннееПотреблениеОстатки
	|		ПО СвободныеОстаткиОстаткиИОбороты.Номенклатура = ЗаказыНаВнутреннееПотреблениеОстатки.Номенклатура
	|			И СвободныеОстаткиОстаткиИОбороты.Склад = ЗаказыНаВнутреннееПотреблениеОстатки.Склад
	|			И СвободныеОстаткиОстаткиИОбороты.Регистратор = ЗаказыНаВнутреннееПотреблениеОстатки.ЗаказНаВнутреннееПотребление
	|ГДЕ
	|	СвободныеОстаткиОстаткиИОбороты.ВРезервеКонечныйОстаток <> 0
	|;

	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвободныеОстаткиОстаткиИОбороты.Номенклатура,
	|	СвободныеОстаткиОстаткиИОбороты.Склад,
	|	СвободныеОстаткиОстаткиИОбороты.Регистратор КАК Заказ,
	|	0 КАК ПодОбеспечение,
	|	СвободныеОстаткиОстаткиИОбороты.ВРезервеОборот КАК ПодРезерв
	|ПОМЕСТИТЬ ЗаказыНаПеремещениеАвто
	|ИЗ
	|	РегистрНакопления.СвободныеОстатки.ОстаткиИОбороты(, , Регистратор, , " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК СвободныеОстаткиОстаткиИОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыНаПеремещение.Остатки(," + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + " ) КАК ЗаказыНаПеремещениеОстатки
	|		ПО СвободныеОстаткиОстаткиИОбороты.Номенклатура = ЗаказыНаПеремещениеОстатки.Номенклатура
	|			И СвободныеОстаткиОстаткиИОбороты.Склад = ЗаказыНаПеремещениеОстатки.ЗаказНаПеремещение.СкладОтправитель
	|			И СвободныеОстаткиОстаткиИОбороты.Регистратор = ЗаказыНаПеремещениеОстатки.ЗаказНаПеремещение
	|ГДЕ
	|	СвободныеОстаткиОстаткиИОбороты.ВРезервеКонечныйОстаток <> 0
	|;

	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбеспечениеЗаказовОстаткиИОбороты.Номенклатура,
	|	ОбеспечениеЗаказовОстаткиИОбороты.Склад,
	|	ОбеспечениеЗаказовОстаткиИОбороты.Регистратор КАК Заказ,
	|	ОбеспечениеЗаказовОстаткиИОбороты.КЗаказуОборот КАК ПодОбеспечение,
	|	ОбеспечениеЗаказовОстаткиИОбороты.НаличиеСоСкладаОборот КАК ПодРезерв
	|ПОМЕСТИТЬ ЗаказыНаПеремещение
	|ИЗ
	|	РегистрНакопления.ОбеспечениеЗаказов.ОстаткиИОбороты(, , Регистратор, , " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК ОбеспечениеЗаказовОстаткиИОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыНаПеремещение.Остатки КАК ЗаказыНаПеремещениеОстатки
	|		ПО ОбеспечениеЗаказовОстаткиИОбороты.Номенклатура = ЗаказыНаПеремещениеОстатки.Номенклатура
	|			И ОбеспечениеЗаказовОстаткиИОбороты.Склад = ЗаказыНаПеремещениеОстатки.ЗаказНаПеремещение.СкладОтправитель
	|			И ОбеспечениеЗаказовОстаткиИОбороты.Регистратор = ЗаказыНаПеремещениеОстатки.ЗаказНаПеремещение
	|ГДЕ
	|	ОбеспечениеЗаказовОстаткиИОбороты.НаличиеСоСкладаКонечныйОстаток >= 0
	|	И ОбеспечениеЗаказовОстаткиИОбороты.КЗаказуКонечныйОстаток >= 0
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпрНоменклатура.Номенклатура,
	|	СпрНоменклатура.Склад КАК Склад,
	|	ЕСТЬNULL(Остатки.ВНаличииОстаток, 0) - ЕСТЬNULL(Остатки.ВРезервеОстаток, 0) КАК СвободныйОстаток,
	|	ЕСТЬNULL(Остатки.ВРезервеОстаток, 0) + ЕСТЬNULL(Обеспечение.НаличиеСоСкладаОстаток, 0) КАК Резерв,
	|	ЕСТЬNULL(Остатки.ВНаличииОстаток, 0) + ЕСТЬNULL(Обеспечение.НаличиеСоСкладаОстаток, 0) КАК Всего,
	|	ЕСТЬNULL(Перемещения.КПоступлениюОстаток, 0) КАК ВПеремещениях,
	|	ЕСТЬNULL(ЗаказыПоставщику.КПоступлениюОстаток, 0) КАК ВЗаказахПоставщику,
	|	ЕСТЬNULL(Остатки.ВНаличииОстаток, 0) - ЕСТЬNULL(Остатки.ВРезервеОстаток, 0) + ЕСТЬNULL(Перемещения.КПоступлениюОстаток, 0) + ЕСТЬNULL(ЗаказыПоставщику.КПоступлениюОстаток, 0) КАК ПрогнозОстаток,
	|	ЕСТЬNULL(Обеспечение.КЗаказуОстаток, 0) КАК Обеспечение,
	|	0 КАК ПодОбеспечение,
	|	0 КАК ПодРезерв,
	|	NULL КАК Заказ
	|ИЗ
	|	Cache_Номенклатура КАК СпрНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.Остатки(, " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК Остатки
	|		ПО (Остатки.Номенклатура = СпрНоменклатура.Номенклатура)
	|			И (Остатки.Склад = СпрНоменклатура.Склад)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКПоступлению.Остатки(
	|				,
	|				ТИПЗНАЧЕНИЯ(ДокументПоступления) <> ТИП(Документ.ЗаказПоставщику)
	|					И " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК Перемещения
	|		ПО (Перемещения.Номенклатура = СпрНоменклатура.Номенклатура)
	|			И (Перемещения.Склад = СпрНоменклатура.Склад)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКПоступлению.Остатки(
	|				,
	|				ТИПЗНАЧЕНИЯ(ДокументПоступления) = ТИП(Документ.ЗаказПоставщику)
	|					И " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК ЗаказыПоставщику
	|		ПО (ЗаказыПоставщику.Номенклатура = СпрНоменклатура.Номенклатура)
	|			И (ЗаказыПоставщику.Склад = СпрНоменклатура.Склад)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОбеспечениеЗаказов.Остатки(, " + ?(Номенклатура.Пустая(), "", "Номенклатура = &Номенклатура") + ") КАК Обеспечение
	|		ПО (Обеспечение.Номенклатура = СпрНоменклатура.Номенклатура)
	|			И (Обеспечение.Склад = СпрНоменклатура.Склад)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпрНоменклатура.Номенклатура,
	|	СпрНоменклатура.Склад,
	|	СпрНоменклатура.Номенклатура.НаименованиеПолное,
	|	ЕСТЬNULL(Остатки.ВНаличииОстаток, 0) - ЕСТЬNULL(Остатки.ВРезервеОстаток, 0),
	|	ЕСТЬNULL(Остатки.ВРезервеОстаток, 0) + ЕСТЬNULL(Обеспечение.НаличиеСоСкладаОстаток, 0),
	|	ЕСТЬNULL(Остатки.ВНаличииОстаток, 0) + ЕСТЬNULL(Обеспечение.НаличиеСоСкладаОстаток, 0),
	|	ЕСТЬNULL(Перемещения.КПоступлениюОстаток, 0),
	|	ЕСТЬNULL(ЗаказыПоставщику.КПоступлениюОстаток, 0),
	|	ЕСТЬNULL(Остатки.ВНаличииОстаток, 0) - ЕСТЬNULL(Остатки.ВРезервеОстаток, 0) + ЕСТЬNULL(Перемещения.КПоступлениюОстаток, 0) + ЕСТЬNULL(ЗаказыПоставщику.КПоступлениюОстаток, 0),
	|	ЕСТЬNULL(Обеспечение.КЗаказуОстаток, 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|

	|ВЫБРАТЬ
	|	ОбеспечениеЗаказов.Номенклатура,
	|	ОбеспечениеЗаказов.Склад,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ОбеспечениеЗаказов.ПодОбеспечение,
	|	ОбеспечениеЗаказов.ПодРезерв,
	|	ОбеспечениеЗаказов.Заказ
	|ИЗ
	|	ОбеспечениеЗаказов КАК ОбеспечениеЗаказов

	|ОБЪЕДИНИТЬ ВСЕ

	|ВЫБРАТЬ
	|	ЗаказыКлиентов.Номенклатура,
	|	ЗаказыКлиентов.Склад,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЗаказыКлиентов.ПодОбеспечение,
	|	ЗаказыКлиентов.ПодРезерв,
	|	ЗаказыКлиентов.Заказ
	|ИЗ
	|	ЗаказыКлиентов КАК ЗаказыКлиентов

	|ОБЪЕДИНИТЬ ВСЕ

	|ВЫБРАТЬ
	|	ЗаказыНаВнутреннееПотребление.Номенклатура,
	|	ЗаказыНаВнутреннееПотребление.Склад,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЗаказыНаВнутреннееПотребление.ПодОбеспечение,
	|	ЗаказыНаВнутреннееПотребление.ПодРезерв,
	|	ЗаказыНаВнутреннееПотребление.Заказ
	|ИЗ
	|	ЗаказыНаВнутреннееПотребление КАК ЗаказыНаВнутреннееПотребление

	|ОБЪЕДИНИТЬ ВСЕ

	|ВЫБРАТЬ
	|	ЗаказыНаПеремещениеАвто.Номенклатура,
	|	ЗаказыНаПеремещениеАвто.Склад,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,

	|	ЗаказыНаПеремещениеАвто.ПодОбеспечение,
	|	ЗаказыНаПеремещениеАвто.ПодРезерв,
	|	ЗаказыНаПеремещениеАвто.Заказ
	|ИЗ
	|	ЗаказыНаПеремещениеАвто КАК ЗаказыНаПеремещениеАвто

	|ОБЪЕДИНИТЬ ВСЕ

	|ВЫБРАТЬ
	|	ЗаказыНаПеремещение.Номенклатура,
	|	ЗаказыНаПеремещение.Склад,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЗаказыНаПеремещение.ПодОбеспечение,
	|	ЗаказыНаПеремещение.ПодРезерв,
	|	ЗаказыНаПеремещение.Заказ
	|ИЗ
	|	ЗаказыНаПеремещение КАК ЗаказыНаПеремещение	
	
	|
	|УПОРЯДОЧИТЬ ПО
	|	Склад" ;
	
	Возврат ТекстЗапроса;
	
КонецФункции


//Выводит результат запроса по остатках товаров на складах в СКД
//
// Параметры:
// СписокНоменклатуры - ТаблицаЗначений - таблица из результата запроса по остатках товаров на складах
&НаСервере
Процедура ВывестиРезультатыЗапроса(СписокНоменклатуры)
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("СписокНоменклатуры", СписокНоменклатуры);
	
	ОбъектОбработки 		= РеквизитФормыВЗначение("Объект");
	СхемаКомпоновкиДанных   = ОбъектОбработки.ПолучитьМакет("СКД_ОстаткиТоваров");
	ЗначениеВРеквизитФормы(ОбъектОбработки, "Объект");
	
	Настройки 					= СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	КомпоновщикМакета 			= Новый КомпоновщикМакетаКомпоновкиДанных;
	ДанныеРасшифровки			= Новый ДанныеРасшифровкиКомпоновкиДанных;
	МакетКомпоновки				= КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанны    = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанны.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВывода				= Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанны);
	
КонецПроцедуры

#КонецОбласти

